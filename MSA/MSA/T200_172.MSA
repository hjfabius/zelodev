
			;#################################
                        ;#              TAJ 200          #
                        ;#        ZELO ELETTRONICA       #
			;#                               #
                        ;#    CONTROLLO DI REGISTRO M/C  #
                        ;# LONGITUDINALE                 #
                        ;# TIRO                          #
                        ;# TRASVERSALE (DA IMPLEMENTARE) #
                        ;#################################
 

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	PARAMETRI FONDAMENTALI PROGRAMMA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
			NAME    T200_172
VERSIONE_PROGRAMMA:	EQU	172	;VERSIONE PROGRAMMA
WATCHDOG_ST7:		EQU	20	;VALORE IN SECONDI DI ATTESA IRQ DA ST7 PRIMA DI RESETTARE (USATO IN REQISFREE)



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	PORTE PIO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

PA:		EQU     00H     ;DATI PORTA A
PB:		EQU     01H     ;DATI PORTA B
PC:     	EQU     63H     ;DATI PORTA C
PD:     	EQU     51H     ;DATI PORTA D

PA_CMD:		EQU	02H	;COMANDO PORTA A
PB_CMD:		EQU	03H	;COMANDO PORTA B

CTC0_CMD:	EQU	70H
CTC1_CMD:	EQU	71H
CTC2_CMD:	EQU	72H
CTC3_CMD:	EQU	73H



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	CONFIGURAZIONE PORTA PB
;
; PA0	IN	COM ST7 - RX DATO			TASA
; PA1	OUT	COM ST7 - RICHIESTA COMUNICAZIONE	OUTPA1
; PA2	OUT	FREE					OUTPA2
; PA3	OUT	FREE					OUTPA3
; PA4	IN	COM ST7 - IRQ COMUNICAZIONE		TASDA
; PA5	OUT	AUTO GAIN			
; PA6	OUT	AUTO GAIN			
; PA7	OUT	ABILITA GATE			
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	CONFIGURAZIONE PORTA PB
;
; PB0	OUT	COM ST7 - CLOCK				DDCLK	
; PB1	IN	COINCIDENZA	
; PB2	IN	S1 SQUADRATO	
; PB3	IN	NO PRINT	
; PB4	IN	2048 IMPULSI	
; PB5	IN	INGRESSO SERIALE
; PB6	OUT	RICERCA AUTOMATICA		
; PB7	OUT	COM ST7 - TX DATO			DDIN		
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	CONFIGURAZIONE PORTA PC
;
; PC0	OUT	AVANTI					OPTO1
; PC1	OUT	RITARDO					OPTO2
; PC2	OUT	AUTOMAN LONG				-----
; PC3	OUT	ALLARME					OPTO5
; PC4	OUT	AUTOMAN TRAV				-----
; PC5	OUT	FUNZIONE AGGIUNTIVA (F6)		OPTO6
; PC6	OUT	DESTRA/TRAVERSA/+TIRO			OPTO3
; PC7	OUT	SINISTRA/OPERATORE/-TIRO		OPTO4
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	CONFIGURAZIONE PORTA PD
;
; PD0	OUT	DA0	SELEZIONE APPLICAZIONE / TESTINA
; PD1	OUT	DA1	SELEZIONE ENCODER
; PD2	OUT	DA2
; PD3	OUT	DA3	DAC CLOCK	
; PD4	OUT	DA4	DAC LOAD
; PD5	OUT	DA5	DAC SRI
; PD6	OUT	DA6
; PD7	OUT	DA7
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	CONFIGURAZIONE CTC
;
; CTC0	AMPIEZZA GATE
; CTC1	RICERCA AUTOMATICA E TEMPO DI CORREZIONE
; CTC2	TEMPO DI CORREZIONE
; CTC3	DOPPIO SEGNO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	AREA DI MEMORIA VOLATILE C000-CFFFH (RESETTATA ALL'AVVIO)
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

TABCON: 	EQU     0C000H   ;INIZIO AREA DI MEMORIA VOLATILE C000-CFFFH

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	AREA DI MEMORIA OPERAZIONI
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
DIVDEN: 	EQU     0C000H   ;DIVIDENDO
DIVSOR: 	EQU     0C002H   ;DIVISORE
QUOZIE: 	EQU     0C004H   ;RISULTATO DIVISIONE
MOLTI:  	EQU     0C006H   ;MOLTIPLICATORE
MOLPL:  	EQU     0C008H   ;MOLTIPLICANDO
PROD:   	EQU     0C00AH   ;PRODOTTO
MOLT24: 	EQU     0C00CH   ;MOLTIPLICATORE		MOLTIPLICA	24 BIT
MOLP24: 	EQU     0C00EH   ;MOLTIPLICANDO			MOLTIPLICA	24 BIT
PROD24: 	EQU     0C010H   ;PRODOTTO			MOLTIPLICA	24 BIT (ARRAY DI 3 BYTES)
QU4096: 	EQU     0C014H   ;QUOZIENTE			DIVISIONE PER 4096
QU1000: 	EQU     0C016H   ;QUOZIENTE			DIVISIONE PER 1000
RESTO:  	EQU     0C018H   ;MEMORIA RESTO			DIVISIONE	16 BIT  
ADDEN0: 	EQU     0C01AH   ;ADDENDO BASSO			SOMMA		32 BIT
ADDEN2: 	EQU     0C01CH   ;ADDENDO ALTO			SOMMA		32 BIT
AUGEN0: 	EQU     0C01EH   ;AUGENDO BASSO			SOMMA		32 BIT
AUGEN2: 	EQU     0C020H   ;AUGENDO ALTO			SOMMA		32 BIT
CADDE0: 	EQU     0C022H   ;ADDENDO BASSO			SOMMA		32 BIT
CADDE2: 	EQU     0C024H   ;ADDENDO ALTO			SOMMA		32 BIT
CAUGE0: 	EQU     0C026H   ;AUGENDO BASSO			SOMMA		32 BIT
CAUGE2: 	EQU     0C028H   ;AUGENDO ALTO			SOMMA		32 BIT
CSOMM0: 	EQU     0C02AH   ;SOMMA BASSA			SOMMA		32 BIT
CSOMM2: 	EQU     0C02CH   ;SOMMA ALTA			SOMMA		32 BIT
MREST0: 	EQU     0C02EH   ;RESTO BASSA			SOTTRAZIONE	32 BIT
MREST2: 	EQU     0C030H   ;RESTO ALTA			SOTTRAZIONE	32 BIT
MDO032: 	EQU     0C032H   ;MOLTIPLICANDO BASSO		MOLTIPLICAZIONE 32 BIT 
MDO232: 	EQU     0C034H   ;MOLTIPLICANDO ALTO		MOLTIPLICAZIONE 32 BIT 
MRE032: 	EQU     0C036H   ;MOLTIPLICATORE BASSO 		MOLTIPLICAZIONE 32 BIT 
MRE232: 	EQU     0C038H   ;MOLTIPLICATORE ALTO 		MOLTIPLICAZIONE 32 BIT
PTO032: 	EQU     0C03AH   ;PRODOTTO BASSO 		MOLTIPLICAZIONE 32 BIT 
PTO232: 	EQU     0C03CH   ;PRODOTTO ALTO  		MOLTIPLICAZIONE 32 BIT
EDEND0: 	EQU     0C03EH   ;DIVIDENDO BASSO		DIVISIONE 4096	32 BIT
EDEND2: 	EQU     0C040H   ;DIVIDENDO ALTO		DIVISIONE 4096	32 BIT
QUOZ32: 	EQU     0C042H   ;QUOZIENTE			DIVISIONE 4096	32 BIT
MINUE0: 	EQU     0C044H   ;MINUENDO BASSO		SOTTRAZIONE	32 BIT
MINUE2: 	EQU     0C046H   ;MINUENDO ALTO			SOTTRAZIONE	32 BIT
SUBTR0: 	EQU     0C048H   ;SOTTRAENDO BASSO		SOTTRAZIONE	32 BIT
SUBTR2: 	EQU     0C04AH   ;SOTTRAENDO ALTO		SOTTRAZIONE	32 BIT               
RESTO0: 	EQU     0C04CH   ;RESTO BASSO			SOTTRAZIONE	32 BIT
RESTO2: 	EQU     0C04EH   ;RESTO ALTO			SOTTRAZIONE	32 BIT

MIXUE0: 	EQU     0C050H   ;MINUENDO ALTERNATIVO BASSO	SOTTRAZIONE	32 BIT
MIXUE2: 	EQU     0C052H   ;MINUENDO ALTERNATIVO ALTO	SOTTRAZIONE	32 BIT
SUXTR0: 	EQU     0C054H   ;SOTTRAENDO ALTERNATIVO BASSO	SOTTRAZIONE	32 BIT
SUXTR2: 	EQU     0C056H   ;SOTTRAENDO ALTERNATIVO ALTO	SOTTRAZIONE	32 BIT

TINUE0: 	EQU     0C058H   ;MINUENDO BASSO		SOTTRAZIONE	32 BIT
TINUE2: 	EQU     0C05AH   ;MINUENDO ALTO			SOTTRAZIONE	32 BIT
TUBTR0: 	EQU     0C05CH   ;SOTTRAENDO BASSO		SOTTRAZIONE	32 BIT
TUBTR2: 	EQU     0C05EH   ;SOTTRAENDO ALTO 		SOTTRAZIONE	32 BIT       
TREST0: 	EQU     0C060H   ;RESTO BASSO 			SOTTRAZIONE	32 BIT
TREST2: 	EQU     0C062H   ;RESTO ALTO 			SOTTRAZIONE	32 BIT

TIXUE0: 	EQU     0C064H   ;MINUENDO ALTERNATIVO BASSO	SOTTRAZIONE	32 BIT
TIXUE2: 	EQU     0C066H   ;MINUENDO ALTERNATIVO ALTO	SOTTRAZIONE	32 BIT
TUXTR0: 	EQU     0C068H   ;SOTTRAENDO ALTERNATIVO BASSO	SOTTRAZIONE	32 BIT
TUXTR2: 	EQU     0C06AH   ;SOTTRAENDO ALTERNATIVO ALTO	SOTTRAZIONE	32 BIT

CINUE0: 	EQU     0C06CH   ;MINUENDO BASSO CSOTT32	SOTTRAZIONE	32 BIT
CINUE2: 	EQU     0C06EH   ;MINUENDO ALTO CSOTT32		SOTTRAZIONE	32 BIT
CUBTR0: 	EQU     0C070H   ;SOTTRAENDO BASSO CSOTT32	SOTTRAZIONE	32 BIT
CUBTR2: 	EQU     0C072H   ;SOTTRAENDO ALTO CSOTT32	SOTTRAZIONE	32 BIT            
CESTO0: 	EQU     0C074H   ;RESTO BASSO CSOTT32		SOTTRAZIONE	32 BIT
CESTO2: 	EQU     0C076H   ;RESTO BASSO CSOTT32		SOTTRAZIONE	32 BIT

MDIVIS: 	EQU     0C078H   ;TEMPORANEA			DIVISIONE 1000
COUNTER32:	EQU	0C07AH	 ;CONTIENE INDIRIZZO DI MEMORIA DEL CONTATORE DA CONTROLLARE
COMP16A:	EQU	0C07CH	 ;WORD A DA COMPARARE/RISULTATO	COMPARATORE	16 BIT
COMP16B:	EQU	0C07EH   ;WORD B DA COMPARARE		COMPARATORE	16 BIT
DIV4096:	EQU	0C080H	 ;DIVIDENDO			DIVISIONE PER 4096	 (ARRAY DI 3 BYTES)
DIV32_DIVID_L:	EQU	0C082H	 ;DIVIDENDO ALTO		DIVISIONE	32 BIT
DIV32_DIVID_H:	EQU	0C084H	 ;DIVIDENDO BASSO		DIVISIONE	32 BIT
DIV32_DIVIS_L:	EQU	0C086H	 ;DIVISORE ALTO			DIVISIONE	32 BIT
DIV32_DIVIS_H:	EQU	0C088H	 ;DIVISORE BASSO		DIVISIONE	32 BIT
DIV32_QUOZ_L:	EQU	0C08AH	 ;QUOZIENTE ALTO		DIVISIONE	32 BIT
DIV32_QUOZ_H:	EQU	0C08CH	 ;QUOZIENTE BASSO		DIVISIONE	32 BIT
;		EQU	0C084H


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	AREA DI MEMORIA VARIABILI D'INPUT/OUTPUT
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

MEMA:   	EQU     0C09AH   ;MEMORIA DELLA PORTA A  
MEMB:   	EQU     0C09BH   ;MEMORIA DELLA PORTA B  
MEMC:   	EQU     0C09CH   ;MEMORIA DELLA PORTA C
MEMD:   	EQU     0C09DH   ;MEMORIA DELLA PORTA C

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	AREA DI MEMORIA ARRAY TEMPORANEA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

ARR_VELO: 	EQU     0C0A0H   ;ARRAY VELOCITA' MEDIA		0C0A0H-0C0BFH
ARR_ERRAVA: 	EQU     0C100H   ;ARRAY ERRORE AVANTI		0C100H-0C15FH  
ARR_ERRRIT: 	EQU     0C160H   ;ARRAY ERRORE RITARDO		0C160H-0C1BFH
ARR_TRASM:	EQU	0C1C0H	 ;ARRAY BYTES TRASMISSIONE	C01C0H-C01CAH 
ARR_RICAUTO:   	EQU     0C1D0H   ;ARRAY RICERCA AUTOMATICA	C01D0H-C01DFH 
					;ARR_RICAUTO+00H - FLAG SE RICERCA AUTOMATICA ATTIVA (01H)
					;ARR_RICAUTO+02H - NUMERO CICLI EFFETTUATI IN RICERCA AUTOMATICA
					;ARR_RICAUTO+04H - FLAG ATTIVA RICERCA "SEGNO DOPPIO" (DA TOGLIERE)
					;ARR_RICAUTO+06H - FLAG NUMERO DI SEGNI PER DARE "SEGNO DOPPIO"
					;ARR_RICAUTO+08H - FLAG PER NON RIPETERE MESSAGGI (DA TOGLIERE)
					;ARR_RICAUTO+0AH - CONTATORE DI CICLI IN CUI SI E' RILEVATO "SEGNO DOPPIO"



ARR_TAB_IDX: 	EQU     0C200H   ;ARRAY TRASCODIFICA INDIRIZZI  0C200H-0C2FFH 

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	AREA DI MEMORIA VARIABILI TEMPORANEE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

RESET:		EQU	0C300H   ;SE ATTIVO FORZA RESET DELLA MACCHINA
VELDIS:		EQU	0C302H   ;ULTIMA VELOCITA' SPEDITA
VELPRE: 	EQU     0C304H   ;VELOCITA' PRECEDENTE
VELIMP: 	EQU     0C306H   ;IMPULSI ENCODER IN 4 mS 
VELIST: 	EQU     0C308H   ;VELOCITA' ISTANTANEA
VELMED: 	EQU     0C30AH   ;MEDIA DELLA VELOCITA'
TMSCOR: 	EQU     0C30CH   ;CORREZIONE TRASVERSALE
CONSAL: 	EQU     0C30EH   ;CONTEG. SALTI DI NON CORREZIONE LONG.
CONSAT: 	EQU     0C310H   ;CONTEG. SALTI DI NON CORCORREZIONE TRAV.
DECORR: 	EQU     0C312H   ;VALORE DI CORREZIONE A DECR.LONGITUD.
MDECOR: 	EQU     0C314H   ;EXTRA VALORE DI CORREZIONE LONGITUD.
CORRE:  	EQU     0C316H   ;VALORE DI CORREZIONE MOTORE
SUPCOR: 	EQU     0C318H   ;AVVISO SPOSTAMENTO > DI 1/2 GATE  
SHIFT3: 	EQU     0C31AH   ;QUANTE VOLTE AMPI40 STA IN SHIFT  
SHIFT4: 	EQU     0C31CH   ;RESTO DELLA DIVISIONE SHIFT AMPIMP 
TIRCIR_ATT: 	EQU     0C31EH   ;USATA NEL TIRO
DACVAL: 	EQU     0C320H   ;VALORE 0-4095 DA SPEDIRE AL DAC
ENCOD1: 	EQU     0C322H   ;USATA IN TEST ENCODER
ENCOD2: 	EQU     0C324H   ;USATA IN TEST ENCODER
ENCOD3: 	EQU     0C326H   ;CONTEGGIO TENTATIVI TEST 
ENCOD4: 	EQU     0C328H   ;CONTEGGIO IMPULSI FINALE  
TEST_ENC:	EQU	0C32AH   ;SEGNALAZIONE RICHIESTA TEST
TMEM4:  	EQU     0C32CH   ;ATTIVAZIONE CORREZIONE TIRO
TMEM5:  	EQU     0C32EH   ;USATA IN CORREZIONE TRASVERSALE
TMEM6:  	EQU     0C330H   ;SENSO CORREZIONE TIRO
CONALL: 	EQU     0C332H   ;CONTEGGIO AVVISI ALLARME CIRC.
MEMW:   	EQU     0C334H   ;USATA IN CALCOLO CORREZIONE MOTORE
MCALER: 	EQU     0C336H   ;SEGNALA AL MAIN DI CALCOLARE
MSCORR: 	EQU     0C338H   ;SENSO DELLA CORREZIONE
MSCORR_PREC: 	EQU     0C33AH   ;SENSO DELL'ERRORE PRECEDENTE
ALTDIV: 	EQU     0C33CH   ;ALTERNA I VALORI NELLA DIVISIONE
MEMLED:		EQU	0C33EH   ;LED PRECEDENTEMENTE SPEDITI
PROPRI: 	EQU     0C340H   ;AVVISO PROTEZIONE MANCANZA STAMPA 
PROVEL:		EQU	0C342H   ;AVVISO PROTEZIONE BASSA VELOCITA 
MATTES: 	EQU     0C344H   ;USATA IN DECREM.CILCI ATTESA
MFASE:   	EQU     0C346H   ;INIZIO GATE DELLA FASE S1
MFASES1: 	EQU     0C348H   ;FASE DI S1
MFASRIF: 	EQU     0C34AH   ;FASE DI RIFERIMENTO DI S1
INCREM: 	EQU     0C34CH   ;MEM.A DECR.INCREMENTA VALORE   
DECREM: 	EQU     0C34EH   ;MEM.A DECR.DECREMENTA VALORE
ERATTU: 	EQU     0C350H   ;ERRORE ATTUALE
ERPREC: 	EQU     0C352H   ;ERRORE PRECEDENTE          
ERMEDI: 	EQU     0C354H   ;MEMORIA ERRORE MEDIO LONG      
ERMEDI_DISP:	EQU	0C356H   ;MEMORIA ERRORE MEDIO LONGITUDINALE DA SPEDIRE
ERMEDI_SIGN:	EQU	0C358H   ;SEGNO ERRORE MEDIO LONG (1=POSITIVO,8=NEGATIVO)
SOMLO1: 	EQU     0C35AH   ;PRIMA SOMMA PER MEDIA LONG    
SOMLO2: 	EQU     0C35CH   ;SECONDA SOMMA PER MEDIA LONG  
PRIMOB: 	EQU     0C35EH   ;VALORE DA DARE AL PRIMO REG B IN MEDIALOMG   
SECONB: 	EQU     0C360H   ;VALORE DA DARE AL SECONDO REG B IN MEDIALOMG 
VALORX: 	EQU     0C362H   ;VALORE DA DARE AL REG IX IN MEDIALOMG
AUTOCENTRA:	EQU	0C364H   ;IMPOSTARE A 1 PER FORZARE LA CENTRATURA DEL GATE (FASE CON OSCILLOSCOPIO)
WATCHDOG_ST7_H: EQU	0C366H   ;CICLI (WORD ALTA) DI ATTESA IRQ DA ST7 PRIMA DI RESETTARE (USATO IN REQISFREE)
WATCHDOG_ST7_L: EQU	0C368H   ;CICLI (WORD BASSA) DI ATTESA IRQ DA ST7 PRIMA DI RESETTARE (USATO IN REQISFREE)
WATCHDOG_ST7_H_TMP: EQU	0C36AH   ;CICLI ATTUALI DEL CONTATORE (WORD ALTA) 
WATCHDOG_ST7_L_TMP: EQU	0C36CH   ;CICLI ATTUALI DEL CONTATORE (WORD BASSA)
CODICE_BIANCO:	EQU	0C36EH	 ;MINIMO SPAZIO BIANCO (IN IMPULSI DI ENCODER) PER RICERCA AUTOMATICA CODICE
CODICE_NERO:	EQU	0C370H	 ;MINIMO SPAZIO NERO (IN IMPULSI DI ENCODER) PER RICERCA AUTOMATICA CODICE
CODICE_ENC_N1:	EQU	0C372H	 ;VALORE ATTUALE CODICE N1 IN 4096 IMPULSI DI ENCODER
CODICE_ENC_B1:	EQU	0C374H   ;VALORE ATTUALE CODICE B1 IN 4096 IMPULSI DI ENCODER
CODICE_ENC_N2:	EQU	0C376H   ;VALORE ATTUALE CODICE N2 IN 4096 IMPULSI DI ENCODER
CODICE_ENC_B2:	EQU	0C378H   ;VALORE ATTUALE CODICE B2 IN 4096 IMPULSI DI ENCODER
DAC_INCREM:	EQU	0C37AH   ;VALORE DI INCREMENTO SU BASE DAC
DAC_DECREM:	EQU	0C37CH   ;VALORE DI DECREMENTO SU BASE DAC
DAC_INCREM_BASE:EQU	0C37EH   ;VALORE DI INCREMENTO SU BASE DAC
DAC_DECREM_BASE:EQU	0C380H   ;VALORE DI DECREMENTO SU BASE DAC
STATUS:		EQU	0C382H   ;CONTIENE LO STATUS DEGLI ALLARMI (2BYTE)
STATU2:		EQU	0C384H   ;CONTIENE LO STATUS INVIATO DEGLI ALLARMI (2BYTE)
				 ; 0 = SVILUPPO CILINDRO
				 ; 1 = AMPIEZZA GATE
				 ; 2 = SEGNO DI RIFERIMENTO
				 ; 3 = ERRORE GENERICO
				 ; 4 = MANCANZA STAMPA
				 ; 5 = VELOCITA' SOTTO SOGLIA
				 ; 6 = SUPERAMENTO SOGLIA D'ERRORE
				 ; 7 = ------ NON USATO  -----
				 ; 8 = TEST TESTINA FALLITO
				 ; 9 = TEST ENCODER FALLITO
				 ;10 = FASATURA FALLITA
				 ;11 = ------ NON USATO  -----
				 ;12 = ------ NON USATO  -----
				 ;13 = ------ NON USATO  -----
				 ;14 = ------ NON USATO  -----
				 ;15 = ------ NON USATO  -----
CORRATT:	EQU	0C386H   ;CONTIENE IL VALORE CON SEGNO DELLA CORREZIONE IN BASE ALL'ERRORE (4BYTE)
MSCORR_ATT: 	EQU     0C38AH   ;SENSO DELL'ERRORE ATTUALE
CORPROPTEN:	EQU	0C38CH   ;VALORE DI TENSIONE DI CORREZIONE PROPORZIONALE IN USCITA (CORAZZA)
CAMBIO_MANAUTO:	EQU	0C38EH	 ;VERIFICA IL PASSAGGIO TRA MAN-AUTO CON DAC AD USCITA PROPORZIONALE (CORAZZA)	
WATCH_LOLA:	EQU	0C390H	 ;MEM CONTATORE SU EVENTUALE BLOCCO INTERRUPT 

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	AREA DI MEMORIA NON VOLATILE D000-DFFFH
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

TAB_DUMMY:	EQU	0D000H	 ;AREA DI MEMORIA DUMMY USATA IN FAITAB

VERPRO:		EQU	0D000H	 ;VERSIONE PROGRAMMA 
;IDMACC:		EQU	0D002H	 ;IDENTIFICAZIONE MACCHINA 
COSVEL: 	EQU     0D004H	 ;COSTANTE USATA IN VELOCITA'
CENTES: 	EQU     0D006H	 ;CENTESIMI IN UN IMP.DI ENCODER
MILLES: 	EQU     0D008H	 ;MILLESIMI IN UN IMP.DI ENCODER
AMPI10: 	EQU     0D00AH	 ;AMPIMP x 40 USATA CON CKTG0 1024 
FASE:   	EQU     0D00CH	 ;INIZIO GATE DELLA FASE S1
FASES1: 	EQU     0D00EH	 ;FASE DI S1
FASRIF: 	EQU     0D010H	 ;FASE DI RIFERIMENTO DI S1
RAPPO1: 	EQU     0D012H	 ;RAPPORTO S1
RAPRIF: 	EQU     0D014H	 ;RAPPORTO DI RIFERIMMENTO
RIFTOT: 	EQU     0D016H	 ;(FASRIFx10)+RAPRIF=RIFTOT
POSTOT: 	EQU     0D018H	 ;(FASES1x10)+RAPRIF=RIFTOT
PIENO1: 	EQU     0D01AH	 ;VALORE DEL PIENO S1
POSIZ1: 	EQU     0D01CH	 ;VALORE DELLA POSIZIONE S1
MEMRIF: 	EQU     0D01EH	 ;RIFERIMENTI
UNOMIL: 	EQU     0D020H	 ;1mm IN IMPULSI ENCODER
AMPIMP: 	EQU     0D022H	 ;AMPIEZZA GATE IN IMPULSI ENCODER
MEZGA:  	EQU     0D024H	 ;MEZGA PER 4 MEZGA=AMPIE. 50% GATE  
CILDIV: 	EQU     0D026H	 ;CILINDRO DIVISO 10
CILIN: 		EQU     0D028H	 ;CILINDRO IN MM
CICL75: 	EQU     0D02AH	 ;75% DEL CICLO DI CORREZIONE 
CIDE75: 	EQU     0D02CH	 ;75% DEL CICLO DI CORREZIONE 
SEQSEG: 	EQU     0D02EH	 ;POS.SEGNO NELLA SEQUENZA 
SEGNO:  	EQU     0D030H	 ;IMP. DA 2048 IN UN SEGNO DA 2mm
SPAZIO: 	EQU     0D032H	 ;IMP. DA 2048 IN UNO SPAZIO DA 5mm
SPAZ40: 	EQU     0D034H	 ;IMP. DA 2048 IN SPAZIO DA 40mm
DISTAN: 	EQU     0D036H	 ;DISTANZA TEORICA TRA S1 E S2
MILL20: 	EQU     0D038H	 ;20mm IN IMPULSI ENCODER     
MILL40: 	EQU     0D03AH	 ;40mm IN IMPULSI ENCODER
GUADS1: 	EQU     0D03CH	 ;VALORE GUADAGNO SEGNALE S1
DMILLE: 	EQU     0D03EH	 ;DECIMILLESIMI IN UN IMP.DI ENCOD
DISTA0: 	EQU     0D040H	 ;DISTANZA BASSA IN DECIMILLESIMI
DISTA2: 	EQU     0D042H	 ;DISTANZA ALTA IN DECIMILLESIMI
TDIST0: 	EQU     0D044H	 ;DISTAN. BASSA IN DECIMILL.TRAV
TDIST2: 	EQU     0D046H	 ;DIATAN. ALTA  IN DECIMILL.TRAV
SHIFT:  	EQU     0D048H	 ;SPOSTAMENTO REGISTRO
AVARIT: 	EQU     0D04AH	 ;SENSO DELLO SPOSTAMENTO LONG
TRAOPE: 	EQU     0D04CH	 ;SENSO DELLO SPOSTAMENTO TRAV
DMEZGA: 	EQU     0D04EH	 ;40% GATE IN CENTESIMI
CENSEN: 	EQU     0D050H	 ;VALORE SEGNO CENTRATO NEL GATE
AMPMEZ: 	EQU     0D052H	 ;AMPIMP DIVISO 2
AMPI40: 	EQU     0D054H	 ;AMPIEZZA GATE DEL40%  IN CENTS  
AUMATI_OLD: 	EQU     0D056H	 ;VALORE DI AUMATI PRIMA DI ENTRARE IN F9
;CICLI_F9:	EQU     0D058H	 ;GIRI ATTESA PRIMA DI ANNULLARE F9
CENTE4: 	EQU     0D05AH	 ;CENTES PER 4 
TDISPR: 	EQU     0D05CH	 ;ERRORE DISPLAY TRAV.PRECEDENTE 
TERATT: 	EQU     0D05EH	 ;ERRORE ATTUALE TRAVERS 
IMPMIL: 	EQU     0D060H	 ;IMPULSI DA 2048 PER mm PIU UNO  
BIANCO:		EQU	0D062H	 ;MINIMO SPAZIO BIANCO PER RA 	
CILMOL:  	EQU     0D064H	 ;SVILUPPO CILINDRO IN DECIMI DI MM
ALLAR:  	EQU     0D066H	 ;VALORE ALLARME IN CENTESIMI
AMPGA:  	EQU     0D068H	 ;AMPIEZZA GATE IN mm  
GUADA:  	EQU     0D06AH	 ;GUAD.min 11 x CIL=2000
CICLO:  	EQU     0D06CH	 ;INTERVALLO DI  DI CORREZIONE LONGITUDINALE
DERIVA: 	EQU     0D06EH	 ;DERIVATIVO LONGITUDINALE
ZONA:   	EQU     0D070H	 ;ZONA MORTA LONGITUDINALE
INSER:  	EQU     0D072H	 ;VELOCITA' MINIMA 
SALTO:  	EQU     0D074H	 ;SALTO ANOMALIE
ZONT:   	EQU     0D076H	 ;ZONA MORTA TRAVERSALE
TRIP:   	EQU     0D078H	 ;SENSIBILITA' CAMBIO VELOCITA' 
GUATRA: 	EQU     0D07AH	 ;GUADAGNO TRAVERSALE
TCICLO: 	EQU     0D07CH	 ;INTERVALLO TRAVERSALE
MESSA:  	EQU     0D07EH	 ;SELEZIONE TIPO DI MESSA A REGISTRO   
REV_CORRECTION: EQU     0D080H	 ;CORREZIONE NORMALE O INVERSA
MM_MC:  	EQU     0D082H	 ;SELEZIONE MM/MC  
SEGRIF: 	EQU     0D084H	 ;SCELTA SEGNO DI RIFERIMENTO SSRR 
VCLONG: 	EQU     0D086H	 ;VELOCITA DI CORREZIONE LONGITUDINALE 
VCTRAV: 	EQU     0D088H	 ;VELOCITA DI CORREZIONE TRASVERSALE  
DIAGON: 	EQU     0D08AH	 ;POSIZIONE IPOTENUSA 
TIRGUA: 	EQU     0D08CH	 ;GUADAGNO DI TIRO
TIRCIC: 	EQU     0D08EH	 ;CICLO DI TIRO
PRESS:  	EQU     0D090H	 ;TIPO DI ROTATIVA
DACOFFSET:	EQU	0D092H	 ;VALORE OFFSET DAC
AUMALO:		EQU	0D094H	 ;MANUALE/AUTOMATICO LONGITUDINALE 
AUMATR:		EQU	0D096H	 ;MANUALE/AUTOMATICO TRASVERSALE
AUMATI: 	EQU     0D098H	 ;MANUALE/AUTOMATICO TIRO
ENB_TRASVERSALE:EQU     0D09AH	 ;ABILITA TRASVERSALE
ENB_TIRO: 	EQU     0D09CH	 ;ABILITA TIRO
ENB_COLDSEAL:	EQU	0D09EH	 ;ABILITA FUNZIONE REGISTRO COLD SEAL
ENB_DAC_LON:	EQU	0D0A0H	 ;ABILITA DAC ERRORE LONG 
ENB_DAC_TRA:	EQU	0D0A2H	 ;ABILITA DAC ERRORE TRAV 
ENB_DAC_TIR:	EQU	0D0A4H	 ;ABILITA DAC TIRO 
DACBASE1000:	EQU     0D0A6H	 ;VALORE 0-1000 DA USARE COME BASE PER IL DAC
DACBASE: 	EQU     0D0A8H	 ;VALORE 0-4095 DA USARE COME BASE PER IL DAC
DACDELTACOR:	EQU	0D0AAH	 ;VALORE DEL DELTA DA APPLICARE IN CORREZIONE
DACDELTATIR:	EQU	0D0ACH	 ;VALORE DEL DELTA DA APPLICARE IN CORREZIONE TIRO
DACMANGUA: 	EQU     0D0AEH	 ;GUADAGNO CORREZIONE MANUALE

LAVORO: 	EQU     0D0B0H	 ;NøDEL LAVORO PRESENTE
TWIN_APP_01:	EQU	0D0B2H	 ;PRIMA FUNZIONALITA' TWIN
TWIN_APP_02:	EQU	0D0B4H	 ;SECONDA FUNZIONALITA' TWIN
TWIN_CURRENTAPP:EQU	0D0B6H	 ;FUNZIONALITA' TWIN CORRENTE
LINGUA: 	EQU     0D0B8H	 ;MEMORIA DELLA LINGUA
ENCOD:		EQU	0D0BAH	 ;FUNZIONAMENTO CON ENCODER NPN O PNP (SI AUTOSETTA SU TEST ENCODER)
CURRENT_ENCODER:EQU	0D0BCH	 ;ENCODER CORRENTEMENTE SELEZIONATO
F4_DIMENSION:	EQU	0D0BEH	 ;MINIMO SPAZIO BIANCO (IN MILLIMETRI) PER RICERCA AUTOMATICA CODICE
ENB_SH_PN:	EQU	0D0C0H	 ;ABILITA MENU AMPLIFICATORE POSITIVO/NEGATIVO
SH_PN:		EQU	0D0C2H	 ;AMPLIFICATORE POSITIVO/NEGATIVO

DELAY_ALLARM:	EQU	0D0C4H	 ;RITARDO ALLARME (numero di cicli prima di attivazione)


TAP1:   	EQU     0DA20H   ;IND. INTERRUPT PIO1A
TAP2:   	EQU     0DA22H   ;IND. INTERRUPT PIO1B
TAB0:   	EQU     0DA30H   ;IND. INTERRUPT CTC CANALE 0
TAB1:   	EQU     0DA32H   ;IND. INTERRUPT CTC CANALE 1
TAB2:   	EQU     0DA34H   ;IND. INTERRUPT CTC CANALE 2
TAB3:   	EQU     0DA36H   ;IND. INTERRUPT CTC CANALE 3

ARR_TMP: 	EQU     0DB00H   ;INIZIO TABELLA MEM. LAVORI
TMP_CURSOR:  	EQU     0D990H   ;USATA NELLA CREAZIONE TABELLE TEST


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;			 BIT DI PC (USCITE)
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
B_PC_AVANTI:	EQU	0
B_PC_RITARDO:	EQU	1
B_PC_ALLARME:	EQU	3
B_PC_PROTEZIONE:EQU	4	;TODO:MA NON E' DA RIMUOVERE?
B_PC_FUNZ:	EQU	5
B_PC_DESTRA:	EQU	6
B_PC_SINISTRA:	EQU	7

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;			POSSIBILI STATI DI DIRECTION
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
D_AVANTI:	EQU	01H
D_RITARDO:	EQU	08H

D_TRAVERSA:	EQU	01H
D_OPERATORE:	EQU	08H

D_INC_TIRO:	EQU	01H
D_DEC_TIRO:	EQU	08H

D_DESTRA:	EQU	01H
D_SINISTRA:	EQU	08H


C_MEMRIF:	EQU	05H


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		INIZIALIZZAZIONE SISTEMA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
		ORG     0000H
		JP      VIA

		ORG     0008H
		JP      VIA

		ORG     0010H
		JP      VIA

		ORG     0018H
		JP      VIA

		ORG     0020H
		JP      VIA

		ORG     0028H
		JP      VIA

		ORG     0030H
		JP      VIA

		ORG     0038H
		JP      VIA


		ORG     100H

VIA:

		

RESET_PIO:            
		LD	HL,INIT_Z80
		LD	(0DFFFH),HL
		LD	(0DFFDH),HL
		LD      SP,0DFFDH       ;DEF.AREA STACK
		RETI

INIT_Z80:
		LD	HL,VIA
		LD	(0DFFFH),HL
		LD      SP,0DFFFH       ;DEF.AREA STACK

		IM      2               ;IMPOSTA MODO PIO = 2

                LD      A,0DAH          ;INDIRIZZO INIZIALE VETTORE DI INTERRUPT
		LD      I,A		;I INTERRUPT VECTOR BASE REGISTER, 8 BITS

                LD      IY,PIOA         ;INSERISCO FUNZIONE DI GESTIONE INTERRUPT
		LD      (TAP1),IY       ;NEL VETTORE DI INTERRUPT

	
		LD      A,20H           ;COMUNICO LA POSIZIONE DEL VETTORE DI INTERRUPT 
		OUT     (PA_CMD),A      ;DA CHIAMARE PER PIO A
					;V7 V6 V5 V4 V3 V2 V1 0			

		LD      A,11001111B     ;PORTA A MODO 3	(SEGUE IO E IRQ)
		OUT     (PA_CMD),A	;M1 M0 X X 1 1 1 1


                LD      A,00010001B     ;IO PORT (0,4 IN)
		OUT     (PA_CMD),A	;IO7 IO6 IO5 IO4 IO3 IO2 IO1 IO0 

		LD      A,10010111B     ;IRQ CONTROL (ABILITA E SEGUE MASK)
		OUT     (PA_CMD),A	;Enable	 AND/	High/	Mask	  0 1 1 1
					;  Int	 OR	Low	Follows

		LD      A,11101111B     ;IRQ MASK (BIT 4)
		OUT     (PA_CMD),A	;MB7 MB6 MB5 MB4 MB3 MB2 MB1 MB0



                LD      IY,PIOB         ;IND.ROUT.INT.
		LD      (TAP2),IY       ;CAR.TAB.INT.
		LD      A,22H           ;VET.INT.PIO B
		OUT     (PB_CMD),A      ;CONTROLLO PIO B
		LD      A,0CFH          ;PORTA B MODO 3
		OUT     (PB_CMD),A
                LD      A,1EH           ;0567 OUT 1234 IN 1E PB 5 MESSO OUT PER PROBLEMA BLOCCO KALE DIVENTA 169 PPUU
		OUT     (PB_CMD),A
		LD      A,97H           ;PAR.CONT.INTER.
		OUT     (PB_CMD),A
                LD      A,0FDH          ;P.MAS.BIT 0234567  ;0DDH TO USE RS232
		OUT     (PB_CMD),A      ;ALL'INTERUPT


                LD      IY,CTC0         ;IND.ROUT.CTC0.
		LD      (TAB0),IY       ;CAR.TAB.INT.CAN0
		LD      A,30H           ;VET.IND.CTC0
		OUT     (CTC0_CMD),A    ;VET.SEMPRE IN 0
                LD      IY,CTC1         ;IND.ROUT.CTC1
		LD      (TAB1),IY       ;CAR.TAB.INT.CAN1
		LD      A,32H           ;VET.IND.CTC1
		OUT     (CTC0_CMD),A
                LD      IY,CTC2         ;IND.ROUT.CTC2
		LD      (TAB2),IY       ;CAR.TAB.INT.CAN2
		LD      A,34H           ;VET.IND.CTC2
		OUT     (CTC0_CMD),A
                LD      IY,CTC3         ;IND.ROUT.CTC3
		LD      (TAB3),IY       ;CAR.TAB.INT.CAN3
		LD      A,36H           ;VET.IND.CTC3
		OUT     (CTC0_CMD),A    ;VET.CARICATO


		;DEFAULT PER PORTA A
		LD	A,(MEMA)	
		RES	1,A		;RESET RICHIESTA DI TRASMISSIONE	
		SET     7,A		;ABILITO SEMPRE LA GENERAZIONE DEL GATE
		LD	(MEMA),A
	 	OUT     (PA),A


		;RESET DELLA PORTA C
		LD      A,(MEMC)        ;RESETTO LE USCITE
		RES     B_PC_AVANTI,A             
		RES     B_PC_RITARDO,A             
		RES	B_PC_ALLARME,A
		RES     B_PC_FUNZ,A             
		RES     B_PC_DESTRA,A             
		RES     B_PC_SINISTRA,A             
		OUT     (PC),A
		LD      (MEMC),A
;PROVATO A MODIFICARE CON LA PARTE CHE SEGUE PERCHE IN MODALITA' TWIN  SALTUARIAMENTE IL RELE
;DI COMMUTAZIONE DALLA CONDIZIONE DI RIPOSO SI ECCITA PER UN  BREVISSIMO TEMPO 3/04/08
;		;RESET DELLA PORTA D
;		LD	A,0FFH		;RESETTO LE USCITE
;		SET	3,A		;SETTO IL CLOCK DEL DAC BASSO IN MODO CHE SIA PRONTO A RICEVERE IL DATO
;		RES	4,A		;SETTO IL DAC ALTO IN MODO CHE SIA PRONTO A RICEVERE IL DATO
;		OUT	(PD),A
;		LD	(MEMD),A

		;NUOVO ASSETTO DELLA PORTA D 3/04/08
		LD	A,(MEMD)		;RESETTO LE USCITE
		SET	3,A		;SETTO IL CLOCK DEL DAC BASSO IN MODO CHE SIA PRONTO A RICEVERE IL DATO
		RES	4,A		;SETTO IL DAC ALTO IN MODO CHE SIA PRONTO A RICEVERE IL DATO
		LD	(MEMD),A
		OUT	(PD),A
		
		LD	BC,(WATCH_LOLA)	;SE QUANDO PASSO DI QUI 
		LD	A,B		;PERCHE' WATCH_LOLA
		CP	00H		;E' ANDATA AZERO SALTO
		JR	NZ,PARDA_1	;L'AZZERAMENTO DELLE
		LD	A,C		;MEMORIE VOLATILI
		CP	00H
		JR	Z,PARDA_2
PARDA_1:	
                ;AZZERAMENTO DA 0C000H A 0CFFFH (MEMORIA VOLATILE)
		LD      A,00H
                LD      (TABCON),A      ;LOC. INIZIALE
                LD      HL,TABCON
                LD      DE,TABCON+01H
                LD      BC,0FFFH
		LDIR                    
PARDA_2
		LD	BC,0AFFH	;CARICO VALORE PER 
		LD	(WATCH_LOLA),BC ;CONTROLLO BLOCCO INTRRUPT

		LD      A,04H           ;PER NON AVERE IN
		LD      (MATTES),A      ;DECREMENTO FFH
		LD      (CONSAL),A

                LD      (CONSAT),A

		LD      A,(TIRCIC)      ;CARICO LE MEM.
		LD      (INCREM),A      ;A DECREMENTO
		LD      (DECREM),A      ;DEL MOTOPOTENZ.
                LD      (TIRCIR_ATT),A

	
		CALL	CAL_WATCHDOG_ST7	;CALCOLA TEMPO WATCHDOG
		CALL	FAITAB	
		CALL	AZZER_MSB
		CALL    DEFAUL
		CALL	FORCE_STATUS		;RESET STATUS ALLARMI
                CALL    VARERR
		CALL	CAL_CILINDRO
		CALL	FASE_COINCIDENZA	;INVIO FASE AI CONTATORI HW

		LD	A,(ENB_DAC_LON)		;VERIFICO SE SONO
		CP	02H			;CON DAC IN USCITA PROPORZIONALE
		JR	NZ,INIZIO_01		;SE LO SONO METTO A RIPOSO IL 
		LD	DE,800H			;MOTORE (CORAZZA)			
		LD	(DACVAL),DE
		CALL	DAC_TRASM
		JR	INIZIO_02
INIZIO_01:
		;RESET DAC A VALORE BASE
		CALL	DAC_INCR_DECR

INIZIO_02:
		;ABILITA INTERRUPT
		EI	

		;RICHIEDI TUTTI I PARAMETRI A ST7
		CALL	SEND_INIT_Z80


;çççççççççççççççççççççççççççççççççççççççççççççççççççç
                LD      IY,ARR_TMP
		LD      (TMP_CURSOR),IY
		
		LD      A,00H
                LD      (ARR_TMP),A      ;LOC. INIZIALE
                LD      HL,ARR_TMP
                LD      DE,ARR_TMP+01H
                LD      BC,00FFH
		LDIR

;ççççççççççççççççççççççççççççççççççççççççççççççççççççç
                

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		MAIN - PROGRAMMA PRINCIPALE DI CALCOLO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

MAIN:

		LD	A,(ENB_DAC_LON)	;VERIFICO SE VI E' STATO
		CP	02H		;UN PASSAGGIO TRA MAN-AUTO
		JR	NZ,MAIN_01	;CON DAC IN USCITA PROPORZIONALE 
		CALL	MANAUTO_STOP	;(CORAZZA)
MAIN_01:	

		LD	HL,(VELIMP)	;SE LA VELOCITA' E' ALTA SPEDISCO 
		LD      DE,0080H	;LO STATO DEI LED SOLO UNA
		SBC     HL,DE		;VOLTA AL GIRO DA QUI
		JR	C,MAIN_LOOP
		CALL	SEND_LED
		CALL	SEND_SPEED
		
		
MAIN_LOOP:		
		LD      A,(MCALER)      ;VEDE SE DEVE 
		CP      00H             ;CALCOLARE O NO
		JP      Z,MAIN_WAITING


MAIN_FASATURA_LONG:

		CALL	FASE_AUTO_LONG
		
		LD      A,00H		;SETTO FLAG PER EVITARE
		LD      (MCALER),A	;DI FARE I CALCOLI DUE VOLTE

;°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;	MODIFICA PRE ELEIMINARE PROBLEMI DI BLOCCO ALLA FINJE DI F6 DIVENTA 168
;°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;		LD	A,(PROPRI)	;SE NO PRINT
;		CP	01H		;SALTO I CALCOLI DELL'ERRORE
;		JP	Z,MAIN_SOGLIA_INSER
;°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°	

		CALL	ERR_CALC	;CALCOLO ERRORE ATTUALE

		LD	A,(AUMALO)	;SE SONO IN AUTOMATICO
		CP	0FFH		;VISUALIZZO ERRORE MEDIATO
		JR	Z,MAIN_ERR1

		LD	A,(MEMC)	;SE SONO IN MANUALE E' STO CORREGGENDO
		BIT	B_PC_AVANTI,A	;IN AVANTI VISUALIZZO ERRORE ISTANTANEO
		JR	NZ,MAIN_ERR2

		LD	A,(MEMC)	;SE SONO IN MANUALE E' STO CORREGGENDO
		BIT	B_PC_RITARDO,A	;IN RITARDO VISUALIZZO ERRORE ISTANTANEO
		JR	NZ,MAIN_ERR2

		JR	MAIN_ERR1	;ALTRIMENTI, SE SONO IN MANUALE 
					;E NON STO CORREGGENDO, VISUALIZZO ERRORE 
					;MEDIATO

MAIN_ERR1:
                CALL    LMEDIA		;CALCOLO MEDIA DELL'ERRORE
		LD	A,(ERMEDI_SIGN)
		LD	(MSCORR),A 
		CALL	SEND_ERR

		JR	MAIN_OVERCORRECTION
						
MAIN_ERR2:
		CALL	RESET_MEDIA	;EQUIVALE A PRENDERE IL VALORE ISTANTANEO
		CALL	SEND_ERR
		JR	MAIN_OVERCORRECTION

MAIN_OVERCORRECTION:

                CALL    OVERCO		; GESTIONE DELLA CORREZIONE SUPERIORE A MEZZO GATE 

MAIN_SOGLIA_INSER:
		CALL	CHKSOGLIN

		CALL	CHKALARM


MAIN_CORREZIONI:
		CALL    CORREZIONI


		CALL	CENTRO		;SE ESEGUITA FASE CON OSCILLOSCOPIO, SI AUTOCENTRA

MAIN_WAITING:

		CALL	WATCH_STWD

MAIN_TRASMISSIONE_DATI:
		LD	HL,(VELIMP)	;SE LA VELOCITA' E' LENTA, CONTINUO
		LD      DE,0080H	;A SPEDIRE LO STATO DEI LED
		SBC     HL,DE		
		JR	NC,MAIN_TRASMISSIONE_DATI2
		CALL	SEND_LED
		CALL	SEND_SPEED
MAIN_TRASMISSIONE_DATI2:
		
		CALL	SEND_STATUS
		

MAIN_CHK_ENCODER:
		LD	A,(TEST_ENC)	;SE FLAG TEST ENCODER E ABILITATO
		BIT	0,A		;ESEGUI TEST
		JR	Z,MAIN_CHK_ALARM
		CALL	ENC1

MAIN_CHK_ALARM:
		LD	A,(AUMALO)	
                CP	0FFH		;SE SONO IN MANUALE 
		JR	Z,MAIN_CHK_SPEED		
		CALL	ALARM_OFF	;DISATTIVO ALLARME

MAIN_CHK_SPEED:
		CALL	CHK_SPEED_ZERO

MAIN_CHK_RESET:
		
		LD	A,(RESET)	;SE FLAG RESET=FF ALLORA RIPARTO DA VIA
		CP	0FFH
		JP	Z,VIA

		CALL	CHK_IRQ_ENABLED
		CALL	CHK_CURRENT_ENCODER
		CALL	CHK_TWIN_APP
		CALL	CHK_SH_PN

MAIN_FINE:	


		;EI	;RESTORE INTERRUPT (IF REQUIRED)
		JP	MAIN



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ROUTINE DI CONTROLLO SE IRQ SONO ATTIVATI
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
CHK_IRQ_ENABLED:
		IN      A,(PA)		;SE BIT DI RICHIESTA DI ABILITAZIONE E' 1
                BIT     7,A		;SIGNIFICA CHE PER QUALCHE MOTIVO GLI IRQ SONO DISABILITATI
                JR      Z,CHK_IRQ_ENABLED_FINE
		EI 			;ABILITO INTERRUPT
CHK_IRQ_ENABLED_FINE:
		RET
				


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ROUTINE DI CONTROLLO SOGLIA INSERZIONE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CHKSOGLIN:
	        CALL    VELO
                LD      HL,(VELIST)  
		LD      DE,(INSER)      
		SBC     HL,DE            
		JR      C,SOGLIN_ERR

SOGLIN_OK:      CALL	SPEEDOK
		JR	CHKSOGLIN_FINE

SOGLIN_ERR:         
		CCF        
		LD      A,(DELAY_ALLARM);BLOCCO ALLARME
		LD      (CONALL),A	;CONTEGGIO AVVISI ALLARME CIRC
		LD      A,07H           ;SE INSER E' MAGGIORE BLOCCO LA CORREZIONE 
		LD      (MATTES),A      ;E AUMENTO I CICLI DI ATTESA.
		
		CALL    SPEED_LOW 
		JR      CHKSOGLIN_FINE

CHKSOGLIN_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ROUTINE DI CONTROLLO SOGLIA ALLARME 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CHKALARM:       
		IN      A,(PB)          
                BIT     3,A             ;SE MANCA IL SEGNO
                JR      NZ,ALLA1        ;SALTO

                LD      DE,(DMEZGA)     ;40% GATE IN CENTESIMI
                LD      HL,(ERMEDI)     ;QUANDO L'ERRORE
                SBC     HL,DE           ;E' MINORE 
                JR      NC,ALLA1        ;DEL 40% DEL
                CCF                     ;GATE E SONO

		CALL	F9_OFF		;DISATTIVO F9

ALLA1:          
		LD	A,(AUMALO)	;SE SONO IN MANUALE
		CP	00H		;DISATTIVO ALLARME
		JR      Z,NOALL
	
		LD	A,(PROVEL)	;SE BASSA VELOCITA 
		CP	01H		;DISATTIVO ALLARME
		JR	Z,NOALL

		LD      A,(ALLAR)       ;SE SOGLIA ALLARME (P2) = 0
		CP      00H             ;SALTA.
		JR      Z,NOALL  

		LD	A,(PROPRI)	;SE NO PRINT
		CP	01H		;ATTIVO ALLARME
		JR	Z,SIALL

		LD      HL,(ALLAR)      ;SE ERRORE > SOGLIA ALLARME (P2)
		LD      DE,(ERMEDI)	;CONTO 5 AVVISI E POI ATTIVO ALLARME
		SBC     HL,DE         
		JR      NC,NOALL   
		
		CCF
		LD      A,(CONALL)      ;SE HO CONTATO 5 CICLI DI AVVISO
		CP      00H             ;ATTIVO L'ALLARME
		JR      Z,SIALL    
		DEC     A
		LD      (CONALL),A
		JR      ALLA_FINE

SIALL:          
		CALL	ALARM_ON	;ATTIVO ALLARME
		JR	ALLA_FINE

NOALL:          
		CALL	ALARM_OFF	;DISATTIVO ALLARME
		LD      A,(DELAY_ALLARM);E CARICO 5 CICLI PRIMA 
		LD      (CONALL),A      ;DI DARE L'ALLARME
		JR	ALLA_FINE
ALLA_FINE:
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		TRASMISSIONE A COINCIDENZA DEL VALORE DI FASE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

FASE_COINCIDENZA:         

		LD      HL,(FASE)
		LD	(COMP16A),HL
		LD      HL,1000H	
		LD	(COMP16B),HL
		CALL	COMPARE16
		LD	A,(COMP16A)
		CP	02H		;FASE<4096 ?
		JP	Z,FASE_COINCIDENZA_INVIO 
		
		LD	HL,0000H	;METTO DI DEFAULT A 0
		LD      (FASE),HL


FASE_COINCIDENZA_INVIO:
		LD      HL,(FASE)
		LD      A,H
		OUT     (20H),A
		LD      A,L
		OUT     (10H),A
FASE_COINCIDENZA_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ROUTINE DI FASATURA AUTOMATICA LONGITUDINALE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

FASE_AUTO_LONG: 
		LD      A,(ARR_RICAUTO+00H)	;SE RICERCA E' GIA' IN CORSO, ESCO
		CP      01H
                JP      NZ,FASE_AUTO_LONG_FINE

		CALL	FASE_AUTO_INIZIA
		CALL    CODICE
		CALL	FASE_TROVATA		;SE ARRIVA QUI LA FASE E' STATA TROVATA
						;ALTRIMENTI CTC1 CHIAMA FASE_FALLITA E RIPARTE DA MAIN
FASE_AUTO_LONG_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ROUTINE DI INIZIO FASATURA AUTOMATICA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
FASE_AUTO_INIZIA:

		LD	BC,(FASE)		;BACKUP VALORI FASE PRECEDENTE
		LD	(MFASE),BC
		LD	BC,(FASES1)
		LD	(MFASES1),BC
		LD	BC,(FASRIF)
		LD	(MFASRIF),BC


		LD       A,00H			;IMPONGO MANUALE
		LD	(AUMALO),A	
		LD	(AUMATI),A	
		LD	(AUMATR),A	

		LD       A,00H			;RESET USCITE
                LD      (MEMC),A		
                OUT     (PC),A

		LD	A,00H
                LD      (SUPCOR),A		;ANNULLO VALORE DI ERRORE MAGGIORE DI MEZZO GATE


		LD      A,60H			;INIZIO CON GUADAGNO MINIMO
		LD      (GUADS1),A

		CALL	CAL_CODICE		;CALCOLO VALORI DI CODICE_BIANCO E CODICE_NERO

		LD      A,(MEMB)
		SET     6,A			;BLOCCO GATE
		LD      (MEMB),A
		OUT     (PB),A

		LD      A,0D7H			;IMPOSTO CTC1 COME CONTATORE DI CICLI
                OUT     (CTC1_CMD),A		
		LD      A,03H			;INTERRUPT OGNI 3 GIRI
		OUT     (CTC1_CMD),A

                LD      A,01H
		LD      (ARR_RICAUTO+06H),A
                LD	A,04H			;SE DOPO CIRCA 12 GIRI RICERCA FALLITA ESCI 
		LD      (ARR_RICAUTO+02H),A

FASE_AUTO_INIZIA_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE DI FASATURA AUTOMATICA ESEGUITA CORRETTAMENTE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

FASE_TROVATA:
		;MEMORIZZO LA NUOVA FASE
		LD	BC,(CODICE_ENC_B1)	
		LD      (FASE),BC  

		CALL    CENTRA  

		LD      A,(MEMC)		;TOLGO EVENTUALE ALLARME
		RES     B_PC_PROTEZIONE,A
		LD      (MEMC),A        
                OUT     (PC),A          
		
		CALL	FORCE_STATUS
		CALL	SEND_OK

		CALL	FASE_AUTO_TERMINA

FASE_TROVATA_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE DI FASATURA AUTOMATICA FALLITA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
FASE_FALLITA:
		CALL	FORCE_STATUS
		LD	DE,(S_FASATURA_FALLITA)
		CALL	SET_STATUS		;INVIO MESSAGGIO FASE FALLITA

		LD	BC,(MFASE)		;BACKUP VALORI FASE PRECEDENTE
		LD	(FASE),BC
		LD	BC,(MFASES1)
		LD	(FASES1),BC
		LD	BC,(MFASRIF)
		LD	(FASRIF),BC

		CALL	FASE_AUTO_TERMINA
FASE_FALLITA_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ROUTINE DI TERMINE FASATURA AUTOMATICA 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
FASE_AUTO_TERMINA:
		LD      A,(MEMB)		;SBLOCCO GATE
		RES     6,A             
		LD      (MEMB),A
		OUT     (PB),A

		LD      A,43H			;RESETTO CTC1 COME CONTATORE CICLI DI FASE
		OUT     (CTC1_CMD),A         

		LD      A,00H			;RESET USCITE DI RICERCA
		LD      (ARR_RICAUTO+00H),A
		LD      (ARR_RICAUTO+02H),A
                LD      (ARR_RICAUTO+0AH),A
                LD      A,02H			;NUMERO SEGNI PRIMA DI DOPPIO
                LD      (ARR_RICAUTO+06H),A
FASE_AUTO_TERMINA_FINE:
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CONTROLLO DELL'ERRORE ATTUALE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

ERR_CALC:
		;CALCOLO RAPPORTO POSIZINE PIENO S1

	        LD      DE,(ERATTU)     ;MEMORIZZO ERRORE
		LD      (ERPREC),DE     ;PRECEDENTE

                LD      A,(MSCORR)      ;
		LD      IX,PIENO1       ;DIVIDO PIENO
		LD      E,(IX+00H)      ;PER DIECI
		LD      D,(IX+01H)      ;E SOTTRAGGO
		LD      (DIVDEN),DE     ;IL RISULTATO
		LD      BC,000AH        ;A POSIZIONE
		LD      (DIVSOR),BC     ;SE IL RISULTATO
		CALL    DIVIS           ;E' NEGATIVO
		LD      IX,QUOZIE       ;VUOLE DIRE CHE
		LD      E,(IX+00H)      ;DIVIDENDO
		LD      D,(IX+01H)      ;POSIZIONE X 10
		LD      IY,POSIZ1       ;CON PIENO AVRO'
		LD      L,(IY+00H)      ;UN NUMERO MINORE
		LD      H,(IY+01H)      ;DI 0,1 PER CUI
		SBC     HL,DE           ;CARICO IN MEM.
		JR      C,RAPP10        ;RAPPORTO ZERO.
		LD      E,(IY+00H)      ;SE LA SOTTRZIONE
		LD      D,(IY+01H)      ;NON DA RISULTATO
		LD      (MOLTI),DE      ;NEGATIVO PROCEDO
		LD      A,0AH           ;NELLA DIVISIONE
		LD      (MOLPL),A       ;POSIZIONE X 10
		CALL    MOLT            ;DIVISO PIENO
		LD      IX,PROD         ;E OTTERRO'UN
		LD      C,(IX+00H)      ;RISULTATO 
		LD      B,(IX+01H)      ;COMPRESO 
		LD      (DIVDEN),BC     ;TRA 0,1 E 0,9
		LD      IY,PIENO1
		LD      E,(IY+00H)
		LD      D,(IY+01H)
		LD      (DIVSOR),DE
		CALL    DIVIS
                LD      DE,(QUOZIE)
		JR      RAPP11
RAPP10:         CCF
                LD      DE,0000H
RAPP11:         LD      (RAPPO1),DE     

		LD      A,(MEMRIF)	;VERIFICO SE E' STATO TROVATO IL SEGNO DI REGISTRO DURANTE LA RICERCA AUTOMATICA 
		CP      00H
                JR      Z,RAPP2

		;SE SONO SUBITO DOPO FASATURA, ANNULLO LA MEDIA
		LD	DE,0000H
	        LD      (ERATTU),DE
		CALL	RESET_MEDIA

		LD      DE,(RAPPO1)     ;PER MEMORIZZARE I RIFERIMENTI QUESTI VENGONO MEMORIZZATI DOPO 4 GIRI
                LD      (RAPRIF),DE
		LD      DE,(FASES1)
		LD      (FASRIF),DE

		LD      HL,MEMRIF	;DECREMENTO MEMRIF
		DEC     (HL)

                LD      A,(MEMRIF)      ;QUANDO ARRIVO A ZERO ESEGUO IL CALCOLO DEL RIFTOT
                CP      00H
                JR      NZ,RAPP2

                LD      DE,(FASRIF)
		LD      (MOLTI),DE
		LD      A,0AH
		LD      (MOLPL),A
		CALL    MOLT
		LD      DE,(PROD)
                LD      HL,(RAPRIF)
		ADD     HL,DE
                LD      (RIFTOT),HL

		;JP	ERR_CALC_FINE


		;CALCOLO DELL'ERRORE IN CENTESIMI ED
		;EVENTUALE CORREZIONE DA ESEGUIRE
                ;(FASRIF-FASES1)+(RAPRIF-RAPPO1)= ERRORE
		;(FASRIFx10)+RAPRIF=RIFTOT
                ;(FASES1x10)+RAPPO1=POSTOT
		;(RIFTOT-POSTOT):10=ERRORE IN DECIMI

RAPP2:          LD      DE,(FASES1)     ;FASES1 x 10
		LD      (MOLTI),DE
		LD      A,0AH
		LD      (MOLPL),A
		CALL    MOLT
		LD      DE,(PROD)
                LD      HL,(RAPPO1)
		ADD     HL,DE
		LD      (POSTOT),HL
		EX      DE,HL
		LD      HL,(RIFTOT)     ;FLO1 PUNTO A
		SBC     HL,DE
                JR      C,ERR1
		EX      DE,HL
                LD      HL,(AMPI10)     ;VEDO SE ERRORE 
                SBC     HL,DE           ;E'> DI AMPI10
		EX      DE,HL
                JR      NC,INDRE        ;FLO1 PUNTO B
		CCF
		LD      HL,(RIFTOT)     ;FLO1 PUNTO C
		LD      DE,(POSTOT)
		SBC     HL,DE
		EX      DE,HL
		LD      HL,0A000H       ;40960
		SBC     HL,DE
                JR      INNAS
ERR1:           CCF
		LD      HL,(POSTOT)     ;FLO1 PUNTO D
		LD      DE,(RIFTOT)
		SBC     HL,DE
		EX      DE,HL
                LD      HL,(AMPI10)     ;FLO1 PUNTO E  
		SBC     HL,DE
		EX      DE,HL
                JR      NC,INNAS
		CCF
		LD      HL,0A000H       ;40960
		LD      DE,(POSTOT)     ;FLO1 PUNTO F
		SBC     HL,DE
		LD      BC,(RIFTOT)
		ADD     HL,BC



INDRE:   
		;SE SONO ABILITATI I DAC SALTO IL REV_CORRECTION 162
		LD	A,(ENB_DAC_LON)
		CP	00H
		JR	Z,INDRE1
		LD	A,(ENB_DAC_TRA)
		CP	00H
		JR	Z,INDRE1
		LD	A,(ENB_DAC_TIR)
		CP	00H
		JR	Z,INDRE1

	        LD	A,(REV_CORRECTION);INSERITO INVERSIONE 
                BIT     0,A		  ;CORREZIONE E DIVENTA 162
                JR      Z,INNAS1       
INDRE1:
		LD	A,(MSCORR_ATT)
		LD	(MSCORR_PREC),A
		LD      A,D_RITARDO     ;SEGNALAZIONE
		LD      (MSCORR),A      ;CORREZ. RITARDO
		LD	(MSCORR_ATT),A
                JR      ERR2
INNAS:      
		;SE SONO ABILITATI I DAC SALTO IL REV_CORRECTION 162
		LD	A,(ENB_DAC_LON)
		CP	00H
		JR	Z,INNAS1
		LD	A,(ENB_DAC_TRA)
		CP	00H
		JR	Z,INNAS1
		LD	A,(ENB_DAC_TIR)
		CP	00H
		JR	Z,INNAS1

		LD	A,(REV_CORRECTION);INSERITO INVERSIONE
                BIT     0,A		;CORREZIONE E DIVENTA 162
                JR      Z,INDRE1       
INNAS1:
		LD	A,(MSCORR_ATT)
		LD	(MSCORR_PREC),A
		LD      A,D_AVANTI      ;SEGNALAZIONE
		LD      (MSCORR),A      ;CORREZ. AVANTI
		LD	(MSCORR_ATT),A

		;(ERR.xCENT.IN DIV.ENC.):10=ERR.IN CENTESIMI


ERR2:           LD      (MOLTI),HL      ;HL CONTIENE ERR.
		LD      A,(CENTES)
		LD      (MOLPL),A
		CALL    MOLT
		LD      DE,(PROD)
		LD      (DIVDEN),DE     ;PRIMA DIVISIONE
		LD      BC,000AH        ;PER DIECI
		LD      (DIVSOR),BC
		CALL    DIVIS
		LD      DE,(QUOZIE)
		LD      (ERATTU),DE
                

		LD      A,(SALTO)       ;SE SALTO E'
                CP      00H             ;ZERO CORREGGO
                JR      Z,ERR_CALC_FINE ;SEMPRE
                LD      HL,0064H        ;SOMMO A ERPREC   
		LD      BC,(ERPREC)     ;1mm IL RISULTATO
		ADD     HL,BC           ;LO SOTTRAGGO A
		SBC     HL,DE           ;ERATTU SE CARRY
                JR      NC,ERR4         ;SE VI E' STATA  
		CCF                     ;UNA LETTURA SBAL-
		LD      BC,(ERPREC)     ;LATA LA IGNORO.
		LD      (ERATTU),BC
                LD      A,(CONSAL)      ;CONTROLLA CHE NON
		DEC     A               ;CHE NON RIMANGA
		LD      (CONSAL),A      ;IN BLOCCO PER PIU'
		CP      00H             ;DI N GIRI MEMORIZZATI 
                JR      Z,ERR3          ;IN SALTO E POI
		JR	ERR_CALC_FINE   

ERR3:           LD      (ERATTU),DE     ;RICARICA L'ULTIMO
		LD      (ERPREC),DE     ;ERRORE LETTO
ERR4:                            
                LD      A,(SALTO)       ;CARICA I GIRI DI
		LD      (CONSAL),A      ;ATTESA.

ERR_CALC_FINE:           
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CONTROLLO CHE NON CI SIA UN'ALTRO
;		AVVISO RICHIESTA DI TRASMISSIONE IN CORSO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

REQISFREE:	
		LD	DE,(WATCHDOG_ST7_L)	;RESET WATCHDOG CON IL VALORE 
		LD	(WATCHDOG_ST7_L_TMP),DE	;PRECALCOLATO
		LD	DE,(WATCHDOG_ST7_H)
		LD	(WATCHDOG_ST7_H_TMP),DE


REQISFREE_LOOP:
		
		;CONTROLLO SE WATCHDOG E' ARRIVATO A 0
		LD	DE,WATCHDOG_ST7_H_TMP
		LD	(COUNTER32),DE
		CALL	CHK32

		LD	DE,(COUNTER32)
		LD	A,D
		AND	E
		CP	0FFH
		JP	Z,REQISFREE_TIMEOUT

		LD	DE,WATCHDOG_ST7_H_TMP
		LD	(COUNTER32),DE
		CALL	DEC32

		

		LD	A,(MEMA)		;CONTROLLO SE
		BIT	1,A			;SETTATO ESCI DAL LOOP
		JR	NZ,REQISFREE_LOOP	;ALTRIMENTI CICLA

		JR	REQISFREE_FINE

REQISFREE_TIMEOUT:

		JP	VIA			;RESET DELLO Z80

REQISFREE_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		AVVISO RICHIESTA DI TRASMISSIONE 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

REQUEST:			
		LD	A,(MEMA)	;SETTO
		SET	1,A		;OUTPA1
		LD	(MEMA),A
	 	OUT     (PA),A
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE INVIO CODICE DI OK
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SEND_OK:		
		LD	BC,0000H
		LD	(STATUS),BC
		CALL	FORCE_STATUS
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE PER FORZARE INVIO CODICE DI STATUS
;		ANCHE SE UGUALE AL PRECEDENTE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

FORCE_STATUS:
		LD	BC,0FFFFH
		LD	(STATU2),BC
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE INVIO CODICE DI STATUS
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SEND_STATUS:	
		EXX
		LD	A,(STATUS)		;INVIA SONO SE DIVERSO DA PRECEDENTE
		LD	BC,(STATU2)
		CP	C
		JR	NZ,SEND_STATUS_DIFFERNT
		LD	A,(STATUS+01H)
		CP	B
		JR	NZ,SEND_STATUS_DIFFERNT
		JR	SEND_STATUS_FINE

SEND_STATUS_DIFFERNT:
		CALL	REQISFREE
		LD	A,(ANSWER)		;MANDA OK A
		LD	(ARR_TRASM+00H),A		;CPU	
		LD	A,00H
		LD	(ARR_TRASM+01H),A
		LD      A,(STATUS+01)
		LD	(ARR_TRASM+02H),A
		LD      A,(STATUS)
		LD	(ARR_TRASM+03H),A
		CALL	REQUEST

		LD	HL,(S_PERMANENT_MASK)	;AND CON LA MASCHERA DI BIT 
		LD	A,(STATUS)		;PERMANENTI
		AND	H
		LD	(STATUS),A
		LD	A,(STATUS+01H)
		AND	L
		LD	(STATUS+01H),A

		LD	BC,(STATUS)		;SALVO IL NUOVO STATO 
		LD	(STATU2),BC		;NELLO STATO PRECEDENTE

		EXX
SEND_STATUS_FINE:
		RET	

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE SETTA UNO STATUS
;		IPOTIZZA IL VALORE SIA NEL REGISTRO DE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SET_STATUS:		
		LD	A,(STATUS)
		OR	D
		LD	(STATUS),A
		LD	A,(STATUS+01H)
		OR	E
		LD	(STATUS+01H),A
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE RESETTA UNO STATUS
;		IPOTIZZA IL VALORE SIA NEL REGISTRO DE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

RES_STATUS:		
		LD	A,D		;COMPLEMENTO DE
		CPL	
		LD	D,A
		LD	A,E
		CPL
		LD	E,A
		LD	A,(STATUS)
		AND	D
		LD	(STATUS),A
		LD	A,(STATUS+01H)
		AND	E
		LD	(STATUS+01H),A
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE RICHIESTA A ST7 DI TUTTI I PARAMETRI
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
SEND_INIT_Z80:
		CALL	REQISFREE

		LD	A,(INITZ80)		;INVIO RICHIESTA PARAMETRI
		LD	(ARR_TRASM+00H),A
		LD	A,00H
		LD	(ARR_TRASM+01H),A
		LD	(ARR_TRASM+02H),A
		LD	(ARR_TRASM+03H),A
		CALL	REQUEST

SEND_INIT_Z80_FINE:
		RET
	


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE INVIO VELOCITA' (SE RICHIESTO) A ST7
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SEND_SPEED:				
		LD	BC,(WATCH_LOLA)	;CONTROLLO CONTATORE 
		DEC	BC		;VERIFICA FUNZIONAMENTO
		LD	(WATCH_LOLA),BC	;INTERRUPT NEL CASO
		LD	A,B		;DI BLOCCO ESEGUE
		CP	00H		;IL RESET
		JR	NZ,MICIOPARDA
		LD	A,C
		CP	00H
		JP	Z,VIA
MICIOPARDA:		
		LD	HL,(VELMED)
		LD      DE,(VELDIS)     ;SE VELOCITA' E' LA STESSA
		SBC     HL,DE           ;NON AGGIORNO IL DISPLAY
                JR      Z,SEND_SPEED_FINE        
		JR	NC,SEND_SPEED_OK
		CCF

SEND_SPEED_OK:
		CALL	REQISFREE
		
		LD      DE,(VELMED)		;AGGIORNO LA VELOCITA'
                LD      (VELDIS),DE		;PRECEDENTE CON LA CORRENTE

		LD	A,(LEGGI)		;INVIO LA VELOCITA' CORRENTE
		LD	(ARR_TRASM+00H),A
		LD	A,(PARVEL)
		LD	(ARR_TRASM+01H),A
		LD	DE,(VELMED)	
		LD	HL,ARR_TRASM+02H
		LD	(HL),D
		INC	HL
		LD	(HL),E
		CALL	REQUEST

SEND_SPEED_FINE:
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE INVIO VALORE BASE DAC 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SEND_DAC_BASE:
		CALL	REQISFREE
		LD	A,(LEGGI)		;INVIO IL VALORE BASE DAC
		LD	(ARR_TRASM+00H),A
		LD	A,(BASEVALDAC)
		LD	(ARR_TRASM+01H),A
		LD	DE,(DACBASE1000)	
		LD	HL,ARR_TRASM+02H
		LD	(HL),D
		INC	HL
		LD	(HL),E
		CALL	REQUEST

SEND_DAC_BASE_FINE:
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE CONTROLLO VELOCITA'==0 
;		E SE RICHIESTO INVIO A ST7
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CHK_SPEED_ZERO:	;CONTROLLO INGRESSO PIN PER UN PO' DI TEMPO
		;SE NON RICEVO UN IMPULSO DELL'ENCODER 2024
		;SIGNIFICA CHE LA MACCHINA E' FERMA

		LD      BC,01FFFH
CHK_SPEED_ZERO1:
		DEC     BC
		LD      A,B
		OR      C
		JR      Z,SPEED_IS_ZERO 
		IN      A,(PB)			;QUANDO LA
		BIT     4,A			;MACCHINA GIRA
		JR      Z,CHK_SPEED_ZERO1		;ESEGUE IL CONTROLLO
CHK_SPEED_ZERO2:
		DEC     BC			;SE NO
		LD      A,B			;ASPETTA E
		OR      C			;METTE ZERO
		JR      Z,SPEED_IS_ZERO		;LA VELOCITA'
		IN      A,(PB)          
		BIT     4,A             
		JR      NZ,CHK_SPEED_ZERO2
		JR      CHK_SPEED_RIPARTI

SPEED_IS_ZERO:           
		LD	BC,0000H		;SE ARRIVO QUI
		lD	(VELIMP),BC
		LD	(VELIST),BC		;LA VELOCITA' = 0
		LD	(VELMED),BC		;SETTO LE VARIABILI ED INVIO
		LD	BC,0FFFFH
		LD	(VELDIS),BC

		LD	A,(ENB_DAC_TIR)		;SE IL DAC TIRO   (CORAZZA)	UGO
		CP	00H			;NON E' ABILITTATO SALTO	
		JR	NZ,CHK_SPEED_ZERO3
		LD      A,(MEMC)		;SE SONO IN MANUALE E
		BIT     B_PC_RITARDO,A		;STO USANDO I TASTI
		JR	NZ,CHK_SPEED_ZERO3	;AVANTI,RITARDO SALTO
		BIT     B_PC_AVANTI,A		;E NON FERMO 
		JR	NZ,CHK_SPEED_ZERO3

		LD	DE,800H					
		LD	(DACVAL),DE
		CALL	DAC_TRASM
CHK_SPEED_ZERO3:
		CALL	SPEED_LOW		;MANDO MESSAGGIO ALLARME

		LD      A,(AUMALO)		;SE NON E' IN MANUALE SALTO
	        CP	00H			
		JR      Z,CHK_SPEED_ZERO_FINE	

               CALL    RESEPC			;FACCIO IL RESET DELLE USCITE

CHK_SPEED_ZERO_FINE:
		RET

CHK_SPEED_RIPARTI:;QUESTA PARTE SERVE PER QUANDO SONO IN MANUALE
		  ;E MI SONO FERMATO. IN MODO CHE ALLA PARTENZA
		  ;VEGA RICARICATO IL VALORE DAC DI BASE
		LD	DE,(DACVAL)	
		LD	HL,800H
		SBC	HL,DE			
		JR	Z,CHK_SPEED_ZERO5      ;SE MI SONO FERMATO AGGIORNO
		JR	C,CHK_SPEED_ZERO4      ;NELLE ALTRE CONDIZIONI ESCO	
		JR	NC,CHK_SPEED_ZERO6
CHK_SPEED_ZERO4:
		CCF
		JR	CHK_SPEED_ZERO6
CHK_SPEED_ZERO5:
		LD	DE,(DACBASE)		;E AGGIORNO DAC
		LD	(DACVAL),DE
		CALL	DAC_TRASM
CHK_SPEED_ZERO6:
		RET
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		GESTIONE COMUNICAZIONE CON ST7
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&


PIOA:           PUSH    BC
		PUSH    DE
		PUSH    HL
		PUSH    AF
		PUSH    IX
		PUSH    IY

		LD	BC,0AFFH		;CARICO VALORE PER 
		LD	(WATCH_LOLA),BC		;CONTROLLO BLOCCO INTRRUPT

		LD	A,(MEMA)		;RESETTO   TODO: Da spostare
		RES	1,A			;OUTPA1
		LD	(MEMA),A
	 	OUT     (PA),A
               
		LD      IX,ARR_TRASM+00H		;LOC BYTE
		LD	IY,ARR_TRASM+04H
 		CALL	TRASM

		CALL	ANALI1
		
		LD      IX,ARR_TRASM+02H		;LOC BYTE
		LD	IY,ARR_TRASM+06H
 		CALL	TRASM

		CALL	ANALI2		

		POP     IY			;INIZIO COSI'
		POP     IX			;AGGIORNA IL
		POP     AF			;TUTTO.
		POP     HL	
		POP     DE
		POP     BC
	        EI
		RETI


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		GESTIONE GATE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

PIOB:		;VIENE GENERATO DALL'IMPULSO DI COINCIDENZA
		;CARICO IL VALORE DELL'AMPIEZZA DEL GATE		
		PUSH    BC              
		PUSH    DE
		PUSH    HL
		PUSH    AF
		PUSH    IX
		PUSH    IY

		LD	BC,0AFFH	        ;CARICO VALORE PER 
		LD	(WATCH_LOLA),BC         ;CONTROLLO BLOCCO INTRRUPT

;ELIMINATO. PB5 DIVENTATO OUT PER PROBLEMA BLOCOO KALE DIVENTA 169	
;                IN      A,(PB)                 ;PPUU 
;                BIT     5,A		        ;ELIMINATO PB5 DIVENTATO OUT	
;                JR      Z,PIOB4                ;PER PROBLEMA BLOCOO KALE

                LD      A,(ARR_RICAUTO+06H)	;VIENE DECR.IN    PPDD
                CP      00H			;MODO CHE IL
                JR      Z,PIOB3			;TEST DEL DOPPIO
                DEC     A			;PIO VENGA LETTO
                LD      (ARR_RICAUTO+06H),A	;UNA VOLTA       PPDD


PIOB3:          LD      A,(ENCOD2)     ;PPDD
                CP      02H
                JR      Z,PIOB_FINE         

		LD      A,0D7H                  ;SETTO IN CTC0 AMPIEZZA GATE IN IMPULSI DI ENC
		OUT     (CTC0_CMD),A         
		LD      A,(AMPIMP)      
		OUT     (CTC0_CMD),A         


		CALL	SET_GUAD_S1


                LD      A,(ARR_RICAUTO+06H)	;VEDO SE DEVO
                CP      00H			;CONTROLLARE IL
                JR      Z,PIOB_FINE		;DOPPIO SEGNO
                LD      A,0D7H			;METTO 2 PER-
                OUT     (73H),A			;CHE NON SENTE
                LD      A,02H			;LA COMMUT.
                OUT     (73H),A			;DEL TRAVERS
                JR      PIOB_FINE	
		
;PIOB4:      ;ELIMINATO. PB5 DIVENTATO OUT PER PROBLEMA BLOCOO KALE DIVENTA 169	    
;               IN      A,(10H)		        ;LEGGO LA POSIZIONE
;		LD      E,A		        ;(PULSANTE FASE MANUALE)
;		IN      A,(20H)
;		LD      D,A				
;		LD      (FASE),DE		
;               CALL    CENTRA        

PIOB_FINE:          

		POP     IY
		POP     IX
		POP     AF
		POP     HL
		POP     DE
		POP     BC
		EI
		RETI



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		VIENE SCATENATO ALLA FINE DEL GATE		
;		VERIFICA POSIZIONE S1
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&


CTC0:           PUSH    BC              
		PUSH    DE
		PUSH    HL
		PUSH    AF
		PUSH    IX
		PUSH    IY

		LD	BC,0AFFH	        ;CARICO VALORE PER 
		LD	(WATCH_LOLA),BC         ;CONTROLLO BLOCCO INTRRUPT

                LD      A,01H			;SEGNALA AL MAIN CHE HA LETTO
		LD      (MCALER),A		;IN MODO CHE FACCIA CALCOLI UNA SOLA VOLTA PER GIRO

		LD	A,(ARR_RICAUTO+00H)	;CONTROLLO SE SONO IN FASE AUTOMATICA
		CP	01H
		JP	Z,CTC0_FINE		;SE FASE SALTO TUTTI CONTROLLI SUCESSIVI

                LD      A,(ENCOD1)		;SE NON STO FACENDO IL TEST DELL'ENCODER   
                CP      00H			;SALTA   
                JR      Z,CTC01  
		
                CP      01H		;SE STO FACENDO IL TEST DELL'ENCODER
                JP      Z,CTC0_FINE
                LD      A,(ENCOD2)     ;INCREMENTO
                INC     A              ;A OGNI 255
                LD      (ENCOD2),A     ;IMPULSI
                LD      A,0DFH         ;RICARICO CTC0 
		OUT     (CTC0_CMD),A
                LD      A,0FFH
                OUT     (CTC0_CMD),A
                JP      CTC0_FINE          

CTC01:          
		IN      A,(30H)         ;LEGGO IL VALORE
		LD      E,A             ;DI PIENO
		IN      A,(40H)
		LD      D,A
                LD      (PIENO1),DE
		IN      A,(50H)         ;LEGGO IL VALORE
		LD      E,A             ;DELLA POSIZIONE
		IN      A,(60H)         ;DI S1
		LD      D,A
                LD      (POSIZ1),DE
		IN      A,(61H)         ;LEGGO IL VALORE
		LD      E,A             ;DI ENCODER
		IN      A,(62H)         ;ALL'ARRIVO DI S1
		LD      D,A
                LD      (FASES1),DE


		CALL	CHKPRINT	;CONTROLLA SE C'E' SEGNO E/O SETTA VARIABILE ALLARME RELATIVO

		OUT     (52H),A         ;IMPULSO A ZERO SU MONOSTABILE DA 0,5 mS 
					;PER RESETTARE TUTTI I FLIP-FLOP

		LD      A,43H		;RESETTO IL CTC 0
		OUT     (CTC0_CMD),A


		LD      HL,(FASE)       ;CARICO IL 
		LD      A,H             ;CONTATORE PER
		OUT     (20H),A         ;LEGGERE POI S1
		LD      A,L
		OUT     (10H),A         
		LD      A,43H
		OUT     (CTC0_CMD),A
CTC03:
		CALL	SPEED_CALC		;AGGIORNA VELOCITA' IMPULSIVA
		CALL	CHK_CILINDRO_GATE	;CONTROLLO DEI PARAMETRI GATE E CILINDRO  ;TODO: PROBABILMENTE E' INUTILE

CTC0_FINE:		
	
		LD	A,(MEMA)	
		SET     7,A		;ABILITO SEMPRE LA GENERAZIONE DEL GATE
		LD	(MEMA),A
	 	OUT     (PA),A

		POP     IY
		POP     IX
		POP     AF
		POP     HL
		POP     DE
		POP     BC
		EI
		RETI


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		INTERRUPT DI GESTIONE TIMER DI CORREZIONE
;		E IN FASE AUTOMATICA CONTATORE DI NUMERO DI GIRI EFFETTUATI
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&


CTC1:
		EX      (SP),HL		;SCRIVO IN HL IL VALORE CORRENTE DELLO STACK POINTER
		PUSH    BC
		PUSH    DE
		PUSH    AF
		PUSH    IX
		PUSH    IY

		LD	BC,0AFFH	;CARICO VALORE PER 
		LD	(WATCH_LOLA),BC ;CONTROLLO BLOCCO INTRRUPT


		LD      A,(ENCOD1)      ;VEDO SE SONO IN TEST ENCODER
		CP      00H
                JR      Z,CTC16
		CP      01H
                JR      Z,CTC17
		CP      02H
                JR      Z,CTC18


CTC17:          LD      A,02H           ;STACK POINTER = PARTENZA TEST ENCODER
		LD      (ENCOD1),A
                LD      HL,ENC2
                JP      CTC1_FINE           

CTC18:          LD      HL,ENC6         ;STACK POINTER = FINE TEST ENCODER            
                JP      CTC1_FINE
		



CTC16:          LD      A,(ARR_RICAUTO+00H)	;CONTROLLA SE SONO IN RICERCA AUTOMATICA
		CP      01H
		JR      Z,CTC12
	
CTC10:          
		;NON SONO NE IN RICERCA AUTOMATICA NE IN TEST ENCODER
		;SPENGO IL CTC1 COME TIMER
		LD      A,43H
		OUT     (CTC1_CMD),A	
		JR	CTC1_FINE		


CTC12:          
		;SONO IN RICERCA AUTOMATICA
		CALL    GAINS1     
		CALL	SET_GUAD_S1


                LD      A,(ARR_RICAUTO+02H)	;CONTROLLO SE ESAURITI I CICLI DI FASATURA AUTOMATICA
		DEC     A
		LD      (ARR_RICAUTO+02H),A
		CP      00H
		JR      NZ,CTC1_FINE		;SE LO SONO INVIO MESSAGGIO DI FASE FALLITA

		CALL	FASE_FALLITA

		LD      HL,MAIN			;STACK POINTER = MAIN
		JR	CTC1_FINE

CTC1_FINE:          
		POP     IY              
		POP     IX              
		POP     AF
		POP     DE      
		POP     BC
		EX      (SP),HL
	        EI              
		RETI




;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		INTERRUPT DI GESTIONE TIMER DI CORREZIONE
;		IN CASCATA A CTC1
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CTC2:           PUSH    BC
		PUSH    DE
		PUSH    HL
		PUSH    AF
		PUSH    IX
		PUSH    IY

		LD	BC,0AFFH	;CARICO VALORE PER 
		LD	(WATCH_LOLA),BC ;CONTROLLO BLOCCO INTRRUPT

                LD      A,(DECORR)      ;SE EXTRA 
                CP      00H             ;CORREZIONE 
                JR      NZ,CTC21        ;ATTIVA SALTO.
CTC23:          LD      A,(MEMC)        ;FINE DELLA
		RES     B_PC_AVANTI,A   ;CORREZIONE          
		RES     B_PC_RITARDO,A             
		LD      (MEMC),A
		OUT     (PC),A
		LD	A,(ENB_DAC_LON)		;SE NON SONO IN DAC 
		CP	02H			;USCITA PROPORZIONALE
		JR	Z,CTC24			;ALL'ERRORE  SALTO (CORAZZA)

		;RESET DAC AL VALORE BASE

		CALL	DAC_INCR_DECR
		JR	CTC25
CTC24:
		LD	DE,800H		   ;SE USO GLI INTERVALLI(CORAZZA)			
		LD	(DACVAL),DE	   ;DI CORREZIONE DEVO FERMARE IL  		
		CALL	DAC_TRASM	   ;MOTORE ALLA FINE DEL CICLO 	(CORAZZA)
CTC25:
		LD      A,43H           ;RESETTO
		OUT     (CTC1_CMD),A    ;CTC1
		OUT     (CTC2_CMD),A    ;CTC2
                JR      CTC22           
CTC21:          LD      A,37H           ;CONTROLLO CTC1
		OUT     (CTC1_CMD),A         
                LD      A,40H           ;COSTANTE DI
		OUT     (CTC1_CMD),A    ;TEMPO
                LD      A,0D7H          ;CONTROLLO CTC2
                OUT     (CTC2_CMD),A    ;CARICO PER 
                LD      A,0FFH          ;EXTRA CORREZ.
		OUT     (CTC2_CMD),A

                LD      A,(DECORR)      ;SE E' MAGGIORE 
                DEC     A               ;DI UNO LA CORREZ-
                LD      (DECORR),A      ;ZIONE DIVENTA
                CP      00H             ;CONTINUA
                JR      NZ,CTC22

                LD      A,(CIDE75)      ;SE FATTO 75% 
                CP      00H             ;DEL CICLO
                JR      Z,CTC23         ;BLOCCO CORREZ.



CTC22:          POP     IY
		POP     IX
		POP     AF
		POP     HL
		POP     DE      
		POP     BC
		EI
		RETI


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		INTERRUPT DI GESTIONE TIMER DI CORREZIONE LATERALE
;		E CONTEGGI SEGNI IN FASE AUTOMATICA (DOPPIO SEGNO)
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CTC3:           PUSH    BC
		PUSH    DE
		PUSH    HL
		PUSH    AF
		PUSH    IX
                PUSH    IY

		LD	BC,0AFFH	;CARICO VALORE PER 
		LD	(WATCH_LOLA),BC ;CONTROLLO BLOCCO INTRRUPT

                LD      A,(ARR_RICAUTO+06H)	;SE NON SONO IN  
		CP      00H			;F4 SALTO
                JR      Z,CTC32			;

                LD      A,(ARR_RICAUTO+0AH)	;PRIMA DI 
                INC     A			;SCRIVERE 
                LD      (ARR_RICAUTO+0AH),A	;DOPPIO ASPET-
                CP      04H			;4 RIPETIZIONI
                JR      Z,CTC31
                LD      A,02H
                LD      (ARR_RICAUTO+06H),A
                JP      CTC33		

CTC31:
		CALL	DOPPIO

                LD      A,00H
                ;LD      (ARR_RICAUTO+04H),A
                LD      (ARR_RICAUTO+06H),A
                LD      (ARR_RICAUTO+0AH),A

                LD      A,43H
                OUT     (73H),A
                JR      CTC33


CTC32:          LD      A,(TMEM4)       ;SE ATTIVATA
		CP      01H             ;CORREZIONE
                JP      NZ,CTC33        ;SALTA
	        LD      BC,(TMEM5)      ;CONTIENE IL
                DEC     BC              ;VALORE 
                LD      (TMEM5),BC      ;DELL'ERRORE
                LD      A,B
                OR      C
                JR      NZ,CTC33        
	        LD      A,(MEMC)        ;FINITO IL DE-  
                RES     B_PC_DESTRA,A   ;CREMENTO DELLO
                RES     B_PC_SINISTRA,A ;ERRORE TOGLI
                LD      (MEMC),A        ;LE CORREZIONI.
		OUT     (PC),A

		LD      A,43H
		OUT     (73H),A
		LD      A,00H
		LD      (TMEM4),A
CTC33:          POP     IY
		POP     IX
		POP     AF
		POP     HL
		POP     DE
		POP     BC
		EI
		RETI


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CONTROLLO SE L'ERRORE E' CAMBIATO
;		E IN TAL CASO LO SPEDISCO A ST7
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SEND_ERR:	
		LD      HL,(ERMEDI)		;SE NON C'E'STATA VARIAZIONE
                LD      DE,(ERMEDI_DISP)	;D'ERRORE NON AGGIORNO ST7
                SBC     HL,DE
                JR      Z,SEND_ERR_FINE

                JR      NC,SEND_ERR1		;RESET CARRY SE NECESSARIO
                CCF
SEND_ERR1:           
		LD      HL,(ERMEDI)		;ERMEDI_DISP = ERMEDI
		LD      (ERMEDI_DISP),HL

;		;INVIO ERRORE A DAC
;		LD	BC,0800H
;		LD	HL,(ERMEDI)
;		ADD	HL,BC	
;
;		LD	(DACVAL),HL
;		CALL	DAC_TRASM	


		;INVIO ERRORE 
		LD	A,(MSCORR)		;IN BASE ALLA DIREZIONE
		CP	D_RITARDO		;SCELGO LA DIREZIONE 
		JR	Z,SEND_ERR_A		;CUI SPEDIRE
		JR	SEND_ERR_R
		
		
SEND_ERR_R:	;INVIO ERRORE RITARDO
		CALL	REQISFREE
		LD	A,(LEGGI)
		LD	(ARR_TRASM+00H),A
		LD	A,(LONRIT)
		LD	(ARR_TRASM+01H),A
		LD	DE,(ERMEDI)
		LD	HL,ARR_TRASM+02H
		LD	(HL),D
		INC	HL
		LD	(HL),E
		CALL	REQUEST
		JR	SEND_ERR_FINE

SEND_ERR_A:	;INVIO ERRORE AVANTI
		CALL	REQISFREE
		LD	A,(LEGGI)
		LD	(ARR_TRASM+00H),A
		LD	A,(LONAVA)
		LD	(ARR_TRASM+01H),A
		LD	DE,(ERMEDI)
		LD	HL,ARR_TRASM+02H
		LD	(HL),D
		INC	HL
		LD	(HL),E
		CALL	REQUEST
		JR	SEND_ERR_FINE

SEND_ERR_FINE:

		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;			CALCOLO CENTRATURA GATE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CENTRA:         EXX
		LD      HL,(FASE)       ;SOTTRAGGO IL VALORE DI 1/2 GATE 
                LD      BC,(MEZGA)
		SBC     HL,BC           ;SE IL RISULTATO E'POSITIVO LO MEMORIZZO IN IN FASE.
		JR      C,RISNEG
		JR      TRFASE
RISNEG:         CCF                     
		LD      HL,1000H        ;SE E'NEGATIVO SOTTRAGGO DA 4096 IL 1/2 GATE E IL RESTO LO SOMMO AL VALORE LETTO.
		SBC     HL,BC
		LD      BC,(FASE)
		ADD     HL,BC
TRFASE:         
		LD      (FASE),HL       

		CALL	FASE_COINCIDENZA

;		LD      A,H
;		OUT     (20H),A
;		LD      A,L
;		OUT     (10H),A

		LD      A,C_MEMRIF           ;4 GIRI ATTESA PER MEMORIZZARE IL RIFERIMENTO
		LD      (MEMRIF),A
		EXX               
		RET
		
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CENTRATURA DOPO FASE CON OSCILLOSCOPIO ESTERNO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		


CENTRO:         
		EXX                     

		LD	A,(AUTOCENTRA)
		CP	00H		;SE AUTOCENTRA = 0 (MESSO SU CAMBIO FASE CON OSCILLOSCOPIO)
		JR	Z,CENTRO_FINE	;ESCI

		CP	01H		;SE AUTOCENTRA = 1 (MESSO SU CAMBIO FASE CON OSCILLOSCOPIO)
		JR	Z,CENTRO_EXECUTE;ESEGUO CENTRATURA GATE

		JR	CENTRO_NEXT	;ALTRIMENTI DECREMENTA AUTOCENTRA

CENTRO_NEXT:
		LD	A,(AUTOCENTRA)
		DEC	A
		LD	(AUTOCENTRA),A
		JR	CENTRO_FINE
		

CENTRO_EXECUTE:
		LD	A,00H		;RESETTO AUTOCENTRA
		LD	(AUTOCENTRA),A

                LD      HL,(FASES1)     ;SOTTRAGGO IL VALORE DI 1/2 GATE DAL VALORE LETTO
		LD      BC,(MEZGA)      
                SBC     HL,BC           ;SE IL RISULTATO E'POSITIVO LO MEMORIZZO IN FASE
		JR      C,CENTRO_02
		JR      CENTRO_03
CENTRO_02:	CCF
		LD      HL,1000H        ;SE E'NEGATIVO
		SBC     HL,BC           ;SOTTRAGGO DA 4096
                LD      BC,(FASES1)     ;IL 1/2 GATE E IL
		ADD     HL,BC           ;RESTO LO SOMMO
CENTRO_03:      LD      (FASE),HL       ;AL VALORE LETTO.
		LD      A,H
		OUT     (20H),A
		LD      A,L
		OUT     (10H),A
		LD      A,C_MEMRIF      ;4 GIRI ATTESA
		LD      (MEMRIF),A      ;PER MEMORIZZARE
					;IL RIFERIMENTO

CENTRO_FINE:	
		EXX                     
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CALCOLO DELLO SPOSTAMENTO REGISTRO E GATE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SPOSTA:         EXX
                LD      BC,(SHIFT)      ;SHIFT x 10 = A
		LD      (MOLTI),BC
                LD      A,0AH
		LD      (MOLPL),A
		CALL    MOLT
		LD      DE,(PROD)       ;FLO2 = A
		LD      (DIVDEN),DE     ;A : CENTES = B
		LD      BC,(CENTES)
		LD      (DIVSOR),BC
		CALL    DIVIS
		LD      DE,(QUOZIE)     ;FLO2 = B


		LD      A,(AVARIT)	
		LD	B,A		
		LD	A,(DIRAVA)	
		CP      B		
		JR      Z,SPOMEN

		LD      HL,(RIFTOT)
		ADD     HL,DE           ;RIFTOT + B = C
		EX      DE,HL           ;SE C < 40960 
		LD      HL,0A000H       ;ALLORA C = Z
		SBC     HL,DE           ;SE C > 40960
		EX      DE,HL           ;SI FARA'
		JR      NC,SPOFIN       ;C - 40960 = Z
		CCF
		LD      DE,0A000H
		SBC     HL,DE
		JR      SPOFIN
SPOMEN:         LD      HL,(RIFTOT)     ;RIFTOT - B = C
		SBC     HL,DE           ;SE C = POSITIVO
		JR      NC,SPOFIN       ;C = Z
		CCF                     ;SE C = NEGATIVO
		LD      HL,0A000H       ;SI FARA'
		SBC     HL,DE           ;40960 - B = D
		LD      DE,(RIFTOT)     ;D + RIFTOT = Z
		ADD     HL,DE
SPOFIN:         LD      (RIFTOT),HL     ;FLO2 = Z

		;RIFTOT/10=POSIZIONE ENCODER
		;POSIZIONE ENCODER - 1/2GATE = NUOVA FASE

		LD      (DIVDEN),HL
		LD      BC,000AH
		LD      (DIVSOR),BC
		CALL    DIVIS
		LD      HL,(QUOZIE)
		LD      DE,(QUOZIE)
                LD      BC,(MEZGA)     ;
		SBC     HL,BC
		JR      C,RISNE1
		JR      TRFAS1
RISNE1:         CCF                     ;SE 1/2GATE > DI
		LD      HL,1000H        ;QUOZIE ALLORA
		SBC     HL,BC           ;4096-1/2GAT = A
		ADD     HL,DE           ;A + QUOZIE=FASE
TRFAS1:         LD      (FASE),HL
		LD      A,H
		OUT     (20H),A
		LD      A,L
		OUT     (10H),A

		LD      A,(AUMATI)      ;SE IN MANUALE
		CP	00H		;TIRO SALTA 
                JR      Z,TRFAS2

                LD      A,(TIRCIC)      ;23/7/02 
                CP      00H             ;A UNO SHIFT
                JR      Z,TRFAS2        ;VIENE TRIPLI-
                LD      B,A             ;CATO IL 
                RLA                     ;TEMPO DI
                ADD     A,B             ;ATTESA  
                LD      (DECREM),A      ;DEL TIRO
                LD      (INCREM),A       
                LD      (TIRCIR_ATT),A      

TRFAS2:         EXX
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE GESTIONE SEGNO STAMPA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CHKPRINT:	
		;SE SONO IN FUNZIONE COLD SEAL, NON FACCIO IL TEST
		;ALTIMENTI MANDO OK APPENA ENTRA IL SEGNO NEL GATE
		LD      A,(MEMC)        
                BIT     B_PC_FUNZ,A             
                JR      NZ,CHKPRINT_END	

		;CONTROLLO SE UN SEGNO E' STATO RILEVATO NEL GATE
		IN      A,(PB)		
		BIT     3,A
                JR      Z,CHKPRINT_OK		

CHKPRINT_ERR:	;NON C'E' SEGNO
		LD      A,02H           ;BLOCCO LA CORREZIONE 
		LD      (MATTES),A      ;AUMENTANDO I CICLI DI ATTESA.
		CALL    NOPRINT		;ATTIVO ALLARME E MANDO MESSAGGIO
		JR      CHKPRINT_END
		
CHKPRINT_OK:    ;C'E' IL SEGNO
		CALL	SIPRINT
		;JR	CHKPRINT_END

CHKPRINT_END:
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE GESTIONE SI PRINT
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SIPRINT:	;SE NON IN ERRORE, RESETTO IL FLAG DELLO STATO D'ALLARME E 
		;RIMUOVO IL MESSAGGIO "NO PRINT"
		LD	DE,(S_MANCANZA_STAMPA)
		CALL	RES_STATUS
              	LD      A,00H
                LD      (PROPRI),A

		LD	A,(MEMD)	;RESETTO IL SEGNALE WALTER
		RES	6,A		;DI MANCANZA STAMPA
		LD	(MEMD),A	;PER NORD MECCANICA
		OUT	(PD),A		;WALTER

		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINTE GESTIONE NO PRINT
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
                
NOPRINT:        ;SE IN ERRORE, SETTO FLAG DELLO STATO D'ALLARME, 
		;INVIO MESSAGGIO "NO PRINT",
		;FACCIO IL RESET DELLE PORTE DI CORREZIONE (SOLO IN AUTOMATICO)

		LD	A,(MEMD)	;SETTO IL SEGNALE WALTER
		SET	6,A		;DI MANCANZA STAMPA 
		LD	(MEMD),A	;PER NORD MECCANICA
		OUT	(PD),A		;WALTER

		LD	DE,(S_MANCANZA_STAMPA)
		CALL	SET_STATUS
              	LD      A,01H 
                LD      (PROPRI),A

		LD	A,(AUMALO)	;SE E' IN MANUALE NON 
		CP	00H		;RESETTARE LE USCITE DI COMANDO
		JR	Z,NOPRI_FINE
		
		CALL	RESEPC
	

NOPRI_FINE:

		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE ACCENDI ALLARME
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

ALARM_ON:	
		LD	A,(AUMALO)	;SE E' IN MANUALE, 
		CP	00H		;NON ATTIVO L'ALLARME
		JR	Z,ALARM_ON_FINE

		LD      A,(MEMC)
		SET     B_PC_ALLARME,A             ;ATTIVO ALLARME
		LD      (MEMC),A
		OUT     (PC),A
 
ALARM_ON_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE SPEGNI ALLARME
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

ALARM_OFF:	LD      A,(MEMC)
		RES     B_PC_ALLARME,A             ;DISATTIVO ALLARME
		LD      (MEMC),A
		OUT     (PC),A
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE ACCENDI F9
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

F9_ON:          LD      A,(MEMC)        
                RES     B_PC_AVANTI,A
                SET     B_PC_RITARDO,A
		SET     B_PC_ALLARME,A
                SET     B_PC_FUNZ,A		;ATTIVO F9
                LD      (MEMC),A
                OUT     (PC),A

		LD	A,(AUMATI)
		LD	(AUMATI_OLD), A

		LD      A,0FFH          ;IMPONGO AUTOMATICO 
		LD	(AUMALO),A

		LD      A,000H          ;IMPONGO MANUALE
		LD	(AUMATR),A	
		LD	(AUMATI),A	

                RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE SPEGNI F9
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

F9_OFF:		LD      A,(MEMC)
		BIT	B_PC_FUNZ,A
		JR	Z,F9_OFF1	;SE GIA' SPENTO, NON FACCIO NULLA

		LD	A,(AUMATI_OLD)	;IMPOSTO IL VALORE PRECEDENTE DI AUMATI
		LD	(AUMATI), A

		
		LD      A,(MEMC)
		RES     B_PC_FUNZ,A             ;ALTRIMENTI DISATTIVO F9
		LD      (MEMC),A
		OUT     (PC),A
		CALL	SEND_OK		;E INVIO OK A CPU


F9_OFF1:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE LOW-SPEED
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SPEED_LOW:
		LD	DE,(S_VELOCITA_SOTTO_SOGLIA)
		CALL	SET_STATUS

		LD	A,(AUMALO)	;SE E' IN MANUALE, NON INVIO
		CP	00H		;ATTIVO ALLARME PER BASSA VELOCITA'
		JP      Z,SPEED_LOW_FINE

		CALL    RESEPC          ;RESET USCITE DI CORREZIONE

		LD      A,01H
                LD      (PROVEL),A  

		LD	A,(ENB_DAC_LON)   ;VERIFICO SE SONO 
		CP	02H		  ;CON DAC IN USCITA PROPORZIONALE
		JR	NZ,SPEED_LOW_FINE ;ALL'ERRORE 

		LD	DE,800H		   ;(CORAZZA)			
		LD	(DACVAL),DE
		CALL	DAC_TRASM

SPEED_LOW_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE SPEED-OK
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SPEEDOK:	LD	DE,(S_VELOCITA_SOTTO_SOGLIA)
		CALL	RES_STATUS
                LD      A,00H
                LD      (PROVEL),A
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINTE GESTIONE DECELLERA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
                
DECEL:          ;SE IN ERRORE, SETTO FLAG DELLO STATO D'ALLARME, 
		;INVIO MESSAGGIO "DECELLERAZIONE",
		;FACCIO IL RESET DELLE PORTE DI CORREZIONE (SOLO IN AUTOMATICO)
		LD	DE,(S_BLOCCO_DECELERAZIONE)
		CALL	SET_STATUS
              	LD      A,01H 
                LD      (PROVEL),A

		LD	A,(AUMALO)	;SE E' IN MANUALE NON 
		CP	00H		;RESETTARE LE USCITE DI COMANDO
		JR	Z,DECEL_FINE
		CALL	RESEPC
		
DECEL_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINTE GESTIONE ACCELERA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
                
ACCEL:          ;SE IN ERRORE, SETTO FLAG DELLO STATO D'ALLARME, 
		;INVIO MESSAGGIO "ACCELERAZIONE",
		;FACCIO IL RESET DELLE PORTE DI CORREZIONE (SOLO IN AUTOMATICO)
		LD	DE,(S_BLOCCO_ACCELERAZIONE)
		CALL	SET_STATUS
              	LD      A,01H 
                LD      (PROVEL),A

		LD	A,(AUMALO)	;SE E' IN MANUALE NON 
		CP	00H		;RESETTARE LE USCITE DI COMANDO
		JR	Z,ACCEL_FINE
		CALL	RESEPC
ACCEL_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE VELOCITA'COSTANTE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

VELCOST:	;SE NON IN ERRORE, RESETTO IL FLAG DELLO STATO D'ALLARME E 
		;RIMUOVO IL MESSAGGIO "ACC/DEC"
		LD	DE,(S_BLOCCO_ACCELERAZIONE)
		CALL	RES_STATUS
		LD	DE,(S_BLOCCO_DECELERAZIONE)
		CALL	RES_STATUS
              	LD      A,00H
                LD      (PROVEL),A
VELCOST_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE DOPPIO SEGNO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
DOPPIO:
		LD	DE,(S_DOPPIO_SEGNO)
		CALL	SET_STATUS
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE CALCOLA VELOCITA IMPULSIVA - VELIMP
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SPEED_CALC:
                LD      BC,011DH        ;4mS TEMPO   
                LD      IX,VELIMP       ;PER MISURARE
                LD      A,00H           ;GLI IMPULSI
                LD      (IX+01H),A
                LD      A,057H          ;SETTO CTC0 COME TIMER
                OUT     (CTC0_CMD),A    ;SENZA INTERUPT
                LD      A,0FFH          ;PARTENDO DA 0FFH
                OUT     (CTC0_CMD),A         
SPEED_C1:         
		IN      A,(CTC0_CMD)    ;NEL CASO IL 
                CP      00H             ;DECREMENTO 
                JR      NZ,SPEED_C2     ;ARRIVI A ZERO
                INC     (IX+01H)        ;LO MEMORIZZO
SPEED_C2:         
		DEC     BC              
                LD      A,B
                OR      C
                JR      NZ,SPEED_C1       
                IN      A,(CTC0_CMD)    ;IMPULSI PROPOR     
                CPL                     ;ALLA VELOCITA.
		LD      (MOLPL),A	;AVENDO FATTO
                LD      BC,0004H	;LA MISURA IN 4mS
		LD      (MOLTI),BC	;DEVO MOLTIPLICARE X 4
                CALL    MOLT		;PROBLEMA SORTO IN UTECO
		LD      BC,(PROD)       ;PRIMA LA MISURA ERA 
                LD      (VELIMP),BC     ;FATTA IN 16mS 
                 
SPEED_CALC_FINE:
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;       SUB-RUTINE FERMO MOTORE IN PASSAGGIO MAN/AUTO USANDO
;	IL DAC CON USCITA PROPOPORZIONALE ALL'ERRORE (CORAZZA)
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

MANAUTO_STOP:
		LD	A,(AUMALO)		;TRASMETTO DI FERMARE 
		LD	B,A			;IL MOTORE QUANDO VEDO
		LD	A,(CAMBIO_MANAUTO)	;CHE VI E' STATO UN 
		CP	B			;PASSAGGIO DA MANUALE 
		JR	Z,MANAUTO_FINE		;AD AUTOMATICO E VICEVERSA
		LD	DE,800H			;CORAZZA
		LD	(DACVAL),DE
		CALL	DAC_TRASM
		LD	A,(AUMALO)
		LD	(CAMBIO_MANAUTO),A
MANAUTO_FINE:
		RET
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CONTROLLO ZONA MORTA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CORREZIONI:     

		LD	A,(AUMALO)	;SE SONO IN MANUALE, NON MI INTERESSA, 
		CP	00H		;DISATTIVARE LE USCITE IN BASE ALLA ZONA MORTA
		JR	Z,CORREZIONI_1

		LD      HL,(ERMEDI)     ;SE ZONA MORTA E'
		LD      DE,(ZONA)       ;> DELL'ERRORE
		SBC     HL,DE           ;NON CORREGGO
		JR      NC,CORREZIONI_1
		CCF
		LD      A,(MEMC)        
		RES     B_PC_AVANTI,A             
		RES     B_PC_RITARDO,A             
		LD      (MEMC),A        
		OUT     (PC),A   	
		LD	A,(ENB_DAC_LON)		;SE NON SONO IN DAC 
		CP	02H			;USCITA PROPORZIONALE
		JP	NZ,CORREZIONI_END	;ALL'ERRORE  SALTO (CORAZZA)

		LD	DE,800H		        ;IN ZONA MORTA FERMO IL MOTORE			
		LD	(DACVAL),DE
		CALL	DAC_TRASM		;UGO

		JP      CORREZIONI_END


		;#####################################################################
		;# CALCOLO CORREZIONE MOTORE                                         #
		;# [Ea x C/10 + (Ea - Ep) x D] x G                                   #
		;# [Ea x (C/10 + D) x G] - [Ep x D x G]	                             #
		;# CORRATT = 80000000h + [Ea x (C/10 + D) x G] - [Ep x D x G]        #
		;#####################################################################


CORREZIONI_1:   
		;[Ea x (C/10 + D) x G]
		LD      DE,(CILDIV)	
		LD	HL,(DERIVA)
		ADD	HL,DE		;HL = C/10+D
		LD	(MDO032),HL
		LD	HL,(GUADA)	;HL = G
		LD	(MRE032),HL
		LD	HL,0000H
		LD	(MDO232),HL
		LD	(MRE232),HL
		CALL	MOL32		;PT = (C/10+D)*G
		LD	HL,(PTO032)
		LD	(MDO032),HL
		LD	HL,(PTO232)
		LD	(MDO232),HL
		LD	HL,(ERATTU)	;HL = Ea
		LD	(MRE032),HL	
		CALL	MOL32		;PT = [Ea x (C/10 + D) x G]

		LD      A,(MSCORR_ATT)	
		CP      D_AVANTI        
		JR      Z,CORREZIONI_2	

		;CORRATT = 80000000h - [Ea x (C/10 + D) x G]
		LD	HL,(PTO032)
		LD	(CUBTR0),HL	;SOTTRAENDO = [Ea x (C/10 + D) x G]
		LD	HL,(PTO232)
		LD	(CUBTR2),HL
		LD	HL,0000H	;MINUENDO = 80000000h
		LD	(CINUE0),HL
		LD	HL,8000H
		LD	(CINUE2),HL
		CALL	CSOTT32		;RESTO = MINUENDO - SOTTRAENDO
		LD	HL,(CESTO0)
		LD	(CORRATT),HL	;BASSO
		LD	HL,(CESTO2)
		LD	(CORRATT+02H),HL ;ALTO
		JR	CORREZIONI_3

CORREZIONI_2:
		;CORRATT = 80000000h + [Ea x (C/10 + D) x G]
		LD	HL,(PTO032)
		LD	(ADDEN0),HL	;ADDENDO = [Ea x (C/10 + D) x G]
		LD	HL,(PTO232)
		LD	(ADDEN2),HL
		LD	HL,0000H	;AUGEND0 = 80000000h
		LD	(AUGEN0),HL
		LD	HL,8000H
		LD	(AUGEN2),HL
		CALL	SOMM32		;AUGENDO = ADDENDO + AUGENDO
		LD	HL,(AUGEN0)
		LD	(CORRATT),HL	;BASSO
		LD	HL,(AUGEN2)
		LD	(CORRATT+02H),HL ;ALTO
		JR	CORREZIONI_3
		

CORREZIONI_3:
		;[Ep x D x G]
		LD      HL,(ERPREC)     
		LD      (MOLTI),HL
		LD      A,(DERIVA)
		LD      (MOLPL),A
		CALL    MOLT
		LD      HL,(PROD)      
		LD      (MOLT24),HL       ;RISULTATO
		LD      A,(GUADA)
                LD      (MOLP24),A      
                CALL    MOL24

		LD      A,(MSCORR_PREC)	
		CP      D_AVANTI        
		JR      Z,CORREZIONI_4	

		;CORRATT = CORRATT + [Ep x D x G]
		LD	HL,(CORRATT)
		LD	(ADDEN0),HL	;ADDENDO = CORRATT
		LD	HL,(CORRATT+02H)
		LD	(ADDEN2),HL
		LD	HL,(PROD24)	;AUGEND0 = [Ep x D x G]
		LD	(AUGEN0),HL
		LD	HL,(PROD24+02H)
		LD	(AUGEN2),HL
		CALL	SOMM32		;AUGENDO = ADDENDO + AUGENDO
		LD	HL,(AUGEN0)
		LD	(CORRATT),HL	;BASSO
		LD	HL,(AUGEN2)
		LD	(CORRATT+02H),HL ;ALTO
		JR	CORREZIONI_5

CORREZIONI_4:
		;CORRATT = CORRATT - [Ep x D x G]
		LD	HL,(PROD24)
		LD	(CUBTR0),HL	;SOTTRAENDO = [Ep x D x G]
		LD	HL,(PROD24+02H)
		LD	(CUBTR2),HL
		LD	HL,(CORRATT)	;MINUENDO = CORRATT
		LD	(CINUE0),HL
		LD	HL,(CORRATT+02H)
		LD	(CINUE2),HL
		CALL	CSOTT32		;RESTO = MINUENDO - SOTTRAENDO
		LD	HL,(CESTO0)
		LD	(CORRATT),HL	;BASSO
		LD	HL,(CESTO2)
		LD	(CORRATT+02H),HL ;ALTO

		JR	CORREZIONI_5
CORREZIONI_5:
		LD	A,(CORRATT+03H) ;IF CORRATT > 80000000h THEN MSCORR=D_AVANTI ELSE MSCORR=D_RITARDO
		BIT	7,A
		JR	Z,CORREZIONI_6

		LD      A,D_AVANTI
		LD      (MSCORR),A
		JR	CORREZIONI_7

CORREZIONI_6:
		LD      A,D_RITARDO
		LD      (MSCORR),A
		JR	CORREZIONI_7

CORREZIONI_7:	
		LD	HL,0000H
		LD	(CUBTR0),HL	;SOTTRAENDO = 80000000h
		LD	HL,8000h
		LD	(CUBTR2),HL
		LD	HL,(CORRATT)	;MINUENDO = CORRATT
		LD	(CINUE0),HL
		LD	HL,(CORRATT+02H)
		LD	(CINUE2),HL
		CALL	CSOTT32		;RESTO = MINUENDO - SOTTRAENDO
		LD	HL,(CESTO0)
		LD	(CORRATT),HL	;BASSO
		LD	HL,(CESTO2)
		LD	(CORRATT+02H),HL ;ALTO
		
CORREZIONI_8:		

		LD      A,(CORRATT+03H)
		AND	7FH
		CP	00H
		JR	Z,CORREZIONI_9
                LD      BC,0FFFFH
                LD      A,0FFH
		JR	CORREZIONI_10
CORREZIONI_9:
                LD      BC,(CORRATT)
                LD      A,(CORRATT+02H)
		JR	CORREZIONI_10
CORREZIONI_10:

                LD      (MDECOR),A      ;VALORE PER 
                CP      00H             ;EXTRA CORREZ.
                JR      NZ,CORREZIONI_11

                LD      A,B

                LD      (CORRE),A
                JR      CORREZIONI_12          
CORREZIONI_11:  LD      A,0FFH
                LD      (CORRE),A



		;ROUTINE DI CORREZIONE


CORREZIONI_12:  LD      A,(CIDE75)      ;DECREMENTO 
                CP      00H             ;DEL 75% DEL
                JR      Z,CORREZIONI_13        ;CICLO
                DEC     A
                LD      (CIDE75),A


CORREZIONI_13:       
		LD      A,(PROPRI)		;SE IN PROTEZIONE 
                CP      01H			;(ALLARME) ATTIVO
                JP      Z,CORREZIONI_END        ;ESCO 
		LD      A,(PROVEL)		;SE IN PROTEZIONE 
                CP      01H			;(ALLARME) ATTIVO
                JP      Z,CORREZIONI_END        ;ESCO 

		LD      A,(MATTES)		;CONTROLLO CICLO
		DEC     A			;DI ATTESA
		LD      (MATTES),A
		CP      00H
		JP      NZ,CORREZIONI_END

                LD      A,(CICL75)		;RICARICO IL 
                LD      (CIDE75),A		;75% DEL CICLO 

                LD      A,(CICLO)		;RICARICO IL CI-
		LD      (MATTES),A		;CLO DI ATTESA.

                LD      HL,(ERMEDI)
		LD      DE,(ZONA)
		SBC     HL,DE			;SE ZONA MORTA
		JR      NC,CORREZIONI_14		;MAGGIORE DI
		CCF				;ERMEDI SALTO
		JP      CORREZIONI_END
CORREZIONI_14:         
		LD	A,(AUMALO)		;SE SONO IN 
		CP	00H			;MANUALE SALTO
		JP      Z,CORREZIONI_END

                LD      A,(CORRE)
                CP      00H			;SALTO
                JP      Z,KRAPIN        

		LD	A,(ENB_DAC_LON)		;CONTROLLO SE SONO
		CP	02H			;IN USCITA PROPORZIONALE
		JR	NZ,DAC_TENS_PROP_01	;ALL'ERRORE (CORAZZA)
		LD      HL,(DACDELTACOR)	;MOLTIPLICO IL DAC GUADAGNO
		LD      (MOLTI),HL		;LONGITUD. PER L'ERRORE
		LD      A,(CORRE)		;E OTTENGO UNA USCITA 
		LD      (MOLPL),A		;PROPORZIONALE
		CALL    MOLT			;(CORAZZA)
		LD      HL,(PROD)      
		LD	(CORPROPTEN),HL

DAC_TENS_PROP_01:
                LD      A,(MDECOR)
                LD      (DECORR),A

		LD	A,(CICLO)		;SE SONO IN DAC PROPORZIONALE
		CP	01			;E SONO IN CILCO 1 DO LA 
		JR	NZ,DAC_TENS_PROP_02	;CORREZIONE CONTINUA 

		LD	A,(ENB_DAC_LON)		;SE NON SONO IN DAC 
		CP	02H			;USCITA PROPORZIONALE
		JR	Z,DAC_TENS_PROP_03	;ALL'ERRORE  SALTO (CORAZZA)
DAC_TENS_PROP_02:
                LD      A,37H			;CONTROLLO CTC1   
		OUT     (CTC1_CMD),A         
                LD      A,40H			;COSTANTE DI TEMPO
		OUT     (CTC1_CMD),A			
		LD      A,0D7H			;CONTROLLO CTC2
		OUT     (CTC2_CMD),A
                LD      A,(CORRE)		;MSB ERRORE  
                OUT     (CTC2_CMD),A

DAC_TENS_PROP_03:
		LD      A,(MSCORR)		;VEDO IN CHE
		CP      D_AVANTI		;SENSO CORREG.
		JR      NZ,AUTRIT
;%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
                LD      A,(TIRCIC)		;SE INTERVALLO TIRO E' A ZERO SALTA
                CP      00H			
                JR      Z,AUTAVA

                LD	A,(AUMATI)		;SE TIRO E' IN MANUALE SALTA 
                CP	00H			
                JR      Z,AUTAVA

		LD	BC,(TIRCIR_ATT)
		LD      A,(DECREM)
		INC	A
		CP	C
		JR	C,CORREZIONI_15 
		LD      A,(TIRCIR_ATT)
CORREZIONI_15:
		LD      (DECREM),A

		LD      A,(INCREM)
		DEC     A
		LD      (INCREM),A
		CP      00H
		JR      NZ,AUTAVA
                LD      A,(TIRCIR_ATT)
		LD      (INCREM),A
                
                LD      A,01H           
                LD      (TMEM6),A		;PIU' CARTA
                LD      (TMEM4),A		;ATTIVO 
                CALL    CORREZIONE_TIRO         ;CORREZ.TIRO
                JP      CORREZIONI_END
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

AUTAVA:         LD      A,(MEMC)        ;CORREZIONE IN
		RES     B_PC_RITARDO,A  ;AVANTI
		SET     B_PC_AVANTI,A
		LD      (MEMC),A
		OUT     (PC),A

		LD	A,(ENB_DAC_LON)		;SE SONO IN DAC
		CP	02H			;USCITA PROPORZIONALE
		JR	Z,DAC_TENS_PROP_04	;SALTO (CORAZZA)
		
		;INCREMENTO TEMPORANEAMENTE IL DAC
		LD	HL,(DACDELTACOR)
		LD	(DAC_INCREM),HL
		CALL	DAC_INCR_DECR
		JR      CORREZIONI_END
DAC_TENS_PROP_04:
		LD	HL,(CORPROPTEN)		;CARICO L'ERRORE DEL DAC
		LD	(DAC_INCREM),HL		;PROPORZ.PER MANDARLO
		CALL	DAC_INCR_DECR		;ALLA SUB-RUTINE DI 
		JR      CORREZIONI_END		;ELABORAZIONE DEL DAC (CORAZZA)

AUTRIT:
                LD      A,(TIRCIC)      ;SE A ZERO IL CICLO   
                CP      00H             ;DEL TIRO  SALTA
                JR      Z,AUTRI1

                LD	A,(AUMATI)	;SE IN MANUALE IL
                CP	00H		;TIRO SALTA 
                JR      Z,AUTRI1

		LD	BC,(TIRCIR_ATT)
		LD      A,(INCREM)
		INC	A
		CP	C
		JR	C,CORREZIONI_16 
		LD      A,(TIRCIR_ATT)
CORREZIONI_16:
		LD      (INCREM),A

		LD      A,(DECREM)
		DEC     A
		LD      (DECREM),A
		CP      00H
		JR      NZ,AUTRI1
                LD      A,(TIRCIR_ATT)      
		LD      (DECREM),A
                LD      A,02H           ;MENO CARTA
                LD      (TMEM6),A
                LD      A,01H           ;ATTIVO
                LD      (TMEM4),A       ;CORREZ.TIRO 
                CALL    CORREZIONE_TIRO
                JR      CORREZIONI_END


AUTRI1:         LD      A,(MEMC)        ;CORREZIONE IN
		RES     B_PC_AVANTI,A   ;RITARDO
		SET     B_PC_RITARDO,A
		LD      (MEMC),A
		OUT     (PC),A

		LD	A,(ENB_DAC_LON)		;SE SONO IN DAC 
		CP	02H			;USCITA PROPORZIONALE
		JR	Z,DAC_TENS_PROP_05	;SALTO(CORAZZA)

		;DECREMENTO TEMPORANEAMENTE IL DAC
		LD	HL,(DACDELTACOR)
		LD	(DAC_DECREM),HL
		CALL	DAC_INCR_DECR
		JR      CORREZIONI_END
DAC_TENS_PROP_05:
		LD	HL,(CORPROPTEN)		;VEDI, DAC_TENS_PROP_04:
		LD	(DAC_DECREM),HL
		CALL	DAC_INCR_DECR
		
CORREZIONI_END:      

		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		RESET USCITE DI CORREZIONE INDESIDERATE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

KRAPIN:
		LD	A,(AUMALO)	;SE E' IN MANUALE NON 
		CP	00H		;RESETTARE LE USCITE DI COMANDO
		JR	Z,KRAPIN1

		CALL	RESEPC		;RESET USCITE DI CORREZIONE INDESIDERATE

KRAPIN1:			
                LD      A,43H           
                OUT     (CTC1_CMD),A         
                OUT     (CTC2_CMD),A
                                     
		LD      A,(TIRCIR_ATT)      
                LD      (INCREM),A
                LD      (DECREM),A
		RET



                ;##################################
                ;# CALCOLO CORREZIONE MOTORE TIRO #
                ;# Et x Gt = TEMPO DI CORREZIONE  #   
                ;##################################
CORREZIONE_TIRO:		
		LD      A,(TIRGUA)		;SE ZERO 
                CP      00H			;SALTO
                JP      Z,CORREZIONE_TIRO_FINE        

                LD      DE,(ERATTU)     
                LD      A,(TIRGUA)       
                LD      (MOLT24),DE     
                LD      (MOLP24),A      
                CALL    MOL24           
                LD      BC,(PROD24)
                LD      A,(PROD24+02H)
                CP      00H
                JR      Z,CORREZIONE_TIRO_1     ;BC = ERRORE    
                LD      B,0FFH			;EXTRA CORR.

CORREZIONE_TIRO_1:         
		LD      HL,0B00H		;SE SUPERA
                SBC     HL,BC			;QUESTO VALORE
                JR      NC,CORREZIONE_TIRO_2    ;IMPONGO UN
                CCF				;VALORE FISSO
                LD      BC,0B00H		;PER NON FAR
CORREZIONE_TIRO_2:         
		LD      (TMEM5),BC		;MUOVERE TANTO
                LD      A,B			;IL MOTOPOTENZIOMETRO
                OR      C
                JP      Z,CORREZIONE_TIRO_RESET        



                ;ROUTINE DI CORREZIONE TIRO

		LD	A,(AUMATI)		;SE IN MANUALE
                CP	00H			;TIRO SALTA 
                JP      Z,CORREZIONE_TIRO_FINE

                LD      A,(TMEM4)      
		CP      00H
                JP      Z,CORREZIONE_TIRO_FINE        

                LD      A,(TMEM6)       
                CP      00H
                JP      Z,CORREZIONE_TIRO_FINE
                CP      01H
                JP      Z,CORREZIONE_TIRO_DEC        
                CP      02H
                JP      Z,CORREZIONE_TIRO_INC        
                JP      CORREZIONE_TIRO_FINE

CORREZIONE_TIRO_DEC:                  
                LD      A,(TIRGUA)		;SE GUATRA >  DI 100 LA
                CP      65H			;CORREZIONE
                JR      C,CORREZIONE_TIRO_DEC_1 
                SBC     A,64H			;DIVENTA FISSA
                LD      BC,0000H		;UNA UNITA'
                LD      (TMEM5),BC		;DELLA SOTTRAZIONE
                LD      (TMEM5),A		;E' UGUALE A 1mS
                LD      A,0B7H                
                OUT     (73H),A         
                LD      A,10H           
                OUT     (73H),A          
                JR      CORREZIONE_TIRO_DEC_2
CORREZIONE_TIRO_DEC_1:         
		CCF                     
                LD      A,0B7H			;CON ERORE 0.1mmm      
                OUT     (73H),A			;E TIMER = 10
                LD      A,02H			;TEMPO DI  ZZZ
                OUT     (73H),A			;CORREZ. = 30mS  
CORREZIONE_TIRO_DEC_2:         

		LD      A,(MEMC)		;DIMINUZIONE
                RES     B_PC_SINISTRA,A		;TIRO
		SET     B_PC_DESTRA,A
		LD      (MEMC),A
		OUT     (PC),A

		;DECREMENTA BASE DAC
		LD	HL,(DACDELTATIR)
		LD	(DAC_INCREM_BASE),HL
		CALL	DAC_INCR_DECR_BASE

                JR      CORREZIONE_TIRO_FINE  



CORREZIONE_TIRO_INC:                   
                LD      A,(TIRGUA)		;VEDI SOPRA 
                CP      65H
                JR      C,CORREZIONE_TIRO_INC_1
                SBC     A,64H
                LD      BC,0000H
                LD      (TMEM5),BC
                LD      (TMEM5),A
                LD      A,0B7H                
                OUT     (73H),A         
                LD      A,10H           
                OUT     (73H),A          
                JR      CORREZIONE_TIRO_INC_2
CORREZIONE_TIRO_INC_1:         
		CCF
                LD      A,0B7H			;CON ERRORE 0.1mmm      
                OUT     (73H),A			;E TIMER = 10
                LD      A,02H			;TEMPO DI  
                OUT     (73H),A			;CORREZ. = 30mS 

CORREZIONE_TIRO_INC_2:        
		LD      A,(MEMC)		;AUMENTO
                RES     B_PC_DESTRA,A           ;TIRO
		SET     B_PC_SINISTRA,A
		LD      (MEMC),A
		OUT     (PC),A

		;INCREMENTA BASE DAC
		LD	HL,(DACDELTATIR)
		LD	(DAC_DECREM_BASE),HL
		CALL	DAC_INCR_DECR_BASE


		JR	CORREZIONE_TIRO_FINE

CORREZIONE_TIRO_FINE:         
		LD      A,00H           
                LD      (TMEM6),A
                
                LD      A,(TIRCIC)      ;RIPRISTINO
                LD      (TIRCIR_ATT),A      ;I VALORI 
                LD      (INCREM),A      ;NORMALI
                LD      (DECREM),A
                RET
		      
CORREZIONE_TIRO_RESET:         
		LD      A,(MEMC)        ;QUANDO L'ERRORE 
                RES     B_PC_DESTRA,A   ;E' A ZERO
                RES     B_PC_SINISTRA,A ;SI ANNULLA 
                LD      (MEMC),A        ;TUTTO PER
                OUT     (PC),A          ;EVITARE CHE
                LD      A,43H           ;CHE AVVENGANO
                OUT     (73H),A         ;CORREZIONI
                LD      A,00H           ;INDESIDERATE
                LD      (TMEM4),A       ;COME A KRAPINA
                RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		RIEMPIO TABELLA ERRORE MEDIO PER CORREZIONE LONG CON 
;		IL VALORE DI ERATTU E MSCORR
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
RESET_MEDIA:
                LD      DE,(ERATTU)      
		LD	BC,(MLUNGHEZZA)
		LD      IX,ARR_ERRAVA
		ADD     IX,BC
		ADD     IX,BC
                DEC     IX
                DEC     IX

		LD      IY,ARR_ERRRIT
		ADD     IY,BC
		ADD     IY,BC
                DEC     IY
                DEC     IY

		LD	B,C

                LD      A,(MSCORR)       
                CP      D_AVANTI              
                JR      Z,RESET_MEDIA_02

RESET_MEDIA_01:

		LD      (IX+00H),E
                LD      (IX+01H),D
                DEC     IX
                DEC     IX
		LD      (IY+00H),00H
                LD      (IY+01H),00H
                DEC     IY
                DEC     IY
		DJNZ	RESET_MEDIA_01

                JR      RESET_MEDIA_FINE

RESET_MEDIA_02:         
		LD      (IX+00H),00H
                LD      (IX+01H),00H
                DEC     IX
                DEC     IX
		LD      (IY+00H),E
                LD      (IY+01H),D
                DEC     IY
                DEC     IY
		DJNZ	RESET_MEDIA_02

RESET_MEDIA_FINE:
		LD	A,(MSCORR)	;FORZO SEGNO CORRENTE COME 
		LD	(ERMEDI_SIGN),A	;SEGNO DELLA MEDIA	
		LD	BC,(ERATTU)	;FORZO ERRORE CORRENTE
		LD	(ERPREC),BC	;COME ERRORE MEDIATO
		LD	(ERMEDI),BC	;ED ERRORE PRECEDENTE
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CALCOLO ERRORE MEDIO PER CORREZIONE LONG
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

LMEDIA:

;		LD	BC,(ERATTU)
;		LD	(ERMEDI),BC
;		LD      BC,(MSCORR)
;		LD	(ERMEDI_SIGN),BC
;		RET



                LD      A,(PRIMOB)      
                LD      B,A
                
                LD      IX,ARR_ERRAVA       ;SU SEDICI
                LD      DE,(VALORX)
                ADD     IX,DE
LMED01:
                LD      E,(IX)          ;DATI LETTI
                LD      D,(IX+01H)      ;INSERENDO
                LD      (IX+02H),E      ;IL NUOVO
                LD      (IX+03H),D      ;ERRORE AL
                DEC     IX              ;PRIMO POSTO
                DEC     IX              ;ED ELIMINANDO
                DJNZ    LMED01          ;L'ULTIMO
                INC     IX
                INC     IX


                LD      A,(PRIMOB)      ; LD      B,2FH           ;
                LD      B,A             ;
                LD      IY,ARR_ERRRIT       ;+5CH   ;DMED1C       
                LD      DE,(VALORX)     ;
                ADD     IY,DE           ;
LMED02:
                LD      E,(IY)          ;RIPETE QUELLO
                LD      D,(IY+01H)      ;VISTO SOPRA
                LD      (IY+02H),E      ;CON ALTRE  
                LD      (IY+03H),D      ;16 CELLE
                DEC     IY
                DEC     IY
                DJNZ    LMED02
                INC     IY
                INC     IY


                LD      DE,(ERATTU)      ;INSERIMENTO
                LD      BC,0000H         ;NUOVO ERRORE
                LD      A,(MSCORR)       ;NELLA PRIMA
                CP      D_AVANTI         ;DELLE 16 CELLE
                JR      Z,LMED04
LMED03:
                LD      (ARR_ERRAVA),DE
                LD      (ARR_ERRRIT),BC
                JR      LMED05
LMED04:         
                LD      (ARR_ERRAVA),BC
                LD      (ARR_ERRRIT),DE
LMED05:
                LD      A,(SECONB)
                LD      B,A             ;SOMMA DELLE  16 celle reali
                JR      NC,LMED06       ;48 CELLE 16 CELLE
                CCF                      ; 
LMED06:         LD      HL,0000H
LMED07:
                LD      E,(IX+00H)
                LD      D,(IX+01H)
                ADC     HL,DE
                INC     IX
                INC     IX
                DJNZ    LMED07
                LD      (SOMLO1),HL

                LD      A,(SECONB)      ;
                LD      B,A             ;SOMMA DELLE  16 celle reali 
                JR      NC,LMED08        ;48 CELLE 16 CELLE   
                CCF
LMED08:         LD      HL,0000H
LMED09:
                LD      E,(IY+00H)
                LD      D,(IY+01H)
                ADC     HL,DE
                INC     IY
                INC     IY
                DJNZ    LMED09
                LD      (SOMLO2),HL

		LD      HL,(SOMLO2)
                LD      DE,(SOMLO1)
		SBC     HL,DE
                JR      C,LMED10        ;SEGNO ERRORE
		LD	A,D_AVANTI	;POSITIVO
		LD	(ERMEDI_SIGN),A
                JR      LMED11 

LMED10:         CCF
		LD	A,D_RITARDO	;NEGATIVO
		LD	(ERMEDI_SIGN),A
                
		EX      DE,HL
                LD      DE,(SOMLO2)
		SBC     HL,DE

LMED11:         LD      (DIVDEN),HL
                LD      BC,(SECONB)     ;DIVISIONE    
                LD      B,00H           ;
                LD      (DIVSOR),BC     ;PER XX 
		CALL    DIVIS
		LD      HL,(QUOZIE)
                LD      (ERMEDI),HL

		RET

            
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SET GUADS1 SU AMPLIFICATORE OPERAZIONALE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SET_GUAD_S1:
		EXX
		LD      A,(GUADS1)      ;SETTO GUADAGNO DI S1
		LD      B,A             
                LD      A,(MEMA)        
		RES     5,A
		RES     6,A
		ADD     A,B
		LD      (MEMA),A
                OUT     (PA),A     

SET_GUAD_S1_FINE:
		EXX
		RET;



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ASPETTA UN IMPULSO DI ENCODER E METTE IN A PB
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

WAIT_ENCODER:      
		IN      A,(PB)                
		BIT     4,A             ;SE ENCODER = 0 ASPETTA
		JR      Z,WAIT_ENCODER
WAIT_ENCODER_01:      
		IN      A,(PB)          ;SE ENCODER = 1 ASPETTA
		BIT     4,A
		JR      NZ,WAIT_ENCODER_01
		;LD	(MEMB),A
WAIT_ENCODER_FINE:
		BIT     2,A		; Z SE BIANCA
					;NZ SE NERA
		RET	



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CALCOLO CODICE BIANCO E CODICE NERO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
CAL_CODICE:
		;RESET VALORI DIVISIONE E MOLTIPLICAZIONE 32BIT
		LD	BC,0000H
		LD	(DIV32_DIVID_H),BC
		LD	(DIV32_DIVID_L),BC
		LD	(DIV32_DIVIS_H),BC
		LD	(DIV32_DIVIS_L),BC
		LD	(DIV32_QUOZ_H),BC
		LD	(DIV32_QUOZ_L),BC

		LD      (MDO032),BC     
		LD      (MDO232),BC     
		LD      (MRE032),BC     
		LD      (MRE232),BC     
		LD	(PTO032),BC
		LD	(PTO232),BC
		
		
CAL_CODICE_NERO:
		;CODICE_NERO = (4096/CILIN) * 0.5 = 2048/CILIN
		LD	BC,0800H	
		LD	(DIV32_DIVID_L),BC
		LD	BC,(CILIN)	
		LD	(DIV32_DIVIS_L),BC
		CALL	DIV32
		LD	BC,(DIV32_QUOZ_L)
		LD	(CODICE_NERO),BC



CAL_CODICE_BIANCO:
		LD	A,(F4_DIMENSION)
		CP	00H
		JR	Z,CAL_CODICE_BIANCO_GATE
		
CAL_CODICE_BIANCO_DIMENSION:
		;CODICE_BIANCO = (4096/CILIN) * F4_DIMENSION	= (4096 * F4_DIMENSION) / CILIN
		LD	BC,1000H
		LD	(MDO032),BC

		LD	BC,(F4_DIMENSION)
		LD	(MRE032),BC

		JR	CAL_CODICE_BIANCO_BOTH

CAL_CODICE_BIANCO_GATE:
		;CODICE_BIANCO = (4096/CILIN) * (GATE/2)	= (2048 * GATE) / CILIN
		LD	BC,0800H
		LD	(MDO032),BC

		LD	BC,(AMPGA)
		LD	(MRE032),BC

		JR	CAL_CODICE_BIANCO_BOTH
		
CAL_CODICE_BIANCO_BOTH:
		CALL    MOL32
		LD      BC,(PTO032)
		LD	(DIV32_DIVID_L),BC

		LD      BC,(PTO232)
		LD	(DIV32_DIVID_H),BC

		LD	BC,(CILIN)	
		LD	(DIV32_DIVIS_L),BC

		CALL	DIV32

		LD	BC,(DIV32_QUOZ_L)
		LD	(CODICE_BIANCO),BC

CAL_CODICE_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		RICONOSCIMENTO CODICE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CODICE:
		CALL	SET_GUAD_S1

		LD	A,(F4_DIMENSION)	;SE F4_DIMENSION = 600 (MASSIMO VALORE CONSENTITO)
		CP	58H			;USO LA RICERCA EASY (PRIMO SEGNO NERO VISIBILE)
		JR	NZ, CODICE_WAIT_B

		LD	A,(F4_DIMENSION+01H)
		CP	02H	
		JR	NZ, CODICE_WAIT_B
		
		CALL	CODICE_EASY

		JP	CODICE_FINE



CODICE_WAIT_B:					;ATTENDO IL PRIMO SEGNO BIANCO
		IN      A,(PB)          
		BIT     2,A		
		JR	Z,CODICE_WAIT_B		


CODICE_N1:					;ATTENDO IL PRIMO SEGNO NERO
		IN      A,(PB)          
		BIT     2,A		
		JR	NZ,CODICE_N1

		IN      A,(10H)			;VALORE ENCODER ATTUALE
		LD      C,A
		IN      A,(20H)
		LD      B,A	
		LD	(CODICE_ENC_N1),BC

CODICE_B1:					;ASPETTA MENTRE LEGGE BIANCO
		IN      A,(PB)          
		BIT     2,A		
		JR	Z,CODICE_B1

		IN      A,(10H)			;VALORE ENCODER ATTUALE
		LD      L,A
		IN      A,(20H)
		LD      H,A	
		LD	(CODICE_ENC_B1),HL

		LD	DE,1000H		;HL = MOD(4096+HL-BC, 4096) 
		ADD	HL,DE
		AND	A
		SBC	HL,BC

		LD	A,H		
		AND	0FH
		LD	H,A
						
		AND	A			;RESET CARRY
		LD	BC,(CODICE_BIANCO)
		SBC	HL,BC			
		LD	BC,(CODICE_ENC_B1)	;AGGIORNO IL VALORE PRECEDENTE
		JR	C,CODICE_N1		;HL<CODICE_BIANCO



CODICE_N2:					;ASPETTA MENTRE LEGGE NERO
		IN      A,(PB)          
		BIT     2,A		
		JR	NZ,CODICE_N2

		IN      A,(10H)			;VALORE ENCODER ATTUALE
		LD      L,A
		IN      A,(20H)
		LD      H,A	
		LD	(CODICE_ENC_N2),HL

		LD	DE,1000H		;HL = MOD(4096+HL-BC, 4096) 
		ADD	HL,DE
		AND	A
		SBC	HL,BC

		LD	A,H		
		AND	0FH
		LD	H,A

		AND	A			;RESET CARRY
		LD	BC,(CODICE_NERO)
		SBC	HL,BC			
		LD	BC,(CODICE_ENC_N2)	;AGGIORNO IL VALORE PRECEDENTE
		JR	NC,CODICE_B2		;HL>=CODICE_NERO

		LD	(CODICE_ENC_N1),BC
		JR	CODICE_B1


CODICE_B2:					;ASPETTA MENTRE LEGGE BIANCO
		IN      A,(PB)          
		BIT     2,A		
		JR	Z,CODICE_B2

		IN      A,(10H)			;VALORE ENCODER ATTUALE
		LD      L,A
		IN      A,(20H)
		LD      H,A	
		LD	(CODICE_ENC_B2),HL

		LD	DE,1000H		;HL = MOD(4096+HL-BC, 4096) 
		ADD	HL,DE
		AND	A
		SBC	HL,BC

		LD	A,H		
		AND	0FH
		LD	H,A
						
		AND	A			;RESET CARRY
		LD	BC,(CODICE_BIANCO)
		SBC	HL,BC			
		LD	BC,(CODICE_ENC_B2)	;AGGIORNO IL VALORE PRECEDENTE
		JP	C,CODICE_N1		;HL<CODICE_BIANCO


		;TROVATO SEGNO
		AND	A		;RESET CARRY

CODICE_FINE:
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		RICONOSCIMENTO CODICE_EASY
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CODICE_EASY:


CODICE_EASY_WAIT_B:				;ATTENDO IL PRIMO SEGNO BIANCO
		IN      A,(PB)          
		BIT     2,A		
		JR	Z,CODICE_EASY_WAIT_B		


CODICE_EASY_N1:					;ATTENDO IL PRIMO SEGNO NERO
		IN      A,(PB)          
		BIT     2,A		
		JR	NZ,CODICE_EASY_N1

CODICE_EASY_B1:					;ASPETTA MENTRE LEGGE BIANCO
		IN      A,(PB)          
		BIT     2,A		
		JR	Z,CODICE_EASY_B1

		IN      A,(10H)			;VALORE ENCODER ATTUALE
		LD      L,A
		IN      A,(20H)
		LD      H,A	
		LD	(CODICE_ENC_B1),HL

CODICE_EASY_FINE:
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE DI RITARDO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

RIT:            IN      A,(PB)
		BIT     4,A
		JR      Z,RIT
RIT1:           IN      A,(PB)
		BIT     4,A
		JR      NZ,RIT1
		DEC     E
		JR      NZ,RIT
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE DI CONTROLLO CODICE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

MARC:           LD      A,(SEGNO)       ;MISURA 2mm IN      
		LD      B,A             ;MODO CHE IN
MAR1:           IN      A,(PB)          ;QUESTO SPAZIO
		BIT     2,A             ;PASSI IL SEGNO
		JR      NZ,RIT0         ;DI CODICE
		BIT     4,A
		JR      Z,MAR1
MAR2:           IN      A,(PB)
		BIT     2,A
		JR      NZ,RIT0
		BIT     4,A
		JR      NZ,MAR2
		DEC     B
		JR      NZ,MAR1
RIT0:           RET




;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ADEGUAMENTO GUADAGNO AMPLIFICAT. ESTERNO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
 
GAINS1:         EXX
		LD      A,(GUADS1)
		CP      60H
		JR      Z,GS11
		CP      40H
		JR      Z,GS12
		CP      20H
		JR      Z,GS13
		CP      00H
		JR      Z,GS14
GS11:           LD      A,40H
		JR      GS15
GS12:           LD      A,20H
		JR      GS15
GS13:           LD      A,00H
		JR      GS15
GS14:           LD      A,60H
GS15:           LD      (GUADS1),A
		EXX
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CONTROLLO PARAMETRI E EVENTUALE
;		INSERIMENTO VALORI DI DEFAULT
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
                
DEFAUL:         LD      IX,TRIP         
		LD      (IX+01H),00H
                LD      A,(IX)
                CP      0FH
                JR      NC,DEF1
		JR      DEF2
DEF1:           LD      A,06H
		LD      (IX),A


DEF2:           LD      IX,GUADA
		LD      (IX+01H),00H
		LD      A,(IX)
		CP      00H
                JR      Z,DEF3
		CP      64H
                JR      NC,DEF3
                JR      DEF4
DEF3:           LD      A,0AH
		LD      (IX),A
		
DEF4:           LD      IX,CICLO
		LD      (IX+01H),00H
		LD      A,(IX)
		CP      00H
                JR      Z,DEF5
		CP      64H
                JR      NC,DEF5
                JR      DEF6
DEF5:           LD      A,03H
		LD      (IX),A
		
DEF6:           LD      IX,DERIVA
		LD      (IX+01H),00H
		LD      A,(IX)
		CP      00H
                JR      Z,DEF7
		CP      0FFH
                JR      NC,DEF7
                JR      DEF8
DEF7:           LD      A,02H
		LD      (IX),A
		
DEF8:           LD      IX,ZONA
		LD      (IX+01H),00H
		LD      A,(IX)
		CP      00H
                JR      Z,DEF9
		CP      65H
                JR      NC,DEF9
                JR      DEF10
DEF9:           LD      A,00H
		LD      (IX),A
		
DEF10:          LD      IX,INSER
		LD      (IX+01H),00H
		LD      A,(IX)
		CP      00H
                JR      Z,DEF11
                CP      0FFH            ;PPNN
                JR      NC,DEF11
                JR      DEF12
DEF11:          LD      A,0AH
		LD      (IX),A

DEF12:          LD      IX,SALTO
		LD      (IX+01H),00H
		LD      A,(IX)
		CP      0AH		
                JR      NC,DEF13
                JR      DEF14           
DEF13:          LD      A,09H
		LD      (IX),A



DEF14:          LD      IX,ZONT
		LD      (IX+01H),00H
		LD      A,(IX)
		CP      00H
                JR      Z,DEF15
                CP      65H
                JR      NC,DEF15
                JR      DEF16
DEF15:          LD      A,00H
		LD      (IX),A

DEF16:          LD      IX,LINGUA
		LD      (IX+01H),00H
		LD      A,(IX)
                CP      0FFH            
                JR      Z,DEF17
                CP      06H
                JR      NC,DEF17
                JR      DEF18
DEF17:          LD      A,01H
		LD      (IX),A

                ;AAOO ELIMINATO OSGAIN

DEF18:          LD      A,(GUADS1)      ;SE IN PARTENZA
                CP      00H             ;NON C'E UN
                JR      Z,DEF19         ;VALORE NORMALE
                CP      20H             ;CREA CASINO 
                JR      Z,DEF19         ;IN SERITA.
                CP      40H
                JR      Z,DEF19
                LD      A,60H
                LD      (GUADS1),A
		CALL	SET_GUAD_S1


DEF19:		LD	BC,VERSIONE_PROGRAMMA	;CARICO LA VERSIONE DEL PROGRAMMA
		LD	(VERPRO),BC

DEF20:		
		LD	A,(AUMALO+01H)
		CP	0FFH
		JR	NZ,DEF21
		LD	A,00H
		LD	(AUMALO),A
		LD	(AUMALO+01H),A
DEF21:		
		LD	A,(AUMATR+01H)
		CP	0FFH
		JR	NZ,DEF22
		LD	A,00H
		LD	(AUMATR),A
		LD	(AUMATR+01H),A
DEF22:		
		LD	A,(AUMATI+01H)
		CP	0FFH
		JR	NZ,DEF23
		LD	A,00H
		LD	(AUMATI),A
		LD	(AUMATI+01H),A

DEF23:		
		LD      HL,(FASE)	;SE POSIZIONE FASE > 4096 (IMPULSI ENCODER)
		LD      DE,1000H	;RESET DEL VALORE
		SBC     HL,DE         
		JR      C,DEF24 
		LD	BC,100H
		LD	(FASE),BC
DEF24:		
		LD      HL,(FASES1)	;SE POSIZIONE S1 > 4096 (IMPULSI ENCODER)
		LD      DE,1000H	;RESET DEL VALORE
		SBC     HL,DE         
		JR      C,DEF25 
		LD	BC,100H
		LD	(FASES1),BC
DEF25:		
		LD      HL,(FASRIF)	;SE POSIZIONE RIFERIMENTO S1 > 4096 (IMPULSI ENCODER)
		LD      DE,1000H	;RESET DEL VALORE
		SBC     HL,DE         
		JR      C,DEF26 
		LD	BC,100H
		LD	(FASRIF),BC

DEF26:          RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ROUTINE DI COMPARAZIONE A 16 BIT
;		COMP16A <=> COMP16B
;		RITORNA IN COMP16A
;		  0 A=B
;		  1 A>B
;		  2 A<B
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

COMPARE16:
		AND	A		;RESET CARRY

		LD	HL,(COMP16A)
		LD	BC,(COMP16B)
		SBC	HL,BC

		JR	Z,COMPARE_A_E_B
		JR	C,COMPARE_A_L_B
		JR	COMPARE_A_G_B

COMPARE_A_E_B:	
		LD	BC,0000H
		LD	(COMP16A),BC
		JR	COMPARE16_FINE
COMPARE_A_G_B:
		LD	BC,0001H
		LD	(COMP16A),BC
		JR	COMPARE16_FINE
COMPARE_A_L_B:
		LD	BC,0002H
		LD	(COMP16A),BC
		JR	COMPARE16_FINE

COMPARE16_FINE:
		AND	A		;RESET CARRY
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ROUTINE DI DIVISIONE A 16 BIT
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

DIVIS:          EXX
		LD      DE,(DIVDEN)
		LD      BC,(DIVSOR)
DIV16:          XOR     A
		LD      H,A
		LD      L,A
		LD      A,10H
DV0:            RL      E
		RL      D
		ADC     HL,HL
		SBC     HL,BC
		JR      NC,DIV1
		ADD     HL,BC
DIV1:           CCF
		DEC     A
		JR      NZ,DV0
		LD	(RESTO),HL	;SSCC
		EX      DE,HL
		ADC     HL,HL
		LD      (QUOZIE),HL
		EXX
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ROUTINE DI MOLTIPLICAZIONE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

MOLT:           EXX
		LD      DE,(MOLTI)      ;MOLTIPLICANDO
		LD      A,(MOLPL)       ;MOLTIPLICATORE
		LD      HL,00H
		LD      B,08H
MULT:           ADD     HL,HL
		RLA
		JR      NC,CHCNT
		ADD     HL,DE
CHCNT:          DJNZ    MULT            ;SE IL RISULTATO
		CP      00H             ;E'> DI FFFFH IL
		JR      Z,CHC1          ;REGISTRO A SARA'
		LD      HL,0FFFFH       ;DIVERSO DA 0 PER
CHC1:           LD      (PROD),HL       ;CUI CARICA IL MAX
		EXX                     ;(SOLO IN CASO DI
		RET                     ;CORREZ.MOTORE)

                
 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ROUTINE DI MOLTIPLICAZIONE A 24 BIT  
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
                
MOL24:          EXX                                                                                        
		LD      DE,(MOLT24)
		LD      A,(MOLP24)
		LD      BC,800H                
		LD      H,C
		LD      L,C
MPX1:           ADD     HL,HL
		RLA
		JR      NC,MPX2
		ADD     HL,DE
		ADC     A,C             ;IL RISULTATO
MPX2:           DJNZ    MPX1            ;SI TROVA IN AHL
		LD      (PROD24),HL
		LD      (PROD24+02H),A
		EXX
		RET

 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		DIVISIONE PER 4096 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
                         

DI4096:         EXX
		LD      HL,(DIV4096)
		LD      A,(DIV4096)
		LD      B,0DH           ;PER DIVIDERE
MPX3:           DJNZ    MPX4            ;IN 4096 SHIFTO
		JR      MPX8            ;A DESTRA 12 VOLTE
MPX4:           SRL     L
		SRL     H
		JR      C,MPX6
MPX5:           SRL     A
		JR      C,MPX7
		JR      MPX3
MPX6:           CCF
		SET     7,L
		JR      MPX5
MPX7:           CCF
		SET     7,H
		JR      MPX3
MPX8:           LD      (QU4096),HL
		EXX
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		DIVISIONE 24 PER 8 BIT (DIVISIONE PER 1000)
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

DI1000:         EXX
                LD      A,(ALTDIV)      
                CP      00H
                JR      Z,DI1004
                CP      01H             
                JR      Z,DI1005
                LD      IX,PTO032
                JR      DI1006
DI1004:         LD      IX,RESTO0
                JR      DI1006
DI1005:         LD      IX,TREST0       
DI1006:         LD      DE,3E8H         ;1000
                LD      BC,0000H
		LD      A,16
		LD      (MDIVIS),A
DI1001:         LD      H,(IX+03H)
		LD      L,(IX+02H)
		SBC     HL,DE
		JP      M,DI1002
		LD      (IX+03H),H      ;RESTO
		LD      (IX+02H),L
		INC     BC
DI1002:         SLA     (IX+00H)        ;ROTAZ.DIV.DO
		RL      (IX+01H)
		RL      (IX+02H)
		RL      (IX+03H)
		SLA     C               ;ROTAZ.QUO.TE
		RL      B
		LD      A,(MDIVIS)
		DEC     A
		LD      (MDIVIS),A
		JR      NZ,DI1001
		LD      H,(IX+03H)
		LD      L,(IX+02H)
		SBC     HL,DE
		JP      M,DI1003
		INC     BC
DI1003:         LD      (QU1000),BC
		EXX
		RET
		

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		DECREMENTA CONTATORE A 32 BIT
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		
DEC32:
		EXX
		
		LD	HL,(COUNTER32)		;INDIRIZZAMENTO INDIRETTO
		INC	HL			;VERSO LA WORD BASSA
		INC	HL

		
		LD	E,(HL)			;CARICO LA WORD BASSA IN DE
		INC	HL
		LD	D,(HL)

		LD	BC,01H
		
		AND     A			;RESET CARRY

		EX	DE,HL			;DE = DE-BC
                SBC     HL,BC	
		EX	DE,HL

		LD	(HL),D			;SCRIVO LA WORD BASSA
		DEC	HL
		LD	(HL),E

		JR      NC,DEC32_FINE		;SE NON C'E' CARRY HO TERMINATO, ALTRIMENTI DECREMENTO PAROLA ALTA



		LD	HL,(COUNTER32)		;INDIRIZZAMENTO INDIRETTO
		LD	E,(HL)			;CARICO LA WORD ALTA IN DE
		INC	HL
		LD	D,(HL)		

		DEC	DE			;DECREMENTO WORD ALTA

		LD	(HL),D			;SCRIVO LA WORD ALTA
		DEC	HL
		LD	(HL),E		



DEC32_FINE:
		EXX
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CHECK CONTATORE A 32 BIT = 00000000H
;		(CONTROLLO 32 BIT A PARTIRE DA AREA DI MEMORIA
;		INDICATA IN COUNTER32
;
;		SE CONTATORE E' = 0, COUNTER32 = FFFFH
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		
CHK32:
		EXX
		LD	HL,(COUNTER32)
		LD	E,(HL)
		INC	HL
		LD	A,(HL)
		OR	E
		JR      NZ,CHK32_FINE		
		
		INC	HL
		LD	E,(HL)
		INC	HL
		LD	A,(HL)
		OR	E
		JR      NZ,CHK32_FINE		

		JR	CHK32_ZERO

CHK32_ZERO:	
		LD	DE,0FFFFH
		LD	(COUNTER32),DE
CHK32_FINE:
		EXX
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		MOLTIPLICAZIONE A 32 BIT
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

MOL32:          EXX
	        
                LD      B,04H
		LD      HL,PTO032
INIT:           LD      (HL),00H
		INC     HL
		DJNZ    INIT
SHIFTX:         LD      B,04H
		LD      IX,MDO032+03H
		XOR     A
RTX:            RR      (IX)
		DEC     IX
		DJNZ    RTX
		JR      NC,ZERO
		LD      B,04H
		LD      HL,PTO032
		LD      IY,MRE032
		CCF
NXTBYT:         LD      A,(IY)
		ADC     A,(HL)
		LD      (HL),A
		INC     IY
		INC     HL
		DJNZ    NXTBYT
ZERO:           LD      B,04H
		LD      IX,MDO032
		XOR     A
ZCHK:           OR      (IX)
		JR      NZ,SHIFTY
		INC     IX
		DJNZ    ZCHK
		JR      END32
SHIFTY:         LD      B,04H
		LD      IY,MRE032
		XOR     A
LEFTY:          RL      (IY)
		INC     IY
		DJNZ    LEFTY
		JR      SHIFTX
END32:          EXX
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		DIVISIONE, 32BIT DIVISO 4096
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		
 

DIV32_4096:      
		EXX
		LD      HL,(EDEND0)
		LD      DE,(EDEND2)
		LD      B,0DH           ;PER DIVIDERE
MPW1:           DJNZ    MPW2            ;IN 4096 SHIFTO
		JR      MPW8            ;A DESTRA 12 VOLTE
MPW2:           SRL     L
		SRL     H
		JR      C,MPW7
MPW3:           SRL     E
		JR      C,MPW6
MPW4:           SRL     D
		JR      C,MPW5
		JR      MPW1
MPW5:           CCF
		SET     7,E
		JR      MPW1
MPW6:           CCF
		SET     7,H
		JR      MPW4
MPW7:           CCF
		SET     7,L
		JR      MPW3
MPW8:           LD      (QUOZ32),HL

DIV32_4096_FINE:
		EXX
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SHIFT 32 BIT (USATO IN DIVISIONE)
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		
RLULONG:                
        LD      HL,06H;#2+4
        ADD     HL,SP
        LD      C,(HL)
        DEC     HL
        LD      D,(HL)
        DEC     HL
        LD      E,(HL)
        DEC     HL
        LD      A,(HL)
        DEC     HL
        LD      L,(HL)
        LD      H,A

        LD      A,C
RLULONG_01:
        OR      A
        RET     Z

        RL      L
        RL      H
        RL      E
        RL      D

        DEC     A        
        JP      RLULONG_01
RLULONG_FINE:


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		DIVISIONE 32 BIT
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		
;#DEFINE MSB_SET(X) ((X >> (8*SIZEOF(X)-1)) & 1)
;UNSIGNED LONG _DIV32 (UNSIGNED LONG X, UNSIGNED LONG Y)
;{
;  UNSIGNED LONG RESTE = 0L;
;  UNSIGNED CHAR COUNT = 32;
;  BOOL C;
;
;  DO
;  {
;    // RESTE: X <- 0;
;    C = MSB_SET(X);
;    X <<= 1;
;    RESTE <<= 1;
;    IF (C)
;      RESTE |= 1L;
;
;    IF (RESTE >= Y)
;    {
;      RESTE -= Y;
;      // X <- (RESULT = 1)
;      X |= 1L;
;    }
;  }
;  WHILE (--COUNT);
;  RETURN X;
;}
;
;
;DIV32 (UNSIGNED LONG X, UNSIGNED LONG Y)
DIV32_INNER:
	PUSH	IX
	LD	IX,00H
	ADD	IX,SP
	LD	HL,0FFFAH
	ADD	HL,SP
	LD	SP,HL
;unsigned long reste = 0L;
	XOR	A
	LD	(IX-4),A
	LD	(IX-3),A
	LD	(IX-2),A
	LD	(IX-1),A
;do
	LD	(IX-5),20H
DIV32_INNER_01:
;c = MSB_SET(x);
	LD	A,(IX+7)
	RLC	A
	AND	01H
	LD	C,A
	LD	(IX-6),C
;x <<= 1;
	LD	A,01H
	PUSH	AF
	INC	SP
	LD	L,(IX+6)
	LD	H,(IX+7)
	PUSH	HL
	LD	L,(IX+4)
	LD	H,(IX+5)
	PUSH	HL
	CALL	RLULONG
	LD	B,H
	LD	C,L
	POP	AF
	POP	AF
	INC	SP
	LD	(IX+4),C
	LD	(IX+5),B
	LD	(IX+6),E
	LD	(IX+7),D
;reste <<= 1;
	LD	A,01H
	PUSH	AF
	INC	SP
	LD	L,(IX-2)
	LD	H,(IX-1)
	PUSH	HL
	LD	L,(IX-4)
	LD	H,(IX-3)
	PUSH	HL
	CALL	RLULONG
	LD	B,H
	LD	C,L
	POP	AF
	POP	AF
	INC	SP
	LD	(IX-4),C
	LD	(IX-3),B
	LD	(IX-2),E
	LD	(IX-1),D
;if (c)
	XOR	A
	OR	(IX-6)
	JP	Z,DIV32_INNER_02
;reste |= 1L;
	LD	A,(IX-4)
	OR	01H
	LD	(IX-4),A
DIV32_INNER_02:
;if (reste >= y)
	LD	A,(IX-4)
	SUB	(IX+8)
	LD	A,(IX-3)
	SBC	A,(IX+9)
	LD	A,(IX-2)
	SBC	A,(IX+10)
	LD	A,(IX-1)
	SBC	A,(IX+11)
	JP	C,DIV32_INNER_03
;reste -= y;
	LD	A,(IX-4)
	SUB	(IX+8)
	LD	(IX-4),A
	LD	A,(IX-3)
	SBC	A,(IX+9)
	LD	(IX-3),A
	LD	A,(IX-2)
	SBC	A,(IX+10)
	LD	(IX-2),A
	LD	A,(IX-1)
	SBC	A,(IX+11)
	LD	(IX-1),A
;x |= 1L;
	LD	A,(IX+4)
	OR	01H
	LD	(IX+4),A
DIV32_INNER_03:
;while (--count);
	DEC	(IX-5)
	XOR	A
	OR	(IX-5)
	JP	NZ,DIV32_INNER_01
;return x;
	LD	L,(IX+4)
	LD	H,(IX+5)
	LD	E,(IX+6)
	LD	D,(IX+7)

	LD	SP,IX
	POP	IX
	RET
DIV32_INNER_END:
             

DIV32:
	PUSH	IX
	LD	IX,00H
	ADD	IX,SP
	LD	HL,0FFFAH
	ADD	HL,SP
	LD	SP,HL
	;(*QUOZIENTE) =   DIV32_INNER ((*DIVISORE), (*DIVIDENDO));
	LD	HL,DIV32_QUOZ_L
	LD	(IX-2),l	;#0X8A
	LD	(IX-1),H	;#0XC0
	LD	HL,DIV32_DIVIS_L
	LD	A,(HL)
	LD	(IX-6),A
	INC	HL
	LD	A,(HL)
	LD	(IX-5),A
	INC	HL
	LD	A,(HL)
	LD	(IX-4),A
	INC	HL
	LD	A,(HL)
	LD	(IX-3),A
	LD	HL,DIV32_DIVID_L
	LD	C,(HL)
	INC	HL
	LD	B,(HL)
	INC	HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	L,(IX-4)
	LD	H,(IX-3)
	PUSH	HL
	LD	L,(IX-6)
	LD	H,(IX-5)
	PUSH	HL
	PUSH	DE
	PUSH	BC
	CALL	DIV32_INNER
	LD	B,H
	LD	C,L
	POP	AF
	POP	AF
	POP	AF
	POP	AF
	LD	L,(IX-2)
	LD	H,(IX-1)
	LD	(HL),C
	INC	HL
	LD	(HL),B
	INC	HL
	LD	(HL),E
	INC	HL
	LD	(HL),D
	LD	SP,IX
	POP	IX
	RET
DIV32_FINE:


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SOTTRAZIONE 32 BIT
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

SOTT32:         EXX
		AND     A
		LD      HL,(MINUE0)     ;SALVO MINU-
		LD      (MIXUE0),HL     ;ENDO E SOT-
		LD      HL,(MINUE2)     ;TRAENDO
		LD      (MIXUE2),HL     ;ORIGINALI
		LD      HL,(SUBTR0)
		LD      (SUXTR0),HL
		LD      HL,(SUBTR2)
		LD      (SUXTR2),HL
		LD      HL,(MIXUE2)     ;VERIFICO CHI E'
		LD      DE,(SUXTR2)     ;IL NUMERO PIU'
		SBC     HL,DE           ;GRANDE PER PER
		JR      Z,SOT01         ;NON AVERE IL
		JR      NC,SOT03        ;RISULTATO NEGA-
		JR      SOT02           ;TIVO E PER
SOT01:          LD      HL,(MIXUE0)     ;DETERMINARE IL
		LD      DE,(SUXTR0)     ;SENSO DI CORREZ-
		SBC     HL,DE           ;ZIONE
		JR      NC,SOT03
SOT02:          LD      HL,(MIXUE0)
		LD      DE,(SUXTR0)
		LD      (MIXUE0),DE
		LD      (SUXTR0),HL
		LD      HL,(MIXUE2)
		LD      DE,(SUXTR2)
		LD      (MIXUE2),DE
		LD      (SUXTR2),HL     
		LD      A,D_RITARDO
		LD      (MSCORR),A
		JR      SOT04
SOT03:          LD      A,D_AVANTI
		LD      (MSCORR),A
SOT04:          LD      HL,(MIXUE0)
		LD      DE,(MIXUE2)
		LD      BC,(SUXTR0)
		AND     A
		SBC     HL,BC
		LD      (RESTO0),HL
                LD      (MREST0),HL     ;MEMORIZZO
                JR      NC,SOT05        ;IL RESTO
                DEC     DE              ;PERCHE' LA
SOT05:          EX      DE,HL           ;LOCAZIONE
                LD      DE,(SUXTR2)     ;REST0 VIENE
                AND     A               ;IN SEGUITO
                SBC     HL,DE           ;MODIFICATA
		LD      (RESTO2),HL
                LD      (MREST2),HL     
		EXX
		RET

                

TSOTT32:        EXX
		AND     A
                LD      HL,(TINUE0)     ;SALVO MINU-
                LD      (TIXUE0),HL     ;ENDO E SOT-
                LD      HL,(TINUE2)     ;TRAENDO
                LD      (TIXUE2),HL     ;ORIGINALI
                LD      HL,(TUBTR0)
                LD      (TUXTR0),HL
                LD      HL,(TUBTR2)
                LD      (TUXTR2),HL
                LD      HL,(TIXUE2)     ;VERIFICO CHI E'
                LD      DE,(TUXTR2)     ;IL NUMERO PIU'
		SBC     HL,DE           ;GRANDE PER PER
                JR      Z,TSOT01        ;NON AVERE IL
                JR      NC,TSOT03       ;RISULTATO NEGA-
                JR      TSOT02          ;TIVO E PER
TSOT01:         LD      HL,(TIXUE0)     ;DETERMINARE IL
                LD      DE,(TUXTR0)     ;SENSO DI CORREZ-
		SBC     HL,DE           ;ZIONE
                JR      NC,TSOT03
TSOT02:         LD      HL,(TIXUE0)
                LD      DE,(TUXTR0)
                LD      (TIXUE0),DE
                LD      (TUXTR0),HL
                LD      HL,(TIXUE2)
                LD      DE,(TUXTR2)
                LD      (TIXUE2),DE
                LD      (TUXTR2),HL     
		LD      A,D_DESTRA
                LD      (TMSCOR),A
                JR      TSOT04
TSOT03:         LD      A,D_SINISTRA
                LD      (TMSCOR),A
TSOT04:         LD      HL,(TIXUE0)
                LD      DE,(TIXUE2)
                LD      BC,(TUXTR0)
		AND     A
		SBC     HL,BC
                LD      (TREST0),HL
                JR      NC,TSOT05
		DEC     DE
TSOT05:         EX      DE,HL
                LD      DE,(TUXTR2)
		AND     A
		SBC     HL,DE
                LD      (TREST2),HL
		EXX
		RET


		
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SOMMA A 32 BIT
;
;	AUGENDO = ADDENDO + AUGENDO
;	si definisce il primo operando addendo e il secondo augendo. 
;	Il risultato somma va poi a sostituire il valore dell'augendo 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		
                

SOMM32:         EXX
		LD      DE,ADDEN0
		LD      HL,AUGEN0
		LD      B,04H
		AND     A
ADDW:           LD      A,(DE)
		ADC     A,(HL)
		LD      (HL),A
		INC     DE              ;IL RISULTATO
		INC     HL              ;SI TROVA NELLA
		DJNZ    ADDW            ;LOCAZIONE 
		EXX                     ;ADDEN0 E ADDEN2
		RET
                
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SOTTRAZIONE A 32 BIT
;
;	RESTO = MINUENDO - SOTTRAENDO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

CSOTT32:        EXX
                AND     A
                LD      HL,(CINUE2)
                LD      DE,(CUBTR2)
                SBC     HL,DE
                JR      NC,COT00
                LD      HL,(CINUE2)
                LD      (CINUE2),DE
                LD      (CUBTR2),HL
                LD      HL,(CINUE0)
                LD      DE,(CUBTR0)
                LD      (CINUE0),DE
                LD      (CUBTR0),HL
COT00:          LD      HL,(CINUE0)
                LD      DE,(CINUE2)
                LD      BC,(CUBTR0)
                AND     A
                SBC     HL,BC
                LD      (CESTO0),HL
                JR      NC,COT01
                DEC     DE
COT01:          EX      DE,HL
                LD      DE,(CUBTR2)
                AND     A
                SBC     HL,DE
                LD      (CESTO2),HL
                EXX
                RET

               
CSOM32:         EXX
                SCF
                CCF
                LD      HL,(CADDE0)
                LD      DE,(CAUGE0)
                ADC     HL,DE
                LD      (CSOMM0),HL
                LD      HL,(CADDE2)
                LD      DE,(CAUGE2)
                ADC     HL,DE
                LD      (CSOMM2),HL
                SCF
                CCF
                EXX
                RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		GESTIONE DELLA CORREZIONE SUPERIORE A MEZZO GATE  
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		


OVERCO:
		LD      A,(SUPCOR)	;SE C'E' STATO UN ERRORE MAGGIORE 
                CP      01H		;DI MEZZO GATE
                JR      NZ,OVERCO_FINE

	        LD      HL,(ERATTU)     ;SE ERRORE > 1mm  
                LD      DE,0064H        ;SALTO SE NO  
                SBC     HL,DE           ;RICARICO LA
                JR      NC,OVERC3       ;SOVRA CORREZ-
                CCF                     ;ZIONE.

                LD      A,(SHIFT3)      ;METTO IN SHIFT
                DEC     A               ;AMPI40 FINO
                LD      (SHIFT3),A      ;A CHE SHIFT1
                CP      00H             ;ARRIVI A ZERO
                JR      Z,OVERC1
                LD      DE,(AMPI40)     
                LD      (SHIFT),DE      
                JR      OVERC2

OVERC1:         LD      DE,(SHIFT4)     ;ALLA FINE 
                LD      (SHIFT),DE      ;CARICO IL
                LD      A,00H           ;RESTO IN
                LD      (SUPCOR),A      ;SHIFT

OVERC2:         CALL    SPOSTA

OVERC3:         LD      A,(TIRCIC)      ;QUADRU-
                RLA                     ;PLICO
                RLA                     ;IL TEMPO DI 
                LD      (INCREM),A      ;ATTESA DEL
                LD      (DECREM),A      ;TIRO 
                LD      (TIRCIR_ATT),A      
OVERCO_FINE:
                RET





;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CALCOLO DELLA  VELOCITA' 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

		;4mS x 4096 = 16384000
		;60\1638400 = 0,000003662
		;0,000003662 x CILINDRO = X
		;X\1000 = COSVEL (COSVEL E' CALCOLATA AL CAMBIO CILINDRO)
		;COSVEL x VELIMP = Y
		;Y/1000 = VELOCITA m/m

VELO:           
		LD      DE,(COSVEL)
		LD      (MDO032),DE
		LD      DE,0000H
		LD      (MDO232),DE
		LD      (MRE232),DE
		LD      DE,(VELIMP)
		LD      (MRE032),DE
		LD      A,02H           
		LD      (ALTDIV),A      
		CALL    MOL32
		CALL    DI1000
		LD      BC,(QU1000)     ;VELOCITA         
		LD      (VELIST),BC     ;ISTANTANEA    
		
                ;MEDIA VELOCITA SU QUATTRO LETTURE  WWNN

                LD      B,03H        
                LD      IX,ARR_VELO      
                LD      DE,0004H        
                ADD     IX,DE
MEDV01:
                LD      E,(IX)          ;DATI LETTI
                LD      D,(IX+01H)      ;INSERENDO
                LD      (IX+02H),E      ;IL NUOVO
                LD      (IX+03H),D      ;DATO AL
                DEC     IX              ;PRIMO POSTO
                DEC     IX              ;ED ELIMINANDO
                DJNZ    MEDV01          ;L'ULTIMO
                INC     IX
                INC     IX


                LD      DE,(VELIST)
                LD      (ARR_VELO),DE

                LD      B,04H            ;SOMMA DELLE
                JR      NC,MEDV02        ;4 CELLE
                CCF                      ; 
MEDV02:         LD      HL,0000H
MEDV03:
                LD      E,(IX+00H)
                LD      D,(IX+01H)
                ADC     HL,DE
                INC     IX
                INC     IX
                DJNZ    MEDV03
                
                LD      (DIVDEN),HL
                LD      BC,0004H  
                LD      (DIVSOR),BC    
		CALL    DIVIS
		LD      HL,(QUOZIE)
		LD      (VELMED),HL

		CALL	CHKTRIP		;CONTROLLO SE LA DIFFERENZA DI VELOCITA' 
					;IN VALORE ASSOLUTO
					;E' SUPERIORE A TRIP

		LD      BC,(VELMED)	;SALVO LA VELOCITA PER FARE IL CALCOLO DEL TRIP
		LD	(VELPRE),BC	;AL PROSSIMO GIRO
MEDV05:        
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE CONTROLLO SE LA DIFFERENZA DI VELOCITA' 
;		IN VALORE ASSOLUTO E' SUPERIORE A TRIP
;		SE VERO... MESSAGGIO ACCELERA O DECELERA SU DISPLAY
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

CHKTRIP:
			
                LD	A,(AUMALO)	;SE E'IN MANUALE
		CP	00H		;ESCO
                JR      Z,ENDCHKTRIP

                LD      IX,TRIP         ;SE TRIP=0 

                LD      A,(IX)          ;SE TRIP=0 
		CP      00H             ;ESCO
                JR      Z,ENDCHKTRIP	;
		
;                LD      A,(PROVEL)      ;SE IN PROTEZIONE 
;                CP      01H             ;(ALLARME) ATTIVO
;                JR      Z,ENDCHKTRIP    ;ESCO
		
                LD      HL,(VELMED)     ;CONTROLLO SE
                LD      DE,(VELPRE)     ;TRA LA NUOVA 
                SBC     HL,DE           ;E LA VECCHIA 
                JR      C,CHKTRIPD      ;VELOCITA'C'E' 
                LD      A,L             ;UN DIFFERENZA
                CP      (IX)            ;TRIP IN METRI 
                JP      M,NO_TRIP	;IN PIU' CHE
                CALL    ACCEL           ;MENO SE C'E'
		JR      ENDCHKTRIP      ;BLOCCO CON

CHKTRIPD:       CCF                     ;LA SCRITTA  
                LD      HL,(VELPRE)     ;APPROPIATA
                LD      DE,(VELMED)
                SBC     HL,DE
                LD      A,L
                CP      (IX)                         
                JP      M,NO_TRIP
                CALL    DECEL
		JR      ENDCHKTRIP      ;BLOCCO CON
NO_TRIP:
		CALL	VELCOST
ENDCHKTRIP:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		TRASMISSIONE LED CORREZIONI
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

SEND_LED:
		EXX
		LD	A,(MEMLED)	;SE E' UGUALE
		LD	B,A		;ALLA PRECEDENTE
		LD	A,(MEMC)	;SALTO 
		AND	0EBH		;0C3H
		CP	B
		JR	Z,SEND_LED_FINE
		LD	E,A

		CALL	REQISFREE
		LD	A,(LEGGI)	;CORREZIONE 
		LD	(ARR_TRASM+00H),A  ;AVANTI
		LD	A,(STATLED)
		LD	(ARR_TRASM+01H),A
		LD	D,00H
		LD	HL,ARR_TRASM+02H
		LD	(HL),D
		INC	HL
		LD	A,(MEMC)	;STATO CORREZIONE
		AND	0EBH		;0C3H
		LD	(HL),A
		CALL	REQUEST

		LD	A,(MEMC)	;STATO CORREZIONE
		LD	(MEMLED),A

		EXX
SEND_LED_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		RESET USCITE CORREZIONE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

RESEPC:
		LD	A,(MEMC)	
		RES	B_PC_AVANTI,A
		RES	B_PC_RITARDO,A
		RES	B_PC_FUNZ,A
		RES	B_PC_DESTRA,A
		RES	B_PC_SINISTRA,A
		LD	(MEMC),A
		OUT	(PC),A

RESPE1:         RET




;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CALCOLO VARIABILI PER MEDIA ERRORE LONG.
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		


VARERR:                                  ;
                LD      A,(CICLO)
                CP      09H
                JR      NC,MECI01
                LD      A,07H
                LD      (PRIMOB),A
                INC     A
                LD      (SECONB),A
                LD      A,0CH
                LD      (VALORX),A
                JR      MECI04
MECI01:
                LD      A,(CICLO)
                CP      11H             ;= 17
                JR      NC,MECI02
                LD      A,0FH
                LD      (PRIMOB),A
                INC     A
                LD      (SECONB),A
                LD      A,1CH           ;= 28
                LD      (VALORX),A
                JR      MECI04
MECI02:
                LD      A,(CICLO)
                CP      21H             ;= 33
                JR      NC,MECI03
                LD      A,1FH           ;= 31
                LD      (PRIMOB),A
                INC     A
                LD      (SECONB),A
                LD      A,3CH           ;= 60
                LD      (VALORX),A
                JR      MECI04
MECI03:
                LD      A,2FH           ;= 47
                LD      (PRIMOB),A
                INC     A
                LD      (SECONB),A
                LD      A,5CH           ;= 92
                LD      (VALORX),A
MECI04:
                
		
                LD      DE,(CICLO)      ;CALCOLO DEL 
                LD      (MOLTI),DE      ;75% DEL CICLO
                LD      A,19H           ;CHE VIENE USATO
                LD      (MOLPL),A       ;ALL'INIZIO DELLE
                CALL    MOLT            ;EXTRA CORREZ.
                LD      DE,(PROD)       ;PER FAR SI CHE
                LD      (DIVDEN),DE     ;VI SIA UNA 
                LD      BC,64H          ;BREVE INTERRU-
                LD      (DIVSOR),BC     ;ZIONE PER 
                CALL    DIVIS           ;EVITARE LA
                LD      DE,(QUOZIE)     ;CORREZIONE
                LD      HL,(CICLO)      ;CONTINUA
                SBC     HL,DE
                LD      (CICL75),HL

		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		VERIFICA VALORI DI SVILUPPO CILINDRO E AMPIEZZA GATE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

CHK_CILINDRO_GATE:
		LD	HL,(CILMOL)
		LD	(COMP16B),HL

		LD	HL,05DCH		;CILINDRO MINIMO 150MM
		LD	(COMP16A),HL
		CALL	COMPARE16		; 1500 <=> CILMOL
		LD	A,(COMP16A)
		CP	01			; 1500 >   CILMOL	
		JR	Z,CHK_CILINDRO_NO


		LD	HL,061A8H		;CILINDRO MASSIMO 2500MM
		LD	(COMP16A),HL
		CALL	COMPARE16		;25000 <=> CILMOL
		LD	A,(COMP16A)
		CP	02			;25000 <  CILMOL	
		JR	Z,CHK_CILINDRO_NO
		
		JR	CHK_CILINDRO_OK		;150<CILIN<2500

CHK_CILINDRO_NO:
		LD	DE,(S_SVILUPPO_CILINDRO)
		CALL	SET_STATUS
		JR	CHK_GATE

CHK_CILINDRO_OK:
		LD	DE,(S_SVILUPPO_CILINDRO)
		CALL	RES_STATUS
		JR	CHK_GATE


CHK_GATE:
		LD	HL,(AMPGA)
		LD	(COMP16B),HL

		LD	HL,0005H		;GATE MINIMO 5MM
		LD	(COMP16A),HL
		CALL	COMPARE16		; 5   <=> GATE
		LD	A,(COMP16A)
		CP	01			; 5   >   GATE	
		JR	Z,CHK_GATE_NO


		LD	HL,0258H		;GATE MASSIMO 600MM
		LD	(COMP16A),HL
		CALL	COMPARE16		;600 <=> GATE
		LD	A,(COMP16A)
		CP	02			;600 <  GATE	
		JR	Z,CHK_GATE_NO
		
		JR	CHK_GATE_OK		;5<GATE<600

CHK_GATE_NO:
		LD	DE,(S_AMPIEZZA_GATE)
		CALL	SET_STATUS
		JR	CHK_CILINDRO_GATE_FINE

CHK_GATE_OK:
		LD	DE,(S_AMPIEZZA_GATE)
		CALL	RES_STATUS
		JR	CHK_CILINDRO_GATE_FINE

CHK_CILINDRO_GATE_FINE:
	        RET                     



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		TRASMISSIONE CON ST7
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ST7:	IDENTIFICAZIONE PARAMETRO/FUNZIONE
;			GESTIONE PRIMI DUE BYTES	
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

ANALI1:
		LD	A,(ARR_TRASM+04H)
		LD	HL,LEGGI
		CP	(HL)
		JR	Z,ANA10
		LD	HL,SCRIVI
		CP	(HL)
		JR	Z,ANA10
		RET

ANA10:
		LD	A,(ARR_TRASM+04H)		;COPIA B4 IN B0
		LD	(ARR_TRASM+00H),A
		LD	A,(ARR_TRASM+05H)		;COPIA B5 IN B1
		LD	(ARR_TRASM+01H),A

		LD	A,(ARR_TRASM+05H)		;MOLTIPLICA PER 2
		JR	NC,ANA11		;E LO SALVA IN A
		CCF	
ANA11:		RLA
		
		LD      L,A			;SOMMA IL VALORE RICEVUTO
		LD      H,00H			;ALLA TRABELLA DI TRANSCODIFICA
		LD      DE,ARR_TAB_IDX
		ADD     HL,DE
		LD      E,(HL)			;HO L'AREA DI MEMORIA INDIRIZZATA IN DE
		INC	HL
		LD      D,(HL)
		EX	DE,HL
		LD      D,(HL)			;SCRIVO IN HL IL VALORE INDIRIZZATO DA DE
		INC	HL
		LD      E,(HL)
		LD	(ARR_TRASM+02H),DE
		RET	


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ST7:	SECONDA IDENTIFICAZIONE PARAMETRI
;			GESTIONE ULTIMI DUE BYTES	
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

ANALI2:
		LD	A,(ARR_TRASM+04H)
		LD	HL,SCRIVI
		CP	(HL)
		JR	Z,ANA20
		CP	(HL)
		JP	NC,ANA30
		RET
ANA20:
		LD	A,(ARR_TRASM+05H)	;MOLTIPLICA PER 2
		JR	NC,ANA21		;E LO SALVA IN A
		CCF	
ANA21:		
		RLA
		LD      L,A			;SOMMA IL VALORE RICEVUTO
		LD      H,00H			;ALLA TRABELLA DI TRANSCODIFICA
		LD      DE,ARR_TAB_IDX
		ADD     HL,DE
		LD      E,(HL)			;HO L'AREA DI MEMORIA INDIRIZZATA IN DE
		INC	HL
		LD      D,(HL)
		EX	DE,HL
		LD	DE,(ARR_TRASM+06H)
		LD	(HL),D
		INC	HL
		LD	(HL),E

		LD	A,(ARR_TRASM+05H)	
		LD	HL,CILINT		;SE CILINDRO, ESEGUO CALCOLI RELATIVI
		CP	(HL)			
		JR	Z,ANA22			

		LD	HL,AMPGAT		;SE AMPIEZZA GATE, ESEGUO CALCOLI RELATIVI
		CP	(HL)
		JR	Z,ANA22 

		LD	HL,ENCSEGN		;SE NUOVA POSIZIONE FASE ESEGUO LE MODIFICHE RELATIVE
		CP	(HL)
		JR	Z,ANA23

		LD	HL,BASEVALDAC		;SE NUOVA BASE PER DAC ESEGUO IL CALCOLO DI DACBASE
		CP	(HL)
		JR	Z,ANA24

		LD	HL,OFFSETVALDAC		;SE NUOVO OFFSET PER DAC ESEGUO IL CALCOLO DI DACBASE
		CP	(HL)
		JR	Z,ANA24

		JR	ANALI2_FINE

ANA22:		
		CALL	CAL_CILINDRO		
		JR	ANALI2_FINE

ANA23:
		;RESET IL CTC CON IN NUOVO VALORE	

		LD      A,00H			;IMPONGO MANUALE
		LD	(AUMALO),A
		LD	(AUMATI),A	
		LD	(AUMATR),A	

		CALL	RESEPC			;ANNULLO COMANDI


		LD      HL,(FASE)		;SPOSTO IL PUNTO DI FASE
		LD      A,H
		OUT     (20H),A		
		LD      A,L
		OUT     (10H),A

		LD	A,05H			;SETTO AUTOCENTRA PER FAR SI CHE APPENA IL SEGNO VIENE TROVATO
		LD	(AUTOCENTRA),A		;IL GATE SI AUTOCENTRI

		LD      A,C_MEMRIF		;4 GIRI ATTESA
		LD      (MEMRIF),A		;PER MEMORIZZARE
		JR	ANALI2_FINE

ANA24:						;SE NUOVO VALORE BASE DI DAC (0-1000)
		CALL	CONVERT_DAC_BASE	;AGGIORNO IL VALORE CORRENTE  (0-4096)
		LD	DE,(DACBASE)		;E AGGIORNO DAC
		LD	(DACVAL),DE
		CALL	DAC_TRASM

		JR	ANALI2_FINE
ANALI2_FINE:		
		RET



ANA30:
		LD	A,(ARR_TRASM+04H)
		LD	HL,AUTFAS
		CP	(HL)
		JR	NZ,ANA30_1
		CALL	FUNZ_AUTO_PHASE		;FASATURA AUTOMATICA
		JP	ANA30_FINE

ANA30_1:
		LD	HL,TESTES
		CP	(HL)
		JR	NZ,ANA30_2
		CALL	TEST_SH			;TEST DELLA TESTA
		JP	ANA30_FINE

ANA30_2:
		LD	HL,TESENC
		CP	(HL)
		JR	NZ,ANA30_3
		CALL	TEST_ENCODER		;TEST DELL'ENCODER
		JP	ANA30_FINE

ANA30_3:
		LD	HL,SHDATO		
		CP	(HL)
		JR	NZ,ANA30_4
		CALL	LTSHIFT		        ;VALORE DELLO SHIFT (IN AUTOMATICO)
		JR	ANA30_FINE

ANA30_4:
		LD	HL,SHMANU
		CP	(HL)
		JR	NZ,ANA30_5
		CALL	CORREZIONE_MANUALE	;SPOSTAMENTO MANUALE
		JR	ANA30_FINE

ANA30_5:
		LD	HL,SHSTOP
		CP	(HL)
		JR	NZ,ANA30_6
		CALL	STOP_CORREZIONE_MANUALE
		JR	ANA30_FINE

ANA30_6:
		LD	HL,NORDF9		
		CP	(HL)
		JR	NZ,ANA30_7
		CALL	F9_ON			;FUNZIONE COLD SEAL
		JR	ANA30_FINE

ANA30_7:
		LD	HL,INIPAR		
		CP	(HL)
		JR	NZ,ANA30_8
		CALL	FUNZ_INIT		;FUNZIONE INIZIALIZZAZIONE
		JR	ANA30_FINE

ANA30_8:
		LD	HL,AZZERA		;FUNZIONE ZERO
		CP	(HL)
		JR	NZ,ANA30_9
		CALL	FUNZ_RESET_ERROR	;FUNZIONE ANNULLA ERRORE
		JR	ANA30_FINE


ANA30_9:
		LD	HL,PULSAN		
		CP	(HL)
		JR	NZ,ANA30_10
		CALL	FUNZ_MANUAL_PHASE	;FASATURA A PULSANTE ESTERNO
		JR	ANA30_FINE

ANA30_10:
		LD	HL,TESTOUT		
		CP	(HL)
		JR	NZ,ANA30_11
		CALL	USCITE			;TEST USCITE
		JR	ANA30_FINE

ANA30_11:
		LD	HL,FORCERESET		
		CP	(HL)
		JR	NZ,ANA30_FINE
		CALL	FUNZ_RESET		;FORZA IL RESET DELLA SCHEDA Z80
		JR	ANA30_FINE
;
;ANA30_12:
;		LD	HL,FUNZ_TWIN_APP_01		
;		CP	(HL)
;		JR	NZ,ANA30_13
;		LD      A,(MEMD)		;IMPONE USCITA SELETTORE A 1 (NEGATO)        
;                RES     0,A			;CAMBIANDO PIN 0 DELLA PORTA D
;                LD      (MEMD),A
;                OUT     (PD),A
;		JR	ANA30_FINE
;
;ANA30_13:
;		LD	HL,FUNZ_TWIN_APP_02		
;		CP	(HL)
;		JR	NZ,ANA30_FINE
;		LD      A,(MEMD)		;IMPONE USCITA SELETTORE A 1 (NEGATO)        
;                SET     0,A			;CAMBIANDO PIN 0 DELLA PORTA D
;                LD      (MEMD),A
;                OUT     (PD),A
;		JR	ANA30_FINE
ANA30_FINE:
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		DAC:		INCREMENTO E DECREMENTO VALORE ATTUALE DAC
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
DAC_INCR_DECR:


		;SE LONGITUDINALE O LATERALE TIRO DISABILITATO, SALTO
		LD	A,(ENB_DAC_LON)
		CP	00H
		JR	Z,DAC_INCR_DECR_01
		CP	02H			;TENSIONE PROPORZIONALE
		JR	Z,DAC_INCR_DECR_01	;ALL'ERRORE (CORAZZA)

		LD	A,(ENB_DAC_TRA)
		CP	00H
		JR	Z,DAC_INCR_DECR_01

		JR	DAC_INCR_DECR_FINE

DAC_INCR_DECR_01:
		
		;SE REV_CORRECTION INVERTO I VALORI
		LD	A,(REV_CORRECTION)
		CP	00H
		JP	NZ,DAC_INCR_DECR_02

		;INVERTO I VALORI DI CORREZIONE
		LD	HL,(DAC_INCREM)
		LD	DE,(DAC_DECREM)
		LD	(DAC_INCREM),DE
		LD	(DAC_DECREM),HL
		
DAC_INCR_DECR_02:
		;INCREMENTO TEMPORANEAMENTE IL VALORE DEL DAC
		LD	HL,(DACBASE)
		LD	DE,(DAC_INCREM)
		ADD	HL,DE
		LD	(DACVAL),HL

		;DECREMENTO TEMPORANEAMENTE IL VALORE DEL DAC ????????H

		;CONTROLLO SE DACVAL-DAC_DECREM < 0    -->   DACVAL = 0
		LD	HL,(DACVAL)
		LD	(COMP16A),HL
		LD	HL,(DAC_DECREM)
		LD	(COMP16B),HL
		CALL	COMPARE16
		LD	A,(COMP16A)
		CP	02H			;DACVAL < DAC_DECREM
		JR	Z,DAC_INCR_DECR_03
		JR	DAC_INCR_DECR_04

DAC_INCR_DECR_03:	;DACVAL-DAC_DECREM < 0    -->   DACVAL = 0
		LD	HL,0000H
		JR	DAC_INCR_DECR_05

DAC_INCR_DECR_04:	;DACVAL-DAC_DECREM >= 0    -->   DACVAL = DACVAL-DAC_DECREM
		LD	HL,(DACVAL)
		LD	DE,(DAC_DECREM)
		AND     A               ;RESET CARRY
		SBC	HL,DE
		AND     A               ;RESET CARRY
		JR	DAC_INCR_DECR_05

DAC_INCR_DECR_05:
		LD	(DACVAL),HL
		CALL	DAC_TRASM

		;RESET VALORI TEMPORANEI
		LD	HL,0000H
		LD	(DAC_INCREM),HL
		LD	(DAC_DECREM),HL

DAC_INCR_DECR_FINE:		
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		DAC:		INCREMENTO E DECREMENTO VALORE BASE DAC
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
DAC_INCR_DECR_BASE:
		;SE TIRO DAC DISABILITATO, SALTO
		LD	A,(ENB_DAC_TIR)
		CP	00H
		JP	NZ,DAC_INCR_DECR_BASE_FINE


		;SE REV_CORRECTION INVERTO I VALORI
		LD	A,(REV_CORRECTION)
		CP	00H
		JP	NZ,DAC_INCR_DECR_BASE_01

		;INVERTO I VALORI DI CORREZIONE
		LD	HL,(DAC_INCREM_BASE)
		LD	DE,(DAC_DECREM_BASE)
		LD	(DAC_INCREM_BASE),DE
		LD	(DAC_DECREM_BASE),HL

DAC_INCR_DECR_BASE_01:

		;INCREMENTO TEMPORANEAMENTE IL VALORE DEL DAC
		LD	HL,(DACBASE)

		LD	DE,(DAC_INCREM_BASE)
		ADD	HL,DE
		LD	(DACBASE),HL


		LD	HL,(DACBASE)
		LD	(COMP16A),HL
		LD	HL,1000H
		LD	(COMP16B),HL
		CALL	COMPARE16
		LD	A,(COMP16A)

		CP	02H			;DACBASE < 1000
		JR	Z,DAC_INCR_DECR_BASE_02

		LD	HL,1000H
		LD	(DACBASE),HL
		
DAC_INCR_DECR_BASE_02:

		;DECREMENTO TEMPORANEAMENTE IL VALORE DEL DAC

		;CONTROLLO SE DACBASE-DAC_DECREM < 0    -->   DACBASE = 0
		LD	HL,(DACBASE)
		LD	(COMP16A),HL
		LD	HL,(DAC_DECREM_BASE)
		LD	(COMP16B),HL
		CALL	COMPARE16
		LD	A,(COMP16A)
		CP	02H			;DACBASE < DAC_DECREM
		JR	Z,DAC_INCR_DECR_BASE_03
		JR	DAC_INCR_DECR_BASE_04

DAC_INCR_DECR_BASE_03:	;DACBASE-DAC_DECREM < 0    -->   DACBASE = 0
		LD	HL,0000H
		JR	DAC_INCR_DECR_BASE_05

DAC_INCR_DECR_BASE_04:	;DACBASE-DAC_DECREM >= 0    -->   DACBASE = DACBASE-DAC_DECREM
		LD	HL,(DACBASE)
		LD	DE,(DAC_DECREM_BASE)
		AND     A               ;RESET CARRY
		SBC	HL,DE
		AND     A               ;RESET CARRY
		JR	DAC_INCR_DECR_BASE_05

DAC_INCR_DECR_BASE_05:
		LD	(DACBASE),HL
		LD	(DACVAL),HL
		CALL	DAC_TRASM

		CALL	CONVERT_DAC_BASE_1000
		CALL	SEND_DAC_BASE

		;RESET VALORI TEMPORANEI
		LD	HL,0000H
		LD	(DAC_INCREM_BASE),HL
		LD	(DAC_DECREM_BASE),HL

DAC_INCR_DECR_BASE_FINE:		
		RET







;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		DAC:		CONVERTO DACBASE1000 (0-1000) IN DACBASE (0-4096)
;				;DACBASE = DACOFFSET+(DACBASE1000*4)
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
CONVERT_DAC_BASE:
		LD	DE,(DACBASE1000)	;AGGIORNO IL VALORE CORRENTE E AGGIORNO DAC
		LD	(MOLTI),DE		
		LD	DE,04H
		LD	(MOLPL),DE
		CALL	MOLT
		LD	DE,(PROD)
		LD	HL,(DACOFFSET)
		ADD	HL,DE
		LD	(DACBASE),HL
CONVERT_DAC_BASE_FINE:		
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		DAC:		CONVERTO  DACBASE (0-4096) IN DACBASE1000 (0-1000) 
;				;DACBASE1000 = (DACBASE - DACOFFSET)/4 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
CONVERT_DAC_BASE_1000:
		AND     A               ;RESET CARRY

		;DACBASE1000 = (DACBASE - DACOFFSET)/4
		LD	HL,(DACBASE)
		LD	DE,(DACOFFSET)
		SBC	HL,DE
		JR      NC,CONVERT_DAC_BASE_1000_01
		
		CCF
		LD	HL,0000H
		LD	(DACBASE1000),HL
		JR	CONVERT_DAC_BASE_1000_FINE

CONVERT_DAC_BASE_1000_01:
		LD	(DACBASE1000),HL
		
		;DIVIDO PER 4
		AND     A               ;RESET CARRY
		SRL     L
		SRL     H
		JR      NC,CONVERT_DAC_BASE_1000_02

		CCF
		SET     7,L

CONVERT_DAC_BASE_1000_02:
		SRL     L
		SRL     H
		JR      NC,CONVERT_DAC_BASE_1000_03

		CCF
		SET     7,L
CONVERT_DAC_BASE_1000_03:

		LD	(DACBASE1000),HL
CONVERT_DAC_BASE_1000_FINE:		
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		DAC:	TRASMISSIONE NUOVO VALORE 
;			VERSO CONVERTITORE 12 BIT
;			
;		DAC_CLK		DA3	/PD3
;		DAC_LOAD	DA4	/PD4
;		DAC_SRI		DA5	/PD5
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

DAC_TRASM:
		PUSH    BC
		PUSH	DE
		PUSH    HL

		LD      A,00H           ;AZZERO IL CARRY
                RLA

		LD      DE,(DACVAL)	;CARICO IL VALORE IN DE
		LD	HL,(MAXDAC)	;CONTROLLO CHE SIA INFERIORE
                SBC     HL,DE		;AL MASSIMO AMMISSIBILE (4096)
		JR      NC,DAC_TRASM1	

		LD	HL,(MAXDAC)	;SE SUPERIORE IMPONGO IL MASSIMO
		LD	(DACVAL),HL
		LD	DE,(MAXDAC)
	
		;TRASMETTO I PRIMI 4 BIT DEL BYTE PIU' SIGNIFICATIVO
DAC_TRASM1:
		LD      A,00H           ;AZZERO IL CARRY
                RLA	

                LD      C,D             ;D CONTIENE MSB DA TRASMETTERE
                RL      C		;SHIFTO DI 4 BIT (DAC A 12 BIT)
                RL      C
                RL      C
                RL      C

                LD      B,04H           ;CONTATORE DI CICLO
DAC_TRASM2:     
		LD      A,(MEMD)        
                SET     3,A		;CLOCK BASSO
                LD      (MEMD),A
                OUT     (PD),A

                RL      C		;SHIFTO DI UN BIT

		CALL	DAC_TRASM_BIT

		LD      A,(MEMD)
                RES     3,A             ;CLOCK ALTO
		LD      (MEMD),A
                OUT     (PD),A

		LD      A,(MEMD)        
                SET     3,A		;CLOCK BASSO
                LD      (MEMD),A
                OUT     (PD),A

                DJNZ    DAC_TRASM2


		;TRASMETTO IL BYTE MENO SIGNIFICATIVO
		LD      H,08H
                LD      B,08H           ;E CONTIENE LSB DA TRASMETTERE
                LD      C,E
DAC_TRASM3:     LD      A,(MEMD)
	
		RL      C		;SHIFTO DI UN BIT
		CALL	DAC_TRASM_BIT
		
		DEC     H
                JR      NZ,DAC_TRASM4

		LD      A,(MEMD) 
                SET     4,A             ;LOAD BASSO
DAC_TRASM4:	RES     3,A             ;CLOCK ALTO
		LD      (MEMD),A
                OUT     (PD),A

		LD      A,(MEMD) 
                SET     3,A             ;CLOCK BASSO
		LD      (MEMD),A
                OUT     (PD),A

                DJNZ    DAC_TRASM3

                LD      A,(MEMD)        
                RES     4,A             ;LOAD ALTO
                LD      (MEMD),A
                OUT     (PD),A

                POP     HL
		POP	DE
                POP     BC
		
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		DAC:		TRASMETTO BIT DATO IN BASE 
;				AL VALORE DI CARRY
;
;		DAC_CLK		DA3	
;		DAC_LOAD	DA4	
;		DAC_SRI		DA5	
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
DAC_TRASM_BIT:
                JR      NC,DAC_TRASM_BIT_1 
		LD      A,(MEMD)
                SET     5,A
                JR      DAC_TRASM_BIT_FINE
DAC_TRASM_BIT_1:	
		LD      A,(MEMD)
 		RES	5,A             
DAC_TRASM_BIT_FINE:  
		LD      (MEMD),A
                OUT     (PD),A
		
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ST7:	TRASMISSIONE RICEZIONE 2 BYTE 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
TRASM:
                LD      B,02H           
TRAS1:          LD      A,(IX)          
		CALL    TX_RX
		LD	A,D
		LD	(IY),A
		INC     IX
		INC	IY 
		DJNZ    TRAS1
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ST7:	TRASSMISSIONE RICEZIONE 1 BYTE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

TX_RX:     
		PUSH    BC              ;TRASMISSIONE
		LD      B,08H           ;RICEZIONE SE-
                LD      C,A             ;RIALE PARALLELO
TX_RX1:         
		LD      A,(MEMB)	;IMPOSTO IL BIT DA MANDARE
                RL      C
                JR      NC,TX_RX2
                SET     7,A
                JR      TX_RX3
TX_RX2:         RES     7,A
TX_RX3:            
		LD      (MEMB),A	;MANDO IL BIT IN TX
                OUT     (PB),A
		LD      A,(MEMB)	;IMPOSTO CLOCK = 1
                SET     0,A
		LD      (MEMB),A	
                OUT     (PB),A
		CALL	SLEEP		;RITARDO
		IN	A,(PA)		;LEGGO IL BIT IN RX	
		RR	A		;SHIFTO PER AVER IL BYTE IN D
		RL	D
		LD      A,(MEMB)	;IMPOSTO CLOCK = 0
                RES     0,A
                OUT     (PB),A
                LD      (MEMB),A
		CALL	SLEEP		;RITARDO
                DJNZ    TX_RX1
                POP     BC

                RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SLEEP FOR 1 SECOND
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
SLEEP_1_SEC:
		PUSH	AF
		PUSH	BC
		PUSH	DE

		;LD	E,1AH
		LD	E,10H
SLEEP_1_SEC_1:	LD	B,0FFH
SLEEP_1_SEC_2:	LD	D,0FFH
SLEEP_1_SEC_3:	DEC	D
		JP	NZ,SLEEP_1_SEC_3
		DEC	B
		JP	NZ,SLEEP_1_SEC_2
		DEC	E
		JP	NZ,SLEEP_1_SEC_1
	
		POP	DE
		POP	BC
		POP	AF

		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SLEEP
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
SLEEP:
		PUSH	HL
                LD      L,4AH
SLEEP1:		DEC     L
                JR      NZ,SLEEP1
		POP	HL
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SLEEP_FAST
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
SLEEP_FAST:
		PUSH	HL
                LD      L,04H
SLEEP_FAST1:	DEC     L
                JR      NZ,SLEEP_FAST1
		POP	HL
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ATTIVA TEST DELL'ENCODER
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

TEST_ENCODER:	
		LD	A,00H
		LD	(AUMALO),A
		LD	(AUMATR),A
		LD	(AUMATI),A
		LD	A,01H			;SEGNALA LA  
		LD	(TEST_ENC),A		;RICHIESTA DI 
		RET				;TEST 

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		FORZA L'INVIO DI ERRORE, VELOCITA' E STATO DEGLI ALLARMI
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		
FUNZ_INIT:	LD	BC,0FFFFH	
		LD	(ERMEDI_DISP),BC	;FORZA A MANDARE ERRORE CORRENTE
		LD	BC,0FFFFH	
		LD	(VELDIS),BC		;FORZA A MANDARE VELOCITA' CORRENTE	
		LD	BC,0FFFFH	
		LD	(STATU2),BC		;FORZA A MANDARE STATO CORRENTE
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		FORZA IL PROGRAMMA A RIPARTIRE DA VIA (RESET Z80)
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		
FUNZ_RESET:	LD	BC,0FFFFH
		LD	(RESET),BC
		LD	A,00H
		LD	(AUMALO),A
		LD	(AUMATR),A
		LD	(AUMATI),A
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ST7:	SCELTA SULLE QUATTRO DIREZIONI DI CORREZIONE IN AUTOMATICO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
		
LTSHIFT:
		LD	A,(ARR_TRASM+05H)
		LD	HL,DIRAVA
		CP	(HL)
		JR	Z,LTAVA
		LD	HL,DIRRIT
		CP	(HL)
		JR	Z,LTRIT
		;LD	HL,DIRTRA
		;CP	(HL)
		;JR	Z,LTTRA
		;LD	HL,DIROPE
		;CP	(HL)
		;JR	Z,LTOPE
		RET

LTAVA:
		LD	A,(HL)
		LD	(AVARIT),A
		JR	LONVAL
LTRIT:		LD	A,(HL)
		LD	(AVARIT),A
LONVAL:		LD	A,(ARR_TRASM+06H)
		LD	D,A
		LD	A,(ARR_TRASM+07H)
		LD	E,A
		LD	(SHIFT),DE

LETT2:          LD      DE,(SHIFT)      ;SE E'IN AUTO 
                LD      HL,(AMPI40)     ;MENTI MASSIMI 
                SBC     HL,DE           ;GATE
		JR      NC,LETT1
		CCF
		LD      A,01H
                LD      (SUPCOR),A      

                LD      HL,(SHIFT)      ;VEDO QUANTE 
                LD      (DIVDEN),HL     ;VOLTE AMPI40
                LD      BC,(AMPI40)     ;STA IN SHIFT
                LD      (DIVSOR),BC     ;E LO MEM. IN
                CALL    DIVIS           ;SHIFT3
                LD      BC,(QUOZIE)
                LD      (SHIFT3),BC     ;IL RESTO LO
                LD      BC,(RESTO)      ;MEM IN SHIFT4
                LD      (SHIFT4),BC     ;
                LD      HL,(SHIFT)      ;SLAVO LA SUPER 
                LD      DE,(AMPI40)
                SBC     HL,DE
                JR      NC,LETT3
                CCF

		JR      LETT1

LETT3:          LD      DE,(AMPI40)     ;CARICO IL MAX 
                LD      (SHIFT),DE      ;ACCETTABILE 

LETT1:
		CALL	SPOSTA
		RET
		

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CORREZIONI IN MANUALE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&


CORREZIONE_MANUALE:

		LD	A,(ARR_TRASM+05H)
		LD	HL,DIRAVA
		CP	(HL)
		JR	Z,MLTAVA

		LD	HL,DIRRIT
		CP	(HL)
		JR	Z,MLTRIT

		LD	HL,DIRTRA
		CP	(HL)
		JR	Z,CORREZIONE_MANUALE_DESTRA

		LD	HL,DIROPE
		CP	(HL)
		JR	Z,CORREZIONE_MANUALE_SINISTRA

		RET

MLTAVA:
		LD	A,(HL)
		LD	(AVARIT),A
		JR	CORREZIONE_MANUALE_AVANTI
MLTRIT:		LD	A,(HL)
		LD	(AVARIT),A
		JR	CORREZIONE_MANUALE_RITARDO
		RET

CORREZIONE_MANUALE_AVANTI:         
		LD      A,(AUMALO)      ;SE E' IN AUTOMATICO SALTO
	        CP	0FFH		
		JR      Z,CORREZIONE_MANUALE_FINE

		LD      A,(MEMC)
		RES     B_PC_RITARDO,A
		SET     B_PC_AVANTI,A
		LD      (MEMC),A
		OUT     (PC),A          ;MOTORE AVANTI


		;INCREMENTO TEMPORANEAMENTE IL DAC
		LD	HL,(DACMANGUA)
		LD	(DAC_INCREM),HL
		CALL	DAC_INCR_DECR


		JR	CORREZIONE_MANUALE_FINE

CORREZIONE_MANUALE_RITARDO:         
		LD      A,(AUMALO)		  ;SE E' IN AUTOMATICO SALTO
	        CP	0FFH		
		JR      Z,CORREZIONE_MANUALE_FINE

		LD      A,(MEMC)
		RES     B_PC_AVANTI,A
		SET     B_PC_RITARDO,A		  ;MOTORE RITARDO
		LD      (MEMC),A
		OUT     (PC),A          

		;DECREMENTO TEMPORANEAMENTE IL DAC
		LD	HL,(DACMANGUA)
		LD	(DAC_DECREM),HL
		CALL	DAC_INCR_DECR

		JR	CORREZIONE_MANUALE_FINE


CORREZIONE_MANUALE_DESTRA:			  ;INCREMENTA TIRO     
		LD      A,(AUMATI)		  ;SE E' IN AUTOMATICO SALTO
	        CP	0FFH
		JR      Z,CORREZIONE_MANUALE_FINE
		LD      A,(MEMC)
		RES     B_PC_SINISTRA,A
		SET     B_PC_DESTRA,A	;MOTORE TRASMISSIONE
		LD      (MEMC),A
		OUT     (PC),A          

		;INCREMENTA BASE DAC
		LD	HL,(DACDELTATIR)
		LD	(DAC_INCREM_BASE),HL
		CALL	DAC_INCR_DECR_BASE

		JR	CORREZIONE_MANUALE_FINE

CORREZIONE_MANUALE_SINISTRA: ;DECREMENTA TIRO         
		LD      A,(AUMATI)      ;SE E' IN AUTOMATICO SALTO
	        CP	0FFH		
		JR      Z,CORREZIONE_MANUALE_FINE
		LD      A,(MEMC)
		RES     B_PC_DESTRA,A
		SET     B_PC_SINISTRA,A	;MOTORE OPERATORE
		LD      (MEMC),A
		OUT     (PC),A  
		
		;DECREMENTA BASE DAC
		LD	HL,(DACDELTATIR)
		LD	(DAC_DECREM_BASE),HL
		CALL	DAC_INCR_DECR_BASE

		JR	CORREZIONE_MANUALE_FINE
CORREZIONE_MANUALE_FINE:
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		STOP CORREZIONI IN MANUALE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

STOP_CORREZIONE_MANUALE:
		LD      A,(MEMC)
		RES     B_PC_AVANTI,A
		RES     B_PC_RITARDO,A
		RES	B_PC_DESTRA,A
		RES	B_PC_SINISTRA,A
		LD      (MEMC),A
		OUT     (PC),A          ;MOTORE STOP

		;RIPRISTINO IL VALORE DI RIFERIMENTO DEL DAC
		CALL	DAC_INCR_DECR

STOP_CORREZIONE_MANUALE_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CALCOLI RELATIVI ALLO SVILUPPO CILINDRO E GATE
;		VENGONO SEMPRE ESEGUITI ENTRAMBI
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CAL_CILINDRO:		
		CALL	CHK_CILINDRO_GATE
				
			
		LD	DE,(S_AMPIEZZA_GATE)	;CONTROLLO SE ERRORE CILINDRO O GATE
		LD	BC,(S_SVILUPPO_CILINDRO)
		LD	HL,STATUS
		LD	A,00H
		OR	D
		OR	B
		AND	(HL)
		CP	00H
		JP	NZ,CAL_CILINDRO_FINE	;SE C'E' ERRORE, NON FARE I CALCOLI			

		INC	HL
		LD	A,00H
		OR	E
		OR	C
		AND	(HL)
		CP	00H
		JP	NZ,CAL_CILINDRO_FINE	;SE C'E' ERRORE, NON FARE I CALCOLI			


CAL_CILINDRO_OK:

		LD	DE,(CILMOL)
		LD	(DIVDEN),DE
		LD      A,000AH
		LD      (DIVSOR),A
		CALL    DIVIS
		LD      DE,(QUOZIE)
		LD      (CILIN),DE	;CILIN  = CILMOL/10

		LD	BC,(CILMOL)
		LD      (MDO032),BC
		LD      DE,0000H        
		LD      (MDO232),DE     
		LD      BC,03E8H	
		LD      (MRE032),BC      
		LD      (MRE232),DE     
		CALL    MOL32
		LD      BC,(PTO032)     
		LD      DE,(PTO232)	;CILIN x 10000

		LD      (EDEND0),BC
		LD      (EDEND2),DE
		CALL    DIV32_4096
		LD      DE,(QUOZ32)
		LD      (DMILLE),DE     ;(CILIN x 10000) / 4096

		LD      (DIVDEN),DE
		LD      BC,000AH
		LD      (DIVSOR),BC
		CALL    DIVIS
		LD      DE,(QUOZIE)
		LD      (MILLES),DE	;(CILIN x 1000) / 4096

		LD      (DIVDEN),DE     
		LD      BC,000AH        
		LD      (DIVSOR),BC     
		CALL    DIVIS           
		LD      DE,(QUOZIE)
		LD      (CENTES),DE	;(CILIN x 100) / 4096

		LD      DE,(CILIN)      
		LD      (DIVDEN),DE     
		LD      BC,000AH
		LD      (DIVSOR),BC
		CALL    DIVIS
		LD      DE,(QUOZIE)
		LD      (CILDIV),DE	;(CILIN / 10)


		LD      DE,2710H        
		LD      (DIVDEN),DE     
		LD      DE,(DMILLE) 
		LD      (DIVSOR),DE     
		CALL    DIVIS           
		LD      DE,(QUOZIE)                             
		LD      (UNOMIL),DE     ;10000/DMILLE = (4096/CILIN)

		LD      DE,4E20H        ;=20000
		LD      (DIVDEN),DE
		LD      BC,(MILLES)	;(CILIN x 1000) / 4096
		LD      (DIVSOR),BC
		CALL    DIVIS
		LD      HL,(QUOZIE)
		LD      (MILL20),HL     ;20mm = (20 x 4096) / CILIN
		ADD     HL,HL
		LD      (MILL40),HL     ;40mm = 20+20

 

                LD      DE,0E4EH        ;4000x4096= 1638400
                LD      (MDO032),DE     
                LD      DE,0000H        ;60\1638400= 0,000003662
                LD      (MDO232),DE
                LD      (MRE232),DE     ;3662=0E4EH
                LD      DE,(CILIN)
                LD      (MRE032),DE
                LD      A,02H           
                LD      (ALTDIV),A      
                CALL    MOL32
                CALL    DI1000
                LD      BC,(QU1000)
                LD      (COSVEL),BC     ;COSTANTE VEL. 

		

		;CALCOLO VALORI PER RICERCA CODICE
		;AL CAMBIO DI FORMATO

		;########################################
		;# UNOMIL : 2 = IMPULSI DA 2048 IN 1mm  #
		;# NøIMPULSI DA 2048 IN 1 mm X 3 =      #   
		;# SPESSORE SEGNO. LO SPAZIO TRA DUE    #
		;# SEGNI E' DI 7 mm PER CUI LO SPAZIO   #
		;# RIMANENTE E' Nø IMPULSI DA 2048      #
		;# IN 1mm X 4 CIOE'3+1=4                #
		;########################################
		
		LD      DE,(UNOMIL)
                
		SRL     E               ;DIVISO DUE

                LD      A,E             ;IMPULSI DA 
                INC     A               ;2048 PER mm+1
                LD      (IMPMIL),A      ;

                LD      B,E             ;PERCHE' 2048
		LD      D,00H
		LD      (MOLTI),DE      ;NøIMPULSI X 3
		LD      A,03H
		LD      (MOLPL),A
		CALL    MOLT
		LD      DE,(PROD)
		LD      (SEGNO),DE
		LD      A,B             ;NøIMPULSI X 3
		ADD     A,E             ;+1= SPAZIO
		LD      (SPAZIO),A
		LD      DE,(MILL40)     ;MILL40:2=A
		SRL     E               ;40mm IN
		LD      A,E             ;IMPULSI DA 2048
		LD      (SPAZ40),A      
		
ENTE4:
              
                LD      DE,(CENTES)     ;
                LD      (MOLTI),DE      ;
                LD      A,04H
                LD      (MOLPL),A
                CALL    MOLT
                LD      DE,(PROD)
                LD      (CENTE4),DE     ;


                LD      DE,(AMPGA)      ;NORD
                LD      (MOLTI),DE      ;MOLTIPLICO
                LD      A,50H           ;AMPIEZZA GATE
                LD      (MOLPL),A       ;PER 80
		CALL    MOLT
                LD      DE,(PROD)       
                LD      (DIVDEN),DE     ;DIVIDO PER 2
                LD      BC,0002H
		LD      (DIVSOR),BC
                CALL    DIVIS           ;AMPIEZZA 40%  
                LD      DE,(QUOZIE)     ;IN CENTESIMI
                LD      (DMEZGA),DE

                LD      DE,(AMPGA)
		LD      (MOLTI),DE      ;AMPIEZZA GATE
		LD      A,64H           ;PER 100
		LD      (MOLPL),A
		CALL    MOLT
		LD      DE,(PROD)       ;AMPIEZA IN CENT.
		LD      (DIVDEN),DE
                LD      BC,(CENTE4)     ;
		LD      (DIVSOR),BC
		CALL    DIVIS           ;AMPIEZZA GATE IN
		LD      DE,(QUOZIE)     ;IMPULSI ENCODER.
		
                LD      A,D             ;CONTROLLO SE IL 
		CP      00H             ;RISULTATO E'MAG-
		JR      Z,ENTE41        ;GIORE DI 255.
		LD      D,00H           ;SE LO E'IMPONGO
		LD      E,0FFH          ;COME VALORE MAX
		LD      (MOLTI),DE      ;255 E RIPETO LE
                LD      A,(CENTE4)      ;OPERAZIONI 
		LD      (MOLPL),A       ;ALL'INVERSO PER
		CALL    MOLT            ;PER CALCOLARE IL
		LD      DE,(PROD)       ;NUOVO AMPAG RI-
		LD      (DIVDEN),DE     ;FERITO A 255.
		LD      BC,0064H
		LD      (DIVSOR),BC
		CALL    DIVIS
		LD      DE,(QUOZIE)
		LD      (AMPGA),DE

		JP	ENTE4
ENTE41:      
		
		LD      (AMPIMP),DE     
		LD      (MOLTI),DE
                LD      A,0B4H          ;CALCOLO IL 180%  
		LD      (MOLPL) ,A      ;DELL'AMPIEZZA DEL
                CALL    MOLT            ;GATE CIOE 45x4
		LD      DE,(PROD)       
		LD      (DIVDEN),DE
		LD      BC,0064H
		LD      (DIVSOR),BC
		CALL    DIVIS
		LD      DE,(QUOZIE)
                LD      (MEZGA),DE     ;

		LD      DE,(AMPIMP)	;CALCOLO DEL	     
		LD      (MOLTI),DE	;BIANCO 
                LD      A,96H           ;CHE E'IL 150%  
		LD      (MOLPL) ,A      ;DI AMPIMP 
                CALL    MOLT            ;
		LD      DE,(PROD)       
		LD      (DIVDEN),DE
		LD      BC,0064H
		LD      (DIVSOR),BC
		CALL    DIVIS
		LD      DE,(QUOZIE)
                LD      (BIANCO),DE      ;
	
                LD      DE,(AMPGA)      ; 
		LD      (MOLTI),DE
                LD      A,28H           ;CALCOLO IL 40% 
		LD      (MOLPL) ,A      ;DELL'AMPIEZZA DEL
		CALL    MOLT            ;GATE
		LD      DE,(PROD)       
                LD      (AMPI40),DE     ;GATE IN CENTS     

                LD      A,(AMPIMP)      ;
                SRL     A
                LD      (AMPMEZ),A      ;

                LD      DE,(AMPIMP)     ;CON CKTG0 A  
                LD      (MOLTI),DE      ;1024 MOLTIPL. 
                LD      A,28H           ;PER 40 ANZICHE 
                LD      (MOLPL),A       ;PER 10 CON CKTG0 
                CALL    MOLT            ;A 4096
		LD      DE,(PROD)
                LD      (AMPI10),DE     
		
		LD      DE,7D0H         ;2000 = 20mmX100
		LD      (MOLTI),DE
		LD      A,(SEQSEG)
		LD      (MOLPL),A
		CALL    MOLT
		LD      HL,(PROD)
		SBC     HL,DE
		LD      (DISTAN),HL
		LD      (MDO032),HL     ;CALCOLO
		LD      BC,0000H        ;DISTANZA IN
		LD      (MDO232),BC     ;CENTIMILLESIMI
		LD      HL,03E8H
		LD      (MRE032),HL
		LD      (MRE232),BC
		CALL    MOL32
		LD      HL,(PTO032)
		LD      (DISTA0),HL     ;DISTANZA IN
                LD      (TDIST0),HL     ;CENTIMILLESIMI
                LD      HL,(PTO232)     ;SIA PER LONG.
                LD      (DISTA2),HL     ;CHE PER TRAVERS
                LD      (TDIST2),HL

CAL_CILINDRO_FINE:
		RET			
		
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		IN BASE AL VALORE DI TWIN_CURRENTAPP
;		SELEZIONO IL VALORE DI USCITA DI PD0
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
CHK_TWIN_APP:
		LD	A,(TWIN_CURRENTAPP)
		LD	DE,(TWIN_APP_01)	;SE APPLICAZIONE CORRENTE = TWIN_01
		CP	E
		JR	Z,CHK_TWIN_APP_01

		LD	DE,(TWIN_APP_02)	;SE APPLICAZIONE CORRENTE = TWIN_01
		CP	E
		JR	Z,CHK_TWIN_APP_02

		JR	CHK_TWIN_APP_01		;ALTRIMENTI DEFAULT

CHK_TWIN_APP_01:
		LD      A,(MEMD)		;IMPONE USCITA SELETTORE A 1 (NEGATO)        
                RES     0,A			;CAMBIANDO PIN 0 DELLA PORTA D
                LD      (MEMD),A
                OUT     (PD),A
		JR	CHK_TWIN_APP_FINE

CHK_TWIN_APP_02:
		LD      A,(MEMD)		;IMPONE USCITA SELETTORE A 0 (NEGATO)        
                SET     0,A			;CAMBIANDO PIN 0 DELLA PORTA D
                LD      (MEMD),A
                OUT     (PD),A
		JR	CHK_TWIN_APP_FINE

CHK_TWIN_APP_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		IN BASE AL VALORE DI SH_P
;		SELEZIONO IL VALORE DI USCITA DI PD2
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
CHK_SH_PN:
		LD	A,(SH_PN)
		CP	01H
		JR	Z,CHK_SH_PN_02		;SE TESTINA NEGATIVA
 		JR	CHK_SH_PN_01		;ALTRIMENTI DEFAULT

CHK_SH_PN_01:
		LD      A,(MEMD)		;IMPONE USCITA SELETTORE A 1 (NEGATO)        
                SET     2,A			;CAMBIANDO PIN 2 DELLA PORTA D
                LD      (MEMD),A
                OUT     (PD),A
		JR	CHK_SH_PN_FINE

CHK_SH_PN_02:
		LD      A,(MEMD)		;IMPONE USCITA SELETTORE A 0 (NEGATO)        
                RES     2,A			;CAMBIANDO PIN 2 DELLA PORTA D
                LD      (MEMD),A
                OUT     (PD),A
		JR	CHK_SH_PN_FINE

CHK_SH_PN_FINE:
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		IN BASE AL VALORE DI CURRENT_ENCODER
;		SELEZIONO IL VALORE DI USCITA DI PD1
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
CHK_CURRENT_ENCODER:
		LD	A,(CURRENT_ENCODER)
		CP	01H
		JR	Z,CHK_CURRENT_ENCODER_01
		JR	CHK_CURRENT_ENCODER_00		;ALTRIMENTI DEFAULT

CHK_CURRENT_ENCODER_00:
		LD      A,(MEMD)		;IMPONE USCITA SELETTORE A 1 (NEGATO)        
                RES     1,A			;CAMBIANDO PIN 1 DELLA PORTA D
                LD      (MEMD),A
                OUT     (PD),A
		JR	CHK_CURRENT_ENCODER_FINE

CHK_CURRENT_ENCODER_01:
		LD      A,(MEMD)		;IMPONE USCITA SELETTORE A 0 (NEGATO)        
                SET     1,A			;CAMBIANDO PIN 1 DELLA PORTA D
                LD      (MEMD),A
                OUT     (PD),A
		JR	CHK_CURRENT_ENCODER_FINE

CHK_CURRENT_ENCODER_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		PREPARA VALORE CONTATORE WATCHDOG PER 
;		ATTESA IRQ DI COMUNICAZIONE ST7 (IN REQISFREE)
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CAL_WATCHDOG_ST7:
		
		LD	DE,WATCHDOG_ST7
		LD      (MDO032),DE	;MOLTIPLICANDO BASSO		MOLTIPLICAZIONE 32 BIT 
		LD      DE,0000H	
		LD      (MDO232),DE	;MOLTIPLICANDO ALTO		MOLTIPLICAZIONE 32 BIT 

		LD      DE,2500H
		LD      (MRE032),DE	;MOLTIPLICATORE BASSO 		MOLTIPLICAZIONE 32 BIT 
		LD      DE,0000H
		LD      (MRE232),DE	;MOLTIPLICATORE ALTO 		MOLTIPLICAZIONE 32 BIT

		CALL	MOL32

		LD	DE,(PTO032)	;PRODOTTO BASSO 		MOLTIPLICAZIONE 32 BIT 
		LD	(WATCHDOG_ST7_L),DE

		LD	DE,(PTO232)	;PRODOTTO ALTO 			MOLTIPLICAZIONE 32 BIT 
		LD	(WATCHDOG_ST7_H),DE

CAL_WATCHDOG_ST7_FINE:
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CARICO INDIRIZZI DI MEMORIA DEI PARAMETRI
;		NELLA TABELLA DI TRANSCODIFICA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

FAITAB:
		LD	HL,CILMOL
		LD	(ARR_TAB_IDX+00H),HL
		LD	HL,ALLAR		;*
		LD	(ARR_TAB_IDX+02H),HL
		LD	HL,AMPGA		;*
		LD	(ARR_TAB_IDX+04H),HL
		LD	HL,SEQSEG
		LD	(ARR_TAB_IDX+06H),HL
		LD	HL,TRIP			;*
		LD	(ARR_TAB_IDX+08H),HL
		LD	HL,GUADA		;*
		LD	(ARR_TAB_IDX+0AH),HL
		LD	HL,GUATRA
		LD	(ARR_TAB_IDX+0CH),HL
		LD	HL,CICLO		;*
		LD	(ARR_TAB_IDX+0EH),HL
		LD	HL,TCICLO
		LD	(ARR_TAB_IDX+10H),HL
		LD	HL,ZONA			;*
		LD	(ARR_TAB_IDX+12H),HL
		LD	HL,ZONT
		LD	(ARR_TAB_IDX+14H),HL
		LD	HL,VCLONG
		LD	(ARR_TAB_IDX+16H),HL
		LD	HL,VCTRAV
		LD	(ARR_TAB_IDX+18H),HL
		LD	HL,AUMALO		;*
		LD	(ARR_TAB_IDX+1AH),HL
		LD	HL,AUMATR
		LD	(ARR_TAB_IDX+1CH),HL
		LD	HL,SALTO		;*
		LD	(ARR_TAB_IDX+1EH),HL
		LD	HL,DERIVA		;*
		LD	(ARR_TAB_IDX+20H),HL
		LD	HL,INSER		;*
		LD	(ARR_TAB_IDX+22H),HL
		LD	HL,LINGUA
		LD	(ARR_TAB_IDX+24H),HL
		LD	HL,SEGRIF
		LD	(ARR_TAB_IDX+26H),HL
		LD	HL,DIAGON
		LD	(ARR_TAB_IDX+28H),HL
		LD	HL,MESSA		;*
		LD	(ARR_TAB_IDX+2AH),HL
		LD	HL,REV_CORRECTION
		LD	(ARR_TAB_IDX+2CH),HL
		LD	HL,MM_MC
		LD	(ARR_TAB_IDX+2EH),HL
		LD	HL,VERPRO
		LD	(ARR_TAB_IDX+30H),HL
		LD	HL,GUADS1		;GUADAGNO S1
		LD	(ARR_TAB_IDX+32H),HL
		LD	HL,TAB_DUMMY		;ERATTU		;VALORE ASSOLUTO
		LD	(ARR_TAB_IDX+34H),HL
		LD	HL,TAB_DUMMY		;ERATTU		;VALORE ASSOLUTO
		LD	(ARR_TAB_IDX+36H),HL
		LD	HL,MEMC			;STATO LED CORREZIONE
		LD	(ARR_TAB_IDX+38H),HL
		LD	HL,ENB_DAC_LON		;ENABLE DAC LONGITUDINALE
		LD	(ARR_TAB_IDX+3AH),HL
		LD	HL,ENB_DAC_TRA		;ENABLE DAC TRASVERSALE
		LD	(ARR_TAB_IDX+3CH),HL	
		LD	HL,ENB_DAC_TIR		;ENABLE DAC TIRO
		LD	(ARR_TAB_IDX+3EH),HL
		LD	HL,VELMED		;VELOCITA
		LD	(ARR_TAB_IDX+40H),HL
		LD	HL,DACBASE1000		;VALORE BASE DAC 0-1000
		LD	(ARR_TAB_IDX+42H),HL
		LD	HL,DACDELTACOR		;DELTA CORREZIONE DAC
		LD	(ARR_TAB_IDX+44H),HL
		LD	HL,AUMATI		;MODO AUTOMATICO-MANUALE DI TIRO
		LD	(ARR_TAB_IDX+46H),HL
		LD	HL,DACDELTATIR		;DELTA CORREZIONE TIRO DAC
		LD	(ARR_TAB_IDX+48H),HL
		LD	HL,DACMANGUA		;GUADAGNO MANUALE TIRO
		LD	(ARR_TAB_IDX+4AH),HL
		LD	HL,STATUS
		LD	(ARR_TAB_IDX+4CH),HL
		LD	HL,PRESS
		LD	(ARR_TAB_IDX+4EH),HL
		LD	HL,FASE			;POSIZIONE SEGNO
		LD	(ARR_TAB_IDX+50H),HL	;RISPETTO ENCODER
		LD	HL,ENCOD		;SEGNO DI ZERO DELL'ENCODER
		LD	(ARR_TAB_IDX+52H),HL	;NPN O PNP
		LD	HL,TIRGUA		;GUADAGNO DI TIRO
		LD	(ARR_TAB_IDX+54H),HL	
		LD	HL,TIRCIC		;CICLICO DI TIRO
		LD	(ARR_TAB_IDX+56H),HL	
		LD	HL,ENB_TIRO		;ENABLE TIRO
		LD	(ARR_TAB_IDX+58H),HL	
		LD	HL,ENB_TRASVERSALE	;ENABLE OP-TRASMISSIONE
		LD	(ARR_TAB_IDX+5AH),HL	
		LD	HL,LAVORO		;LAVORO CORRENTE
		LD	(ARR_TAB_IDX+5CH),HL	
		LD	HL,FASRIF		;FASE DI RIFERIMENTO
		LD	(ARR_TAB_IDX+5EH),HL	
		LD	HL,ENB_COLDSEAL		;ENABLE COLD SEAL (F9)
		LD	(ARR_TAB_IDX+60H),HL
		LD	HL,TAB_DUMMY		;CARICA I DUMMY
		LD	(ARR_TAB_IDX+62H),HL
		LD	HL,TWIN_APP_01		;PRIMA FUNZIONALITA' TWIN
		LD	(ARR_TAB_IDX+64H),HL
		LD	HL,TWIN_APP_02		;SECONDA FUNZIONALITA' TWIN
		LD	(ARR_TAB_IDX+66H),HL
		LD	HL,TWIN_CURRENTAPP	;FUNZIONALITA' TWIN CORRENTE
		LD	(ARR_TAB_IDX+68H),HL
		LD	HL,CURRENT_ENCODER	;ENCODER CORRENTE
		LD	(ARR_TAB_IDX+6AH),HL
		LD	HL,F4_DIMENSION		;DIMENSIONE BIANCO (MM) IN F4
		LD	(ARR_TAB_IDX+6CH),HL
		LD	HL,ENB_SH_PN		;ABILITA MENU AMPLIFICATORE POSITIVO/NEGATIVO
		LD	(ARR_TAB_IDX+6EH),HL
		LD	HL,SH_PN		;AMPLIFICATORE POSITIVO/NEGATIVO
		LD	(ARR_TAB_IDX+70H),HL
		LD	HL,DACOFFSET		;VALORE OFFSET DAC
		LD	(ARR_TAB_IDX+72H),HL
		LD	HL,DELAY_ALLARM		;RITARDO ALLARME (numero di cicli prima dell'attivazione)
		LD	(ARR_TAB_IDX+74H),HL
		LD	HL,TAB_DUMMY		;CARICA I DUMMY
		LD	(ARR_TAB_IDX+76H),HL
		LD	HL,TAB_DUMMY		;CARICA I DUMMY
		LD	(ARR_TAB_IDX+78H),HL
		LD	HL,TAB_DUMMY		;CARICA I DUMMY
		LD	(ARR_TAB_IDX+7AH),HL
		LD	HL,TAB_DUMMY		;CARICA I DUMMY
		LD	(ARR_TAB_IDX+7CH),HL
		LD	HL,TAB_DUMMY		;CARICA I DUMMY
		LD	(ARR_TAB_IDX+7EH),HL
		LD	HL,TAB_DUMMY		;CARICA I DUMMY
		LD	(ARR_TAB_IDX+80H),HL
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		AZZERAMENTO DOPO REGISTRO MANUALE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

FUNZ_RESET_ERROR:          
		LD      A,00H			;IMPONGO MANUALE
		LD	(AUMALO),A
		LD	(AUMATI),A	
		LD	(AUMATR),A	
		LD      (MEMC),A        
		OUT     (PC),A

		LD      BC,(ERATTU)		;CARICO L'ERRORE LETTO IN SHIFT 
		LD      (SHIFT),BC		                                         
		LD      A,(MSCORR)		;VEDO SE IN AVANTI O IN RITARDO
		CP      D_AVANTI		
		JR      Z,FUNZ_RESET_ERROR_A   
		LD      A,(DIRAVA)
		LD      (AVARIT),A
		JR      FUNZ_RESET_ERROR_R
FUNZ_RESET_ERROR_A:           
		LD      A,(DIRRIT)
		LD      (AVARIT),A
FUNZ_RESET_ERROR_R:           
		CALL    SPOSTA			;CHIAMO LA ROUTINE SPOSTA PER CENTRARLO NEL GATE.

		LD	BC,0000H		;SVUOTA LA MEDIA
		LD	(ERATTU),BC
		CALL	RESET_MEDIA

		CALL	SEND_OK

FUNZ_RESET_ERROR_FINE:
		RET

	
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SEGNALAZIONE FASATURA AUTOMATICA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&


FUNZ_AUTO_PHASE:          
                LD      A,01H			;SET FLAG RICERCA AUTOMATICA
		LD      (ARR_RICAUTO+00H),A
		RET			

            	
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SEGNALAZIONE FASATURA MANUALE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&


FUNZ_MANUAL_PHASE:       
                LD      A,00H           ;IMPONGO MANUALE
                LD      (MEMC),A
                OUT     (PC),A
		;CALL	RESEPC		;NON ESEGUE IL RESET DEGLI ALLARMI
		LD	(SHIFT3),A
		LD	(SHIFT4),A

	        IN      A,(10H)         ;LEGGO LA POSIZIONE
		LD      L,A             ;(PULSANTE FASE MANUALE)
		IN      A,(20H)
		LD      H,A
		LD      (FASE),HL
	        LD      (MOLTI),HL      ;MOLTIPLICO PER 
                LD      A,0AH           ;10 PER AVERE IL
                LD      (MOLPL),A       ;RIFERIMENTO TO-
                CALL    MOLT            ;TALE TRASCURAN-
                LD      DE,(PROD)       ;DO IL RAPPORTO
                LD      (RIFTOT),DE

                CALL    CENTRA        

		RET




;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		VERIFICA FUNZIONAMENTO TESTA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

TEST_SH:		
		LD      A,(MEMB)
		SET     6,A             ;BLOCCO GATE
		LD      (MEMB),A
		OUT     (PB),A
		LD      D,0FFH		;5 SECONDI
TES1:           LD      BC,0A00H        
TES2:           IN      A,(PB)          
		BIT     2,A
		JR      NZ,TESSI
		DEC     BC		;DI TEST 
		LD      A,B
		
		OR      C                
		JR      NZ,TES2         
		DEC     D
		LD      A,00H
		CP      D
		JR      NZ,TES1
		CALL	FORCE_STATUS
		LD	DE,(S_TEST_TESTINA)
		CALL	SET_STATUS		;TEST FALLITO
		JR	ENDTEST
TESSI:
		CALL	FORCE_STATUS
		LD	DE,(S_TEST_TESTINA)
	        CALL	RES_STATUS		;TEST OK
		
ENDTEST:
		LD      A,(MEMB)
		RES     6,A             ;SBLOCCO GATE
		LD      (MEMB),A
		OUT     (PB),A
		RET;


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		VERIFICA FUNZIONAMENTO ENCODER
;		ROTATIVA DEVE GIRARE AL DI SOTTO DI 50 GIRI/MIN
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

ENC1:                
                LD      A,01H          ;CARICO UN
                LD      (ENCOD1),A     ;GIRO IN CTC1
                LD      A,0D7H         ;AL GIRO
                OUT     (CTC1_CMD),A        
                LD      A,01H          
                OUT     (CTC1_CMD),A        
                LD      A,00H          ;AZZERO IL 
                LD      (ENCOD2),A     ;CONTATORE
		LD	(TEST_ENC),A
                LD      BC,0000H       ;2
                LD      (ENCOD4),BC
                EI                     ;PER 16
	        LD      BC,0FFFFH      ;RITARDO ANTI
ENC8:           
		DEC     BC              ;RIMBALZO
		LD      A,B
		OR      C
		JR      NZ,ENC8
		JR	ENC1		;AL POSTO DIJP TES1
ENC2:          
                LD      A,37H           ;DISABILITO
                OUT     (03H),A         ;INTERUPT DI
                LD      A,0FFH          ;PIOB
                OUT     (03H),A         ;
ENC3:
                IN      A,(PB)          ;
		BIT     4,A
                JR      Z,ENC3
ENC7:           
		IN      A,(PB)
                BIT     4,A
                JR      NZ,ENC7
                LD      BC,(ENCOD4)
                INC     BC
                LD      (ENCOD4),BC
                JR      ENC3            ;

ENC6:           LD      A,00H
                LD      (ENCOD1),A      ;
                LD      A,(ENCOD2)
		LD      A,43H
		OUT     (CTC1_CMD),A
                LD      A,(ENCOD3)      ;TENTA PER
                INC     A               ;10 VOLTE 
                LD      (ENCOD3),A      ;IL TEST
                CP      03H             ;
                JP      NZ,ENC1

                LD      HL,(ENCOD4)     ;2
                LD      DE,07FDH        ;2045
                SBC     HL,DE
                JR      C,ENC4
ENC5:        
		CALL	FORCE_STATUS		;TEST OK
		LD	DE,(S_TEST_ENCODER)
		CALL	RES_STATUS
		JR	ENDENC

ENC4:
                CCF                    ;TEST FALLITO
		CALL	FORCE_STATUS
		LD	DE,(S_TEST_ENCODER)
		CALL	SET_STATUS
ENDENC:
		JP	VIA



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		TEST DELLE USCITE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

USCITE:

		LD	A,00H
		LD	(AUMALO),A
		LD	(AUMATR),A
		LD	(AUMATI),A


		LD	A,00H
		LD	(MEMC),A
		OUT	(PC),A
		SET	B_PC_AVANTI,A
		CALL	RITOUT
		RES	B_PC_AVANTI,A
		SET	B_PC_RITARDO,A
		CALL	RITOUT
		RES	B_PC_RITARDO,A
		SET	B_PC_DESTRA,A
		CALL	RITOUT
		RES	B_PC_DESTRA,A
		SET	B_PC_SINISTRA,A
		CALL	RITOUT
		RES	B_PC_SINISTRA,A
		SET	B_PC_ALLARME,A
		CALL	RITOUT
		RES	B_PC_ALLARME,A
		SET	B_PC_FUNZ,A
		CALL	RITOUT
		RES	B_PC_FUNZ,A
		LD	(MEMC),A
		OUT	(PC),A
		
		CALL	SEND_OK

		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		RITARDO PER TEST DELLE USCITE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

RITOUT:
		OUT	(PC),A
RITDIS:         LD      D,06H
RITDI1:         LD      BC,0FFFFH
RITDI2:         DEC     BC
		LD      A,B
		OR      C
		JR      NZ,RITDI2
		CALL	WATCH_STWD	;SENZA QUESTO IL CICLO DI TEST
		DEC     D		;VIENE INTERROTTO DAL WATCHDOG
		JR      NZ,RITDI1	;DIVENTA 172
		
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		GENERAZIONE SEGNALI PER DISATTIVARE WATCHDOG STWD100
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&


WATCH_STWD:		
		LD      A,(MEMB)	;MODIFICA PER INVIARE
                SET     5,A		;I SEGNALI AL CIRCUITO
		LD      (MEMB),A	;ESTERNO DI WATCH DOG
                OUT     (PB),A		;DIVENTA 170
		CALL	SLEEP
		LD      A,(MEMB)	;
                RES     5,A
		LD      (MEMB),A	
                OUT     (PB),A
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		DEFINIZIONE COSTANTI
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

ANSWOK:		DEFB    00H
CILINT:		DEFB	00H
AMPGAT:		DEFB	02H
SEQSEGT:	DEFB	03H
AUMALOT:	DEFB	0DH
AUMATRT:	DEFB	0EH
LONAVA:		DEFB	1AH
LONRIT:		DEFB	1BH
STATLED:	DEFB	1CH
PARVEL:		DEFB	20H
BASEVALDAC:	DEFB	21H


ALLCIL:		DEFB	20H
ALLAMPG:	DEFB	21H
ALLRIF:		DEFB	22H
ALLERR:		DEFB	23H
ALLNOPR:	DEFB	24H
ALLVELO:	DEFB	25H
ALLFAS:		DEFB	26H
ALLTES:		DEFB	27H
ALLENC:		DEFB	28H
ENCSEGN:	DEFB	28H
SUPSOGL:	DEFB	29H

OFFSETVALDAC:	DEFB	39H

LEGGI:		DEFB	80H
SCRIVI:		DEFB	81H
AUTFAS:		DEFB	82H		
INITZ80:	DEFB	83H   
TESTES:		DEFB	84H
TESENC:		DEFB	85H
CICLOF:		DEFB    86H
;AVAGAT:		DEFB	87H
;RITGAT:		DEFB	88H
;STOPGA:		DEFB	89H	
SHDATO:		DEFB	8AH
SHMANU:		DEFB	8BH
SHSTOP:		DEFB	8CH
NORDF9:		DEFB	8DH
INIPAR:		DEFB	8EH
AZZERA:		DEFB	90H
PULSAN:		DEFB	91H
TESTOUT:	DEFB	92H
FORCERESET:	DEFB	93H
FUNZ_TWIN_APP_01: DEFB  94H
FUNZ_TWIN_APP_02: DEFB	95H

ANSWER:		DEFB	0C4H
MLUNGHEZZA:	DEFW	0030H	 ;LUNGHEZZA DI ARR_ERRAVA e ARR_ERRRIT


DIRAVA:		DEFB    03H
DIRRIT:		DEFB	0CH
DIRTRA:		DEFB	30H
DIROPE:		DEFB	0C0H

MAXDAC:		DEFW	0FFFH	;VALORE SERIALE DEL DAC (DACVAL) DEVE ESSERE COMPRESO TRA 0 E QUESTO VALORE


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;			POSSIBILI STATI DI STATUS
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

S_SVILUPPO_CILINDRO:		DEFW	0001H	;1 
S_AMPIEZZA_GATE:		DEFW	0002H	;2
S_SEGNO_DI_RIFERIMENTO:		DEFW	0004H	;4
S_ERRORE_GENERICO:		DEFW	0008H	;8

S_MANCANZA_STAMPA:		DEFW	0010H	;16 
S_VELOCITA_SOTTO_SOGLIA:	DEFW	0020H	;32 
S_SUPERAMENTO_SOGLIA_ERRORE:	DEFW	0040H	;64
S_DOPPIO_SEGNO: 		DEFW	0080H	;128

S_TEST_TESTINA:			DEFW	0100H	;256
S_TEST_ENCODER:			DEFW	0200H	;512
S_FASATURA_FALLITA:		DEFW	0400H	;1024
S_BLOCCO_DECELERAZIONE:		DEFW	0800H	;2048

S_BLOCCO_ACCELERAZIONE:		DEFW	1000H	;4096
;-----NON USATO----------::	DEFW	2000H
;-----NON USATO----------::	DEFW	4000H
;-----NON USATO----------::	DEFW	8000H
				
					
S_PERMANENT_MASK:		DEFW   0F87FH   ;1111 1000 0111 1111





;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;			PICCO DI TEST SUL BIT PC 5
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
  
OUTEST:
		LD	A,(MEMC)
		SET	5,A
		OUT	(PC),A
		LD	D,0FFH
CAZ:		DEC	D
		JR	NZ,CAZ
		RES	5,A
		LD	(MEMC),A
		OUT	(PC),A
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	TABELLA PER IL SALVATAGGIO TEMPORANEO DI UN VALORE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

HISTORY:
                PUSH    BC
		PUSH    DE
		PUSH    HL
		PUSH    AF
		PUSH    IX
		PUSH    IY


                LD      IY,(TMP_CURSOR)
		LD	BC,(VELMED)  ;<<- SOSTITUIRE CON LA VARIABILE DA MONITORARE
                LD      (IY+00H),B
                INC     IY
                LD      (IY+00H),C
                INC     IY

                LD      (TMP_CURSOR),IY
                LD      HL,0DBFFH
                LD      DE,(TMP_CURSOR)
                SBC     HL,DE
                JR      NC,HISTORYEXIT
                CCF
                LD      IY,ARR_TMP
                LD      (TMP_CURSOR),IY
HISTORYEXIT:
                POP     IY
		POP     IX
		POP     AF
		POP     HL
		POP     DE
		POP     BC
                RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	AZZERRAMENTO DEL MSB DEI PARAMETRI A UN SOLO BYTE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

AZZER_MSB:

		LD	A,00H
		LD	(ALLAR+01H),A		
		LD	(AMPGA+01H),A		
		LD	(SEQSEG+01H),A
		LD	(TRIP+01H),A		
		LD	(GUADA+01H),A		
		LD	(GUATRA+01H),A
		LD	(CICLO+01H),A		
		LD	(TCICLO+01H),A
		LD	(ZONA+01H),A		
		LD	(ZONT+01H),A
		LD	(VCLONG+01H),A
		LD	(VCTRAV+01H),A
		LD	(AUMALO+01H),A		;MODO AUTOMATICO-MANUALE LONGITUDINALE
		LD	(AUMATR+01H),A		;MODO AUTOMATICO-MANUALE TRASVERSALE
		LD	(AUMATI+01H),A		;MODO AUTOMATICO-MANUALE TIRO
		LD	(SALTO+01H),A		
		LD	(DERIVA+01H),A		
		LD	(INSER+01H),A		
		LD	(LINGUA+01H),A
		LD	(SEGRIF+01H),A
		LD	(DIAGON+01H),A
		LD	(MESSA+01H),A	
		LD	(REV_CORRECTION+01H),A
		LD	(MM_MC+01H),A
		LD	(ENB_TRASVERSALE+01H),A	;ENABLE OP-TRASMISSIONE
		LD	(ENB_TIRO+01H),A	;ENABLE TIRO
		LD	(ENB_COLDSEAL+01H),A	;ENABLE COLD SEAL
		LD	(ENB_DAC_LON+01H),A	;ENABLE DAC_LONGITUDINALE
		LD	(ENB_DAC_TRA+01H),A	;ENABLE DAC_TRASVERSALE
		LD	(ENB_DAC_TIR+01H),A	;ENABLE DAC_TIRO
		LD	(PRESS+01H),A
		LD	(ENCOD+01H),A		;SEGNO DI ZERO DELL'ENCODER
		LD	(TIRGUA+01H),A		;GUADAGNO DI TIRO
		LD	(TIRCIC+01H),A		;CICLICO DI TIRO
		LD	(TWIN_APP_01+01H),A	;PRIMA FUNZIONALITA' TWIN                                          
		LD	(TWIN_APP_02+01H),A	;SECONDA FUNZIONALITA' TWIN                                        
		LD	(TWIN_CURRENTAPP+01H),A	;FUNZIONALITA' TWIN CORRENTE                                       
		LD	(LINGUA+01H),A		;MEMORIA DELLA LINGUA                                              
		LD	(ENCOD+01H),A		;FUNZIONAMENTO CON ENCODER NPN O PNP (SI AUTOSETTA SU TEST ENCODER)
		LD	(SH_PN+01H),A
		LD	(ENB_SH_PN+01H),A


		RET
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		FINE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
                END


