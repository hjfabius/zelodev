			;#################################
                        ;#              TAJ 200          #
                        ;#        ZELO ELETTRONICA       #
			;#                               #
                        ;#    CONTROLLO DI REGISTRO M/C  #
                        ;# LONGITUDINALE                 #
                        ;# TIRO                          #
                        ;# TRASVERSALE (DA IMPLEMENTARE) #
                        ;#################################
 

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;			PARAMETRI IDENTIFICATIVI DEL PROGRAMMA E MACCHINA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

VERSIONE_PROGRAMMA:	EQU	122	; 1.22 	
ID_MACCHINA:		EQU	0071H	;113	


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	PORTE PIO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

PA:		EQU     00H     ;DATI PORTA A
PB:		EQU     01H     ;DATI PORTA B
PC:     	EQU     63H     ;DATI PORTA C

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	CONFIGURAZIONE PORTA PC
;
; PC0	AVANTI				OPTO1
; PC1	RITARDO				OPTO2
; PC2	AUTOMAN LONG			-----
; PC3	ALLARME				OPTO5
; PC4	AUTOMAN TRAV			-----
; PC5	FUNZIONE AGGIUNTIVA (F6)	OPTO6
; PC6	DESTRA/TRAVERSA/+TIRO		OPTO3
; PC7	SINISTRA/OPERATORE/-TIRO	OPTO4
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&




;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	AREA DI MEMORIA VOLATILE C000-CFFFH (RESETTATA ALL'AVVIO)
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&


TABCON: 	EQU     0C000H   ;INIZIO AREA DI MEMORIA VOLATILE C000-CFFFH
DIVDEN: 	EQU     0C014H   ;MEM. DEL DIVIDENDO
DIVSOR: 	EQU     0C016H   ;MEM. DEL DIVISORE
QUOZIE: 	EQU     0C018H   ;MEM. RISULTATO DIVISIONE
MOLTI:  	EQU     0C01AH   ;MEMORIA DEL MOLTIPLICATORE
MOLPL:  	EQU     0C01CH   ;MEMORIA DEL MOLTIPLICANDO
PROD:   	EQU     0C01EH   ;MEMORIA DEL PRODOTTO
MOLT24: 	EQU     0C020H   ;MOLTIPLICATORE  MOLTIPLICA 24 BIT
MOLP24: 	EQU     0C022H   ;MOLTIPLICANDO MOLTIPLICA 24 BIT
PROD24: 	EQU     0C024H   ;C026H PRODOTTO MOLTIPLICA 24 BIT 
QU4096: 	EQU     0C028H   ;QUOZIENTE DIVISIONE PER 4096
QU1000: 	EQU     0C02AH   ;QUOZIENTE DIVISIONE PER 1000
MDIVIS: 	EQU     0C02CH   ;MEM. USATA IN DIVISIONE 1000

RESTO:  	EQU     0C02EH   ;RESTO DELLA DIVISIONE A 16 BIT  

ADDEN0: 	EQU     0C030H   ;ADDENDO BASSO SOMMA A 32 BIT
ADDEN2: 	EQU     0C032H   ;ADDENDO ALTO SOMMA A 32 BIT
AUGEN0: 	EQU     0C034H   ;AUGENDO BASSO SOMMA A 32 BIT
AUGEN2: 	EQU     0C036H   ;AUGENDO ALTO SOMMA A 32 BIT


CONSAL: 	EQU     0C03AH   ;CONTEG. SALTI DI NON COR.LONG. EEAA
CONSAT: 	EQU     0C03CH   ;CONTEG. SALTI DI NON COR.TRAV. EEAA

;VELBCD: 	EQU     0C040H   ;0C042H MEM. VELOCITA BCD
;VELMEM: 	EQU     0C044H   ;USATA NELLA CONVERSIONE VELOCITA
;VMEDIA: 	EQU     0C046H   ;USATA IN CALCOLO VELOCITA'MEDIA 
VELDIS:		EQU	0C046H   ;MEMORIA DELL'ULTIMA VELOCITA' SPEDITA
VELPRE: 	EQU     0C048H   ;MEM. DELLA VELOCITA' PRECEDENTE
IMPVEL: 	EQU     0C04AH   ;IMPULSI ENCODER IN 4 mS 
ISTVEL: 	EQU     0C04CH   ;VELOCITA' ISTANTANEA

VELMED: 	EQU     0C04EH   ;MEM.MEDIA DELLA VELOCITA' AAVV

MVEL00: 	EQU     0C050H   ;DA 0C050H A 0C06FH USATA IN WWAA
                         ;CALCOLO VELOCITA' MEDIA

DECORR: 	EQU     0C080H   ;MEM.VALORE DI CORREZ.A DECR.LONGITUD.     WWAA
MDECOR: 	EQU     0C082H   ;MEM.EXTRA VALORE DI CORREZ. LONG. 
CORRE:  	EQU     0C084H   ;MEM.VALORE DI CORREZ.MOTORE

;ADDRCG: 	EQU     0C08AH   ;MEM.ADDRES GENERAZIONE CARATTERI 
DATOTR:		EQU	0C100H	 ;DA C100H A C10AH MEMORIE PER TRASMISSIONE 

;BINSET: 	EQU     0C200H   ;MEM.TRASM.DISPLAY 4200H-4210H
;BINSE1: 	EQU     0C220H   ;MEM.TRASM.DISPLAY 4220H-422FH
;BINSE2: 	EQU     0C230H   ;MEM.TRASM.DISPLAY 4230H-423FH        

SUPCOR: 	EQU     0C250H   ;AVVISO SPOSTAMENTO > DI 1/2 GATE  
SHIFT3: 	EQU     0C252H   ;QUANTE VOLTE AMPI40 STA IN SHIFT  
SHIFT4: 	EQU     0C254H   ;RESTO DELLA DIVISIONE SHIFT AMPIMP 
MOTPO1: 	EQU     0C256H   ;MEM.TEMPORANEA IN USO TASTIERA E SUPRCO 
;MDATE1:	EQU	0C270H	;MEM. F1 ERRATO EEOO 
;MDATE2:	EQU	0C272H	;MEM. F3 ERRATO EEOO                

;MAGGIU: 	EQU     0C2E0H   ;MEM.USATA IN AGGIUST.DECIM.  
;MAGGI1: 	EQU     0C2E2H   ;USATA IN AGGIUSTAMENTO AMPGATE OSCIL 
          
;SPOINC: 	EQU     0C2E6H   ;MEM.INCREMENTO DELLO SPOSTAMENTO OSCIL. 

SIMCOL: 	EQU     0C300H   ;SIMBOLO PER DISPLAY
;SIMCOT: 	EQU     0C304H   ;SIMBOLO PER DISPLAY
;ALTERN: 	EQU     0C308H   ;CONTEGGIO ALTERNANZA
;MAZTRA: 	EQU     0C30AH   ;AVVISO AZZERAMENTO TRAVERS
ENCOD1: 	EQU     0C310H   ;USATA IN TEST ENCODER
ENCOD2: 	EQU     0C312H   ;USATA IN TEST ENCODER
ENCOD3: 	EQU     0C314H   ;CONTEGGIO TENTATIVI TEST XXTT
ENCOD4: 	EQU     0C316H   ;CONTEGGIO IMPULSI FINALE  XXTT
TEST_ENC:	EQU	0C318H	 ;SEGNALAZIONE RICHIESTA TEST
MPIEN1: 	EQU     0C31AH   ;MEM TEMPORANEA PIENO 1 LONG
MPOSI1: 	EQU     0C31CH   ;MEM TEMPORANEA POSIZ.1 LONG
MASES1: 	EQU     0C31EH   ;MEM TEMPORANEA FASE 1 LONG
MPIEN2: 	EQU     0C326H   ;MEM TEMPORANEA PIENO 2 LONG
MPOSI2: 	EQU     0C328H   ;MEM TEMPORANEA POSIZ.2 LONG
MASES2: 	EQU     0C32AH   ;MEM TEMPORANEA FASE 2 LONG
TPIEN2: 	EQU     0C32CH   ;MEM TEMPORANEA PIENO 2 TRAV
TPOSI2: 	EQU     0C32EH   ;MEM TEMPORANEA POSIZ.2 LONG
TASES2: 	EQU     0C330H   ;MEM TEMPORANEA FASE 2 TRAV
TMEM4:  	EQU     0C332H   ;ATTIVAZZIONE CORREZ.TITRO
TMEM5:  	EQU     0C334H   ;USATA IN CORREZ.TRAVER
TMEM6:  	EQU     0C336H   ;SENSO CORREZZIONE TIRO
CONALL: 	EQU     0C338H   ;CONTEGGIO AVVISI ALLARME CIRC.
CONALT: 	EQU     0C33AH   ;CONTEGGIO AVVISI ALLARME TRAV.
;MEM0:   	EQU     0C400H   ;MEMORIA DELLA FUNZIONE 0
;MEM1:   	EQU     0C402H   ;MEMORIA DELLA FUNZIONE 1
;MEM2:   	EQU     0C404H   ;MEMORIA DELLA FUNZIONE 2
;MEM3:   	EQU     0C406H   ;MEMORIA DELLA FUNZIONE 3
;MEM5:   	EQU     0C408H   ;MEMORIA DELLA FUNZIONE 5 
;MEM6:   	EQU     0C40AH   ;MEMORIA DELLA FUNZIONE 6
;MEM6A:  	EQU     0C40CH   ;MEM. USATA NELLA FUNZIONE 6
;MEM7:   	EQU     0C40EH   ;MEMORIA DELLA FUNZIONE 7
MEMW:   	EQU     0C410H   ;USATA IN CALCOLO CORREZ.MOTORE
;MEMID:  	EQU     0C412H   ;MEM.INIZIALIZZIONE DISPLAY
DIRERR:		EQU     0C414H   ;DIREZIONE ERRORE
MDATER: 	EQU     0C418H   ;MEM.INSERIMENTO DATO ERRATO
MCALER: 	EQU     0C41AH   ;SEGNALA AL MAIN DI CALCOLARE
MSCORR: 	EQU     0C41CH   ;MEM.INDICA IL SENSO DELLA CORR.
ALTDIV: 	EQU     0C41EH   ;ALTERNA I VALORI NELLA DIVISIONE
CADDE0: 	EQU     0C420H   ;ADDENDO BASSO SOMMA A 32 BIT
CADDE2: 	EQU     0C422H   ;ADDENDO ALTO SOMMA A 32BIT
CAUGE0: 	EQU     0C424H   ;AUGENDO BASSO SOMMA A 32 BIT
CAUGE2: 	EQU     0C426H   ;AUGENDO ALTO SOMMA A 32 BIT
CSOMM0: 	EQU     0C428H   ;SOMMA BASSA A 32 BIT
CSOMM2: 	EQU     0C42AH   ;SOMMA ALTA A 32 BIT
MREST0: 	EQU     0C42CH   ;MEM RESTO BASSA SOTT32
MREST2: 	EQU     0C42EH   ;MEM RESTO ALTA SOTT32
;MEMALL:	EQU	0C430H	 ;MEM ALLARME ATTIVATO/DISATTIVATO 
MEMLED:		EQU	0C432H	 ;MEM. LED PRECEDENTI PER NON RIPETERE TRASM.
INCEXT: 	EQU     0C438H   ;AUMENTA VELOCITA SCANSIONE IN FUNZ5 FF55
;MOSCI1:		EQU	0C43AH	 ;USATA PER FASE CON OSCILLOSCOPIO ESTERNO FF57
PROPRI: 	EQU     0C450H   ;AVVISO PROTEZIONE MANCANZA STAMPA PPRR
PROVEL:		EQU	0C452H	 ;AVVISO PROTEZIONE BASSA VELOCITA PPRR
RESET:		EQU	0C454H	 ;SE ATTIVO FORZA RESET DELLA MACCHINA
STATUS:		EQU	0C460H	 ;CONTIENE LO STATUS DEGLI ALLARMI (2BYTE)
STATU2:		EQU	0C462H	 ;CONTIENE LO STATUS INVIATO DEGLI ALLARMI (2BYTE)
				 ; 0 = SVILUPPO CILINDRO
				 ; 1 = AMPIEZZA GATE
				 ; 2 = SEGNO DI RIFERIMENTO
				 ; 3 = ERRORE GENERICO
				 ; 4 = MANCANZA STAMPA
				 ; 5 = VELOCITA' SOTTO SOGLIA
				 ; 6 = SUPERAMENTO SOGLIA D'ERRORE
				 ; 7 = ------ NON USATO  -----
				 ; 8 = TEST TESTINA FALLITO
				 ; 9 = TEST ENCODER FALLITO
				 ;10 = FASATURA FALLITA
				 ;11 = ------ NON USATO  -----
				 ;12 = ------ NON USATO  -----
				 ;13 = ------ NON USATO  -----
				 ;14 = ------ NON USATO  -----
				 ;15 = ------ NON USATO  -----
MATTES: 	EQU     0C464H   ;USATA IN DECREM.CILCI ATTESA
;MTASTI: 	EQU     0C466H   ;SEGNALA PASSAGGIO DALLA TASTIERA
 
;ALTERR: 	EQU     0C468H   ;ALT CALCOLO ERR.DOPO USO TASTIER 
;ZEROSE: 	EQU     0C46AH   ;SEGNO IN ZERO ENCODER         
;ALTGUA: 	EQU     0C46CH   ;DOPO N GIRI FISSA IL GUADAGNO 

MFASE:   	EQU     0C466H   ;MEM. INIZIO GATE DELLA FASE S1
MFASES1: 	EQU     0C468H   ;MEMORIA DELLA FASE DI S1
MFASRIF: 	EQU     0C46AH   ;MEM.FASE DI RIFERIMENTO DI S1


MRIC:   	EQU     0C470H   ;GRUPPO MEME IN RICERCA AUTOMATICA
MDO032: 	EQU     0C480H   ;MOLTIPLICANDO BASSO A 32 BIT 
MDO232: 	EQU     0C482H   ;MOLTIPLICANDO ALTO A 32 BIT 
MRE032: 	EQU     0C484H   ;MOLTIPLICATORE BASSO 32 BIT 
MRE232: 	EQU     0C486H   ;MOLTIPLICATORE ALTO A 32 BIT
PTO032: 	EQU     0C488H   ;PRODOTTO BASSO A 32 BIT 
PTO232: 	EQU     0C48AH   ;PRODOTTO ALTO  A 32 BIT
EDEND0: 	EQU     0C48CH   ;DIVIDENDO BASSO DIVISIONE 32BIT
EDEND2: 	EQU     0C48EH   ;DIVIDENDO ALTO DIVISIONEO 32BIT
QUOZ32: 	EQU     0C490H   ;QUOZ DIVISIONE 32BIT DIVISO 4096
MINUE0: 	EQU     0C492H   ;MINUENDO BASSO SOTT. A 32 BIT
MINUE2: 	EQU     0C494H   ;MINUENDO ALTO SOTT. A 32 BIT
SUBTR0: 	EQU     0C496H   ;SOTTRAENDO BASSO SOTT. A 32 BIT
SUBTR2: 	EQU     0C498H   ;SOTTRAENDO ALTO SOTT. A 32 BIT                     
RESTO0: 	EQU     0C49AH   ;RESTO BASSO SOTT. A 32 BIT
RESTO2: 	EQU     0C49CH   ;RESTO ALTO SOTT. A 32 BIT
MIXUE0: 	EQU     0C49EH   ;MINUENDO ALTERNATIVO BASSO
MIXUE2: 	EQU     0C4A0H   ;MINUENDO ALTERNATIVO ALTO
SUXTR0: 	EQU     0C4A2H   ;SOTTRAENDO ALTERNATIVO BASSO
SUXTR2: 	EQU     0C4A4H   ;SOTTRAENDO ALTERNATIVO ALTO
TMSCOR: 	EQU     0C4D4H   ;CORREZIONE TRASVERSALE
TINUE0: 	EQU     0C4DCH   ;MINUENDO BASSO SOTT. A 32 BIT
TINUE2: 	EQU     0C4DEH   ;MINUENDO ALTO SOTT. A 32 BIT
TUBTR0: 	EQU     0C4E0H   ;SOTTRAENDO BASSO SOTT. A 32 BIT
TUBTR2: 	EQU     0C4E2H   ;SOTTRAENDO ALTO SOTT. A 32 BIT                     
TREST0: 	EQU     0C4E4H   ;RESTO BASSO SOTT. A 32 BIT
TREST2: 	EQU     0C4E6H   ;RESTO ALTO SOTT. A 32 BIT
TIXUE0: 	EQU     0C4E8H   ;MINUENDO ALTERNATIVO BASSO
TIXUE2: 	EQU     0C4EAH   ;MINUENDO ALTERNATIVO ALTO
TUXTR0: 	EQU     0C4ECH   ;SOTTRAENDO ALTERNATIVO BASSO
TUXTR2: 	EQU     0C4EEH   ;SOTTRAENDO ALTERNATIVO ALTO
CINUE0: 	EQU     0C4F0H   ;MINUENDO BASSO CSOTT32
CINUE2: 	EQU     0C4F2H   ;MINUENDO ALTO CSOTT32
CUBTR0: 	EQU     0C4F4H   ;SOTTRAENDO BASSO CSOTT32
CUBTR2: 	EQU     0C4F6H   ;SOTTRAENDO ALTO CSOTT32                     
CESTO0: 	EQU     0C4F8H   ;RESTO BASSO CSOTT32
CESTO2: 	EQU     0C4FAH   ;RESTO BASSO CSOTT32
CODIS0: 	EQU     0C4FCH   ;DIST.BASSA TRAVER COMPENSATA
CODIS2: 	EQU     0C4FEH   ;DIST.ALTA TRAVER COMPENSATA
INCREM: 	EQU     0C500H   ;MEM.A DECR.INCREMENTA VALORE   
DECREM: 	EQU     0C502H   ;MEM.A DECR.DECREMENTA VALORE
;SECFUN: 	EQU     0C600H   ;SECONDA RIGA FUNZ LCD
;SECERR: 	EQU     0C620H   ;SECONDA RIGA ERRORE
;NUMCOL: 	EQU     0C640H   ;RIGA NUMERO COLORE

;SCRIOS: 	EQU     0C660H   ;0C6AH SCRITTURA OSCILLOSCOPIO 
;TRIGFA: 	EQU     0C700H   ;MEM.FASE DI TRIGGER OSCILLO. 

MEMA:   	EQU     0C800H   ;MEMORIA DELLA PORTA A  
MEMB:   	EQU     0C802H   ;MEMORIA DELLA PORTA B  

MAVA00: 	EQU     0C900H   ;DA 0C900H A 0C95FH  SONO 96 LOCAZIONI WWAA
				 ;USATE IN CALCOLO MEDIA ERRORE AVANTI
MRIT00: 	EQU     0C960H   ;DA 0C960H A 0C9BFH  SONO 96 LOCAZIONI WWAA
				 ;USATE IN CALCOLO MEDIA ERRORE RITARDO
;DAVA00: 	EQU     0C9C0H   ;DA 0C9C0H A 0C9DFH  SONO 16 LOCAZIONI WWAA
;				 ;USATE IN CALCOLO MEDIA ERRORE AVANTI DISPLAY
;DRIT00: 	EQU     0C9E0H   ;DA 0C9E0H A 0C9FFH  SONO 16 LOCAZIONI WWAA
;				 ;USATE IN CALCOLO MEDIA ERRORE RITARDO DISPLAY
ERATTU: 	EQU     0CA00H   ;ERRORE ATTUALE   ;LLTT FFXX   MMDD  WWAA
ERPREC: 	EQU     0CA02H   ;ERRORE PRECEDENTE   LLTT FFXX      WWAA
ERMEDI: 	EQU     0CA04H   ;MEMORIA ERRORE MEDIO LONG LLTT FFXX    WWAA
ERMEDI_DISP:	EQU	0CA06H   ;MEMORIA ERRORE MEDIO LONGITUDINALE DA SPEDIRE
ERMEDI_SIGN:	EQU	0CA08H	 ;SEGNO ERRORE MEDIO LONG (1=POSITIVO,8=NEGATIVO)
SOMLO1: 	EQU     0CA18H   ;PRIMA SOMMA PER MEDIA LONG GGAA   WWAA
SOMLO2: 	EQU     0CA1AH   ;SECONDA SOMMA PER MEDIA LONG GGAA WWAA
SOMDL1: 	EQU     0CA1CH   ;USATA IN CALCOLO MEDIA DIPLAY LONG.
SOMDL2: 	EQU     0CA1EH   ;USATA IN CALCOLO MEDIA DIPLAY LONG.


PRIMOB: 	EQU     0CD00H   ;VALORE DA DARE AL PRIMO REG B IN MEDIALOMG   VVVV
SECONB: 	EQU     0CD02H   ;VALORE DA DARE AL SECONDO REG B IN MEDIALOMG VVVV
VALORX: 	EQU     0CD04H   ;VALORE DA DARE AL REG IX IN MEDIALOMG        VVVV
TABELL: 	EQU     0CE00H   ;0CE00H A 0CD00H TABELLA DI TRANSCODIFICA INDIRIZZI PARAMETRI 

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	AREA DI MEMORIA NON VOLATILE D000-DFFFH
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

MEMC:   	EQU     0D000H   ;MEMORIA DELLA PORTA C

CILIN:  	EQU     0D004H   ;MEM. SVILUPPO CILINDRO
ALLAR:  	EQU     0D006H   ;MEM. VALORE ALLARME IN CENTESIMI
COSVEL: 	EQU     0D008H   ;COSTANTE USATA IN VELOCITA'
CENTES: 	EQU     0D00AH   ;CENTESIMI IN UN IMP.DI ENCODER
MILLES: 	EQU     0D00CH   ;MILLESIMI IN UN IMP.DI ENCODER
AMPI10: 	EQU     0D00EH   ;AMPIMP x 40 USATA CON CKTG0 1024 
FASE:   	EQU     0D010H   ;MEM. INIZIO GATE DELLA FASE S1
FASES1: 	EQU     0D012H   ;MEMORIA DELLA FASE DI S1
FASRIF: 	EQU     0D014H   ;MEM.FASE DI RIFERIMENTO DI S1
RAPPO1: 	EQU     0D016H   ;MEM. RAPPORTO S1
RAPRIF: 	EQU     0D018H   ;RAPPORTO DI RIFERIMMENTO
RIFTOT: 	EQU     0D01AH   ;(FASRIFx10)+RAPRIF=RIFTOT
POSTOT: 	EQU     0D01CH   ;(FASES1x10)+RAPRIF=RIFTOT
PIENO1: 	EQU     0D01EH   ;MEM. VALORE DEL PIENO S1
POSIZ1: 	EQU     0D020H   ;MEM. VALORE DELLA POSIZIONE S1
MEMRIF: 	EQU     0D022H   ;USATA PER MEMORIZ. I RIFERIMENTI
;TASES1: 	EQU     0D024H   ;MEM TEMPORANEA FASE 1 TRAV
;TRAPP1: 	EQU     0D026H   ;MEM.RAPPORTO S1 TRAVERS
;TPOST1: 	EQU     0D028H   ;POSIZIONE TOTALE S1 TRAVERS
;TPIEN1: 	EQU     0D02AH   ;MEM TEMPORANEA PIENO 1 TRAV
;TPOSI1: 	EQU     0D02CH   ;MEM TEMPORANEA POSIZ.1 TRAV
;SPELET: 	EQU     0D02EH   ;SPESSORE LETTO
;SPERIF: 	EQU     0D030H   ;SPESSORE DI RIFERIMENTO
UNOMIL: 	EQU     0D032H   ;1mm IN IMPULSI ENCODER
AMPIMP: 	EQU     0D034H   ;AMPIEZZA GATE IN IMPULSI ENCODER
MEZGA:  	EQU     0D036H   ;MEZGA PER 4 MEZGA=AMPIE. 50% GATE  
CILDIV: 	EQU     0D038H   ;CILINDRO DIVISO 10
CILMOL: 	EQU     0D03AH   ;CILINDRO MOLTIPLICATO x 10
CICL75: 	EQU     0D03CH   ;75% DEL CICLO DI CORREZZIONE 
CIDE75: 	EQU     0D03EH   ;MEM. DEL 75% DEL CICLO DI CORREZZIONE 
MEMAB:  	EQU     0D040H   ;E D042H USATE IN ABILITAZ.GATE
SEQSEG: 	EQU     0D046H   ;E D048H POS.SEGNO NELLA SEQUENZA 
SEGNO:  	EQU     0D04AH   ;IMP. DA 2048 IN UN SEGNO DA 2mm
SPAZIO: 	EQU     0D04CH   ;IMP.DA 2048 IN UNO SPAZIO DA 5mm
SPAZ40: 	EQU     0D04EH   ;IMP.DA 2048 IN SPAZIO DA 40mm
DISTAN: 	EQU     0D050H   ;DISTANZA TEORICA TRA S1 E S2
MILL20: 	EQU     0D052H   ;20mm IN IMPULSI ENCODER     
MILL40: 	EQU     0D054H   ;40mm IN IMPULSI ENCODER
GUADS1: 	EQU     0D056H   ;VALORE GUADAGNO SEGNALE S1
;GUADS2: 	EQU     0D058H   ;VALORE GUADAGNO SEGNALE S2 
DMILLE: 	EQU     0D05AH   ;DECIMILLESIMI IN UN IMP.DI ENCOD
DISTA0: 	EQU     0D05CH   ;DISTANZA BASSA IN DECIMILLESIMI
DISTA2: 	EQU     0D05EH   ;DISTANZA ALTA IN DECIMILLESIMI
;INFLAS: 	EQU     0D060H   ;INTERVALLO FLASH PROTEZIONI
LAVORO: 	EQU     0D062H   ;NøDEL LAVORO PRESENTE
SHIFT:  	EQU     0D064H   ;MEM.DELLO SPOSTAMENTO REGISTRO
;TSHIFT: 	EQU     0D066H   ;MEM. SPOSTAMENTO REG.TRAVERS
AVARIT: 	EQU     0D068H   ;MEM.SENSO DELLO SPOSTAMENTO LONG
TRAOPE: 	EQU     0D06AH   ;MEM.SENSO DELLO SPOSTAMENTO TRAV
;SHIFT0: 	EQU     0D06AH   ;MEM.BASSA SPOSTAMENTO REGISTRO
;SHIFT2: 	EQU     0D06CH   ;MEM. ALTA SPOSTAMENTO REGISTRO

;POSGAT: 	EQU     0D06EH   ;POSIZIONE CARATTERE INIZIO GATE   

;PIENO2: 	EQU     0D070H   ;MEM. VALORE DEL PIENO S2 
;POSIZ2: 	EQU     0D074H   ;MEM. VALORE DELLA POSIZIONE S2 
;RAPPO2: 	EQU     0D078H   ;MEM. RAPPORTO S2 
;IMPENC: 	EQU     0D07AH   ;VALORE SPOSTAM.IN IMPULS.ENCODER
;XUNZ:   	EQU     0D07CH   ;MEMORIA FUNZIONE MOD. PARAMETRI
;XTASTO: 	EQU     0D07EH   ;MEMORIA ULTIMO TASTO PREMUTO
;TIUMEN: 	EQU     0D080H   ;MEM.SENSO DELLO SPOSTAM.TRAV
TDIST0: 	EQU     0D082H   ;DISTAN.BASSA IN DECIMILL.TRAV
TDIST2: 	EQU     0D084H   ;DIATAN. ALTA IN DECIMILL.TRAV
;TATTES: 	EQU     0D086H   ;ATTESA CORREZ.TRAVERS
;FASE2:  	EQU     0D088H   ;MEM. INIZIO GATE DELLA FASE S2   
;FASES2: 	EQU     0D08AH   ;MEMORIA DELLA FASE DI S2 
;POSTO2: 	EQU     0D08CH   ;POSIZIONE TOTALE S2
;CICATT: 	EQU     0D08EH   ;CICLI IN FUNZ. POSIZ. CILIN. LONG. 
;TCICAT: 	EQU     0D090H   ;CICLI IN FUNZ. POSIZ. CILIN. TRAV. 
AMPGA:  	EQU     0D092H   ;AMPIEZZA GATE IN mm  

GUADA:  	EQU     0D0A0H   ;MEM.DEL GUAD.min 11 x CIL=2000
CICLO:  	EQU     0D0A2H   ;MEMORIA CICLO DI CORREZIONE
DERIVA: 	EQU     0D0A4H   ;MEMORIA DEL VALORE DERIVATIVO
ZONA:   	EQU     0D0A6H   ;MEMORIA VALORE ZONA MORTA
INSER:  	EQU     0D0A8H   ;MEMORIA VALORE INSERZIONE AUTO
SALTO:  	EQU     0D0AAH   ;MEM. VALORE SALTO DI CORREZIONE
ZONT:   	EQU     0D0ACH   ;MEM. ZONA MORTA TRAVERS
TRIP:   	EQU     0D0AEH   ;SOGLIA BLOCCO PER VELOCITA' 
GUATRA: 	EQU     0D0B0H   ;MEM. GUADAGNO TRAVERS 
TCICLO: 	EQU     0D0B2H   ;MEM. CILCO TRAVERS 
MESSA:  	EQU     0D0B4H   ;SELEZIONE TIPO DI MESSA A REGISTRO  CCMM 
NORINV: 	EQU     0D0B6H   ;MEM. CORREZZIONE NORMALE O REVERS NNII  
MM_MC:  	EQU     0D0B8H   ;SELEZIONE MM/MC  
SEGRIF: 	EQU     0D0BAH   ;SCELTA SEGNO DI RIFERIMENTO SSRR 
VCLONG: 	EQU     0D0BCH   ;VELOCITA DI CORREZIONE LONGITUDINALE 
VCTRAV: 	EQU     0D0BEH   ;VELOCITA DI CORREZIONE TRASVERSALE  
DIAGON: 	EQU     0D0C0H   ;MEM. POSIZIONE IPOTENUSA 
TIRGUA: 	EQU     0D0C2H   ;MEM. GUADAGNO DI TIRO
TIRCIC: 	EQU     0D0C4H   ;MEM. CICLO DI TIRO
PRESS:  	EQU     0D0C6H   ;MEM.TIPO DI ROTATIVA DDDD RRRR   
CODE:		EQU	0D0C8H	 ;FUNZIONAMENTO CON CODICE O SENZA
LINGUA: 	EQU     0D0CAH   ;MEMORIA DELLA LINGUA

ENCOD:		EQU	0D17CH	 ;FUNZIONAMENTO CON ENCODER NPN O PNP (SI AUTOSETTA SU TEST ENCODER)
DMEZGA: 	EQU     0D17EH   ;40% GATE IN CENTESIMI
MANGUA: 	EQU     0D186H   ;GUADAGNO CORREZIONE MANUALE
CENSEN: 	EQU     0D190H   ;VALORE SEGNO CENTRATO NEL GATE
AMPMEZ: 	EQU     0D192H   ;AMPIMP DIVISO 2
AMPI40: 	EQU     0D194H   ;AMPIEZZA GATE DEL40%  IN CENTS  
ENTRAS: 	EQU     0D196H   ;ABILITA TRASVERSALE
ENTIRO: 	EQU     0D198H   ;ABILITA TIRO
SPACOD: 	EQU     0D19AH   ;E' LA COPIA DI MILL40  TTGG
CICLI_F9:	EQU     0D19CH   ;GIRI ATTESA PRIMA DI ANNULLARE F9
CENTE4: 	EQU     0D19EH   ;CENTES PER 4 
EN_F9:		EQU	0D1A0H	 ;ABILITI FUNZIONE REGISTRO COLD SEAL
;DISPRE: 	EQU     0D1A0H   ;ERRORE DISPLAY LONG.PRECEDENTE 
TDISPR: 	EQU     0D1A2H   ;ERRORE DISPLAY TRAV.PRECEDENTE 
IMPMIL: 	EQU     0D1A4H   ;IMPULSI DA 2048 PER mm PIU UNO  
BIANCO:		EQU	0D1A6H   ;MINIMO SPAZIO BIANCO PER RA BBBB	

AUMALO:		EQU	0D1A8H	 ;TABELLA MANUALE/AUTOMATICO LONGITUDINALE 
AUMATR:		EQU	0D1AAH	 ;TABELLA MANUALE/AUTOMATICO TRAVARS 
AUMATI: 	EQU     0D1ACH   ;MODO DI TIRO (AUTOMATICO MANUALE)

MDIAGO: 	EQU     0D1B0H   ;USATA IN SCELTA DIAGONALE

;TABELL: 	EQU     0D800H   ;0D800H A 0D900H TABELLA DI TRANSCODIFICA INDIRIZZI PARAMETRI 

VERPRO:		EQU	0D910H	 ;VERSIONE PROGRAMMA 
IDMACC:		EQU	0D912H	 ;IDENTIFICAZIONE MACCHINA 
DACLON:		EQU	0D914H	 ;ABILITA DAC ERRORE LONG 
DACTRA:		EQU	0D916H	 ;ABILITA DAC ERRORE TRAV 
DACINS:		EQU	0D918H	 ;ABILITA DAC INSETTER 
TERATT: 	EQU     0D91AH   ;ERRORE ATTUALE TRAVERS 
;LED:		EQU	0D91CH	 ;STATO DEI LED 
                         ;
PROVLT: 	EQU     0D98CH   ;DA ELIMINARE IN PROGRAMMA REGOLARE  PRRR
SALVA:  	EQU     0D990H   ;USATA NELLA CREAZIONE TABELLE TEST
                         ;

TAP1:   	EQU     0DA20H   ;TABELLA IND. INTER. PIO1A
TAP2:   	EQU     0DA22H   ;TABELLA IND. INTER. PIO1B
TAB0:   	EQU     0DA30H   ;TABELLA IND. INTER. CTC CANALE 0
TAB1:   	EQU     0DA32H   ;TABELLA IND. INTER. CTC CANALE 1
TAB2:   	EQU     0DA34H   ;TABELLA IND. INTER. CTC CANALE 2
TAB3:   	EQU     0DA36H   ;TABELLA IND. INTER. CTC CANALE 3
TABLAV: 	EQU     0DB00H   ;INIZIO TABELLA MEM. LAVORI


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;			 BIT DI PC (USCITE)
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
B_PC_AVANTI:	EQU	0
B_PC_RITARDO:	EQU	1
B_PC_PROTEZIONE:EQU	4	;TODO:MA NON E' DA RIMUOVERE?
B_PC_ALLARME:	EQU	3
B_PC_FUNZ:	EQU	5
B_PC_DESTRA:	EQU	6
B_PC_SINISTRA:	EQU	7


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;			POSSIBILI STATI DI DIRECTION
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
D_AVANTI:	EQU	01H
D_RITARDO:	EQU	08H

D_TRAVERSA:	EQU	01H
D_OPERATORE:	EQU	08H

D_INC_TIRO:	EQU	01H
D_DEC_TIRO:	EQU	08H

D_DESTRA:	EQU	01H
D_SINISTRA:	EQU	08H

GIGIO:	EQU	0DC00H


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		INIZIALIZZAZIONE SISTEMA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

		ORG     0000H

W1:
                JP      VIA


		ORG     38H

		JP      VIA

		ORG     100H

VIA:            
		LD      SP,0DFFFH       ;DEF.AREA STACK
		IM      2               ;MODO 2
                LD      A,0DAH          ;IND.ALTO TAB.
		LD      I,A
                LD      IY,PIOA         ;IND.ROUT.INT.
		LD      (TAP1),IY       ;CAR.TAB.INT.
		LD      A,20H           ;VET.INTER.PIO A
		OUT     (02H),A         ;CONTROLLO PIO A
		LD      A,0CFH          ;PORTA A MODO 3
		OUT     (02H),A
                LD      A,11H           ;0-4 IN 123567 OUT
		OUT     (02H),A
		LD      A,97H           ;PAR.CONT.INTER.
		OUT     (02H),A 
		LD      A,0EFH          ;P.M.BIT 0123567
		OUT     (02H),A
                LD      IY,PIOB         ;IND.ROUT.INT.
		LD      (TAP2),IY       ;CAR.TAB.INT.
		LD      A,22H           ;VET.INT.PIO B
		OUT     (03H),A         ;CONTROLLO PIO B
		LD      A,0CFH          ;PORTA B MODO 3
		OUT     (03H),A
                LD      A,3EH           ;067 OUT 12345 IN 1E PPUU
		OUT     (03H),A
		LD      A,97H           ;PAR.CONT.INTER.
		OUT     (03H),A
                LD      A,0DDH          ;P.MAS.BIT 023467 FD PPUU
		OUT     (03H),A         ;ALL'INTERUPT
                LD      IY,CTC0         ;IND.ROUT.CTC0.
		LD      (TAB0),IY       ;CAR.TAB.INT.CAN0
		LD      A,30H           ;VET.IND.CTC0
		OUT     (70H),A         ;VET.SEMPRE IN 0
                LD      IY,CTC1         ;IND.ROUT.CTC1
		LD      (TAB1),IY       ;CAR.TAB.INT.CAN1
		LD      A,32H           ;VET.IND.CTC1
		OUT     (70H),A
                LD      IY,CTC2         ;IND.ROUT.CTC2
		LD      (TAB2),IY       ;CAR.TAB.INT.CAN2
		LD      A,34H           ;VET.IND.CTC2
		OUT     (70H),A
                LD      IY,CTC3         ;IND.ROUT.CTC3
		LD      (TAB3),IY       ;CAR.TAB.INT.CAN3
		LD      A,36H           ;VET.IND.CTC3
		OUT     (70H),A         ;VET.CARICATO

		;DEFAULT PER PORTA A
		LD	A,(MEMA)	
		RES	1,A		;RESET RICHIESTA DI TRASMISSIONE	
		SET     7,A		;ABILITO SEMPRE LA GENERAZIONE DEL GATE
		LD	(MEMA),A
	 	OUT     (PA),A


		;RESET DELLA PORTA C

		LD      A,(MEMC)        ;RESETTO LE USCITE
		RES     B_PC_AVANTI,A             
		RES     B_PC_RITARDO,A             
		RES	B_PC_ALLARME,A
		RES     B_PC_FUNZ,A             
		RES     B_PC_DESTRA,A             
		RES     B_PC_SINISTRA,A             
		OUT     (PC),A
		LD      (MEMC),A

                ;AZZERAMENTO DA 0C000H A 0CFFFH (MEMORIA VOLATILE)
		LD      A,00H
                LD      (TABCON),A      ;LOC. INIZIALE
                LD      HL,TABCON
                LD      DE,TABCON+01H
                LD      BC,0FFFH
		LDIR                    

		LD      A,04H           ;PER NON AVERE IN
		LD      (MATTES),A      ;DECREMENTO FFH
		LD      (CONSAL),A

                LD      (CONSAT),A

		LD      A,(TIRCIC)      ;CARICO LE MEM.
		LD      (INCREM),A      ;A DECREMENTO
		LD      (DECREM),A      ;DEL MOTOPOTENZ.
                LD      (MOTPO1),A

	
		CALL	FAITAB	
		CALL	AZZER_MSB
		CALL    DEFAUL
		CALL	FORCE_STATUS		;RESET STATUS ALLARMI
                CALL    VARERR

		EI
                CALL	VERIFI

		


;çççççççççççççççççççççççççççççççççççççççççççççççççççç
                LD      IY,TABLAV
		LD      (SALVA),IY
;ççççççççççççççççççççççççççççççççççççççççççççççççççççç
                



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		MAIN - PROGRAMMA PRINCIPALE DI CALCOLO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

MAIN:
		LD	HL,(IMPVEL)	;SE LA VELOCITA' E' ALTA SPEDISCO 
		LD      DE,0080H	;LO STATO DEI LED SOLO UNA
		SBC     HL,DE		;VOLTA AL GIRO DA QUI
		JR	C,MAIN_LOOP
		CALL	SEND_LED
		CALL	SEND_SPEED
		
		
MAIN_LOOP:		
		LD      A,(MCALER)      ;VEDE SE DEVE ABBB
		CP      00H             ;CALCOLARE O NO
		JP      Z,MAIN_WAITING


MAIN_FASATURA_LONG:
		CALL	FASE_AUTO_LONG
		
		LD      A,00H		;SETTO FLAG PER EVITARE
		LD      (MCALER),A	;DI FARE I CALCOLI DUE VOLTE

		LD	A,(PROPRI)	;SE NO PRINT
		CP	01H		;SALTO I CALCOLI DELL'ERRORE
		JP	Z,MAIN_SOGLIA_INSER
		
		CALL	CHK_PARAM	;VERIFICA TIPO DI ERRORE DA SCRIVERE

		CALL	ERR_CALC	;CALCOLO ERRORE ATTUALE

		LD	A,(AUMALO)	;SE SONO IN AUTOMATICO
		CP	0FFH		;VISUALIZZO ERRORE MEDIATO
		JR	Z,MAIN_ERR1

		LD	A,(MEMC)	;SE SONO IN MANUALE E' STO CORREGGENDO
		BIT	B_PC_AVANTI,A	;IN AVANTI VISUALIZZO ERRORE ISTANTANEO
		JR	NZ,MAIN_ERR2

		LD	A,(MEMC)	;SE SONO IN MANUALE E' STO CORREGGENDO
		BIT	B_PC_RITARDO,A	;IN RITARDO VISUALIZZO ERRORE ISTANTANEO
		JR	NZ,MAIN_ERR2

		JR	MAIN_ERR1	;ALTRIMENTI, SE SONO IN MANUALE 
					;E NON STO CORREGGENDO, VISUALIZZO ERRORE 
					;MEDIATO

MAIN_ERR1:
                CALL    LMEDIA		;CALCOLO MEDIA DELL'ERRORE
		LD	A,(ERMEDI_SIGN)
		LD	(MSCORR),A 
		CALL	SEND_ERR

		JR	MAIN_OVERCORRECTION
						
MAIN_ERR2:
		CALL	RESET_MEDIA	;EQUIVALE A PRENDERE IL VALORE ISTANTANEO
		CALL	SEND_ERR
		JR	MAIN_OVERCORRECTION

MAIN_OVERCORRECTION:

                CALL    OVERCO		; GESTIONE DELLA CORREZIONE SUPERIORE A MEZZO GATE 

MAIN_SOGLIA_INSER:
		CALL	CHKSOGLIN

		CALL	CHKALARM


MAIN_CORREZIONI:
		CALL    CORREZIONI

MAIN_WAITING:


MAIN_TRASMISSIONE_DATI:
		LD	HL,(IMPVEL)	;SE LA VELOCITA' E' LENTA, CONTINUO
		LD      DE,0080H	;A SPEDIRE LO STATO DEI LED
		SBC     HL,DE		
		JR	NC,MAIN_TRASMISSIONE_DATI2
		CALL	SEND_LED
		CALL	SEND_SPEED
MAIN_TRASMISSIONE_DATI2:
		CALL	SEND_STATUS


MAIN_CHK_ENCODER:
		LD	A,(TEST_ENC)	;SE FLAG TEST ENCODER E ABILITATO
		BIT	0,A		;ESEGUI TEST
		JR	Z,MAIN_CHK_ALARM
		CALL	ENC1

MAIN_CHK_ALARM:
		LD	A,(AUMALO)	
                CP	0FFH		;SE SONO IN MANUALE 
		JR	Z,MAIN_CHK_SPEED		
		CALL	ALARM_OFF	;DISATTIVO ALLARME

MAIN_CHK_SPEED:
		CALL	CHK_SPEED_ZERO

MAIN_CHK_RESET:
		
		LD	A,(RESET)	;SE FLAG RESET=FF ALLORA RIPARTO DA VIA
		CP	0FFH
		JP	Z,VIA
MAIN_FINE:	
		JP	MAIN





;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ROUTINE DI CONTROLLO SOGLIA INSERZIONE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CHKSOGLIN:
	        CALL    VELO
                LD      HL,(ISTVEL)  
		LD      DE,(INSER)      
		SBC     HL,DE            
		JR      C,SOGLIN_ERR

SOGLIN_OK:      CALL	SPEEDOK
		JR	CHKSOGLIN_FINE

SOGLIN_ERR:         
		CCF        
		LD      A,05H           ;BLOCCO ALLARME
		LD      (CONALL),A	;CONTEGGIO AVVISI ALLARME CIRC
		LD      A,07H           ;SE INSER E' MAGGIORE BLOCCO LA CORREZIONE 
		LD      (MATTES),A      ;E AUMENTO I CICLI DI ATTESA.
		
		CALL    SPEED_LOW 
		JR      CHKSOGLIN_FINE

CHKSOGLIN_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ROUTINE DI CONTROLLO SOGLIA ALLARME 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CHKALARM:       
		IN      A,(PB)          
                BIT     3,A             ;SE MANCA IL SEGNO
                JR      NZ,ALLA2        ;SALTO

                LD      DE,(DMEZGA)     ;40% GATE IN CENTESIMI
                LD      HL,(ERMEDI)     ;QUANDO L'ERRORE
                SBC     HL,DE           ;E' MINORE 
                JR      NC,ALLA2        ;DEL 40% DEL
                CCF                     ;GATE E SONO
                LD      A,(CICLI_F9)    ;PASSATI 2
                DEC     A               ;GIRI TOLGO
                LD      (CICLI_F9),A    ;USCITA F9
                CP      02H             ;
                JR      NC,ALLA1         
                CP      00H             
                JR      NZ,ALLA2   
		CALL	F9_OFF		;DISATTIVO F9
                JR      ALLA2

ALLA1:          LD      A,02H           ;EVITO CHE
                LD      (CICLI_F9),A    ;CICLI_F9 > 2

ALLA2:          
		LD	A,(AUMALO)	;SE SONO IN MANUALE
		CP	00H		;DISATTIVO ALLARME
		JR      Z,NOALL
	
		LD	A,(PROVEL)	;SE BASSA VELOCITA 
		CP	01H		;DISATTIVO ALLARME
		JR	Z,NOALL

		LD      A,(ALLAR)       ;SE SOGLIA ALLARME (P2) = 0
		CP      00H             ;SALTA.
		JR      Z,NOALL  

		LD	A,(PROPRI)	;SE NO PRINT
		CP	01H		;ATTIVO ALLARME
		JR	Z,SIALL

		LD      HL,(ALLAR)      ;SE ERRORE > SOGLIA ALLARME (P2)
		LD      DE,(ERMEDI)	;CONTO 5 AVVISI E POI ATTIVO ALLARME
		SBC     HL,DE         
		JR      NC,NOALL   
		
		CCF
		LD      A,(CONALL)      ;SE HO CONTATO 5 CICLI DI AVVISO
		CP      00H             ;ATTIVO L'ALLARME
		JR      Z,SIALL    
		DEC     A
		LD      (CONALL),A
		JR      ALLA_FINE

SIALL:          
		CALL	ALARM_ON	;ATTIVO ALLARME
		JR	ALLA_FINE

NOALL:          
		CALL	ALARM_OFF	;DISATTIVO ALLARME
		LD      A,05H           ;E CARICO 5 CICLI PRIMA 
		LD      (CONALL),A      ;DI DARE L'ALLARME
		JR	ALLA_FINE
ALLA_FINE:
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ROUTINE DI FASATURA AUTOMATICA TRASVERSALE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

FASE_AUTO_TRAS:         
		LD      HL,(FASE)
		LD      A,H
		OUT     (20H),A
		LD      A,L
		OUT     (10H),A
FASE_AUTO_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ROUTINE DI FASATURA AUTOMATICA LONGITUDINALE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

FASE_AUTO_LONG: 
		LD      A,(MRIC+00H)	;SE RICERCA E' GIA' IN CORSO, ESCO
		CP      01H
                JP      NZ,FASE_AUTO_LONG_FINE

		;TODO: Ma non si dovrebbero resettare tutte le uscite?
;		LD      A,(MEMC)        ;RESET USCITA DI LIBERO
;		RES     5,A
;		LD      (MEMC),A
;		OUT     (PC),A   
		CALL	RESEPC
		
		CALL    CODICE
		CALL	FASE_TROVATA
		
;		LD      A,(MEMB)	;RESET RA - SMETTO DI FORZARE
;		RES     6,A		;IL GATE BASSO
;		LD      (MEMB),A
;		OUT     (PB),A
;		CALL	FUNZ0

		LD      A,00H		;RESET USCITE DI RICERCA
		LD      (MRIC+00H),A
		LD      (MRIC+02H),A
                LD      (MRIC+0AH),A
                LD      A,60H           
		LD      (GUADS1),A      ;METTO GUADAGNO MINIMO
                LD      A,02H
                LD      (MRIC+06H),A

FASE_AUTO_LONG_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CONTROLLO DELL'ERRORE ATTUALE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

ERR_CALC:
		;CALCOLO RAPPORTO POSIZINE PIENO S1

	        LD      DE,(ERATTU)     ;MEMORIZZO ERRORE
		LD      (ERPREC),DE     ;PRECEDENTE
                LD      A,(MSCORR)      ;
		LD      IX,PIENO1       ;DIVIDO PIENO
		LD      E,(IX+00H)      ;PER DIECI
		LD      D,(IX+01H)      ;E SOTTRAGGO
		LD      (DIVDEN),DE     ;IL RISULTATO
		LD      BC,000AH        ;A POSIZIONE
		LD      (DIVSOR),BC     ;SE IL RISULTATO
		CALL    DIVIS           ;E' NEGATIVO
		LD      IX,QUOZIE       ;VUOLE DIRE CHE
		LD      E,(IX+00H)      ;DIVIDENDO
		LD      D,(IX+01H)      ;POSIZIONE X 10
		LD      IY,POSIZ1       ;CON PIENO AVRO'
		LD      L,(IY+00H)      ;UN NUMERO MINORE
		LD      H,(IY+01H)      ;DI 0,1 PER CUI
		SBC     HL,DE           ;CARICO IN MEM.
		JR      C,RAPP10        ;RAPPORTO ZERO.
		LD      E,(IY+00H)      ;SE LA SOTTRZIONE
		LD      D,(IY+01H)      ;NON DA RISULTATO
		LD      (MOLTI),DE      ;NEGATIVO PROCEDO
		LD      A,0AH           ;NELLA DIVISIONE
		LD      (MOLPL),A       ;POSIZIONE X 10
		CALL    MOLT            ;DIVISO PIENO
		LD      IX,PROD         ;E OTTERRO'UN
		LD      C,(IX+00H)      ;RISULTATO 
		LD      B,(IX+01H)      ;COMPRESO 
		LD      (DIVDEN),BC     ;TRA 0,1 E 0,9
		LD      IY,PIENO1
		LD      E,(IY+00H)
		LD      D,(IY+01H)
		LD      (DIVSOR),DE
		CALL    DIVIS
                LD      DE,(QUOZIE)
		JR      RAPP11
RAPP10:         CCF
                LD      DE,0000H
RAPP11:         LD      (RAPPO1),DE     ;VERIFICO SE E'
		LD      A,(MEMRIF)      ;STATO TROVATO
		CP      00H             ;IL SEGNO DI
                JR      Z,RAPP2         ;REGISTRO DURANTE
                LD      DE,(RAPPO1)     ;LA RICERCA AUTO-
                LD      (RAPRIF),DE     ;MATICA PER MEMO-
		LD      DE,(FASES1)     ;RIZZARE I RIFE-
		LD      (FASRIF),DE     ;RIMENTI QUESTI
		LD      HL,MEMRIF       ;VENGONO MEMORIZ-
		DEC     (HL)            ;ZATI DOPO 4 GIRI
                LD      A,(MEMRIF)      ;QUANDO ARRIVO
                CP      00H             ;A ZERO ESEGUO
                JR      NZ,RAPP2        ;IL CALCOLO
                LD      DE,(FASRIF)     ;DEL RIFTOT
		LD      (MOLTI),DE
		LD      A,0AH
		LD      (MOLPL),A
		CALL    MOLT
		LD      DE,(PROD)
                LD      HL,(RAPRIF)
		ADD     HL,DE
                LD      (RIFTOT),HL



		;CALCOLO DELL'ERRORE IN CENTESIMI ED
		;EVENTUALE CORREZIONE DA ESEGUIRE
                ;(FASRIF-FASES1)+(RAPRIF-RAPPO1)= ERRORE
		;(FASRIFx10)+RAPRIF=RIFTOT
                ;(FASES1x10)+RAPPO1=POSTOT
		;(RIFTOT-POSTOT):10=ERRORE IN DECIMI

RAPP2:          LD      DE,(FASES1)     ;FASES1 x 10
		LD      (MOLTI),DE
		LD      A,0AH
		LD      (MOLPL),A
		CALL    MOLT
		LD      DE,(PROD)
                LD      HL,(RAPPO1)
		ADD     HL,DE
		LD      (POSTOT),HL
		EX      DE,HL
		LD      HL,(RIFTOT)     ;FLO1 PUNTO A
		SBC     HL,DE
                JR      C,ERR1
		EX      DE,HL
                LD      HL,(AMPI10)     ;VEDO SE ERRORE 
                SBC     HL,DE           ;E'> DI AMPI10
		EX      DE,HL
                JR      NC,INDRE        ;FLO1 PUNTO B
		CCF
		LD      HL,(RIFTOT)     ;FLO1 PUNTO C
		LD      DE,(POSTOT)
		SBC     HL,DE
		EX      DE,HL
		LD      HL,0A000H       ;40960
		SBC     HL,DE
                JR      INNAS
ERR1:           CCF
		LD      HL,(POSTOT)     ;FLO1 PUNTO D
		LD      DE,(RIFTOT)
		SBC     HL,DE
		EX      DE,HL
                LD      HL,(AMPI10)     ;FLO1 PUNTO E  
		SBC     HL,DE
		EX      DE,HL
                JR      NC,INNAS
		CCF
		LD      HL,0A000H       ;40960
		LD      DE,(POSTOT)     ;FLO1 PUNTO F
		SBC     HL,DE
		LD      BC,(RIFTOT)
		ADD     HL,BC
INDRE:          LD      A,D_RITARDO     ;SEGNALAZIONE
		LD      (MSCORR),A      ;CORREZ. RITARDO
                JR      ERR2
INNAS:          LD      A,D_AVANTI      ;SEGNALAZIONE
		LD      (MSCORR),A      ;CORREZ. AVANTI


		;(ERR.xCENT.IN DIV.ENC.):100=ERR.IN DECIM


ERR2:           LD      (MOLTI),HL      ;HL CONTIENE ERR.
		LD      A,(CENTES)
		LD      (MOLPL),A
		CALL    MOLT
		LD      DE,(PROD)
		LD      (DIVDEN),DE     ;PRIMA DIVISIONE
		LD      BC,000AH        ;PER DIECI
		LD      (DIVSOR),BC
		CALL    DIVIS
		LD      DE,(QUOZIE)
		LD      HL,(ERPREC)     ;FACCIO UNA MEDIA
		ADD     HL,DE           ;TRA ERRORE LETTO
		LD      (DIVDEN),HL     ;E IL PRECEDENTE. 
		LD      BC,0002H        
		LD      (DIVSOR),BC      
		CALL    DIVIS

  

		LD      DE,(QUOZIE)
                LD      (ERATTU),DE
                LD      A,(SALTO)       ;SE SALTO E'
                CP      00H             ;ZERO CORREGGO
                JR      Z,ERR_CALC_FINE ;SEMPRE
                LD      HL,0064H        ;SOMMO A ERPREC   
		LD      BC,(ERPREC)     ;1mm IL RISULTATO
		ADD     HL,BC           ;LO SOTTRAGGO A
		SBC     HL,DE           ;ERATTU SE CARRY
                JR      NC,ERR4         ;SE VI E' STATA  
		CCF                     ;UNA LETTURA SBAL-
		LD      BC,(ERPREC)     ;LATA LA IGNORO.
		LD      (ERATTU),BC
                LD      A,(CONSAL)      ;CONTROLLA CHE NON
		DEC     A               ;CHE NON RIMANGA
		LD      (CONSAL),A      ;IN BLOCCO PER PIU'
		CP      00H             ;DI N GIRI MEMO- 
                JR      Z,ERR3          ;RIZZATI IN SALTO  
		JR	ERR_CALC_FINE   ;E POI

ERR3:           LD      (ERATTU),DE     ;RICARICA L'ULTIMO
		LD      (ERPREC),DE     ;ERRORE LETTO
                JR      ERR4            
ERR4:           LD      A,(SALTO)       ;CARICA I GIRI DI
		LD      (CONSAL),A      ;ATTESA.

ERR_CALC_FINE:           
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CONTROLLO CHE NON CI SIA UN'ALTRO
;		AVVISO RICHIESTA DI TRASMISSIONE IN CORSO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

REQISFREE:	
		LD	A,(MEMA)	;CONTROLLO SE
		BIT	1,A		;SETTATO ESCI DAL LOOP
		JR	NZ,REQISFREE	;ALTRIMENTI CICLA
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		AVVISO RICHIESTA DI TRASMISSIONE 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

REQUEST:			
		LD	A,(MEMA)	;SETTO
		SET	1,A		;OUTPA1
		LD	(MEMA),A
	 	OUT     (PA),A
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE INVIO CODICE DI OK
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SEND_OK:		
		LD	BC,0000H
		LD	(STATUS),BC
		CALL	FORCE_STATUS
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE PER FORZARE INVIO CODICE DI STATUS
;		ANCHE SE UGUALE AL PRECEDENTE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

FORCE_STATUS:
		LD	BC,0FFFFH
		LD	(STATU2),BC
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE INVIO CODICE DI STATUS
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SEND_STATUS:		
		EXX
		LD	A,(STATUS)		;INVIA SONO SE DIVERSO DA PRECEDENTE
		LD	BC,(STATU2)
		CP	C
		JR	NZ,SEND_STATUS_DIFFERNT
		LD	A,(STATUS+01H)
		CP	B
		JR	NZ,SEND_STATUS_DIFFERNT
		JR	SEND_STATUS_FINE

SEND_STATUS_DIFFERNT:
		CALL	REQISFREE
		LD	A,(ANSWER)		;MANDA OK A
		LD	(DATOTR+00H),A		;CPU	
		LD	A,00H
		LD	(DATOTR+01H),A
		LD      A,(STATUS+01)
		LD	(DATOTR+02H),A
		LD      A,(STATUS)
		LD	(DATOTR+03H),A
		CALL	REQUEST

		LD	HL,(S_PERMANENT_MASK)	;AND CON LA MASCHERA DI BIT 
		LD	A,(STATUS)		;PERMANENTI
		AND	H
		LD	(STATUS),A
		LD	A,(STATUS+01H)
		AND	L
		LD	(STATUS+01H),A

		LD	BC,(STATUS)		;SALVO IL NUOVO STATO 
		LD	(STATU2),BC		;NELLO STATO PRECEDENTE

		EXX
SEND_STATUS_FINE:
		RET	

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE SETTA UNO STATUS
;		IPOTIZZA IL VALORE SIA NEL REGISTRO DE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SET_STATUS:		
		LD	A,(STATUS)
		OR	D
		LD	(STATUS),A
		LD	A,(STATUS+01H)
		OR	E
		LD	(STATUS+01H),A
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE RESETTA UNO STATUS
;		IPOTIZZA IL VALORE SIA NEL REGISTRO DE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

RES_STATUS:		
		LD	A,D		;COMPLEMENTO DE
		CPL	
		LD	D,A
		LD	A,E
		CPL
		LD	E,A
		LD	A,(STATUS)
		AND	D
		LD	(STATUS),A
		LD	A,(STATUS+01H)
		AND	E
		LD	(STATUS+01H),A
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE INVIO VELOCITA' (SE RICHIESTO) A ST7
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SEND_SPEED:
		LD	HL,(VELMED)
		LD      DE,(VELDIS)     ;SE VELOCITA' E' LA STESSA
		SBC     HL,DE           ;NON AGGIORNO IL DISPLAY
                JR      Z,SEND_SPEED_FINE        
		JR	NC,SEND_SPEED_OK
		CCF

SEND_SPEED_OK:
		CALL	REQISFREE
		
		LD      DE,(VELMED)		;AGGIORNO LA VELOCITA'
                LD      (VELDIS),DE		;PRECEDENTE CON LA CORRENTE

		LD	A,(LEGGI)		;INVIO LA VELOCITA' CORRENTE
		LD	(DATOTR+00H),A
		LD	A,(PARVEL)
		LD	(DATOTR+01H),A
		LD	DE,(VELMED)	
		LD	HL,DATOTR+02H
		LD	(HL),D
		INC	HL
		LD	(HL),E
		CALL	REQUEST

SEND_SPEED_FINE:
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE CONTROLLO VELOCITA'==0 
;		E SE RICHIESTO INVIO A ST7
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CHK_SPEED_ZERO:	;CONTROLLO INGRESSO PIN PER UN PO' DI TEMPO
		;SE NON RICEVO UN IMPULSO DELL'ENCODER 2024
		;SIGNIFICA CHE LA MACCHINA E' FERMA

		LD      BC,01FFFH
CHK_SPEED_ZERO1:  DEC     BC
		LD      A,B
		OR      C
		JR      Z,SPEED_IS_ZERO 
		IN      A,(PB)			;QUANDO LA
		BIT     4,A			;MACCHINA GIRA
		JR      Z,CHK_SPEED_ZERO1		;ESEGUE IL CONTROLLO
CHK_SPEED_ZERO2:	DEC     BC			;SE NO
		LD      A,B			;ASPETTA E
		OR      C			;METTE ZERO
		JR      Z,SPEED_IS_ZERO		;LA VELOCITA'
		IN      A,(PB)          
		BIT     4,A             
		JR      NZ,CHK_SPEED_ZERO2
		JR      CHK_SPEED_ZERO_FINE

SPEED_IS_ZERO:           
		LD	BC,0000H		;SE ARRIVO QUI
		lD	(IMPVEL),BC
		LD	(ISTVEL),BC		;LA VELOCITA' = 0
		LD	(VELMED),BC		;SETTO LE VARIABILI ED INVIO
		LD	BC,0FFFFH
		LD	(VELDIS),BC
		CALL	SPEED_LOW		;MANDO MESSAGGIO ALLARME

		LD      A,(AUMALO)		;SE NON E' IN MANUALE SALTO
	        CP	00H			
		JR      Z,CHK_SPEED_ZERO_FINE	

                CALL    RESEPC			;FACCIO IL RESET DELLE USCITE

CHK_SPEED_ZERO_FINE:
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		GESTIONE COMUNICAZIONE CON ST7
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&


PIOA:           PUSH    BC
		PUSH    DE
		PUSH    HL
		PUSH    AF
		PUSH    IX
		PUSH    IY

		LD	A,(MEMA)		;RESETTO 
		RES	1,A			;OUTPA1
		LD	(MEMA),A
	 	OUT     (PA),A
               
		LD      IX,DATOTR+00H		;LOC BYTE
		LD	IY,DATOTR+04H
 		CALL	TRASM

		CALL	ANALI1
		
		LD      IX,DATOTR+02H		;LOC BYTE
		LD	IY,DATOTR+06H
 		CALL	TRASM

		CALL	ANALI2		

		POP     IY			;INIZIO COSI'
		POP     IX			;AGGIORNA IL
		POP     AF			;TUTTO.
		POP     HL	
		POP     DE
		POP     BC
	        EI
		RETI


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		GESTIONE GATE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

PIOB:		;VIENE GENERATO DALL'IMPULSO DI COINCIDENZA
		;CARICO IL VALORE DELL'AMPIEZZA DEL GATE		
		PUSH    BC              
		PUSH    DE
		PUSH    HL
		PUSH    AF
		PUSH    IX
		PUSH    IY

                IN      A,(PB)          ;PPUU
                BIT     5,A
                JR      Z,PIOB4         ;PPUU

                LD      A,(MRIC+06H)   ;VIENE DECR.IN    PPDD
                CP      00H            ;MODO CHE IL
                JR      Z,PIOB3        ;TEST DEL DOPPIO
                DEC     A              ;PIO VENGA LETTO
                LD      (MRIC+06H),A   ;UNA VOLTA       PPDD


PIOB3:          LD      A,(ENCOD2)     ;PPDD
                CP      02H
                JR      Z,PIOB_FINE         

		LD      A,0D7H          ;SETTO IN CTC0 AMPIEZZA GATE IN IMPULSI DI ENC
		OUT     (70H),A         
		LD      A,(AMPIMP)      
		OUT     (70H),A         

		LD      A,(GUADS1)      ;SETTO GUADAGNO DI S1
		LD      B,A             
                LD      A,(MEMA)        
		RES     5,A
		RES     6,A
		ADD     A,B
		LD      (MEMA),A
                OUT     (PA),A          


                LD      A,(MRIC+06H)    ;VEDO SE DEVO  PPDD
                CP      00H             ;CONTROLLARE IL
                JR      Z,PIOB_FINE     ;DOPPIO SEGNO
                LD      A,0D7H          ;METTO 2 PER-
                OUT     (73H),A         ;CHE NON SENTE
                LD      A,02H           ;LA COMMUT.   AADD
                OUT     (73H),A         ;DEL TRAVERS   PPDD
                JR      PIOB_FINE       ;PPUU
PIOB4:          
                IN      A,(10H)         ;LEGGO LA POSIZIONE
		LD      E,A             ;(PULSANTE FASE MANUALE)
		IN      A,(20H)
		LD      D,A
		LD      (FASE),DE
                CALL    CENTRA        

PIOB_FINE:          

		POP     IY
		POP     IX
		POP     AF
		POP     HL
		POP     DE
		POP     BC
		EI
		RETI



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		VIENE SCATENATO ALLA FINE DEL GATE		
;		VERIFICA POSIZIONE S1
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&


CTC0:           PUSH    BC              
		PUSH    DE
		PUSH    HL
		PUSH    AF
		PUSH    IX
		PUSH    IY

                LD      A,01H           ;SEGNALA AL MAIN CHE HA LETTO
		LD      (MCALER),A      ;IN MODO CHE FACCIA CALCOLI UNA SOLA VOLTA PER GIRO

		LD	A,(MRIC+00H)   ;CONTROLLO SE SONO IN FASE AUTOMATICA
		CP	01H
		JP	Z,CTC0_FINE		;SE FASE SALTO TUTTI CONTROLLI SUCESSIVI

                LD      A,(ENCOD1)     ;SE NON STO FACENDO IL TEST DELL'ENCODER   
                CP      00H            ;SALTA   
                JR      Z,CTC01  
		
                CP      01H		;SE STO FACENDO IL TEST DELL'ENCODER
                JP      Z,CTC0_FINE
                LD      A,(ENCOD2)     ;INCREMENTO
                INC     A              ;A OGNI 255
                LD      (ENCOD2),A     ;IMPULSI
                LD      A,0DFH         ;RICARICO CTC0 
		OUT     (70H),A
                LD      A,0FFH
                OUT     (70H),A
                JP      CTC0_FINE          

CTC01:          
		IN      A,(30H)         ;LEGGO IL VALORE
		LD      E,A             ;DI PIENO
		IN      A,(40H)
		LD      D,A
                LD      (PIENO1),DE
		IN      A,(50H)         ;LEGGO IL VALORE
		LD      E,A             ;DELLA POSIZIONE
		IN      A,(60H)         ;DI S1
		LD      D,A
                LD      (POSIZ1),DE
		IN      A,(61H)         ;LEGGO IL VALORE
		LD      E,A             ;DI ENCODER
		IN      A,(62H)         ;ALL'ARRIVO DI S1
		LD      D,A
                LD      (FASES1),DE


		CALL	CHKPRINT	;CONTROLLA SE C'E' SEGNO E/O SETTA VARIABILE ALLARME RELATIVO

		OUT     (52H),A         ;IMPULSO A ZERO SU MONOSTABILE DA 0,5 mS 
					;PER RESETTARE TUTTI I FLIP-FLOP

		LD      A,43H		;RESETTO IL CTC 0
		OUT     (70H),A


		LD      HL,(FASE)       ;CARICO IL 
		LD      A,H             ;CONTATORE PER
		OUT     (20H),A         ;LEGGERE POI S1
		LD      A,L
		OUT     (10H),A         
		LD      A,43H
		OUT     (70H),A
CTC03:
		CALL	SPEED_CALC	;AGGIORNA VELOCITA' IMPULSIVA
		CALL	VERIFI		;CONTROLLO DEI PARAMETRI GATE E CILINDRO

CTC0_FINE:		
	
		LD	A,(MEMA)	
		SET     7,A		;ABILITO SEMPRE LA GENERAZIONE DEL GATE
		LD	(MEMA),A
	 	OUT     (PA),A

		POP     IY
		POP     IX
		POP     AF
		POP     HL
		POP     DE
		POP     BC
		EI
		RETI


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		INTERRUPT DI GESTIONE TIMER DI CORREZIONE
;		E IN FASE AUTOMATICA CONTATORE DI NUMERO DI GIRI EFFETTUATI
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&


CTC1:           PUSH    BC
		PUSH    DE
		PUSH    HL
		PUSH    AF
		PUSH    IX
		PUSH    IY

		LD      A,(ENCOD1)      ;VEDO SE SONO
		CP      00H             ;IN TEST
                JR      Z,CTC16         ;ENCODER
		CP      01H
                JR      Z,CTC17
		CP      02H
                JR      Z,CTC18
CTC17:          LD      A,02H           ;PARTENZA TEST
		LD      (ENCOD1),A
                LD      HL,ENC2
                JP      CTC1_FINE           
CTC18:          LD      HL,ENC6         ;FINE TEST                        
                JP      CTC1_FINE
		
CTC16:          LD      A,(MRIC+00H)    ;CONTROLLA
		CP      01H             ;SE FALLITA
		JR      Z,CTC12         ;RICERCA CODICE
	
CTC10:          LD      A,43H		;NON ATTIVA LA RICERCA, SPENGO IL CTC COME TIMER
		OUT     (71H),A	
		JR	CTC1_FINE		


CTC12:          CALL    GAINS1         
                LD      A,(MRIC+02H)
		DEC     A
		LD      (MRIC+02H),A
		CP      00H
		JR      NZ,CTC13

		LD      A,01H           ;SEGNALO
		LD      (MDATER),A      ;RICERCA FALLITA

		LD      A,(MEMB)
		RES     6,A             ;SBLOCCO GATE
		LD      (MEMB),A
		OUT     (PB),A
			

		LD      BC,0000H        ;DISATTIVO FLAG DI RICERCA AUTOMATICA
		LD      (MRIC+00H),BC   

		LD      A,43H		;DISATTIVO CTC COME CONTATORE DI CICLI DI FASE
		OUT     (71H),A

		LD      A,01H           ;DISATTIVO CONTROLLO DOPPIO SEGNO
		LD      (MRIC+04H),A    

                LD      BC,0000H	;DISATTIVO MEMORIE DI RICERCA AUTOMATICA
		LD      (MRIC+02H),BC
		LD	(MRIC+08H),BC

		CALL	FORCE_STATUS
		LD	DE,(S_FASATURA_FALLITA)
		CALL	SET_STATUS		;INVIO MESSAGGIO FASE FALLITA

		LD      HL,MAIN;RIPARTO DA MAIN
		JR      CTC1_FINE
CTC13:         
		CALL    GAINS1
                LD      HL,CODICE
CTC1_FINE:          
		POP     IY              
		POP     IX              
		POP     AF
		POP     DE      
		POP     BC
		EX      (SP),HL
	        EI              
		RETI




;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		INTERRUPT DI GESTIONE TIMER DI CORREZIONE
;		IN CASCATA A CTC1
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CTC2:           PUSH    BC
		PUSH    DE
		PUSH    HL
		PUSH    AF
		PUSH    IX
		PUSH    IY

                LD      A,(DECORR)      ;SE EXTRA 
                CP      00H             ;CORREZIONE 
                JR      NZ,CTC21        ;ATTIVA SALTO.
CTC23:          LD      A,(MEMC)        ;FINE DELLA
		RES     B_PC_AVANTI,A   ;CORREZIONE          
		RES     B_PC_RITARDO,A             
		LD      (MEMC),A
		OUT     (PC),A


		LD      A,43H           ;RESETTO
		OUT     (71H),A         ;CTC1
		OUT     (72H),A         ;CTC2
                JR      CTC22           
CTC21:          LD      A,37H           ;CONTROLLO CTC1
		OUT     (71H),A         
                LD      A,40H           ;COSTANTE DI
		OUT     (71H),A         ;TEMPO
                LD      A,0D7H          ;CONTROLLO CTC2
                OUT     (72H),A         ;CARICO PER 
                LD      A,0FFH          ;EXTRA CORREZ.
		OUT     (72H),A

                LD      A,(DECORR)      ;SE E' MAGGIORE 
                DEC     A               ;DI UNO LA CORREZ-
                LD      (DECORR),A      ;ZIONE DIVENTA
                CP      00H             ;CONTINUA
                JR      NZ,CTC22

                LD      A,(CIDE75)      ;SE FATTO 75% 
                CP      00H             ;DEL CICLO
                JR      Z,CTC23         ;BLOCCO CORREZ.



CTC22:          POP     IY
		POP     IX
		POP     AF
		POP     HL
		POP     DE      
		POP     BC
		EI
		RETI


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		INTERRUPT DI GESTIONE TIMER DI CORREZIONE LATERALE
;		E CONTEGGI SEGNI IN FASE AUTOMATICA (DOPPIO SEGNO)
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CTC3:           PUSH    BC
		PUSH    DE
		PUSH    HL
		PUSH    AF
		PUSH    IX
                PUSH    IY

                LD      A,(MRIC+06H)    ;SE NON SONO IN  
		CP      00H             ;F4 SALTO
                JR      Z,CTC32         ;

                LD      A,(MRIC+0AH)    ;PRIMA DI 
                INC     A               ;SCRIVERE 
                LD      (MRIC+0AH),A    ;DOPPIO ASPET-
                CP      04H             ;4 RIPETIZIONI
                JR      Z,CTC31
                LD      A,02H           ;AADD
                LD      (MRIC+06H),A
                JP      CTC33           ;AADD

CTC31:
		CALL	DOPPIO

                LD      A,00H           ;
                LD      (MRIC+04H),A    ;

                LD      (MRIC+06H),A    ;PPDD  
                LD      (MRIC+0AH),A    ;AADD


                LD      A,43H           ;AADD
                OUT     (73H),A         ;AADD
                JR      CTC33           ;AADD


CTC32:          LD      A,(TMEM4)       ;SE ATTIVATA   PPDD
		CP      01H             ;CORREZIONE
                JP      NZ,CTC33        ;SALTA
	        LD      BC,(TMEM5)      ;CONTIENE IL
                DEC     BC              ;VALORE 
                LD      (TMEM5),BC      ;DELL'ERRORE
                LD      A,B
                OR      C
                JR      NZ,CTC33        
	        LD      A,(MEMC)        ;FINITO IL DE-  
                RES     B_PC_DESTRA,A   ;CREMENTO DELLO
                RES     B_PC_SINISTRA,A ;ERRORE TOGLI
                LD      (MEMC),A        ;LE CORREZIONI.
		OUT     (PC),A

		LD      A,43H
		OUT     (73H),A
		LD      A,00H
		LD      (TMEM4),A
CTC33:          POP     IY
		POP     IX
		POP     AF
		POP     HL
		POP     DE
		POP     BC
		EI
		RETI


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CONTROLLO SE L'ERRORE E' CAMBIATO
;		E IN TAL CASO LO SPEDISCO A ST7
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SEND_ERR:	
		LD      HL,(ERMEDI)		;SE NON C'E'STATA VARIAZIONE
                LD      DE,(ERMEDI_DISP)	;D'ERRORE NON AGGIORNO ST7
                SBC     HL,DE
                JR      Z,SEND_ERR_FINE

                JR      NC,SEND_ERR1		;RESET CARRY SE NECESSARIO
                CCF
SEND_ERR1:           
		LD      HL,(ERMEDI)		;ERMEDI_DISP = ERMEDI
		LD      (ERMEDI_DISP),HL


		;INVIO ERRORE 
		LD	A,(MSCORR)		;IN BASE ALLA DIREZIONE
		CP	D_RITARDO		;SCELGO LA DIREZIONE 
		JR	Z,SEND_ERR_A		;CUI SPEDIRE
		JR	SEND_ERR_R
		
		
SEND_ERR_R:	;INVIO ERRORE RITARDO
		CALL	REQISFREE
		LD	A,(LEGGI)
		LD	(DATOTR+00H),A
		LD	A,(LONRIT)
		LD	(DATOTR+01H),A
		LD	DE,(ERMEDI)
		LD	HL,DATOTR+02H
		LD	(HL),D
		INC	HL
		LD	(HL),E
		CALL	REQUEST
		JR	SEND_ERR_FINE

SEND_ERR_A:	;INVIO ERRORE AVANTI
		CALL	REQISFREE
		LD	A,(LEGGI)
		LD	(DATOTR+00H),A
		LD	A,(LONAVA)
		LD	(DATOTR+01H),A
		LD	DE,(ERMEDI)
		LD	HL,DATOTR+02H
		LD	(HL),D
		INC	HL
		LD	(HL),E
		CALL	REQUEST
		JR	SEND_ERR_FINE

SEND_ERR_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;			CALCOLO CENTRATURA GATE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CENTRA:         EXX
		LD      HL,(FASE)       ;SOTTRAGGO IL VA-
                LD      BC,(MEZGA)     ;LORE DI 1/2 GATE 
                ;LD      B,00H
		SBC     HL,BC           ;SE IL RISULTATO
		JR      C,RISNEG        ;E'POSITIVO LO
		JR      TRFASE          ;MEMORIZZO IN
RISNEG:         CCF                     ;IN FASE.
		LD      HL,1000H        ;SE E'NEGATIVO
		SBC     HL,BC           ;SOTTRAGGO DA 4096
		LD      BC,(FASE)       ;IL 1/2 GATE E IL
		ADD     HL,BC           ;RESTO LO SOMMO
TRFASE:         LD      (FASE),HL       ;AL VALORE LETTO.
		LD      A,H
		OUT     (20H),A
		LD      A,L
		OUT     (10H),A
		LD      A,04H           ;4 GIRI ATTESA
		LD      (MEMRIF),A      ;PER MEMORIZZARE
		EXX                     ;IL RIFERIMENTO
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CALCOLO DELLO SPOSTAMENTO REGISTRO E GATE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SPOSTA:         EXX
                LD      BC,(SHIFT)      ;SHIFT x 10 = A
		LD      (MOLTI),BC
                LD      A,0AH
		LD      (MOLPL),A
		CALL    MOLT
		LD      DE,(PROD)       ;FLO2 = A
		LD      (DIVDEN),DE     ;A : CENTES = B
		LD      BC,(CENTES)
		LD      (DIVSOR),BC
		CALL    DIVIS
		LD      DE,(QUOZIE)     ;FLO2 = B


		LD      A,(AVARIT)	
		LD	B,A		
		LD	A,(DIRAVA)	
		CP      B		
		JR      Z,SPOMEN

		LD      HL,(RIFTOT)
		ADD     HL,DE           ;RIFTOT + B = C
		EX      DE,HL           ;SE C < 40960 
		LD      HL,0A000H       ;ALLORA C = Z
		SBC     HL,DE           ;SE C > 40960
		EX      DE,HL           ;SI FARA'
		JR      NC,SPOFIN       ;C - 40960 = Z
		CCF
		LD      DE,0A000H
		SBC     HL,DE
		JR      SPOFIN
SPOMEN:         LD      HL,(RIFTOT)     ;RIFTOT - B = C
		SBC     HL,DE           ;SE C = POSITIVO
		JR      NC,SPOFIN       ;C = Z
		CCF                     ;SE C = NEGATIVO
		LD      HL,0A000H       ;SI FARA'
		SBC     HL,DE           ;40960 - B = D
		LD      DE,(RIFTOT)     ;D + RIFTOT = Z
		ADD     HL,DE
SPOFIN:         LD      (RIFTOT),HL     ;FLO2 = Z

		;RIFTOT/10=POSIZIONE ENCODER
		;POSIZIONE ENCODER - 1/2GATE = NUOVA FASE

		LD      (DIVDEN),HL
		LD      BC,000AH
		LD      (DIVSOR),BC
		CALL    DIVIS
		LD      HL,(QUOZIE)
		LD      DE,(QUOZIE)
                LD      BC,(MEZGA)     ;
		SBC     HL,BC
		JR      C,RISNE1
		JR      TRFAS1
RISNE1:         CCF                     ;SE 1/2GATE > DI
		LD      HL,1000H        ;QUOZIE ALLORA
		SBC     HL,BC           ;4096-1/2GAT = A
		ADD     HL,DE           ;A + QUOZIE=FASE
TRFAS1:         LD      (FASE),HL
		LD      A,H
		OUT     (20H),A
		LD      A,L
		OUT     (10H),A

		LD      A,(AUMATI)      ;SE IN MANUALE
		CP	00H		;TIRO SALTA 
                JR      Z,TRFAS2

                LD      A,(TIRCIC)      ;23/7/02 
                CP      00H             ;A UNO SHIFT
                JR      Z,TRFAS2        ;VIENE TRIPLI-
                LD      B,A             ;CATO IL 
                RLA                     ;TEMPO DI
                ADD     A,B             ;ATTESA  
                LD      (DECREM),A      ;DEL TIRO
                LD      (INCREM),A       
                LD      (MOTPO1),A      

TRFAS2:         EXX
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE GESTIONE SEGNO STAMPA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CHKPRINT:	
		;SE SONO IN FUNZIONE COLD SEAL, NON FACCIO IL TEST
		;ALTIMENTI MANDO OK APPENA ENTRA IL SEGNO NEL GATE
		LD      A,(MEMC)        
                BIT     B_PC_FUNZ,A             
                JR      NZ,CHKPRINT_END	


		;CONTROLLO SE UN SEGNO E' STATO RILEVATO NEL GATE
		IN      A,(PB)		
		BIT     3,A
                JR      Z,CHKPRINT_OK		

CHKPRINT_ERR:	;NON C'E' SEGNO
		LD      A,02H           ;BLOCCO LA CORREZIONE 
		LD      (MATTES),A      ;AUMENTANDO I CICLI DI ATTESA.
		CALL    NOPRINT		;ATTIVO ALLARME E MANDO MESSAGGIO
		JR      CHKPRINT_END
		
CHKPRINT_OK:    ;C'E' IL SEGNO
		CALL	SIPRINT
		;JR	CHKPRINT_END

CHKPRINT_END:
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE GESTIONE SI PRINT
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SIPRINT:	;SE NON IN ERRORE, RESETTO IL FLAG DELLO STATO D'ALLARME E 
		;RIMUOVO IL MESSAGGIO "NO PRINT"
		LD	DE,(S_MANCANZA_STAMPA)
		CALL	RES_STATUS
              	LD      A,00H
                LD      (PROPRI),A
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINTE GESTIONE NO PRINT
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
                
NOPRINT:        ;SE IN ERRORE, SETTO FLAG DELLO STATO D'ALLARME, 
		;INVIO MESSAGGIO "NO PRINT",
		;FACCIO IL RESET DELLE PORTE DI CORREZIONE (SOLO IN AUTOMATICO)
		LD	DE,(S_MANCANZA_STAMPA)
		CALL	SET_STATUS
              	LD      A,01H 
                LD      (PROPRI),A

		LD	A,(AUMALO)	;SE E' IN MANUALE NON 
		CP	00H		;RESETTARE LE USCITE DI COMANDO
		JR	Z,NOPRI_FINE
		
;		LD      A,(CICLI_F9)	;SE SONO IN FUNZ9 NON
;		CP	00H		;RESETTARE LE USCITE COMANDO
;		JR	NZ,NOPRI_FINE
		CALL	RESEPC
		
NOPRI_FINE:
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE ACCENDI ALLARME
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

ALARM_ON:	
		LD	A,(AUMALO)	;SE E' IN MANUALE, 
		CP	00H		;NON ATTIVO L'ALLARME
		JR	Z,ALARM_ON_FINE

		LD      A,(MEMC)
		SET     B_PC_ALLARME,A             ;ATTIVO ALLARME
		LD      (MEMC),A
		OUT     (PC),A
 
ALARM_ON_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE SPEGNI ALLARME
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

ALARM_OFF:	LD      A,(MEMC)
		RES     B_PC_ALLARME,A             ;DISATTIVO ALLARME
		LD      (MEMC),A
		OUT     (PC),A
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE ACCENDI F9
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

F9_ON:          LD      A,(MEMC)        
                RES     B_PC_AVANTI,A
                SET     B_PC_RITARDO,A
		SET     B_PC_ALLARME,A
                SET     B_PC_FUNZ,A		;ATTIVO F9
                LD      (MEMC),A
                OUT     (PC),A

		LD      A,0FFH          ;IMPONGO AUTOMATICO 
		LD	(AUMALO),A

		LD      A,000H          ;IMPONGO MANUALE
		LD	(AUMATI),A	
		LD	(AUMATR),A	


                LD      A,02H           ;CARICO LA MEM DA DECREMENTARE
                LD      (CICLI_F9),A
                RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE SPEGNI F9
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

F9_OFF:		LD      A,(MEMC)
		BIT	B_PC_FUNZ,A
		JR	Z,F9_OFF1	;SE GIA' SPENTO, NON FACCIO NULLA

		LD      A,(MEMC)
		RES     B_PC_FUNZ,A             ;ALTRIMENTI DISATTIVO F9
		LD      (MEMC),A
		OUT     (PC),A
		CALL	SEND_OK		;E INVIO OK A CPU
F9_OFF1:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE LOW-SPEED
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SPEED_LOW:
		LD	DE,(S_VELOCITA_SOTTO_SOGLIA)
		CALL	SET_STATUS

		LD	A,(AUMALO)	;SE E' IN MANUALE, NON INVIO
		CP	00H		;ATTIVO ALLARME PER BASSA VELOCITA'
		JP      Z,SPEED_LOW_FINE

		CALL    RESEPC          ;RESET USCITE DI CORREZIONE

		LD      A,01H
                LD      (PROVEL),A  
SPEED_LOW_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE SPEED-OK
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SPEEDOK:	LD	DE,(S_VELOCITA_SOTTO_SOGLIA)
		CALL	RES_STATUS
                LD      A,00H
                LD      (PROVEL),A
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINTE GESTIONE DECELLERA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
                
DECEL:          ;SE IN ERRORE, SETTO FLAG DELLO STATO D'ALLARME, 
		;INVIO MESSAGGIO "DECELLERAZIONE",
		;FACCIO IL RESET DELLE PORTE DI CORREZIONE (SOLO IN AUTOMATICO)
		LD	DE,(S_BLOCCO_DECELERAZIONE)
		CALL	SET_STATUS
              	LD      A,01H 
                LD      (PROVEL),A

		LD	A,(AUMALO)	;SE E' IN MANUALE NON 
		CP	00H		;RESETTARE LE USCITE DI COMANDO
		JR	Z,DECEL_FINE
		CALL	RESEPC
		
DECEL_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINTE GESTIONE ACCELERA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
                
ACCEL:          ;SE IN ERRORE, SETTO FLAG DELLO STATO D'ALLARME, 
		;INVIO MESSAGGIO "ACCELERAZIONE",
		;FACCIO IL RESET DELLE PORTE DI CORREZIONE (SOLO IN AUTOMATICO)
		LD	DE,(S_BLOCCO_ACCELERAZIONE)
		CALL	SET_STATUS
              	LD      A,01H 
                LD      (PROVEL),A

		LD	A,(AUMALO)	;SE E' IN MANUALE NON 
		CP	00H		;RESETTARE LE USCITE DI COMANDO
		JR	Z,ACCEL_FINE
		CALL	RESEPC
ACCEL_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE VELOCITA'COSTANTE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

VELCOST:	;SE NON IN ERRORE, RESETTO IL FLAG DELLO STATO D'ALLARME E 
		;RIMUOVO IL MESSAGGIO "ACC/DEC"
		LD	DE,(S_BLOCCO_ACCELERAZIONE)
		CALL	RES_STATUS
		LD	DE,(S_BLOCCO_DECELERAZIONE)
		CALL	RES_STATUS
              	LD      A,00H
                LD      (PROVEL),A
VELCOST_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE DOPPIO SEGNO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
DOPPIO:
		LD	DE,(S_DOPPIO_SEGNO)
		CALL	SET_STATUS
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUB-ROUTINE CALCOLA VELOCITA IMPULSIVA - IMPVEL
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

SPEED_CALC:
                LD      BC,0474H        ;16mS TEMPO   
                LD      IX,IMPVEL       ;PER MISURARE
                LD      A,00H           ;GLI IMPULSI
                LD      (IX+01H),A
                LD      A,057H          ;SETTO CTC0 COME TIMER
                OUT     (70H),A         ;SENZA INTERUPT
                LD      A,0FFH          ;PARTENDO DA 0FFH
                OUT     (70H),A         
SPEED_C1:         
		IN      A,(70H)         ;NEL CASO IL 
                CP      00H             ;DECREMENTO 
                JR      NZ,SPEED_C2     ;ARRIVI A ZERO
                INC     (IX+01H)        ;LO MEMORIZZO
SPEED_C2:         
		DEC     BC              
                LD      A,B
                OR      C
                JR      NZ,SPEED_C1       
                IN      A,(70H)         
                CPL                     ;IMPULSI PROPOR
                LD      (IMPVEL),A      ;ALLA VELOCITA.

SPEED_CALC_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CONTROLO ZONA MORTA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CORREZIONI:         
		LD      HL,(ERMEDI)     ;SE ZONA MORTA E'
		LD      DE,(ZONA)       ;> DELL'ERRORE
		SBC     HL,DE           ;NON CORREGGO
		JR      NC,CORRE1
		CCF
		LD      A,(MEMC)        
		RES     B_PC_AVANTI,A             
		RES     B_PC_RITARDO,A             
		LD      (MEMC),A        
		OUT     (PC),A   		
		JP      CORREZIONI_END


		;########################################
		;# CALCOLO CORREZIONE MOTORE            #
		;# [Ea x C/10 + (Ea - Ep) x D] x G      #
		;########################################


CORRE1:         LD      DE,(ERATTU)     ;Ea x C/10 = W
		LD      (MOLTI),DE
		LD      A,(CILDIV)
		LD      (MOLPL),A
		CALL    MOLT
		LD      DE,(PROD)       ;SALVA IL PRIMO
		LD      (MEMW),DE       ;RISULTATO
		LD      B,00H           ;Ea - Ep = ED
		LD      HL,(ERATTU)
		LD      DE,(ERPREC)
		SBC     HL,DE
		JR      NC,CORRE2
		CCF
		LD      HL,(ERPREC)     ;SE NEGATIVO
		LD      DE,(ERATTU)     ;ALLORA
		SBC     HL,DE           ;Ep - Ea = (-ED)
		LD      B,01H           ;E SEGNALALO

CORRE2:         LD      (MOLTI),HL      ;ED x D = Y
		LD      A,(DERIVA)
		LD      (MOLPL),A
		CALL    MOLT            
		LD      A,B             ;SEGALA (-ED)
		CP      00H
		JR      Z,CORRE3
		LD      DE,(PROD)       ;W + (-Y) = Z
		LD      HL,(MEMW)
		SBC     HL,DE
		JR      NC,CORRE4
		CCF                     ;SE Z E' NEGATIVO
		LD      DE,(MEMW)       ;ALLORA
		LD      HL,(PROD)       ;(-Y) + W = Z
		SBC     HL,DE           ;PERO' DEVO 
		LD      A,(MSCORR)      ;INVERTE IL SENSO
		CP      D_AVANTI        ;DI CORREZIONE
		JR      Z,INVER1
		LD      A,D_AVANTI
		LD      (MSCORR),A
		JR      CORRE4
INVER1:         LD      A,D_RITARDO
		LD      (MSCORR),A
		JR      CORRE4
CORRE3:         LD      DE,(PROD)       ;W + Y = Z
		LD      HL,(MEMW)
                ADD     HL,DE           ;JJJ
CORRE4:         LD      (MOLT24),HL     ;Z x G = CORREZ.  
		LD      A,(GUADA)
                LD      (MOLP24),A      
                CALL    MOL24
                LD      BC,(PROD24)
                LD      A,(PROD24+02H)
                LD      (MDECOR),A      ;VALORE PER 
                CP      00H             ;EXTRA CORREZ.
                JR      NZ,CORRE6

                LD      A,B

                LD      (CORRE),A
                JR      CORRE7          
CORRE6:         LD      A,0FFH
                LD      (CORRE),A



		;ROUTINE DI CORREZIONE


CORRE7:         LD      A,(CIDE75)      ;DECREMENTO 
                CP      00H             ;DEL 75% DEL
                JR      Z,CORRE8        ;CICLO
                DEC     A
                LD      (CIDE75),A


CORRE8:       
		LD      A,(PROPRI)      ;SE IN PROTEZIONE PPRR
                CP      01H             ;(ALLARME) ATTIVO
                JP      Z,CORREZIONI_END        ;ESCO PPRR
		LD      A,(PROVEL)      ;SE IN PROTEZIONE PPRR
                CP      01H             ;(ALLARME) ATTIVO
                JP      Z,CORREZIONI_END        ;ESCO PPRR

		LD      A,(MATTES)      ;CONTROLLO CICLO
		DEC     A               ;DI ATTESA
		LD      (MATTES),A
		CP      00H
		JP      NZ,CORREZIONI_END

                LD      A,(CICL75)      ;RICARICO IL 
                LD      (CIDE75),A      ;75% DEL CICLO 

                LD      A,(CICLO)       ;RICARICO IL CI-
		LD      (MATTES),A      ;CLO DI ATTESA.
                LD      HL,(ERMEDI)
		LD      DE,(ZONA)
		SBC     HL,DE           ;SE ZONA MORTA
		JR      NC,CORRE5       ;MAGGIORE DI
		CCF                     ;ERMEDI SALTO
		JP      CORREZIONI_END
CORRE5:         
		LD	A,(AUMALO)	;SE SONO IN TTMM
		CP	00H		;MANUALE SALTO
		JP      Z,CORREZIONI_END

                LD      A,(CORRE)
                CP      00H		;SALTO
                JP      Z,KRAPIN        

                LD      A,(MDECOR)
                LD      (DECORR),A


                LD      A,37H           ;CONTROLLO CTC1   
		OUT     (71H),A         
                LD      A,40H           ;COSTANTE DI
		OUT     (71H),A         ;TEMPO
		LD      A,0D7H          ;CONTROLLO CTC2
		OUT     (72H),A
                LD      A,(CORRE)       ;MSB ERRORE  
                OUT     (72H),A


		LD      A,(MSCORR)      ;VEDO IN CHE
		CP      D_AVANTI        ;SENSO CORREG.
		JR      NZ,AUTRIT
;%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
                LD      A,(TIRCIC)      ;SE E' A ZERO
                CP      00H             ;SALTA
                JR      Z,AUTAVA

                LD	A,(AUMATI)	;SE IN MANUALE
                CP	00H		;TIRO SALTA 
                JR      Z,AUTAVA
                LD      A,(MOTPO1)      ;RICARICO LA
		LD      (DECREM),A      ;MEM.A DECREM
		LD      A,(INCREM)      ;DI DECREM E
		DEC     A               ;DECR.INCREMA
		LD      (INCREM),A      ;SE E A ZERO
		CP      00H             ;MUOVO IN SENSO 
		JR      NZ,AUTAVA       ;ORARIO IL  
                LD      A,(MOTPO1)      ;MOTOPOTENZ.
		LD      (INCREM),A
                
                LD      A,01H           
                LD      (TMEM6),A       ;PIU' CARTA
                LD      (TMEM4),A       ;ATTIVO 
                CALL    CORREZIONE_TIRO          ;CORREZ.TIRO
                JR      CORREZIONI_END
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

AUTAVA:         LD      A,(MEMC)        ;CORREZIONE IN
		RES     B_PC_RITARDO,A  ;AVANTI
		SET     B_PC_AVANTI,A
		LD      (MEMC),A
		OUT     (PC),A

		JR      CORREZIONI_END

AUTRIT:
                LD      A,(TIRCIC)      ;SE A ZERO  
                CP      00H             ; SALTA
                JR      Z,AUTRI1

                LD	A,(AUMATI)	;SE IN MANUALE
                CP	00H		;TIRO SALTA 
                JR      Z,AUTRI1

                LD      A,(MOTPO1)      ;RICARICO LA
		LD      (INCREM),A      ;MEM.A DECREM
		LD      A,(DECREM)      ;DI INCREM E
		DEC     A               ;DECR.DECREM
		LD      (DECREM),A      ;SE E A ZERO
		CP      00H             ;MUOVO IN SENSO 
		JR      NZ,AUTRI1       ;ANTIORARIO IL
                LD      A,(MOTPO1)      
		LD      (DECREM),A
                LD      A,02H           ;MENO CARTA
                LD      (TMEM6),A
                LD      A,01H           ;ATTIVO
                LD      (TMEM4),A       ;CORREZ.TIRO 
                CALL    CORREZIONE_TIRO
                JR      CORREZIONI_END


AUTRI1:         LD      A,(MEMC)        ;CORREZIONE IN
		RES     B_PC_AVANTI,A   ;RITARDO
		SET     B_PC_RITARDO,A
		LD      (MEMC),A
		OUT     (PC),A
		
CORREZIONI_END:         RET




;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		RESET USCITE DI CORREZIONE INDESIDERATE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

KRAPIN:
		LD	A,(AUMALO)	;SE E' IN MANUALE NON 
		CP	00H		;RESETTARE LE USCITE DI COMANDO
		JR	Z,KRAPIN1

		CALL	RESEPC		;RESET USCITE DI CORREZIONE INDESIDERATE

KRAPIN1:			
                LD      A,43H           
                OUT     (71H),A         
                OUT     (72H),A
                                     
		LD      A,(MOTPO1)      
                LD      (INCREM),A
                LD      (DECREM),A
		RET



                ;##################################
                ;# CALCOLO CORREZIONE MOTORE TIRO #
                ;# Et x Gt = TEMPO DI CORREZIONE  #   
                ;##################################
CORREZIONE_TIRO:		
		LD      A,(TIRGUA)		;SE ZERO 
                CP      00H			;SALTO
                JP      Z,CORREZIONE_TIRO_FINE        

                LD      DE,(ERATTU)     
                LD      A,(TIRGUA)       
                LD      (MOLT24),DE     
                LD      (MOLP24),A      
                CALL    MOL24           
                LD      BC,(PROD24)
                LD      A,(PROD24+02H)
                CP      00H
                JR      Z,CORREZIONE_TIRO_1     ;BC = ERRORE    
                LD      B,0FFH			;EXTRA CORR.

CORREZIONE_TIRO_1:         
		LD      HL,0B00H		;SE SUPERA
                SBC     HL,BC			;QUESTO VALORE
                JR      NC,CORREZIONE_TIRO_2    ;IMPONGO UN
                CCF				;VALORE FISSO
                LD      BC,0B00H		;PER NON FAR
CORREZIONE_TIRO_2:         
		LD      (TMEM5),BC		;MUOVERE TANTO
                LD      A,B			;IL MOTOPOTENZIOMETRO
                OR      C
                JP      Z,CORREZIONE_TIRO_RESET        



                ;ROUTINE DI CORREZIONE TIRO

		LD	A,(AUMATI)		;SE IN MANUALE
                CP	00H			;TIRO SALTA 
                JR      Z,CORREZIONE_TIRO_FINE

                LD      A,(TMEM4)      
		CP      00H
                JR      Z,CORREZIONE_TIRO_FINE        

                LD      A,(TMEM6)       
                CP      00H
                JR      Z,CORREZIONE_TIRO_FINE
                CP      01H
                JR      Z,CORREZIONE_TIRO_DEC        
                CP      02H
                JR      Z,CORREZIONE_TIRO_INC        
                JR      CORREZIONE_TIRO_FINE

CORREZIONE_TIRO_DEC:                  
                LD      A,(TIRGUA)		;SE GUATRA >  DI 100 LA
                CP      65H			;CORREZZIONE
                JR      C,CORREZIONE_TIRO_DEC_1 
                SBC     A,64H			;DIVENTA FISSA
                LD      BC,0000H		;UNA UNITA'
                LD      (TMEM5),BC		;DELLA SOTTRAZIONE
                LD      (TMEM5),A		;E' UGUALE A 1mS
                LD      A,0B7H                
                OUT     (73H),A         
                LD      A,10H           
                OUT     (73H),A          
                JR      CORREZIONE_TIRO_DEC_2
CORREZIONE_TIRO_DEC_1:         
		CCF                     
                LD      A,0B7H			;CON ERORE 0.1mmm      
                OUT     (73H),A			;E TIMER = 10
                LD      A,02H			;TEMPO DI  ZZZ
                OUT     (73H),A			;CORREZ. = 30mS  
CORREZIONE_TIRO_DEC_2:         
		LD      A,(MEMC)		;DIMINUZIONE
                RES     B_PC_SINISTRA,A		;TIRO
		SET     B_PC_DESTRA,A
		LD      (MEMC),A
		OUT     (PC),A
                JR      CORREZIONE_TIRO_FINE  



CORREZIONE_TIRO_INC:                   
                LD      A,(TIRGUA)		;VEDI SOPRA 
                CP      65H
                JR      C,CORREZIONE_TIRO_INC_1
                SBC     A,64H
                LD      BC,0000H
                LD      (TMEM5),BC
                LD      (TMEM5),A
                LD      A,0B7H                
                OUT     (73H),A         
                LD      A,10H           
                OUT     (73H),A          
                JR      CORREZIONE_TIRO_INC_2
CORREZIONE_TIRO_INC_1:         
		CCF
                LD      A,0B7H			;CON ERRORE 0.1mmm      
                OUT     (73H),A			;E TIMER = 10
                LD      A,02H			;TEMPO DI  
                OUT     (73H),A			;CORREZ. = 30mS 

CORREZIONE_TIRO_INC_2:        
		LD      A,(MEMC)		;AUMENTO
                RES     B_PC_DESTRA,A           ;TIRO
		SET     B_PC_SINISTRA,A
		LD      (MEMC),A
		OUT     (PC),A

CORREZIONE_TIRO_FINE:         
		LD      A,00H           
                LD      (TMEM6),A
                
                LD      A,(TIRCIC)      ;RIPRISTINO
                LD      (MOTPO1),A      ;I VALORI 
                LD      (INCREM),A      ;NORMALI
                LD      (DECREM),A
                RET
		      
CORREZIONE_TIRO_RESET:         
		LD      A,(MEMC)        ;QUANDO L'ERRORE 
                RES     B_PC_DESTRA,A   ;E' A ZERO
                RES     B_PC_SINISTRA,A ;SI ANNULLA 
                LD      (MEMC),A        ;TUTTO PER
                OUT     (PC),A          ;EVITARE CHE
                LD      A,43H           ;CHE AVVENGANO
                OUT     (73H),A         ;CORREZIONI
                LD      A,00H           ;INDESIDERATE
                LD      (TMEM4),A       ;COME A KRAPINA
                RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		RIEMPIO TABELLA ERRORE MEDIO PER CORREZIONE LONG CON 
;		IL VALORE DI ERATTU E MSCORR
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
RESET_MEDIA:
                LD      DE,(ERATTU)      
		LD	BC,(MLUNGHEZZA)
		LD      IX,MAVA00
		ADD     IX,BC
		ADD     IX,BC
                DEC     IX
                DEC     IX

		LD      IY,MRIT00
		ADD     IY,BC
		ADD     IY,BC
                DEC     IY
                DEC     IY

		LD	B,C

                LD      A,(MSCORR)       
                CP      D_AVANTI              
                JR      Z,RESET_MEDIA_02

RESET_MEDIA_01:

		LD      (IX+00H),E
                LD      (IX+01H),D
                DEC     IX
                DEC     IX
		LD      (IY+00H),00H
                LD      (IY+01H),00H
                DEC     IY
                DEC     IY
		DJNZ	RESET_MEDIA_01

                JR      RESET_MEDIA_FINE

RESET_MEDIA_02:         
		LD      (IX+00H),00H
                LD      (IX+01H),00H
                DEC     IX
                DEC     IX
		LD      (IY+00H),E
                LD      (IY+01H),D
                DEC     IY
                DEC     IY
		DJNZ	RESET_MEDIA_02

RESET_MEDIA_FINE:
		LD	A,(MSCORR)	;FORZO SEGNO CORRENTE COME 
		LD	(ERMEDI_SIGN),A	;SEGNO DELLA MEDIA	
		LD	BC,(ERATTU)	;FORZO ERRORE CORRENTE
		LD	(ERPREC),BC	;COME ERRORE MEDIATO
		LD	(ERMEDI),BC	;ED ERRORE PRECEDENTE
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CALCOLO ERRORE MEDIO PER CORREZIONE LONG
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

LMEDIA:
                LD      A,(PRIMOB)      
                LD      B,A
                
                LD      IX,MAVA00       ;SU SEDICI
                LD      DE,(VALORX)
                ADD     IX,DE
LMED01:
                LD      E,(IX)          ;DATI LETTI
                LD      D,(IX+01H)      ;INSERENDO
                LD      (IX+02H),E      ;IL NUOVO
                LD      (IX+03H),D      ;ERRORE AL
                DEC     IX              ;PRIMO POSTO
                DEC     IX              ;ED ELIMINANDO
                DJNZ    LMED01          ;L'ULTIMO
                INC     IX
                INC     IX


                LD      A,(PRIMOB)      ; LD      B,2FH           ;WWAA
                LD      B,A             ;WWAA
                LD      IY,MRIT00       ;+5CH   ;DMED1C       WWAA
                LD      DE,(VALORX)     ;WWAA
                ADD     IY,DE           ;WWAA
LMED02:
                LD      E,(IY)          ;RIPETE QUELLO
                LD      D,(IY+01H)      ;VISTO SOPRA
                LD      (IY+02H),E      ;CON ALTRE  
                LD      (IY+03H),D      ;16 CELLE
                DEC     IY
                DEC     IY
                DJNZ    LMED02
                INC     IY
                INC     IY


                LD      DE,(ERATTU)      ;INSERIMENTO
                LD      BC,0000H         ;NUOVO ERRORE
                LD      A,(MSCORR)       ;NELLA PRIMA
                CP      D_AVANTI         ;DELLE 16 CELLE
                JR      Z,LMED04
LMED03:
                LD      (MAVA00),DE
                LD      (MRIT00),BC
                JR      LMED05
LMED04:         
                LD      (MAVA00),BC
                LD      (MRIT00),DE
LMED05:
                LD      A,(SECONB)
                LD      B,A             ;SOMMA DELLE  16 celle reali
                JR      NC,LMED06       ;48 CELLE 16 CELLE
                CCF                      ; 
LMED06:         LD      HL,0000H
LMED07:
                LD      E,(IX+00H)
                LD      D,(IX+01H)
                ADC     HL,DE
                INC     IX
                INC     IX
                DJNZ    LMED07
                LD      (SOMLO1),HL

                LD      A,(SECONB)      ;
                LD      B,A             ;SOMMA DELLE  16 celle reali 
                JR      NC,LMED08        ;48 CELLE 16 CELLE   
                CCF
LMED08:         LD      HL,0000H
LMED09:
                LD      E,(IY+00H)
                LD      D,(IY+01H)
                ADC     HL,DE
                INC     IY
                INC     IY
                DJNZ    LMED09
                LD      (SOMLO2),HL

		LD      HL,(SOMLO2)
                LD      DE,(SOMLO1)
		SBC     HL,DE
                JR      C,LMED10        ;SEGNO ERRORE
		LD	A,D_AVANTI	;POSITIVO
		LD	(ERMEDI_SIGN),A
                JR      LMED11 

LMED10:         CCF
		LD	A,D_RITARDO	;NEGATIVO
		LD	(ERMEDI_SIGN),A
                
		EX      DE,HL
                LD      DE,(SOMLO2)
		SBC     HL,DE

LMED11:         LD      (DIVDEN),HL
                LD      BC,(SECONB)     ;DIVISIONE    WWAA
                LD      B,00H           ;WWAA
                LD      (DIVSOR),BC     ;PER XX GGAA
		CALL    DIVIS
		LD      HL,(QUOZIE)
                LD      (ERMEDI),HL

		RET

            
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		RICONOSCIMENTO CODICE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

CODICE:         
		;MISURA SPAZIO BIANCO
		LD      A,(MEMB)        
		SET     6,A             ;BLOCCO GATE
		LD      (MEMB),A
		OUT     (PB),A
                LD      A,(GUADS1)      ;CARICO 
		LD      B,A             ;GUADAGNO DI
                LD      A,(MEMA)        ;S1 
		ADD     A,B
		LD      (MEMA),A
                OUT     (PA),A     

CODICE_01:      LD      BC,(BIANCO)	;VALORE DI BIANCO BBBB
		IN      A,(PB)          ;SE STAMPA 
		BIT     2,A             ;ASPETTA
		JR      NZ,CODICE_01
CODICE_02:      IN      A,(PB)                
		BIT     4,A             ;SE 2048 = 0
		JR      Z,CODICE_02           ;ASPETTA
CODICE_03:      IN      A,(PB)          ;SE 2048 = 1
		BIT     4,A             ;ASPETTA
		JR      NZ,CODICE_03
		DEC     C               ;CONTEGGIO SPA-
		JR      NZ,CODICE_02          ;ZIO BIANCO
		IN      A,(PB)
		BIT     2,A
		JR      NZ,CODICE_01

		;MISURA SPESSORE SEGNO

                LD      BC,(UNOMIL)     

CODICE_04:      IN      A,(PB)          ;SE MANCA
		BIT     2,A             ;STAMPA ASPETTA
		JR      Z,CODICE_04
CODICE_05:      IN      A,(PB)
		BIT     4,A
		JR      Z,CODICE_05
CODICE_06:      IN      A,(PB)
		BIT     4,A
		JR      NZ,CODICE_06
		IN      A,(PB)
		BIT     2,A             
		JR      Z,CODICE_01           
		DEC     C
		JR      NZ,CODICE_05

		;MISURA BIANCO POSTERIORE

                LD      BC,(BIANCO)      ;VALORE BIANCO BBBB
CODICE_07:      IN      A,(PB)
		BIT     4,A
		JR      Z,CODICE_07
CODICE_08:      IN      A,(PB)                
		BIT     4,A
		JR      NZ,CODICE_08
		DEC     C
		JR      NZ,CODICE_07
		IN      A,(PB)
		BIT     2,A
		JR      NZ,CODICE_01
CODICE_09:      IN      A,(61H)
		LD      E,A
		IN      A,(62H)
		LD      D,A
		LD      (FASE),DE  ;FASE TROVATA
CODICE_FINE:
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE DI RITARDO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

FASE_TROVATA:
		;FASE TROVATA

		LD      A,(MEMB)
		RES     6,A             ;SBLOCCO GATE
		LD      (MEMB),A
		OUT     (PB),A
		CALL    CENTRA  
			

		LD      A,00H           ;SE RICERCA OK
		LD      (MRIC+00H),A    ;ANNULLO 
		LD      A,43H           ;RESETTO CTC1
		OUT     (71H),A         
		LD      A,01H           ;CONTROLLO DOPPIO
		LD      (MRIC+04H),A    ;DOPPIO SEGNO
		LD      A,(MEMC)        ;TOLGO EVENTUALE PROTEZIONE
		RES     B_PC_PROTEZIONE,A
		LD      (MEMC),A        
                OUT     (PC),A          

		LD	A,(MRIC+08H)	;SERVE PER DARE
		CP	01H		;RICERCA OK UNA
		JR	NZ,FASE_TROVATA_FINE	;SOLA VOLTA

                LD      A,00H		           
		LD      (MRIC+02H),A
		LD	(MRIC+08H),A
		
		CALL	FORCE_STATUS
		CALL	SEND_OK

		LD	BC,0000H
		LD	(ERATTU),BC
		CALL	RESET_MEDIA

FASE_TROVATA_FINE:
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE DI RITARDO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

RIT:            IN      A,(PB)
		BIT     4,A
		JR      Z,RIT
RIT1:           IN      A,(PB)
		BIT     4,A
		JR      NZ,RIT1
		DEC     E
		JR      NZ,RIT
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE DI CONTROLLO CODICE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

MARC:           LD      A,(SEGNO)       ;MISURA 2mm IN      
		LD      B,A             ;MODO CHE IN
MAR1:           IN      A,(PB)          ;QUESTO SPAZIO
		BIT     2,A             ;PASSI IL SEGNO
		JR      NZ,RIT0         ;DI CODICE
		BIT     4,A
		JR      Z,MAR1
MAR2:           IN      A,(PB)
		BIT     2,A
		JR      NZ,RIT0
		BIT     4,A
		JR      NZ,MAR2
		DEC     B
		JR      NZ,MAR1
RIT0:           RET




;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ADEGUAMENTO GUADAGNO AMPLIFICAT. ESTERNO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
 
GAINS1:         EXX
		LD      A,(GUADS1)
		CP      60H
		JR      Z,GS11
		CP      40H
		JR      Z,GS12
		CP      20H
		JR      Z,GS13
		CP      00H
		JR      Z,GS14
GS11:           LD      A,40H
		JR      GS15
GS12:           LD      A,20H
		JR      GS15
GS13:           LD      A,00H
		JR      GS15
GS14:           LD      A,60H
GS15:           LD      (GUADS1),A
		EXX
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CONTROLLO PARAMETRI E EVENTUALE
;		INSERIMENTO VALORI DI DEFAULT
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
                
DEFAUL:         LD      IX,TRIP         
		LD      (IX+01H),00H
                LD      A,(IX)
                CP      0FH
                JR      NC,DEF1
		JR      DEF2
DEF1:           LD      A,06H
		LD      (IX),A


DEF2:           LD      IX,GUADA
		LD      (IX+01H),00H
		LD      A,(IX)
		CP      00H
                JR      Z,DEF3
		CP      64H
                JR      NC,DEF3
                JR      DEF4
DEF3:           LD      A,0AH
		LD      (IX),A
		
DEF4:           LD      IX,CICLO
		LD      (IX+01H),00H
		LD      A,(IX)
		CP      00H
                JR      Z,DEF5
		CP      64H
                JR      NC,DEF5
                JR      DEF6
DEF5:           LD      A,03H
		LD      (IX),A
		
DEF6:           LD      IX,DERIVA
		LD      (IX+01H),00H
		LD      A,(IX)
		CP      00H
                JR      Z,DEF7
		CP      0FFH
                JR      NC,DEF7
                JR      DEF8
DEF7:           LD      A,02H
		LD      (IX),A
		
DEF8:           LD      IX,ZONA
		LD      (IX+01H),00H
		LD      A,(IX)
		CP      00H
                JR      Z,DEF9
		CP      65H
                JR      NC,DEF9
                JR      DEF10
DEF9:           LD      A,00H
		LD      (IX),A
		
DEF10:          LD      IX,INSER
		LD      (IX+01H),00H
		LD      A,(IX)
		CP      00H
                JR      Z,DEF11
                CP      0FFH            ;PPNN
                JR      NC,DEF11
                JR      DEF12
DEF11:          LD      A,0AH
		LD      (IX),A

DEF12:          LD      IX,SALTO
		LD      (IX+01H),00H
		LD      A,(IX)
		CP      0AH
                JR      NC,DEF13
                JR      DEF14           
DEF13:          LD      A,09H
		LD      (IX),A



DEF14:          LD      IX,ZONT
		LD      (IX+01H),00H
		LD      A,(IX)
		CP      00H
                JR      Z,DEF15
                CP      65H
                JR      NC,DEF15
                JR      DEF16
DEF15:          LD      A,00H
		LD      (IX),A

DEF16:          LD      IX,LINGUA
		LD      (IX+01H),00H
		LD      A,(IX)
                CP      0FFH            
                JR      Z,DEF17
                CP      06H
                JR      NC,DEF17
                JR      DEF18
DEF17:          LD      A,01H
		LD      (IX),A

                ;AAOO ELIMINATO OSGAIN

DEF18:          LD      A,(GUADS1)      ;SE IN PARTENZA
                CP      00H             ;NON C'E UN
                JR      Z,DEF19         ;VALORE NORMALE
                CP      20H             ;CREA CASINO 
                JR      Z,DEF19         ;IN SERITA.
                CP      40H
                JR      Z,DEF19
                LD      A,60H
                LD      (GUADS1),A

DEF19:		LD	BC,VERSIONE_PROGRAMMA	;CARICO LA VERSIONE DEL PROGRAMMA
		LD	(VERPRO),BC
		LD	BC,ID_MACCHINA		;CARICO L'IDENTIFICATIVO DELLA MACCHINA
		LD	(IDMACC),BC

DEF20:		
		LD	A,(AUMALO+01H)
		CP	0FFH
		JR	NZ,DEF21
		LD	A,00H
		LD	(AUMALO),A
		LD	(AUMALO+01H),A
DEF21:		
		LD	A,(AUMATR+01H)
		CP	0FFH
		JR	NZ,DEF22
		LD	A,00H
		LD	(AUMATR),A
		LD	(AUMATR+01H),A
DEF22:		
		LD	A,(AUMATI+01H)
		CP	0FFH
		JR	NZ,DEF23
		LD	A,00H
		LD	(AUMATI),A
		LD	(AUMATI+01H),A

DEF23:		
		LD      HL,(FASE)	;SE POSIZIONE FASE > 4096 (IMPULSI ENCODER)
		LD      DE,1000H	;RESET DEL VALORE
		SBC     HL,DE         
		JR      C,DEF24 
		LD	BC,100H
		LD	(FASE),BC
DEF24:		
		LD      HL,(FASES1)	;SE POSIZIONE S1 > 4096 (IMPULSI ENCODER)
		LD      DE,1000H	;RESET DEL VALORE
		SBC     HL,DE         
		JR      C,DEF25 
		LD	BC,100H
		LD	(FASES1),BC
DEF25:		
		LD      HL,(FASRIF)	;SE POSIZIONE RIFERIMENTO S1 > 4096 (IMPULSI ENCODER)
		LD      DE,1000H	;RESET DEL VALORE
		SBC     HL,DE         
		JR      C,DEF26 
		LD	BC,100H
		LD	(FASRIF),BC

DEF26:          RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ROUTINE DI DIVISIONE A 16 BIT
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

DIVIS:          EXX
		LD      DE,(DIVDEN)
		LD      BC,(DIVSOR)
DIV16:          XOR     A
		LD      H,A
		LD      L,A
		LD      A,10H
DV0:            RL      E
		RL      D
		ADC     HL,HL
		SBC     HL,BC
		JR      NC,DIV1
		ADD     HL,BC
DIV1:           CCF
		DEC     A
		JR      NZ,DV0
		LD	(RESTO),HL	;SSCC
		EX      DE,HL
		ADC     HL,HL
		LD      (QUOZIE),HL
		EXX
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ROUTINE DI MOLTIPLICAZIONE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

MOLT:           EXX
		LD      DE,(MOLTI)      ;MOLTIPLICANDO
		LD      A,(MOLPL)       ;MOLTIPLICATORE
		LD      HL,00H
		LD      B,08H
MULT:           ADD     HL,HL
		RLA
		JR      NC,CHCNT
		ADD     HL,DE
CHCNT:          DJNZ    MULT            ;SE IL RISULTATO
		CP      00H             ;E'> DI FFFFH IL
		JR      Z,CHC1          ;REGISTRO A SARA'
		LD      HL,0FFFFH       ;DIVERSO DA 0 PER
CHC1:           LD      (PROD),HL       ;CUI CARICA IL MAX
		EXX                     ;(SOLO IN CASO DI
		RET                     ;CORREZ.MOTORE)

                
 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ROUTINE DI MOLTIPLICAZIONE A 24 BIT  
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
                
MOL24:          EXX                                                                                        
		LD      DE,(MOLT24)
		LD      A,(MOLP24)
		LD      BC,800H                
		LD      H,C
		LD      L,C
MPX1:           ADD     HL,HL
		RLA
		JR      NC,MPX2
		ADD     HL,DE
		ADC     A,C             ;IL RISULTATO
MPX2:           DJNZ    MPX1            ;SI TROVA IN AHL
		LD      (PROD24),HL
		LD      (PROD24+02H),A
		EXX
		RET

 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		DIVISIONE PER 4096 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
                         

DI4096:         EXX
		LD      HL,(PROD24)
		LD      A,(PROD24+02H)
		LD      B,0DH           ;PER DIVIDERE
MPX3:           DJNZ    MPX4            ;IN 4096 SHIFTO
		JR      MPX8            ;A DESTRA 12 VOLTE
MPX4:           SRL     L
		SRL     H
		JR      C,MPX6
MPX5:           SRL     A
		JR      C,MPX7
		JR      MPX3
MPX6:           CCF
		SET     7,L
		JR      MPX5
MPX7:           CCF
		SET     7,H
		JR      MPX3
MPX8:           LD      (QU4096),HL
		EXX
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		DIVISIONE 24 PER 8 BIT (DIVISIONE PER 1000)
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

DI1000:         EXX
                LD      A,(ALTDIV)      
                CP      00H
                JR      Z,DI1004
                CP      01H             
                JR      Z,DI1005
                LD      IX,PTO032
                JR      DI1006
DI1004:         LD      IX,RESTO0
                JR      DI1006
DI1005:         LD      IX,TREST0       
DI1006:         LD      DE,3E8H         ;1000
                LD      BC,0000H
		LD      A,16
		LD      (MDIVIS),A
DI1001:         LD      H,(IX+03H)
		LD      L,(IX+02H)
		SBC     HL,DE
		JP      M,DI1002
		LD      (IX+03H),H      ;RESTO
		LD      (IX+02H),L
		INC     BC
DI1002:         SLA     (IX+00H)        ;ROTAZ.DIV.DO
		RL      (IX+01H)
		RL      (IX+02H)
		RL      (IX+03H)
		SLA     C               ;ROTAZ.QUO.TE
		RL      B
		LD      A,(MDIVIS)
		DEC     A
		LD      (MDIVIS),A
		JR      NZ,DI1001
		LD      H,(IX+03H)
		LD      L,(IX+02H)
		SBC     HL,DE
		JP      M,DI1003
		INC     BC
DI1003:         LD      (QU1000),BC
		EXX
		RET
		

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		MOLTIPLICZIONE A 32 BIT
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

MOL32:          EXX
	        
                LD      B,04H
		LD      HL,PTO032
INIT:           LD      (HL),00H
		INC     HL
		DJNZ    INIT
SHIFTX:         LD      B,04H
		LD      IX,MDO032+03H
		XOR     A
RTX:            RR      (IX)
		DEC     IX
		DJNZ    RTX
		JR      NC,ZERO
		LD      B,04H
		LD      HL,PTO032
		LD      IY,MRE032
		CCF
NXTBYT:         LD      A,(IY)
		ADC     A,(HL)
		LD      (HL),A
		INC     IY
		INC     HL
		DJNZ    NXTBYT
ZERO:           LD      B,04H
		LD      IX,MDO032
		XOR     A
ZCHK:           OR      (IX)
		JR      NZ,SHIFTY
		INC     IX
		DJNZ    ZCHK
		JR      END32
SHIFTY:         LD      B,04H
		LD      IY,MRE032
		XOR     A
LEFTY:          RL      (IY)
		INC     IY
		DJNZ    LEFTY
		JR      SHIFTX
END32:          EXX
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		DIVISIONE, 32BIT DIVISO 4096
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		
 

DIV32:          EXX
		LD      HL,(EDEND0)
		LD      DE,(EDEND2)
		LD      B,0DH           ;PER DIVIDERE
MPW1:           DJNZ    MPW2            ;IN 4096 SHIFTO
		JR      MPW8            ;A DESTRA 12 VOLTE
MPW2:           SRL     L
		SRL     H
		JR      C,MPW7
MPW3:           SRL     E
		JR      C,MPW6
MPW4:           SRL     D
		JR      C,MPW5
		JR      MPW1
MPW5:           CCF
		SET     7,E
		JR      MPW1
MPW6            CCF
		SET     7,H
		JR      MPW4
MPW7:           CCF
		SET     7,L
		JR      MPW3
MPW8:           LD      (QUOZ32),HL
		EXX
		RET

             
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SOTTRAZIONE 32 BIT
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

SOTT32:         EXX
		AND     A
		LD      HL,(MINUE0)     ;SALVO MINU-
		LD      (MIXUE0),HL     ;ENDO E SOT-
		LD      HL,(MINUE2)     ;TRAENDO
		LD      (MIXUE2),HL     ;ORIGINALI
		LD      HL,(SUBTR0)
		LD      (SUXTR0),HL
		LD      HL,(SUBTR2)
		LD      (SUXTR2),HL
		LD      HL,(MIXUE2)     ;VERIFICO CHI E'
		LD      DE,(SUXTR2)     ;IL NUMERO PIU'
		SBC     HL,DE           ;GRANDE PER PER
		JR      Z,SOT01         ;NON AVERE IL
		JR      NC,SOT03        ;RISULTATO NEGA-
		JR      SOT02           ;TIVO E PER
SOT01:          LD      HL,(MIXUE0)     ;DETERMINARE IL
		LD      DE,(SUXTR0)     ;SENSO DI CORREZ-
		SBC     HL,DE           ;ZIONE
		JR      NC,SOT03
SOT02:          LD      HL,(MIXUE0)
		LD      DE,(SUXTR0)
		LD      (MIXUE0),DE
		LD      (SUXTR0),HL
		LD      HL,(MIXUE2)
		LD      DE,(SUXTR2)
		LD      (MIXUE2),DE
		LD      (SUXTR2),HL     
		LD      A,D_RITARDO
		LD      (MSCORR),A
		JR      SOT04
SOT03:          LD      A,D_AVANTI
		LD      (MSCORR),A
SOT04:          LD      HL,(MIXUE0)
		LD      DE,(MIXUE2)
		LD      BC,(SUXTR0)
		AND     A
		SBC     HL,BC
		LD      (RESTO0),HL
                LD      (MREST0),HL     ;MEMORIZZO
                JR      NC,SOT05        ;IL RESTO
                DEC     DE              ;PERCHE' LA
SOT05:          EX      DE,HL           ;LOCAZIONE
                LD      DE,(SUXTR2)     ;REST0 VIENE
                AND     A               ;IN SEGUITO
                SBC     HL,DE           ;MODIFICATA
		LD      (RESTO2),HL
                LD      (MREST2),HL     
		EXX
		RET

                

TSOTT32:        EXX
		AND     A
                LD      HL,(TINUE0)     ;SALVO MINU-
                LD      (TIXUE0),HL     ;ENDO E SOT-
                LD      HL,(TINUE2)     ;TRAENDO
                LD      (TIXUE2),HL     ;ORIGINALI
                LD      HL,(TUBTR0)
                LD      (TUXTR0),HL
                LD      HL,(TUBTR2)
                LD      (TUXTR2),HL
                LD      HL,(TIXUE2)     ;VERIFICO CHI E'
                LD      DE,(TUXTR2)     ;IL NUMERO PIU'
		SBC     HL,DE           ;GRANDE PER PER
                JR      Z,TSOT01        ;NON AVERE IL
                JR      NC,TSOT03       ;RISULTATO NEGA-
                JR      TSOT02          ;TIVO E PER
TSOT01:         LD      HL,(TIXUE0)     ;DETERMINARE IL
                LD      DE,(TUXTR0)     ;SENSO DI CORREZ-
		SBC     HL,DE           ;ZIONE
                JR      NC,TSOT03
TSOT02:         LD      HL,(TIXUE0)
                LD      DE,(TUXTR0)
                LD      (TIXUE0),DE
                LD      (TUXTR0),HL
                LD      HL,(TIXUE2)
                LD      DE,(TUXTR2)
                LD      (TIXUE2),DE
                LD      (TUXTR2),HL     
		LD      A,D_DESTRA
                LD      (TMSCOR),A
                JR      TSOT04
TSOT03:         LD      A,D_SINISTRA
                LD      (TMSCOR),A
TSOT04:         LD      HL,(TIXUE0)
                LD      DE,(TIXUE2)
                LD      BC,(TUXTR0)
		AND     A
		SBC     HL,BC
                LD      (TREST0),HL
                JR      NC,TSOT05
		DEC     DE
TSOT05:         EX      DE,HL
                LD      DE,(TUXTR2)
		AND     A
		SBC     HL,DE
                LD      (TREST2),HL
		EXX
		RET


		
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SOMMA A 32 BIT
;
;	AUGENDO = ADDENDO + AUGENDO
;	si definisce il primo operando addendo e il secondo augendo. 
;	Il risultato somma va poi a sostituire il valore dell'augendo 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		
                

SOMM32:         EXX
		LD      DE,ADDEN0
		LD      HL,AUGEN0
		LD      B,04H
		AND     A
ADDW:           LD      A,(DE)
		ADC     A,(HL)
		LD      (HL),A
		INC     DE              ;IL RISULTATO
		INC     HL              ;SI TROVA NELLA
		DJNZ    ADDW            ;LOCAZIONE 
		EXX                     ;ADDEN0 E ADDEN2
		RET

                

CSOTT32:        EXX
                AND     A
                LD      HL,(CINUE2)
                LD      DE,(CUBTR2)
                SBC     HL,DE
                JR      NC,COT00
                LD      HL,(CINUE2)
                LD      (CINUE2),DE
                LD      (CUBTR2),HL
                LD      HL,(CINUE0)
                LD      DE,(CUBTR0)
                LD      (CINUE0),DE
                LD      (CUBTR0),HL
COT00:          LD      HL,(CINUE0)
                LD      DE,(CINUE2)
                LD      BC,(CUBTR0)
                AND     A
                SBC     HL,BC
                LD      (CESTO0),HL
                JR      NC,COT01
                DEC     DE
COT01:          EX      DE,HL
                LD      DE,(CUBTR2)
                AND     A
                SBC     HL,DE
                LD      (CESTO2),HL
                EXX
                RET

               
CSOM32:         EXX
                SCF
                CCF
                LD      HL,(CADDE0)
                LD      DE,(CAUGE0)
                ADC     HL,DE
                LD      (CSOMM0),HL
                LD      HL,(CADDE2)
                LD      DE,(CAUGE2)
                ADC     HL,DE
                LD      (CSOMM2),HL
                SCF
                CCF
                EXX
                RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		GESTIONE DELLA CORREZIONE SUPERIORE A MEZZO GATE  
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		


OVERCO:
		LD      A,(SUPCOR)	;SE C'E' STATO UN ERRORE MAGGIORE 
                CP      01H		;DI MEZZO GATE
                JR      NZ,OVERCO_FINE

	        LD      HL,(ERATTU)     ;SE ERRORE > 1mm  
                LD      DE,0064H        ;SALTO SE NO  
                SBC     HL,DE           ;RICARICO LA
                JR      NC,OVERC3       ;SOVRA CORREZ-
                CCF                     ;ZIONE.

                LD      A,(SHIFT3)      ;METTO IN SHIFT
                DEC     A               ;AMPI40 FINO
                LD      (SHIFT3),A      ;A CHE SHIFT1
                CP      00H             ;ARRIVI A ZERO
                JR      Z,OVERC1
                LD      DE,(AMPI40)     
                LD      (SHIFT),DE      
                JR      OVERC2

OVERC1:         LD      DE,(SHIFT4)     ;ALLA FINE 
                LD      (SHIFT),DE      ;CARICO IL
                LD      A,00H           ;RESTO IN
                LD      (SUPCOR),A      ;SHIFT

OVERC2:         CALL    SPOSTA

OVERC3:         LD      A,(TIRCIC)      ;QUADRU-
                RLA                     ;PLICO
                RLA                     ;IL TEMPO DI 
                LD      (INCREM),A      ;ATTESA DEL
                LD      (DECREM),A      ;TIRO 
                LD      (MOTPO1),A      
OVERCO_FINE:
                RET





;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CALCOLO DELLA  VELOCITA' 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

		;4mS x 4096 = 16384000
		;60\1638400 = 0,000003662
		;0,000003662 x CILINDRO = X
		;X\1000 = COSVEL (COSVEL E' CALCOLATA
		;AL CAMBIO CILINDRO)
		;COSVEL x IMPVEL = Y
		;Y/1000 = VELOCITA m/m

VELO:           
		LD      DE,(COSVEL)
		LD      (MDO032),DE
		LD      DE,0000H
		LD      (MDO232),DE
		LD      (MRE232),DE
		LD      DE,(IMPVEL)
		LD      (MRE032),DE
		LD      A,02H           
		LD      (ALTDIV),A      
		CALL    MOL32
		CALL    DI1000
		LD      BC,(QU1000)     ;VELOCITA         
		LD      (ISTVEL),BC     ;ISTANTANEA    
		
                ;MEDIA VELOCITA SU QUATTRO LETTURE WWAA WWNN

                LD      B,03H        
                LD      IX,MVEL00      
                LD      DE,0004H        
                ADD     IX,DE
MEDV01:
                LD      E,(IX)          ;DATI LETTI
                LD      D,(IX+01H)      ;INSERENDO
                LD      (IX+02H),E      ;IL NUOVO
                LD      (IX+03H),D      ;DATO AL
                DEC     IX              ;PRIMO POSTO
                DEC     IX              ;ED ELIMINANDO
                DJNZ    MEDV01          ;L'ULTIMO
                INC     IX
                INC     IX


                LD      DE,(ISTVEL)
                LD      (MVEL00),DE

                LD      B,04H            ;SOMMA DELLE
                JR      NC,MEDV02        ;4 CELLE
                CCF                      ; 
MEDV02:         LD      HL,0000H
MEDV03:
                LD      E,(IX+00H)
                LD      D,(IX+01H)
                ADC     HL,DE
                INC     IX
                INC     IX
                DJNZ    MEDV03
                
                LD      (DIVDEN),HL
                LD      BC,0004H  
                LD      (DIVSOR),BC    
		CALL    DIVIS
		LD      HL,(QUOZIE)
		LD      (VELMED),HL

		CALL	CHKTRIP		;CONTROLLO SE LA DIFFERENZA DI VELOCITA' 
					;IN VALORE ASSOLUTO
					;E' SUPERIORE A TRIP

		LD      BC,(VELMED)	;SALVO LA VELOCITA PER FARE IL CALCOLO DEL TRIP
		LD	(VELPRE),BC	;AL PROSSIMO GIRO
MEDV05:        
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SUBROUTINE CONTROLLO SE LA DIFFERENZA DI VELOCITA' 
;		IN VALORE ASSOLUTO E' SUPERIORE A TRIP
;		SE VERO... MESSAGGIO ACCELERA O DECELERA SU DISPLAY
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

CHKTRIP:
			
                LD	A,(AUMALO)	;SE E'IN MANUALE
		CP	00H		;ESCO
                JR      Z,ENDCHKTRIP

                LD      IX,TRIP         ;SE TRIP=0 

                LD      A,(IX)          ;SE TRIP=0 
		CP      00H             ;ESCO
                JR      Z,ENDCHKTRIP	;
		
;                LD      A,(PROVEL)      ;SE IN PROTEZIONE PPRR
;                CP      01H             ;(ALLARME) ATTIVO
;                JR      Z,ENDCHKTRIP    ;ESCO
		
                LD      HL,(VELMED)     ;CONTROLLO SE
                LD      DE,(VELPRE)     ;TRA LA NUOVA 
                SBC     HL,DE           ;E LA VECCHIA 
                JR      C,CHKTRIPD      ;VELOCITA'C'E' 
                LD      A,L             ;UN DIFFERENZA
                CP      (IX)            ;TRIP IN METRI 
                JP      M,NO_TRIP	;IN PIU' CHE
                CALL    ACCEL           ;MENO SE C'E'
		JR      ENDCHKTRIP      ;BLOCCO CON

CHKTRIPD:       CCF                     ;LA SCRITTA  
                LD      HL,(VELPRE)     ;APPROPIATA
                LD      DE,(VELMED)
                SBC     HL,DE
                LD      A,L
                CP      (IX)                         
                JP      M,NO_TRIP
                CALL    DECEL
		JR      ENDCHKTRIP      ;BLOCCO CON
NO_TRIP:
		CALL	VELCOST
ENDCHKTRIP:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		TRASMISSIONE LED CORREZIONI
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

SEND_LED:
		EXX
		LD	A,(MEMLED)	;SE E' UGUALE
		LD	B,A		;ALLA PRECEDENTE
		LD	A,(MEMC)	;SALTO 
		AND	0EBH		;0C3H
		CP	B
		JR	Z,SEND_LED_FINE
		LD	E,A

		CALL	REQISFREE
		LD	A,(LEGGI)	;CORREZIONE 
		LD	(DATOTR+00H),A  ;AVANTI
		LD	A,(STATLED)
		LD	(DATOTR+01H),A
		LD	D,00H
		LD	HL,DATOTR+02H
		LD	(HL),D
		INC	HL
		LD	A,(MEMC)	;STATO CORREZIONE
		AND	0EBH		;0C3H
		LD	(HL),A
		CALL	REQUEST

		LD	A,(MEMC)	;STATO CORREZIONE
		LD	(MEMLED),A

		EXX
SEND_LED_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		RESET USCITE CORREZIONE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

RESEPC:
		LD	A,(MEMC)	
		RES	B_PC_AVANTI,A
		RES	B_PC_RITARDO,A
		RES	B_PC_FUNZ,A
		RES	B_PC_DESTRA,A
		RES	B_PC_SINISTRA,A
		LD	(MEMC),A
		OUT	(PC),A

RESPE1:         RET




;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CALCOLO VARIABILI PER MEDIA ERRORE LONG.
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		


VARERR:                                  ;WWAA
                LD      A,(CICLO)
                CP      09H
                JR      NC,MECI01
                LD      A,07H
                LD      (PRIMOB),A
                INC     A
                LD      (SECONB),A
                LD      A,0CH
                LD      (VALORX),A
                JR      MECI04
MECI01:
                LD      A,(CICLO)
                CP      11H             ;= 17
                JR      NC,MECI02
                LD      A,0FH
                LD      (PRIMOB),A
                INC     A
                LD      (SECONB),A
                LD      A,1CH           ;= 28
                LD      (VALORX),A
                JR      MECI04
MECI02:
                LD      A,(CICLO)
                CP      21H             ;= 33
                JR      NC,MECI03
                LD      A,1FH           ;= 31
                LD      (PRIMOB),A
                INC     A
                LD      (SECONB),A
                LD      A,3CH           ;= 60
                LD      (VALORX),A
                JR      MECI04
MECI03:
                LD      A,2FH           ;= 47
                LD      (PRIMOB),A
                INC     A
                LD      (SECONB),A
                LD      A,5CH           ;= 92
                LD      (VALORX),A
MECI04:
                
		
                LD      DE,(CICLO)      ;CALCOLO DEL 
                LD      (MOLTI),DE      ;75% DEL CICLO
                LD      A,19H           ;CHE VIENE USATO
                LD      (MOLPL),A       ;ALL'INIZIO DELLE
                CALL    MOLT            ;EXTRA CORREZ.
                LD      DE,(PROD)       ;PER FAR SI CHE
                LD      (DIVDEN),DE     ;VI SIA UNA 
                LD      BC,64H          ;BREVE INTERRU-
                LD      (DIVSOR),BC     ;ZIONE PER 
                CALL    DIVIS           ;EVITARE LA
                LD      DE,(QUOZIE)     ;CORREZZIONE
                LD      HL,(CICLO)      ;CONTINUA
                SBC     HL,DE
                LD      (CICL75),HL

		
		
		RET



		
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CENTRATURA DOPO FASE CON OSCILLOSCOPIO ESTERNO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		


CENTRO:         EXX                     ;SOTTRAGGO IL VA- FF57
                LD      HL,(FASES1)     ;LORE DI 1/2 GATE
		LD      BC,(MEZGA)      ;DAL VALORE LETTO
                SBC     HL,BC           ;SE IL RISULTATO
		JR      C,CENTR1        ;E'POSITIVO LO
		JR      CENTR2          ;MEMORIZZO IN
CENTR1:         CCF                     ;IN FASE.
		LD      HL,1000H        ;SE E'NEGATIVO
		SBC     HL,BC           ;SOTTRAGGO DA 4096
                LD      BC,(FASES1)     ;IL 1/2 GATE E IL
		ADD     HL,BC           ;RESTO LO SOMMO
CENTR2:         LD      (FASE),HL       ;AL VALORE LETTO.
		LD      A,H
		OUT     (20H),A
		LD      A,L
		OUT     (10H),A
		LD      A,04H           ;4 GIRI ATTESA
		LD      (MEMRIF),A      ;PER MEMORIZZARE
		EXX                     ;IL RIFERIMENTO
		RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		VERIFICA VALORI DI SVILUPPO CILINDRO E AMPIEZZA GATE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		


VERIFI:
		LD	HL,(CILIN)	;EEOO
		LD	DE,0FFFFH	;EEOO
		ADD	HL,DE
		JR	C,VERF11 	;EEUU
		CALL	ERR_P1
		JR	VERF3		;EEOO
VERF1:		CCF
VERF11:		LD	HL,(CILIN)	;EEUU
		LD	DE,9D4H		;2500mm	MAX SIZE
		SBC	HL,DE
		JR	NC,VERF13 	;EEUU
		CCF
		JR	VERF3
VERF13:		CALL	ERR_P1		;EEUU
VERF3:
		LD	A,(AMPGA)	;EEUU
		CP	00H
		JR	NZ,VERF4
		CALL	ERR_P3

VERF4:
              
	        RET                     ;EEUU


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		VERIFICA TIPO DI ERRORE DA SCRIVERE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		
              
CHK_PARAM:      LD      A,(MDATER)
		CP      00H
		JR      Z,CHK_PARAM_OK
		CP	01H
		JR	Z,CHK_PARAM_OK
		CP	02H
		JR	Z,CHK_PARAM_P1
		CP	03H
		JR	Z,CHK_PARAM_P3
		JR	CHK_PARAM_OK

CHK_PARAM_P1:	CALL	ERR_P1
		JR	CHK_PARAM_OK

CHK_PARAM_P3:	CALL	ERR_P3

CHK_PARAM_OK:
		RET





;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		TRASMISSIONE CON ST7
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ST7:	IDENTIFICAZIONE PARAMETRO/FUNZIONE
;			GESTIONE PRIMI DUE BYTES	
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

ANALI1:
		LD	A,(DATOTR+04H)
		LD	HL,LEGGI
		CP	(HL)
		JR	Z,ANA10
		LD	HL,SCRIVI
		CP	(HL)
		JR	Z,ANA10
		RET

ANA10:
		LD	A,(DATOTR+04H)		;COPIA B4 IN B0
		LD	(DATOTR+00H),A
		LD	A,(DATOTR+05H)		;COPIA B5 IN B1
		LD	(DATOTR+01H),A

		LD	A,(DATOTR+05H)		;MOLTIPLICA PER 2
		JR	NC,ANA11		;E LO SALVA IN A
		CCF	
ANA11:		RLA
		
		LD      L,A			;SOMMA IL VALORE RICEVUTO
		LD      H,00H			;ALLA TRABELLA DI TRANSCODIFICA
		LD      DE,TABELL
		ADD     HL,DE
		LD      E,(HL)			;HO L'AREA DI MEMORIA INDIRIZZATA IN DE
		INC	HL
		LD      D,(HL)
		EX	DE,HL
		LD      D,(HL)			;SCRIVO IN HL IL VALORE INDIRIZZATO DA DE
		INC	HL
		LD      E,(HL)
		LD	(DATOTR+02H),DE
		RET	


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ST7:	SECONDA IDENTIFICAZIONE PARAMETRI
;			GESTIONE ULTIMI DUE BYTES	
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

ANALI2:
		LD	A,(DATOTR+04H)
		LD	HL,SCRIVI
		CP	(HL)
		JR	Z,ANA20
		CP	(HL)
		JP	NC,ANA30
		RET
ANA20:
		LD	A,(DATOTR+05H)		;MOLTIPLICA PER 2
		JR	NC,ANA21		;E LO SALVA IN A
		CCF	
ANA21:		
		RLA
		LD      L,A			;SOMMA IL VALORE RICEVUTO
		LD      H,00H			;ALLA TRABELLA DI TRANSCODIFICA
		LD      DE,TABELL
		ADD     HL,DE
		LD      E,(HL)			;HO L'AREA DI MEMORIA INDIRIZZATA IN DE
		INC	HL
		LD      D,(HL)
		EX	DE,HL
		LD	DE,(DATOTR+06H)
		LD	(HL),D
		INC	HL
		LD	(HL),E

		LD	A,(DATOTR+05H)	;SE VIENE SCELTO
		LD	HL,CILINT	;CILINDRO O
		CP	(HL)		;AMPGATE VADO
		JR	Z,ANA22	        ;AD ESEGUIRE I
		LD	HL,AMPGAT	;CALCOLI RELATIVI
		CP	(HL)
		JR	Z,ANA22 
		LD	HL,ENCSEGN
		CP	(HL)
		JR	Z,ANA23
		JR	ANA24
ANA22:		
		CALL	ELCILI		
		JR	ANA24
ANA23:
		;RESET IL CTC CON IN NUOVO VALORE	

		LD      A,00H           ;IMPONGO MANUALE
		LD	(AUMALO),A
		LD	(AUMATI),A	
		LD	(AUMATR),A	

		CALL	RESEPC		;ANNULLO COMANDI

		LD      HL,(FASE)       ;SPOSTO IL PUNTO DI FASE
		LD      A,H
		OUT     (20H),A		
		LD      A,L
		OUT     (10H),A
		LD      A,04H           ;4 GIRI ATTESA
		LD      (MEMRIF),A      ;PER MEMORIZZARE
ANA24:		
		RET

ANA30:
		LD	A,(DATOTR+04H)
		LD	HL,AUTFAS
		CP	(HL)
		JR	NZ,ANA30_1
		CALL	FUNZ4			;FASTATURA AUTOMATICA
		JR	ANA30_FINE

ANA30_1:
		LD	HL,TESTES
		CP	(HL)
		JR	NZ,ANA30_2
		CALL	TEST_SH			;TEST DELLA TESTA
		JR	ANA30_FINE

ANA30_2:
		LD	HL,TESENC
		CP	(HL)
		JR	NZ,ANA30_3
		CALL	TEST_ENCODER		;TEST DLL'ENCODER
		JR	ANA30_FINE

ANA30_3:
		LD	HL,SHDATO		
		CP	(HL)
		JR	NZ,ANA30_4
		CALL	LTSHIFT		;VALORE DELLO SHIFT (IN AUTOMATICO)
		JR	ANA30_FINE

ANA30_4:
		LD	HL,SHMANU
		CP	(HL)
		JR	NZ,ANA30_5
		CALL	CORREZIONE_MANUALE	;SPOSTAMENTO MANUALE
		JR	ANA30_FINE

ANA30_5:
		LD	HL,SHSTOP
		CP	(HL)
		JR	NZ,ANA30_6
		CALL	STOP_CORREZIONE_MANUALE
		JR	ANA30_FINE

ANA30_6:
		LD	HL,NORDF9		
		CP	(HL)
		JR	NZ,ANA30_7
		CALL	F9_ON			;FUNZIONE COLD SEAL
		JR	ANA30_FINE

ANA30_7:
		LD	HL,INIPAR		
		CP	(HL)
		JR	NZ,ANA30_8
		CALL	FUNZ_INIT		;FUNZIONE INIZIALIZZAZIONE
		JR	ANA30_FINE

ANA30_8:
		LD	HL,AZZERA		;FUNZIONE ZERO
		CP	(HL)
		JR	NZ,ANA30_9
		CALL	FUNZ0			;FUNZIONE ANNULLA ERRORE
		JR	ANA30_FINE


ANA30_9:
		LD	HL,PULSAN		
		CP	(HL)
		JR	NZ,ANA30_10
		CALL	FUNZ5			;FASATURA A PULSANTE ESTERNO
		JR	ANA30_FINE

ANA30_10:
		LD	HL,TESTOUT		
		CP	(HL)
		JR	NZ,ANA30_11
		CALL	USCITE			;TEST USCITE
		JR	ANA30_FINE

ANA30_11:
		LD	HL,FORCERESET		
		CP	(HL)
		JR	NZ,ANA30_FINE
		CALL	FUNZ_RESET		;FORZA IL RESET DELLA SCHEDA Z80
		JR	ANA30_FINE

ANA30_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ST7:	TRASMISSIONE RICEZIONE 2 BYTE 
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
TRASM:
                LD      B,02H           
TRAS1:          LD      A,(IX)          
		CALL    TX_RX
		LD	A,D
		LD	(IY),A
		INC     IX
		INC	IY 
		DJNZ    TRAS1
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ST7:	TRASSMISSIONE RICEZIONE 1 BYTE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

TX_RX:     
		PUSH    BC              ;TRASMISSIONE
		LD      B,08H           ;RICEZIONE SE-
                LD      C,A             ;RIALE PARALLELO
TX_RX1:         
		LD      A,(MEMB)	;IMPOSTO IL BIT DA MANDARE
                RL      C
                JR      NC,TX_RX2
                SET     7,A
                JR      TX_RX3
TX_RX2:         RES     7,A
TX_RX3:            
		LD      (MEMB),A	;MANDO IL BIT IN TX
                OUT     (PB),A
		LD      A,(MEMB)	;IMPOSTO CLOCK = 1
                SET     0,A
		LD      (MEMB),A	
                OUT     (PB),A
		CALL	SLEEP		;RITARDO
		IN	A,(PA)		;LEGGO IL BIT IN RX	
		RR	A		;SHIFTO PER AVER IL BYTE IN D
		RL	D
		LD      A,(MEMB)	;IMPOSTO CLOCK = 0
                RES     0,A
                OUT     (PB),A
                LD      (MEMB),A
		CALL	SLEEP		;RITARDO
                DJNZ    TX_RX1
                POP     BC
                RET

SLEEP:
		LD	A,4AH
		;LD	A,1fH		;PER AUMENTARE LE PERFORMANCE
		LD      H,A		;DI TRASMISSIONE RIDOTTO DA 4AH A 1FH
SLEEP1:		DEC	H
		JR	NZ,SLEEP1
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ATTIVA TEST DELL'ENCODER
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		

TEST_ENCODER:	
		LD	A,00H
		LD	(AUMALO),A
		LD	(AUMATR),A
		LD	(AUMATI),A
		LD	A,01H			;SEGNALA LA  
		LD	(TEST_ENC),A		;RICHIESTA DI 
		RET				;TEST 

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		FORZA L'INVIO DI ERRORE, VELOCITA' E STATO DEGLI ALLARMI
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		
FUNZ_INIT:	LD	BC,0FFFFH	
		LD	(ERMEDI_DISP),BC	;FORZA A MANDARE ERRORE CORRENTE
		LD	BC,0FFFFH	
		LD	(VELDIS),BC		;FORZA A MANDARE VELOCITA' CORRENTE	
		LD	BC,0FFFFH	
		LD	(STATU2),BC		;FORZA A MANDARE STATO CORRENTE
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		FORZA IL PROGRAMMA A RIPARTIRE DA VIA (RESET Z80)
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&		
FUNZ_RESET:	LD	BC,0FFFFH
		LD	(RESET),BC
		LD	A,00H
		LD	(AUMALO),A
		LD	(AUMATR),A
		LD	(AUMATI),A
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		ST7:	SCELTA SULLE QUATTRO DIREZIONI DI CORREZIONE IN AUTOMATICO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
		
LTSHIFT:
		LD	A,(DATOTR+05H)
		LD	HL,DIRAVA
		CP	(HL)
		JR	Z,LTAVA
		LD	HL,DIRRIT
		CP	(HL)
		JR	Z,LTRIT
		;LD	HL,DIRTRA
		;CP	(HL)
		;JR	Z,LTTRA
		;LD	HL,DIROPE
		;CP	(HL)
		;JR	Z,LTOPE
		RET

LTAVA:
		LD	A,(HL)
		LD	(AVARIT),A
		JR	LONVAL
LTRIT:		LD	A,(HL)
		LD	(AVARIT),A
LONVAL:		LD	A,(DATOTR+06H)
		LD	D,A
		LD	A,(DATOTR+07H)
		LD	E,A
		LD	(SHIFT),DE

LETT2:          LD      DE,(SHIFT)      ;SE E'IN AUTO 
                LD      HL,(AMPI40)     ;MENTI MASSIMI 
                SBC     HL,DE           ;GATE
		JR      NC,LETT1
		CCF
		LD      A,01H
                LD      (SUPCOR),A      

                LD      HL,(SHIFT)      ;VEDO QUANTE 
                LD      (DIVDEN),HL     ;VOLTE AMPI40
                LD      BC,(AMPI40)     ;STA IN SHIFT
                LD      (DIVSOR),BC     ;E LO MEM. IN
                CALL    DIVIS           ;SHIFT3
                LD      BC,(QUOZIE)
                LD      (SHIFT3),BC     ;IL RESTO LO
                LD      BC,(RESTO)      ;MEM IN SHIFT4
                LD      (SHIFT4),BC     ;
                LD      HL,(SHIFT)      ;SLAVO LA SUPER 
                LD      DE,(AMPI40)
                SBC     HL,DE
                JR      NC,LETT3
                CCF
                LD      A,01H
                LD      (MDATER),A
                JR      LETT1

LETT3:          LD      DE,(AMPI40)     ;CARICO IL MAX 
                LD      (SHIFT),DE      ;ACCETTABILE 

;LETTO:          LD      HL,(TABCON)
;		LD      (TSHIFT),HL
;		;LD      A,(BINSET+10H)
;		LD      (TIUMEN),A
;		JR      LETT1
;LETTT:          LD      HL,(TABCON)
;		LD      (TSHIFT),HL
;		;LD      A,(BINSET+10H)
;		LD      (TIUMEN),A

LETT1:          ;EXX
		CALL	SPOSTA
		RET
		

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CORREZIONI IN MANUALE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&


CORREZIONE_MANUALE:
		LD	A,(DATOTR+05H)
		LD	HL,DIRAVA
		CP	(HL)
		JR	Z,MLTAVA

		LD	HL,DIRRIT
		CP	(HL)
		JR	Z,MLTRIT

		LD	HL,DIRTRA
		CP	(HL)
		JR	Z,CORREZIONE_MANUALE_DESTRA

		LD	HL,DIROPE
		CP	(HL)
		JR	Z,CORREZIONE_MANUALE_SINISTRA

		RET

MLTAVA:
		LD	A,(HL)
		LD	(AVARIT),A
		JR	CORREZIONE_MANUALE_AVANTI
MLTRIT:		LD	A,(HL)
		LD	(AVARIT),A
		JR	CORREZIONE_MANUALE_RITARDO
		RET

CORREZIONE_MANUALE_AVANTI:         
		LD      A,(AUMALO)      ;SE E' IN AUTOMATICO SALTO
	        CP	0FFH		
		JR      Z,CORREZIONE_MANUALE_FINE

		LD      A,(MEMC)
		RES     B_PC_RITARDO,A
		SET     B_PC_AVANTI,A
		LD      (MEMC),A
		OUT     (PC),A          ;MOTORE AVANTI
		JR	CORREZIONE_MANUALE_FINE

CORREZIONE_MANUALE_RITARDO:         
		LD      A,(AUMALO)      ;SE E' IN AUTOMATICO SALTO
	        CP	0FFH		
		JR      Z,CORREZIONE_MANUALE_FINE

		LD      A,(MEMC)
		RES     B_PC_AVANTI,A
		SET     B_PC_RITARDO,A	;MOTORE RITARDO
		LD      (MEMC),A
		OUT     (PC),A          
		JR	CORREZIONE_MANUALE_FINE


CORREZIONE_MANUALE_DESTRA:         
		LD      A,(AUMATI)      ;SE E' IN AUTOMATICO SALTO
	        CP	0FFH
		JR      Z,CORREZIONE_MANUALE_FINE
		LD      A,(MEMC)
		RES     B_PC_SINISTRA,A
		SET     B_PC_DESTRA,A	;MOTORE TRASMISSIONE
		LD      (MEMC),A
		OUT     (PC),A          
		JR	CORREZIONE_MANUALE_FINE

CORREZIONE_MANUALE_SINISTRA:         
		LD      A,(AUMATI)      ;SE E' IN AUTOMATICO SALTO
	        CP	0FFH		
		JR      Z,CORREZIONE_MANUALE_FINE
		LD      A,(MEMC)
		RES     B_PC_DESTRA,A
		SET     B_PC_SINISTRA,A	;MOTORE OPERATORE
		LD      (MEMC),A
		OUT     (PC),A          
		JR	CORREZIONE_MANUALE_FINE
CORREZIONE_MANUALE_FINE:
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		STOP CORREZIONI IN MANUALE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

STOP_CORREZIONE_MANUALE:
		LD      A,(MEMC)
		RES     B_PC_AVANTI,A
		RES     B_PC_RITARDO,A
		RES	B_PC_DESTRA,A
		RES	B_PC_SINISTRA,A
		LD      (MEMC),A
		OUT     (PC),A          ;MOTORE STOP
STOP_CORREZIONE_MANUALE_FINE:
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CALCOLI RELATIVI ALLO SVILUPPO CILINDRO E GATE
;		VENGONO SEMPRE ESEGUITI ENTRAMBI
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

ELCILI:		
		CALL	VERIFI
		LD	HL,(CILIN)	;EEOO
		LD	DE,0FFFFH	;EEOO
		ADD	HL,DE
		JR	C,ELCILI1 	;EEUU
		JP	END_ELCILI
ELCILI1:	CCF

		LD      DE,(CILIN)      ;MOLTIPLICO PER
		LD      (MOLTI),DE      ;10 IL CILINDRO
		LD      A,0AH           ;PER POTER USARE
		LD      (MOLPL),A       ;AGGIUSTAMENTO
		CALL    MOLT            ;DECIMALE
		LD      BC,(PROD)
		LD      (CILMOL),BC     ;CILINDRO x 10
		LD      (MDO032),BC
		LD      DE,0000H        
		LD      (MDO232),DE     
		LD      BC,03E8H         
		LD      (MRE032),BC      
		LD      (MRE232),DE     
		CALL    MOL32
		LD      BC,(PTO032)     ;CILNDRO X 10000
		LD      DE,(PTO232)
		LD      (EDEND0),BC
		LD      (EDEND2),DE
		CALL    DIV32
		LD      DE,(QUOZ32)
		LD      (DMILLE),DE     ;DECIMILLESIMI
		LD      (DIVDEN),DE     ;CALCOLO QUANTI
		LD      BC,000AH        ;MILLESIMI CI 
		LD      (DIVSOR),BC     ;SONO IN UN
		CALL    DIVIS           ;CILINDRO
		LD      DE,(QUOZIE)
		LD      (MILLES),DE     ;MILLESIMI
		LD      (DIVDEN),DE     ;DIVIDO PER 10
		LD      BC,000AH        ;PER AVERE 
		LD      (DIVSOR),BC     ;I CENTESIMI
		CALL    DIVIS           
		LD      DE,(QUOZIE)
		LD      (CENTES),DE
		LD      DE,(CILIN)      ;DIVISIONE DEL
		LD      (DIVDEN),DE     ;CILNDRO PER 10
		LD      BC,000AH
		LD      (DIVSOR),BC
		CALL    DIVIS
		LD      DE,(QUOZIE)
		LD      (CILDIV),DE
		LD      DE,2710H        ;10000/DMILLE=                                     
		LD      (DIVDEN),DE     ;IMPULSI ENCO-
		LD      DE,(DMILLE)     ;DER PER 1mm
		LD      (DIVSOR),DE     
		CALL    DIVIS           
		LD      DE,(QUOZIE)     
                           
		LD      (UNOMIL),DE     
		LD      DE,4E20H        ;=20000
		LD      (DIVDEN),DE
		LD      BC,(MILLES)
		LD      (DIVSOR),BC
		CALL    DIVIS
		LD      HL,(QUOZIE)
		LD      (MILL20),HL     ;20mm
		ADD     HL,HL
		LD      (MILL40),HL     ;40mm 

                LD      (SPACOD),HL     ;SERVE IN OSCFX1 TTGG

                LD      DE,0E4EH        ;4000x4096=  
                LD      (MDO032),DE     ;1638400
                LD      DE,0000H        ;60\1638400=
                LD      (MDO232),DE     ;0,000003662
                LD      (MRE232),DE     ;3662=0E4EH
                LD      DE,(CILIN)
                LD      (MRE032),DE
                LD      A,02H           
                LD      (ALTDIV),A      
                CALL    MOL32
                CALL    DI1000
                LD      BC,(QU1000)
                LD      (COSVEL),BC     ;COSTANTE VEL. 

		

		;CALCOLO VALORI PER RICERCA CODICE
		;AL CAMBIO DI FORMATO

		;########################################
		;# UNOMIL : 2 = IMPULSI DA 2048 IN 1mm  #
		;# NøIMPULSI DA 2048 IN 1 mm X 3 =      #   
		;# SPESSORE SEGNO. LO SPAZIO TRA DUE    #
		;# SEGNI E' DI 7 mm PER CUI LO SPAZIO   #
		;# RIMANENTE E' Nø IMPULSI DA 2048      #
		;# IN 1mm X 4 CIOE'3+1=4                #
		;########################################
		
		LD      DE,(UNOMIL)
                
		SRL     E               ;DIVISO DUE

                LD      A,E             ;IMPULSI DA 
                INC     A               ;2048 PER mm+1
                LD      (IMPMIL),A      ;

                LD      B,E             ;PERCHE' 2048
		LD      D,00H
		LD      (MOLTI),DE      ;NøIMPULSI X 3
		LD      A,03H
		LD      (MOLPL),A
		CALL    MOLT
		LD      DE,(PROD)
		LD      (SEGNO),DE
		LD      A,B             ;NøIMPULSI X 3
		ADD     A,E             ;+1= SPAZIO
		LD      (SPAZIO),A
		LD      DE,(MILL40)     ;MILL40:2=A
		SRL     E               ;40mm IN
		LD      A,E             ;IMPULSI DA 2048
		LD      (SPAZ40),A      
		
ENTE4:
              
                LD      DE,(CENTES)     ;
                LD      (MOLTI),DE      ;
                LD      A,04H
                LD      (MOLPL),A
                CALL    MOLT
                LD      DE,(PROD)
                LD      (CENTE4),DE     ;


                LD      DE,(AMPGA)      ;NORD
                LD      (MOLTI),DE      ;MOLTIPLICO
                LD      A,50H           ;AMPIEZZA GATE
                LD      (MOLPL),A       ;PER 80
		CALL    MOLT
                LD      DE,(PROD)       
                LD      (DIVDEN),DE     ;DIVIDO PER 2
                LD      BC,0002H
		LD      (DIVSOR),BC
                CALL    DIVIS           ;AMPIEZZA 40%  
                LD      DE,(QUOZIE)     ;IN CENTESIMI
                LD      (DMEZGA),DE

                LD      DE,(AMPGA)
		LD      (MOLTI),DE      ;AMPIEZZA GATE
		LD      A,64H           ;PER 100
		LD      (MOLPL),A
		CALL    MOLT
		LD      DE,(PROD)       ;AMPIEZA IN CENT.
		LD      (DIVDEN),DE
                LD      BC,(CENTE4)     ;
		LD      (DIVSOR),BC
		CALL    DIVIS           ;AMPIEZZA GATE IN
		LD      DE,(QUOZIE)     ;IMPULSI ENCODER.
		
                LD      A,D             ;CONTROLLO SE IL 
		CP      00H             ;RISULTATO E'MAG-
		JR      Z,ENTE41        ;GIORE DI 255.
		LD      D,00H           ;SE LO E'IMPONGO
		LD      E,0FFH          ;COME VALORE MAX
		LD      (MOLTI),DE      ;255 E RIPETO LE
                LD      A,(CENTE4)      ;OPERAZIONI 
		LD      (MOLPL),A       ;ALL'INVERSO PER
		CALL    MOLT            ;PER CALCOLARE IL
		LD      DE,(PROD)       ;NUOVO AMPAG RI-
		LD      (DIVDEN),DE     ;FERITO A 255.
		LD      BC,0064H
		LD      (DIVSOR),BC
		CALL    DIVIS
		LD      DE,(QUOZIE)
		LD      (AMPGA),DE

		JP	ENTE4
ENTE41:      
		
		LD      (AMPIMP),DE     
		LD      (MOLTI),DE
                LD      A,0B4H          ;CALCOLO IL 180%  
		LD      (MOLPL) ,A      ;DELL'AMPIEZZA DEL
                CALL    MOLT            ;GATE CIOE 45x4
		LD      DE,(PROD)       
		LD      (DIVDEN),DE
		LD      BC,0064H
		LD      (DIVSOR),BC
		CALL    DIVIS
		LD      DE,(QUOZIE)
                LD      (MEZGA),DE     ;

		LD      DE,(AMPIMP)	;CALCOLO DEL	BBBB     
		LD      (MOLTI),DE	;BIANCO BBBB
                LD      A,96H           ;CHE E'IL 150% BBBB 
		LD      (MOLPL) ,A      ;DI AMPIMP BBBB
                CALL    MOLT            ;
		LD      DE,(PROD)       
		LD      (DIVDEN),DE
		LD      BC,0064H
		LD      (DIVSOR),BC
		CALL    DIVIS
		LD      DE,(QUOZIE)
                LD      (BIANCO),DE      ;BBBB
	
                LD      DE,(AMPGA)      ; 
		LD      (MOLTI),DE
                LD      A,28H           ;CALCOLO IL 40% 
		LD      (MOLPL) ,A      ;DELL'AMPIEZZA DEL
		CALL    MOLT            ;GATE
		LD      DE,(PROD)       
                LD      (AMPI40),DE     ;GATE IN CENTS     

                LD      A,(AMPIMP)      ;
                SRL     A
                LD      (AMPMEZ),A      ;

                LD      DE,(AMPIMP)     ;CON CKTG0 A  
                LD      (MOLTI),DE      ;1024 MOLTIPL. 
                LD      A,28H           ;PER 40 ANZICHE 
                LD      (MOLPL),A       ;PER 10 CON CKTG0 
                CALL    MOLT            ;A 4096
		LD      DE,(PROD)
                LD      (AMPI10),DE     
		
		LD      DE,7D0H         ;2000 = 20mmX100
		LD      (MOLTI),DE
		LD      A,(SEQSEG)
		LD      (MOLPL),A
		CALL    MOLT
		LD      HL,(PROD)
		SBC     HL,DE
		LD      (DISTAN),HL
		LD      (MDO032),HL     ;CALCOLO
		LD      BC,0000H        ;DISTANZA IN
		LD      (MDO232),BC     ;CENTIMILLESIMI
		LD      HL,03E8H
		LD      (MRE032),HL
		LD      (MRE232),BC
		CALL    MOL32
		LD      HL,(PTO032)
		LD      (DISTA0),HL     ;DISTANZA IN
                LD      (TDIST0),HL     ;CENTIMILLESIMI
                LD      HL,(PTO232)     ;SIA PER LONG.
                LD      (DISTA2),HL     ;CHE PER TRAVERS
                LD      (TDIST2),HL
END_ELCILI:
		RET			
		

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		CARICO INDIRIZZI DI MEMORIA DEI PARAMETRI
;		NELLA TABELLA DI TRANSCODIFICA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

FAITAB:
		LD	HL,CILIN		;*
		LD	(TABELL+00H),HL
		LD	HL,ALLAR		;*
		LD	(TABELL+02H),HL
		LD	HL,AMPGA		;*
		LD	(TABELL+04H),HL
		LD	HL,SEQSEG
		LD	(TABELL+06H),HL
		LD	HL,TRIP			;*
		LD	(TABELL+08H),HL
		LD	HL,GUADA		;*
		LD	(TABELL+0AH),HL
		LD	HL,GUATRA
		LD	(TABELL+0CH),HL
		LD	HL,CICLO		;*
		LD	(TABELL+0EH),HL
		LD	HL,TCICLO
		LD	(TABELL+10H),HL
		LD	HL,ZONA			;*
		LD	(TABELL+12H),HL
		LD	HL,ZONT
		LD	(TABELL+14H),HL
		LD	HL,VCLONG
		LD	(TABELL+16H),HL
		LD	HL,VCTRAV
		LD	(TABELL+18H),HL
		LD	HL,AUMALO		;*
		LD	(TABELL+1AH),HL
		LD	HL,AUMATR
		LD	(TABELL+1CH),HL
		LD	HL,SALTO		;*
		LD	(TABELL+1EH),HL
		LD	HL,DERIVA		;*
		LD	(TABELL+20H),HL
		LD	HL,INSER		;*
		LD	(TABELL+22H),HL
		LD	HL,LINGUA
		LD	(TABELL+24H),HL
		LD	HL,SEGRIF
		LD	(TABELL+26H),HL
		LD	HL,DIAGON
		LD	(TABELL+28H),HL
		LD	HL,MESSA		;*
		LD	(TABELL+2AH),HL
		LD	HL,NORINV
		LD	(TABELL+2CH),HL
		LD	HL,MM_MC
		LD	(TABELL+2EH),HL
		LD	HL,VERPRO
		LD	(TABELL+30H),HL
		LD	HL,IDMACC
		LD	(TABELL+32H),HL
;		LD	HL,ERATTU		;VALORE ASSOLUTO
;		LD	(TABELL+34H),HL
;		LD	HL,ERATTU		;VALORE ASSOLUTO
;		LD	(TABELL+36H),HL
		LD	HL,MEMC			;STATO LED CORREZIONE
		LD	(TABELL+38H),HL
		LD	HL,DACLON
		LD	(TABELL+3AH),HL
		LD	HL,DACTRA
		LD	(TABELL+3CH),HL
		LD	HL,DACINS
		LD	(TABELL+3EH),HL
		LD	HL,VELMED		;VELOCITA
		LD	(TABELL+40H),HL
;		LD	HL,TERATT		;VALORE ASSOLUTO
;		LD	(TABELL+42H),HL
;		LD	HL,TERATT		;VALORE ASSOLUTO
;		LD	(TABELL+44H),HL
		LD	HL,AUMATI		;MODO AUTOMATICO-MANUALE DI TIRO
		LD	(TABELL+46H),HL
		LD	HL,CODE
		LD	(TABELL+48H),HL
		LD	HL,MANGUA
		LD	(TABELL+4AH),HL
		LD	HL,STATUS
		LD	(TABELL+4CH),HL
		LD	HL,PRESS
		LD	(TABELL+4EH),HL
		LD	HL,FASE			;POSIZIONE SEGNO
		LD	(TABELL+50H),HL		;RISPETTO ENCODER
		LD	HL,ENCOD		;SEGNO DI ZERO DELL'ENCODER
		LD	(TABELL+52H),HL		;NPN O PNP
		LD	HL,TIRGUA		;GUADAGNO DI TIRO
		LD	(TABELL+54H),HL		;
		LD	HL,TIRCIC		;CICLICO DI TIRO
		LD	(TABELL+56H),HL		;
		LD	HL,ENTIRO		;ENABLE TIRO
		LD	(TABELL+58H),HL		;
		LD	HL,ENTRAS		;ENABLE OP-TRASMISSIONE
		LD	(TABELL+5AH),HL		;
		LD	HL,LAVORO		;LAVORO CORRENTE
		LD	(TABELL+5CH),HL		;
		LD	HL,FASRIF		;FASE DI RIFERIMENTO
		LD	(TABELL+5EH),HL		;
		LD	HL,EN_F9		;ENABLE COLD SEAL (F9)
		LD	(TABELL+60H),HL
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		AZZERAMENTO DOPO REGISTRO MANUALE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

FUNZ0:          LD      A,00H           ;IMPONGO MANUALE
		LD	(AUMALO),A
		LD	(AUMATI),A	
		LD	(AUMATR),A	
		LD      (MEMC),A        
		OUT     (PC),A
FUN3:           
		LD      BC,(ERATTU)     ;CARICO L'ERRORE
		LD      (SHIFT),BC      ;LETTO IN SHIFT                                          
		LD      A,(MSCORR)      ;VEDO SE IN AVANTI
		CP      D_AVANTI        ;O IN RITARDO E
		JR      Z,FUN1          ;CHIAMO LA ROUTINE
		LD      A,(DIRAVA)      ;SPOSTA PER CEN-
		LD      (AVARIT),A      ;TRARLO NEL GATE.
		JR      FUN2
FUN1:           LD      A,(DIRRIT)
		LD      (AVARIT),A
FUN2:           CALL    SPOSTA

		LD	BC,0000H
		LD	(ERATTU),BC
		CALL	RESET_MEDIA

		CALL	SEND_OK

		RET

	
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SEGNALAZIONE FASATURA AUTOMATICA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&


FUNZ4:          LD       A,00H           ;IMPONGO MANUALE
		LD	(AUMALO),A	
		LD	(AUMATI),A	
		LD	(AUMATR),A	
                LD      (MEMC),A        
                OUT     (PC),A
                LD      (SUPCOR),A      ;
                LD      A,01H
		LD      (MRIC+00H),A
                LD      (MRIC+06H),A    ;
		LD	(MRIC+08H),A	;PER NON 2 VOLTE OK 
;		LD      A,0FH           ;DOPO 16 GIRI RRAA
;		LD      (ALTGUA),A      ;STOP GUAD.AUTO
		LD      A,60H           ;INIZIO CON
		LD      (GUADS1),A      ;GUADAGNO MINIMO
                LD	A,03H		;LD      A,05H
		LD      (MRIC+02H),A

		LD      A,(MEMB)
		SET     6,A             ;BLOCCO GATE
		LD      (MEMB),A
		OUT     (PB),A
		LD      A,0D7H          ;SE DOPO CIRCA 
                OUT     (71H),A         ;9 GIRI RICERCA 
		LD      A,03H           ;FALLITA ESCI RRAA
		OUT     (71H),A
		
		LD	BC,(FASE)
		LD	(MFASE),BC
		LD	BC,(FASES1)
		LD	(MFASES1),BC
		LD	BC,(FASRIF)
		LD	(MFASRIF),BC
		RET			
  	
		;JP      ESCI


            	
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SEGNALAZIONE FASATURA MANUALE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&


FUNZ5:       
                LD      A,00H           ;IMPONGO MANUALE
                LD      (MEMC),A
                OUT     (PC),A
		LD	(SHIFT3),A	;SSCC
		LD	(SHIFT4),A	;SSCC

	        IN      A,(10H)         ;LEGGO LA POSIZIONE
		LD      L,A             ;(PULSANTE FASE MANUALE)
		IN      A,(20H)
		LD      H,A
		LD      (FASE),HL
	        LD      (MOLTI),HL      ;MOLTIPLICO PER 
                LD      A,0AH           ;10 PER AVERE IL
                LD      (MOLPL),A       ;RIFERIMENTO TO-
                CALL    MOLT            ;TALE TRASCURAN-
                LD      DE,(PROD)       ;DO IL RAPPORTO
                LD      (RIFTOT),DE

                CALL    CENTRA        

		RET




;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SCRITTURA FORMATO CILINDRO ERRATO
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

ERR_P1:
		LD	DE,(S_SVILUPPO_CILINDRO)
		CALL	SET_STATUS
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		SCRITTURA AMPIEZZA GATE ERRATA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

ERR_P3:
		LD	DE,(S_AMPIEZZA_GATE)
		CALL	SET_STATUS
		RET





;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		VERIFICA FUNZIONAMENTO TESTA
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

TEST_SH:		
		LD      A,(MEMB)
		SET     6,A             ;BLOCCO GATE
		LD      (MEMB),A
		OUT     (PB),A
		LD      D,0FFH		;5 SECONDI
TES1:           LD      BC,0A00H        
TES2:           IN      A,(PB)          
		BIT     2,A
		JR      NZ,TESSI
		DEC     BC ;DI TEST 
		LD      A,B
		
		OR      C                
		JR      NZ,TES2         
		DEC     D
		LD      A,00H
		CP      D
		JR      NZ,TES1
		CALL	FORCE_STATUS
		LD	DE,(S_TEST_TESTINA)
		CALL	SET_STATUS		;TEST FALLITO
		JR	ENDTEST
TESSI:
		CALL	FORCE_STATUS
		LD	DE,(S_TEST_TESTINA)
	        CALL	RES_STATUS		;TEST OK
		
ENDTEST:
		LD      A,(MEMB)
		RES     6,A             ;SBLOCCO GATE
		LD      (MEMB),A
		OUT     (PB),A
		RET;


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		VERIFICA FUNZIONAMENTO ENCODER
;		ROTATIVA DEVE GIRARE AL DI SOTTO DI 50 GIRI/MIN
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

ENC1:                
                LD      A,01H          ;CARICO UN
                LD      (ENCOD1),A     ;GIRO IN CTC1
                LD      A,0D7H         ;AL GIRO
                OUT     (71H),A        
                LD      A,01H          
                OUT     (71H),A        
                LD      A,00H          ;AZZERO IL 
                LD      (ENCOD2),A     ;CONTATORE
		LD	(TEST_ENC),A
                LD      BC,0000H       ;XXTT2
                LD      (ENCOD4),BC
                EI                     ;PER 16
	        LD      BC,0FFFFH      ;RITARDO ANTI
ENC8:           
		DEC     BC              ;RIMBALZO
		LD      A,B
		OR      C
		JR      NZ,ENC8
		JR	ENC1		;AL POSTO DIJP TES1
ENC2:          
                LD      A,37H           ;DISABILITO
                OUT     (03H),A         ;INTERUPT DI
                LD      A,0FFH          ;PIOB
                OUT     (03H),A         ;
ENC3:
                IN      A,(PB)          ;XXTT
		BIT     4,A
                JR      Z,ENC3
ENC7:           
		IN      A,(PB)
                BIT     4,A
                JR      NZ,ENC7
                LD      BC,(ENCOD4)
                INC     BC
                LD      (ENCOD4),BC
                JR      ENC3            ;XXTT

ENC6:           LD      A,00H
                LD      (ENCOD1),A      ;
                LD      A,(ENCOD2)
		LD      A,43H
		OUT     (71H),A
                LD      A,(ENCOD3)      ;TENTA PER
                INC     A               ;10 VOLTE 
                LD      (ENCOD3),A      ;IL TEST
                CP      03H             ;XXTT
                JP      NZ,ENC1

                LD      HL,(ENCOD4)     ;XXTT2
                LD      DE,07FDH        ;2045
                SBC     HL,DE
                JR      C,ENC4
ENC5:        
		CALL	FORCE_STATUS		;TEST OK
		LD	DE,(S_TEST_ENCODER)
		CALL	RES_STATUS
		JR	ENDENC

ENC4:
                CCF                    ;TEST FALLITO
		CALL	FORCE_STATUS
		LD	DE,(S_TEST_ENCODER)
		CALL	SET_STATUS
ENDENC:
		JP	VIA



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		TEST DELLE USCITE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

USCITE:

		LD	A,00H
		LD	(AUMALO),A
		LD	(AUMATR),A
		LD	(AUMATI),A


		LD	A,00H
		LD	(MEMC),A
		OUT	(PC),A
		SET	B_PC_AVANTI,A
		CALL	RITOUT
		RES	B_PC_AVANTI,A
		SET	B_PC_RITARDO,A
		CALL	RITOUT
		RES	B_PC_RITARDO,A
		SET	B_PC_DESTRA,A
		CALL	RITOUT
		RES	B_PC_DESTRA,A
		SET	B_PC_SINISTRA,A
		CALL	RITOUT
		RES	B_PC_SINISTRA,A
		SET	B_PC_ALLARME,A
		CALL	RITOUT
		RES	B_PC_ALLARME,A
		SET	B_PC_FUNZ,A
		CALL	RITOUT
		RES	B_PC_FUNZ,A
		LD	(MEMC),A
		OUT	(PC),A
		
		CALL	SEND_OK

		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		RITARDO PER TEST DELLE USCITE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

RITOUT:
		OUT	(PC),A
RITDIS:         LD      D,06H
RITDI1:         LD      BC,0FFFFH
RITDI2:         DEC     BC
		LD      A,B
		OR      C
		JR      NZ,RITDI2
		DEC     D
		JR      NZ,RITDI1
		RET

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		VISUALIZZAZIONE CICLI DI CORREZIONE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

;ATCICLO:
;             
;ATC1:           LD      BC,0FFFFH       ;RITARDO ANTI EETT
;ATC2:           DEC     BC              ;RIMBALZO
;		LD      A,B
;		OR      C
;                JR      NZ,ATC2         ;EETT
;                IN      A,(PA)          ;EETT
;                BIT     4,A
;                JR      NZ,ATC1         ;EETT 
;
;                ;LD      DE,(CICATT)
;		;LD      A,01H           ;COSI' QUANDO ZZZ
;                ;LD      (MEM6A),A       ;RITORNA VA
;                EI                      ;POI A VIA  ZZZ
;
;		RET     
		

;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		DEFINIZIONE COSTANTI
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
ANSWOK:		DEFB    00H
CILINT:		DEFB	00H
AMPGAT:		DEFB	02H
SEQSEGT:	DEFB	03H
AUMALOT:	DEFB	0DH
AUMATRT:	DEFB	0EH
LONAVA:		DEFB	1AH
LONRIT:		DEFB	1BH
STATLED:	DEFB	1CH
PARVEL:		DEFB	20H
ALLCIL:		DEFB	20H
ALLAMPG:	DEFB	21H
ALLRIF:		DEFB	22H
ALLERR:		DEFB	23H
ALLNOPR:	DEFB	24H
ALLVELO:	DEFB	25H
ALLFAS:		DEFB	26H
ALLTES:		DEFB	27H
ALLENC:		DEFB	28H
ENCSEGN:	DEFB	28H
SUPSOGL:	DEFB	29H
LEGGI:		DEFB	80H
SCRIVI:		DEFB	81H
AUTFAS:		DEFB	82H		
OSCFAS:		DEFB	83H   
TESTES:		DEFB	84H
TESENC:		DEFB	85H
CICLOF:		DEFB    86H
;AVAGAT:		DEFB	87H
;RITGAT:		DEFB	88H
;STOPGA:		DEFB	89H	
SHDATO:		DEFB	8AH
SHMANU:		DEFB	8BH
SHSTOP:		DEFB	8CH
NORDF9:		DEFB	8DH
INIPAR:		DEFB	8EH
AZZERA:		DEFB	90H
PULSAN:		DEFB	91H
TESTOUT:	DEFB	92H
FORCERESET:	DEFB	93H
ANSWER:		DEFB	0C4H
MLUNGHEZZA:	DEFW	0030H	 ;LUNGHEZZA DI MAVA00 e MRIT00


DIRAVA:		DEFB    03H
DIRRIT:		DEFB	0CH
DIRTRA:		DEFB	30H
DIROPE:		DEFB	0C0H


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;			POSSIBILI STATI DI STATUS
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

S_SVILUPPO_CILINDRO:		DEFW	0001H	;1 
S_AMPIEZZA_GATE:		DEFW	0002H	;2
S_SEGNO_DI_RIFERIMENTO:		DEFW	0004H	;4
S_ERRORE_GENERICO:		DEFW	0008H	;8

S_MANCANZA_STAMPA:		DEFW	0010H	;16 
S_VELOCITA_SOTTO_SOGLIA:	DEFW	0020H	;32 
S_SUPERAMENTO_SOGLIA_ERRORE:	DEFW	0040H	;64
S_DOPPIO_SEGNO: 		DEFW	0080H	;128

S_TEST_TESTINA:			DEFW	0100H	;256
S_TEST_ENCODER:			DEFW	0200H	;512
S_FASATURA_FALLITA:		DEFW	0400H	;1024
S_BLOCCO_DECELERAZIONE:		DEFW	0800H	;2048

S_BLOCCO_ACCELERAZIONE:		DEFW	1000H	;4096
;-----NON USATO----------::	DEFW	2000H
;-----NON USATO----------::	DEFW	4000H
;-----NON USATO----------::	DEFW	8000H
				
					
S_PERMANENT_MASK:		DEFW   0F87FH   ;1111 1000 0111 1111




;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;			PICCO DI TEST SUL BIT PC 5
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
  
OUTEST:
		LD	A,(MEMC)
		SET	5,A
		OUT	(PC),A
		LD	D,0FFH
CAZ:		DEC	D
		JR	NZ,CAZ
		RES	5,A
		LD	(MEMC),A
		OUT	(PC),A
		RET


;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	TABELLA PER IL SALVATAGGIO TEMPORANEO DI UN VALORE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

HISTORY:
                PUSH    BC
		PUSH    DE
		PUSH    HL
		PUSH    AF
		PUSH    IX
		PUSH    IY


                LD      IY,(SALVA)
		LD	BC,(VELMED)  ;<<- SOSTITUIRE CON LA VARIABILE DA MONITORARE
                LD      (IY+00H),B
                INC     IY
                LD      (IY+00H),C
                INC     IY

                LD      (SALVA),IY
                LD      HL,0DBFFH
                LD      DE,(SALVA)
                SBC     HL,DE
                JR      NC,HISTORYEXIT
                CCF
                LD      IY,TABLAV
                LD      (SALVA),IY
HISTORYEXIT:
                POP     IY
		POP     IX
		POP     AF
		POP     HL
		POP     DE
		POP     BC
                RET



;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;	AZZERRAMENTO DEL MSB DEI PARAMETRI A UN SOLO BYTE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&

AZZER_MSB:

		LD	A,00H
		LD	(ALLAR+01H),A		
		LD	(AMPGA+01H),A		
		LD	(SEQSEG+01H),A
		LD	(TRIP+01H),A		
		LD	(GUADA+01H),A		
		LD	(GUATRA+01H),A
		LD	(CICLO+01H),A		
		LD	(TCICLO+01H),A
		LD	(ZONA+01H),A		
		LD	(ZONT+01H),A
		LD	(VCLONG+01H),A
		LD	(VCTRAV+01H),A
		LD	(AUMALO+01H),A		
		LD	(AUMATR+01H),A
		LD	(SALTO+01H),A		
		LD	(DERIVA+01H),A		
		LD	(INSER+01H),A		
		LD	(LINGUA+01H),A
		LD	(SEGRIF+01H),A
		LD	(DIAGON+01H),A
		LD	(MESSA+01H),A	
		LD	(NORINV+01H),A
		LD	(MM_MC+01H),A
		LD	(DACLON+01H),A
		LD	(DACTRA+01H),A
		LD	(DACINS+01H),A
		LD	(AUMATI+01H),A		;MODO AUTOMATICO-MANUALE DI TIRO
		LD	(CODE+01H),A
		LD	(MANGUA+01H),A
		LD	(PRESS+01H),A
		LD	(ENCOD+01H),A		;SEGNO DI ZERO DELL'ENCODER
		LD	(TIRGUA+01H),A		;GUADAGNO DI TIRO
		LD	(TIRCIC+01H),A		;CICLICO DI TIRO
		LD	(ENTRAS+01H),A		;ENABLE OP-TRASMISSIONE
		LD	(ENTIRO+01H),A		;ENABLE TIRO
		
		RET
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
;		FINE
;&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&
                END
